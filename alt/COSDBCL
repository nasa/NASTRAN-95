$        COSDBCL.ALT
$
$        DMAP ALTER PACKAGE FOR
$        DYNAMIC DELAMINATION BUCKLING IN COMPOSITE LAMINATES UNDER
$        IMPACK LOADING: COMPUTATIONAL SIMULATION
$        FROM PAPER OF THE SAME TITLE BY J. E. GRADY et al
$        NASA TECHNICAL MEMORANDUM 100192, 1987, CLEWIS RESEARCH CENTER,
$        AND ALSO A SIMILAR PAPER BY R. A. AIELLO AND J. E. GRADY,
$        NASA CONFERENCE PUBLICATION 3029, 1989 (17TH NASTRAN USERS'S
$        COLLOQUIUM, PP. 187-200)
$
$        VAX AND UNIX USER: MAKE SURE YOUR FILE EXTENSION LIMIT IS SET
$        TO 420 BEFORE RUNNING THIS DEMO PROBLEM.
$
$ ALTER 146     $ 91 COSMIC/NASTRAN RF 9,  FOLLOWING LABEL  P2
INSERT XYTRAN(2),-1 $
$
PARAML   UPV//*TRAILER*/1/V,N,NOCUPV $
COPY     TIP1/CLUSI/0 $
COPY     TIP1/BUBLI/0 $
PARAM    //*SUB*/SHIFT/NOCUPV/ 1 $
LABEL    BUBTOP $
FILE     BUBLI=SAVE/CLUSI=SAVE $
PARTN    BUBLI,,BAS1/DUMMY,,,/7 $
MERGE    DUMMY,,,,,TIP1/BUBLJ/7 $
ADD      CLUSI,BUBLJ/CLUSJ/ $
SWITCH   BUBLJ,BUBLI//-1 $
SWITCH   CLUSJ,CLUSI//-1 $
REPT     BUBTOP,SHIFT $
PARTN    TIP1,,CLUSJ/,MNTRI,,/7 $
PARTN    BUBLJ,,CLUSJ/,BOOTI,,/7 $
COPY     MNTRI/MNTRJ/0 $
TA1      ECT,EPT,BGPDT,SIL,GPTT,CSTM,,EQEXIN/X1,X2,X3,ECPT,GPCT,,,/
         LUSET/NOSIMP/0/NOGENL/GENEL $     
DPD      DYNAMICS,GPL,SIL,USET/GPLD,SILD,USETD,,,,,,,EED,EQDYN/V,N,             
         LUSET/V,N,LUSETD/V,N,NOTFL/V,N,NODLT/V,N,NOPSDL/V,N,NOFRL/             
         V,N,NONLFT/V,N,NOTRL/S,N,NOEED/C,N,123/V,N,NOUE $                      
COND     ERROR5,NOEED $                                                         
PARAM    //*NOP*/V,N,COLNUM=1 $                                                 
LABEL    RAALOOP $
PARAM    //*ADD*/COLNUM/COLNUM/3 $
PARAM    //*LE*/V,N,GETOUT/NOCUPV/COLNUM $
COND     QUITRAA,GETOUT $
LABEL    CORTOP $
PARTN    MNTRJ,,BOOTI/DUM11,,,/7 $
MERGE    DUM11,,,,,MNTRI/MNTRJ/7 $
REPT     CORTOP, 2  $
PARTN    UPV,MNTRJ,/,,COLUPV,/1  $
DSMG1    CASECC,GPTT,SIL,EDT,COLUPV,CSTM,MPT,ECPT,GPCT,DIT/                     
         KDGG/DSCOSET $
EQUIV    KDGG,KDNN/MPCF2 $
COND     LBL2D,MPCF2 $
MCE2     USET,GM,KDGG,,,/KDNN,,, $
LABEL    LBL2D $
EQUIV    KDNN,KDFF/SINGLE $
COND     LBL3D,SINGLE $
SCE1     USET,KDNN,,,/KDFF,KDFS,,,, $
LABEL    LBL3D $
EQUIV    KDFF,KDAA/OMIT $
COND     LBL5D,OMIT $
SMP2     USET,GO,KDFF/KDAA $
LABEL    LBL5D $
ADD      KDAA,/KDAAM/C,N,(-1.0,0.0)/C,N,(0.0,0.0) $
READ     KAA,KDAAM,,,EED,USET,CASECC/LAMA,PHIA,MI,OEIGS/C,N,BUCKLING/           
         S,N,NEIGV/C,N,2 $
COND     ERROR4,NEIGV $
PARAML   LAMA//*TABLE1*/2/3/V,N,EIGV $
PRTPARM  //0/*EIGV*   $
$ OFP    OEIGS,LAMA,,,,//S,N,CARDNO $
OFP      LAMA,,,,,//S,N,CARDNO $
SDR1     USET,,PHIA,,,GO,GM,,KFS,,/PHIG,,BQG/C,N,1/C,N,BKL1 $
SDR2     CASECC,CSTM,MPT,DIT,EQEXIN,SIL,,,BGPDT,LAMA,BQG,PHIG,EST,,,/,
         OBQG1,OPHIG,OBES1,OBEF1,PPHIG,,/C,N,BKL1 $
$ OFP    OPHIG,OBQG1,OBEF1,OBES1,,//S,N,CARDNO $
COND     P3,JUMPPLOT $
PLOT     PLTPAR,GPSETS,ELSETS,CASECC,BGPDT,EQEXIN,SIL,,PPHIG,GPECT,
         OBES1,,/PLOTX3/V,N,NSIL/V,N,LUSET/V,N,JUMPPLOT/V,N,PLTFLG/
         S,N,PFILE   $        
PRTMSG   PLOTX3// $
LABEL    P3 $
REPT     RAALOOP,1000 $
JUMP     QUITRAA $
LABEL    ERROR5 $ 
PRTPARM  //C,N,-3/C,N,BUCKLING $
JUMP     QUITRAA $
LABEL    ERROR4 $ 
PRTPARM  //C,N,-4/C,N,BUCKLING $
LABEL    QUITRAA $
JUMP     FINIS $
ENDALTER                                                                        
