$ COSMIC ALTERS FOR HYDROELASTIC ANALYSIS - MODAL FORMULATION (COSHYD2)
$ ALTER    1,1 $  COSMIC/NASTRAN RF 3. REPLACING BEGIN
DELETE BEGIN $
XDMAP    GO,ERR=2 $
BEGIN    HYDROELASTIC ANALYSIS - MODAL FORMULATION
$ ALTER    3 $  AFTER PRECHK/FILE
INSERT FILE $
COMPOFF  NEW1,NEWMODE $
$ ALTER    46 $  AFTER OFP/COND/PURGE
INSERT GP4,3 $
FLBMG    GEOM2,ECT,BGPDT,SIL,MPT,GEOM3,CSTM,USET,EQEXIN/USETF,USETS,AF,
         DKGG/S,N,NOGRAV/S,N,NOFREE/S,N,TILT $
VEC      USETF/PV1/*G*/*X*/*Y* $
PARTN    KGG,PV1,/KXX,,,KYY $
PARTN    MGG,PV1,/MXX,,, $
PARTN    RG,PV1,/RX,,,/1 $
EQUIV    RX,RG $
PARTN    AF,PV1,/,,AXY,AYY $
COND     MODAL1,NOGRAV $
PARTN    DKGG,PV1,/DKXX,,,DKYY $
COND     MODAL1,NOFREE $
VEC      USETF/PV2/*Y*/*FR*/*COMP* $
PARTN    AYY,,PV2/AFRY,,,/0 $
PARTN    DKYY,PV2,/DKFRFR,,, $
LABEL    MODAL1 $
LABEL    NEW1 $
DPD      DYNAMICS,GPL,SIL,USET/GPLD,SILD,USETD,,,,,,,EED,EQDYN/LUSET/
         LUSETD/NOTFL/NODLT/NOPSDL/NOFRL/NONLFT/NOTRL/S,N,NOEED//NOUE $
COND     ERROR2,NOEED $
COMPOFF  NEW2,NEWMODE $
PARAM    //*MPY*/CARDNO/0/0 $
COMPOFF  NOSTRUC,OLDSTR $
COMPON   2,DIFSTIF $
PARAMR   //*COMPLEX*//V,Y,DIFSCALE=1.0/0.0/DIFSCAL/// $
ADD      KXX,KDGG/KGG/(1.0,0.0)/DIFSCAL $
COMPOFF  1,DIFSTIF $
EQUIV    KXX,KGG $
EQUIV    MXX,MGG $
$ ALTER    49,50 $  REPLACING MCE1, MCE2
DELETE MCE1,MCE2 $
MCE1     USETS,RG/GM $
MCE2     USETS,GM,KGG,MGG,,/KNN,MNN,, $
$ ALTER    54,54 $  REPLACING SCE1
DELETE SCE1 $
SCE1     USETS,KNN,MNN,,/KFF,KFS,,MFF,, $
$ ALTER    59,60 $  REPLACING SMP1, SMP2
DELETE SMP1,SMP2 $
SMP1     USETS,KFF,,,/GO,KAA,KOO,LOO,,,,, $
SMP2     USETS,GO,MFF/MAA $
$ ALTER    63,63 $  REPLACING RBMG1
DELETE RBMG1 $
RBMG1    USETS,KAA,MAA/KLL,KLR,KRR,MLL,MLR,MRR $
$ ALTER    68,69 $  REPLACING DPD, COND
DELETE DPD,DPD,1 $
CASE     CASECC,/CASE1/*REIGEN*/S,N,REPT/S,N,LOLP $
$ ALTER    71,71 $  REPLACING READ
DELETE READ $
READ     KAA,MAA,MR,DM,EED,USETS,CASECC/LAMA,PHIA,MI,OEIGS/*MODES*/
         S,N,NEIGV $
$ ALTER    75,77 $  REPLACING SDR1, COND, EQMCK
DELETE SDR1,EQMCK $
SDR1     USETS,,PHIA,,,GO,GM,,KFS,,/PHIG,,QG/1/*REIG* $
COND     NOMPCF,GRDEQ $
EQMCK    CASECC,EQEXIN,GPL,BGPDT,SIL,USETS,KGG,GM,PHIG,LAMA,QG,CSTM/
         OQM1/V,Y,OPT=0/V,Y,GRDEQ/-1 $
$ ALTER    80,80 $  REPLACING SDR2
DELETE SDR2 $
MERGE    PHIG,,,,,PV1/PHIGS/0 $
MERGE    QG,,,,,PV1/QGS/0 $
SDR2     CASE1,CSTM,MPT,DIT,EQEXIN,SIL,,,BGPDT,LAMA,QGS,PHIGS,EST,,,/,
         OQGS,OPHIGS,,OEFS,PPHIGS,,/*REIG* $
OFP      OPHIGS,OQGS,OEFS,,,//S,N,CARDNO $
LABEL    NOSTRUC $
PURGE    DKAA/NOGRAV $
COND     MODAL4,NOGRAV $
EQUIV    DKXX,DKNN/MPCF1 $
COND     MODAL2,MPCF2 $
MCE2     USETS,GM,DKXX,,,/DKNN,,, $
LABEL    MODAL2 $
EQUIV    DKNN,DKFF/SINGLE $
COND     MODAL3,SINGLE $
SCE1     USETS,DKNN,,,/DKFF,,,,, $
LABEL    MODAL3 $
EQUIV    DKFF,DKAA/OMIT $
COND     MODAL4,OMIT $
SMP2     USETS,GO,DKFF/DKAA $
LABEL    MODAL4 $
GFSMA    AXY,AFRY,KYY,DKAA,DKFRFR,,,,,,USETF,PHIA,PHIG,LAMA/KMAT,MMAT,
         GIH,PV4,/NOGRAV/NOFREE/V,Y,KCOMP/V,Y,COMPTYP/FORM=1/
         S,Y,LMODES $
JUMP     OLD2 $
LABEL    NEW2 $
PARAM    //*MPY*/REPT/1/1 $
LABEL    OLD2 $
CASE     CASECC,/CASE2/*REIGEN*/S,N,REPT/S,N,LOLP $
PARAM    //*MPY*/NEIGV/1/-1 $
READ     KMAT,MMAT,,,EED,USETF,CASE2/LAMAT,PHIH,MH,OEIGH/*MODES*/
         S,N,NEIGV $
OFP      LAMAT,OEIGH,,,,//S,N,CARDNO $
COND     FINIS,NEIGV $
MPYAD    GIH,PHIH,/PHII/0/1/0 $
EQUIV    PHIH,PHIZ/NOFREE $
EQUIV    PHII,PHIY/NOFREE $
COND     MODAL5,NOFREE $
PARTN    PHIH,,PV4/PHIZ,PHIFR,,/0 $
MERGE    PHIFR,PHII,,,,PV2/PHIY/0 $
LABEL    MODAL5 $
COND     ALLMODES,LMODES
TRAILER  PHIG//*STORE*/1/V,Y,LMODES $
TRAILER  QG//*STORE*/1/V,Y,LMODES $
LABEL    ALLMODES $
MPYAD    PHIG,PHIZ,/PHIX/0/1/0 $
MPYAD    QG,PHIZ,/QX/0/1/0 $
MERGE    PHIX,PHIY,,,,PV1/PHIGT/0 $
MERGE    QX,,,,,PV1/QGT/0 $
SDR2     CASE2,CSTM,MPT,DIT,EQEXIN,SIL,,,BGPDP,LAMAT,QGT,PHIGT,EST,,,
         PCOMPS/,OQG1,OPHIG,OES1,OEF1,PPHIG,OES1L,OEF1L/*REIG* $
ENDALTER $
