$ COSMIC ALTERS FOR HYDROELASTIC ANALYSIS - DIRECT FORMULATION (COSHYD1)
$ ALTER    1,1 $  COSMIC/NASTRAN RF 3.  REPLACING BEGIN
DELETE BEGIN $
XDMAP    GO,ERR=2 $
BEGIN    HYDROELASTIC ANALYSIS - DIRECT FORMULATION $
$ ALTER    3 $  AFTER PRECHK/FILE
INSERT FILE $
COMPOFF  NEWM,NEWMODE $
$ ALTER    46 $  AFTER OFP/COND/PURGE
INSERT GP4,3 $
FLBMG    GEOM2,ECT,BGPDT,SIL,MPT,GEOM3,CSTM,USET,EQEXIN/USETF,USETS,AF,
         DKGG/S,N,NOGRAV/S,N,NOFREE/S,N,TILT $
VEC      USETF/PV1/*G*/*X*/*Y* $
PARTN    KGG,PV1,/KXX,,,KYY $
PARTN    MGG,PV1,/MXX,,, $
PARTN    RG,PV1,/RX,,,/1 $
EQUIV    RX,RG $
PARTN    AF,PV1,/,,AXY,AYY $
COND     DIRECT1,NOGRAV $
PARTN    DKGG,PV1,/DKXX,,,DKYY $
COND     DIRECT1,NOFREE $
VEC      USETF/PV2/*Y*/*FR*/*COMP* $
PARTN    AYY,,PV2/AFRY,,,/0 $
PARTN    DKYY,PV2,/DKFRFR,,, $
LABEL    DIRECT1 $
COMPOFF  NOSTRUC,OLDSTR $
COMPON   2,DIFSTIF $
PARAMR   //*COMPLEX*//V,Y,DIFSCALE=1.0/0.0/DIFSCAL/// $
ADD      KXX,KDGG/KGG/(1.0,0.0)/DIFSCAL $
COMPOFF  1,DIFSTIF $
EQUIV    KXX,KGG $
EQUIV    MXX,MGG $
$ ALTER    49,50 $  REPLACING MCE1, MCE2
DELETE MCE1,MCE2 $
MCE1     USETS,RG/GM $
MCE2     USETS,GM,KGG,MGG,,/KNN,MNN,, $
$ ALTER    54,54 $  REPLACING SCE1
DELETE SCE1 $
SCE1     USETS,KNN,MNN,,/KFF,KFS,,MFF,, $
$ ALTER    59,60 $  REPLACING SMP1, SMP2
DELETE SMP1,SMP2 $
SMP1     USETS,KFF,,,/GO,KAA,KOO,LOO,,,,, $
SMP2     USETS,GO,MFF/MAA $
$ ALTER    61 $  ALTER LABEL LBL5
INSERT SMP2,1 $
LABEL    NOSTRUC $
PURGE    DKAA/NOGRAV $
COND     DIRECT4,NOGRAV $
EQUIV    DKXX,DKNN/MPCF1 $
COND     DIRECT2,MPCF2 $
MCE2     USETS,GM,DKXX,,,/DKNN,,, $
LABEL    DIRECT2 $
EQUIV    DKNN,DKFF/SINGLE $
COND     DIRECT3,SINGLE $
SCE1     USETS,DKNN,,,/DKFF,,,,, $
LABEL    DIRECT3 $
EQUIV    DKFF,DKAA/OMIT $
COND     DIRECT4,OMIT $
SMP2     USETS,GO,DKFF/DKAA $
LABEL    DIRECT4 $
GFSMA    AXY,AFRY,KYY,DKAA,DKFRFR,KAA,MAA,GM,GO,USETS,USETF,,,/KMAT,
         MMAT,GIA,,HC/NOGRAV/NOFREE/V,Y,KCOMP/V,Y,COMPTYP/FORM=-1 $
EQUIV    KMAT,KAA//MMAT,MAA $
$ ALTER    63,63 $  REPLACING RBMG1
DELETE RBMG1 $
RBMG1    USETF,KAA,MAA/KLL,KLR,KRR,MLL,MLR,MRR $
$ ALTER    67 $  AFTER LABEL LBL6
INSERT DPD,-1 $
LABEL    NEWM $
$ ALTER    68,68 $  REPLACING DPD
DELETE DPD $
DPD      DYNAMICS,GPL,SIL,USETF/GPLD,SILD,USETD,,,,,,,EED,EQDYN/LUSET/
         LUSETD/NOTFL/NODLT/NOPSDL/NOFRL/NONLFT/NOTRL/S,N,NOEED//NOUE $
$ ALTER    71,71 $  REPLACING READ
DELETE READ $
READ     KAA,MAA,MR,DM,EED,USETF,CASECC/LAMA,PHIA,MI,OEIGS/*MODES*/
         S,N,NEIGV $
$ ALTER    75,75 $  REPLACING SDR1
DELETE SDR1 $
COND     NOCOMP,COMPTYP $
MPYAD    HC,PHIA,/PHIAC/0/1/0 $
EQUIV    PHIAC,PHIA $
LABEL    NOCOMP $
MPYAD    GIA,PHIA,/PHII/0/1/0 $
EQUIV    PHII,PHIY/NOFREE $
COND     DIRECT5,NOFREE $
VEC      USETF/PV3/*A*/*COMP*/*FR* $
PARTN    PHIA,,PV3/PHIAB,PHIFR,,/0 $
EQUIV    PHIAB,PHIA $
MERGE    PHIFR,PHII,,,,PV2/PHIY/0 $
LABEL    DIRECT5 $
SDR1     USETS,,PHIA,,,GO,GM,,KFS,,/PHIX,,QX/1/*REIG* $
MERGE    PHIX,PHIY,,,,PV1/PHIG/0 $
MERGE    QX,,,,,PV1/QG/0 $
$ ALTER    77,77 $  REPLACING EQMCK
DELETE EQMCK $
EQMCK    CASECC,EQEXIN,GPL,BGPDT,SIL,USETS,KGG,GM,PHIG,LAMA,QG,CSTM/
         OQM1/V,Y,OPT=0/V,Y,GRDEQ/-1 $
ENDALTER $
