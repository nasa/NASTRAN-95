=PAGE=
AERO10

APR.93
$$$$$$$$
BEGIN    AERO 10 - MODAL FLUTTER ANALYSIS - APR. 1993 $ 
****CARD   1-  6,  8- 26, 29, 32, 34- 40, 55- 62
****RFMT 187-204,207-217
$$$$
PRECHK   ALL $  
****CARD   1-  6,  8- 26, 29, 32, 34- 40, 55- 62
****RFMT 187-204,207-217
$$$$
FILE     PHIHL=APPEND/AJJL=APPEND/FSAVE=APPEND/CASEYY=APPEND/   
         CLAMAL=APPEND/OVG=APPEND/QHHL=APPEND/SKJ=APPEND/QHJL=APPEND/   
         QKHL=APPEND/ $ 
****SBST   4
****CARD   1-  6,  8- 26, 29, 32, 34- 40, 55- 62
****FILE 127,138
****RFMT 187-204,207-217
$$$$
PARAM    //*MPY*/CARDNO/0/0 $   
****CARD   1-  6,  8- 15, 19, 21, 23, 24, 58, 59
****FILE 101,112,119,137,140
****RFMT 199-201,204-217
$$$$
$$$$ GP1 generates coordinate system transformation matrices, tables of grid 
$$$$ point locations, and tables relating the internal and external grid point 
$$$$ numbers.
GP1      GEOM1,GEOM2,/GPL,EQEXIN,GPDT,CSTM,BGPDT,SIL/S,N,LUSET/ 
         S,N,NOGPDT/MINUS1=-1 $ 
****CARD   1, 24
****FILE  94
****RFMT 199-201,204-217
$$$$
$$$$ Go to label ERROR5 and print Error Message No. 5 if no grid points are 
$$$$ defined.
COND     ERROR5,NOGPDT $
****CARD   1, 24
****FILE  94
****RFMT 187-204,207-217
$$$$
$$$$ GP2 generates Element Connection Table with internal indices.
GP2      GEOM2,EQEXIN/ECT $ 
****CARD   1,  2,  4,  5, 16
****FILE  95
****RFMT 199-201,204-217
$$$$
PARAML   PCDB//*PRES*////JUMPPLOT $ 
****SBST   7
****CARD  18
****FILE 122,125
****RFMT 187-204,207-217
$$$$
$$$$ GP3 generates Static Loads Table and Grid Point Temperature Table.
GP3      GEOM3,EQEXIN,GEOM2/,GPTT/NOGRAV $  
****SBST   5
****CARD   1,  2, 13
****FILE  96
****RFMT 199-201,204-217
$$$$
$$$$ TA1 generates element tables for use in matrix assembly and stress 
$$$$ recovery.
TA1      ECT,EPT,BGPDT,SIL,GPTT,CSTM,MPT,EQEXIN/EST,GEI,GPECT,,,MPTX,
         PCOMPS,EPTX/LUSET/S,N,NOSIMP/1/S,N,NOGENL/GENEL/S,N,COMPS $   
****SBST   5
****CARD   1-  6, 13, 16, 24
****FILE  97
****RFMT 199-201,204-217
$$$$
EQUIV    MPTX,MPT/COMPS/EPTX,EPT/COMPS $
****SBST   5
****CARD   1-  6,  8- 26, 29, 32, 34- 40, 55- 62
****FILE  97
****RFMT 199-201,204-217
$$$$
$$$$ Go to label ERROR1 and print Error Message No. 1 if no structural 
$$$$ elements have been defined.
COND     ERROR1,NOSIMP $
****SBST   5
****CARD   1,  2,  4-  6, 13, 16
****FILE  97
****RFMT 187-204,207-217
$$$$
PARAM    //*ADD*/NOKGGX/1/0 $   
****SBST   4
****CARD   1-  3,  6,  8, 13, 14, 24
****FILE 147
****RFMT 199-201,204-217
$$$$
PARAM    //*ADD*/NOMGG /1/0 $   
****SBST   4
****CARD   1-  3,  5,  8, 13, 14, 24
****FILE 147
****RFMT 199-201,204-217
$$$$
$$$$ EMG generates structural element stiffness and mass matrix tables and 
$$$$ dictionaries for later assembly by the EMA module.
EMG      EST,CSTM,MPT,DIT,GEOM2,/KELM,KDICT,MELM,MDICT,,,/S,N,NOKGGX/   
         S,N,NOMGG////C,Y,COUPMASS/C,Y,CPBAR/C,Y,CPROD/ 
         C,Y,CPQUAD1/C,Y,CPQUAD2/C,Y,CPTRIA1/C,Y,CPTRIA2/C,Y,CPTUBE/
         C,Y,CPQDPLT/C,Y,CPTRPLT/C,Y,CPTRBSC/   
         C,Y,VOLUME/C,Y,SURFACE $   
****SBST   4
****CARD   1-  3,  5,  6,  8, 13, 14, 24
****FILE 147
****RFMT 199-201,204-217
$$$$
PURGE    KGGX/NOKGGX $  
****SBST   4
****CARD   1-  3,  6,  8, 13, 24
****FILE  98
****RFMT 199-201,204-217
$$$$
$$$$ Go to label JMPKGGX if no stiffness matrix is to be assembled.
COND     JMPKGGX,NOKGGX $   
****SBST   4
****CARD   1-  3,  6,  8, 13, 24
****FILE  98
****RFMT 199-201,204-217
$$$$                                  x
$$$$ EMA assembles stiffness matrix [K  ] and Grid Point Singularity Table.
$$$$                                  gg
$$$$
EMA      GPECT,KDICT,KELM/KGGX $
****SBST   4
****CARD   1-  3,  6,  8, 13, 24
****FILE  98
****RFMT 199-201,204-217
$$$$
PURGE    KDICT,KELM/MINUS1 $
****SBST   4
****CARD   1-  3,  6,  8, 13, 24
****FILE 147
****RFMT 199-201,204-217
$$$$
LABEL    JMPKGGX $  
****SBST   4
****CARD   1-  3,  6,  8, 13, 24
****FILE  98
****RFMT 199-201,204-217
$$$$
$$$$ Go to label ERROR1 and print Error Message No. 1 if no mass matrix is to 
$$$$ be assembled.
COND     ERROR1,NOMGG $ 
****SBST   4
****CARD   1-  3,  5,  8, 13, 14, 24
****FILE 147
****RFMT 187-204,207-217
$$$$
$$$$ EMA assembles mass matrix [M  ].
$$$$                             gg
$$$$
EMA      GPECT,MDICT,MELM/MGG/-1/C,Y,WTMASS=1.0 $   
****SBST   4
****CARD   1-  3,  5,  8, 13, 14, 24
****FILE  99
****RFMT 199-201,204-217
$$$$
PURGE    MDICT,MELM/MINUS1 $
****SBST   4
****CARD   1-  3,  5,  8, 13, 14, 24
****FILE 147
****RFMT 199-201,204-217
$$$$
$$$$ Go to label LGPWG if no weight and balance information is requested.
COND     LGPWG,GRDPNT $ 
****SBST   4
****CARD   1-  3,  5,  8, 13- 15, 24
****FILE 140
****RFMT 199-201,204-217
$$$$
$$$$ GPWG generates weight and balance information.
GPWG     BGPDT,CSTM,EQEXIN,MGG/OGPWG/V,Y,GRDPNT=-1/C,Y,WTMASS $ 
****SBST   4,  8
****CARD   1-  3,  5,  8, 13- 15, 24
****FILE 140
****RFMT 199-201,204-217
$$$$
$$$$ OFP formats the weight and balance information prepared by GPWG and 
$$$$ places it on the system output file for printing.
OFP      OGPWG,,,,,//S,N,CARDNO $   
****SBST   4,  8
****CARD   1-  3,  5,  8, 13- 15, 24
****FILE 140
****RFMT 199-201,204-217
$$$$
LABEL    LGPWG $
****SBST   4,  8
****CARD   1-  3,  5,  8, 13- 15, 24
****FILE 140
****RFMT 199-201,204-217
$$$$               x
$$$$ Equivalence [K  ] to [K  ] if there are no general elements.
$$$$               gg       gg
$$$$
EQUIV    KGGX,KGG/NOGENL $  
****SBST   4
****CARD   1-  4,  6,  8, 13, 24
****FILE 100
****RFMT 199-201,204-217
$$$$
$$$$ Go to label LBL11 if there are no general elements.
COND     LBL11,NOGENL $ 
****SBST   4
****CARD   1-  4,  6,  8, 13, 24
****FILE 100
****RFMT 199-201,204-217
$$$$                                                   y
$$$$ SMA3 forms the general element stiffness matrix [K  ].
$$$$                                                   gg
$$$$
SMA3     GEI,KGGX/KGG/LUSET/NOGENL/NOSIMP $ 
****SBST   4
****CARD   1-  4,  6,  8, 13, 24
****FILE 100
****RFMT 199-201,204-217
$$$$
LABEL    LBL11 $
****SBST   4
****CARD   1-  4,  6,  8, 13, 24
****FILE 100
****RFMT 199-201,204-217
$$$$
GPSTGEN  KGG,SIL/GPST $ 
****SBST   4
****CARD   1-  4,  6,  8, 13, 24
****FILE 102
****RFMT 199-201,204-217
$$$$
$$$$ GP4 generates flags defining members of various displacement sets
$$$$ (USET), and forms multipoint constraint equations [R ] {u } = 0.
$$$$                                                     g    g
$$$$
GP4      CASECC,GEOM4,EQEXIN,GPDT,BGPDT,CSTM,GPST/RG,,USET, 
         ASET,OGPST/LUSET/S,N,MPCF1/S,N,MPCF2/S,N,SINGLE/S,N,OMIT/  
         S,N,REACT/0/REPEAT/S,N,NOSET/S,N,NOL/S,N,NOA/  
         C,Y,ASETOUT/C,Y,AUTOSPC $  
****SBST   4
****CARD   1-  4,  6,  8- 13, 22- 24
****FILE 101
****RFMT 199-201,204-217
$$$$
$$$$ OFP formats the table of potential grid point similarities prepared by 
$$$$ GPSP and places it on the system output file for printing.
OFP      OGPST,,,,,//S,N,CARDNO $   
****SBST   4
****CARD   1-  4,  6,  8- 13, 22- 24
****FILE 101
****RFMT 199-201,204-217
$$$$
$$$$ Equivalence [K  ] to [K  ] and [M  ] to [M  ] if no multipoint 
$$$$               gg       nn        gg       nn
$$$$ constraints exist.
EQUIV    KGG,KNN/MPCF1/MGG,MNN/MPCF1 $  
****SBST   4
****CARD   1-  6,  8,  9, 13, 14, 24
****FILE 104
****RFMT 199-201,204-217
$$$$
PURGE    GM/MPCF1/DM,MR/REACT $ 
****SBST   4
****CARD   1-  6,  8- 14, 24
****FILE 103,109,110
****RFMT 199-201,204-217
$$$$
$$$$ Go to label LBL2 if no multipoint constraints exist.
COND     LBL2,MPCF1 $   
****SBST   4
****CARD   1-  6,  8,  9, 13, 14, 24
****FILE 103,104
****RFMT 199-201,204-217
$$$$
$$$$ MCE1 partitions multipoint constraint equation [R ] = [R |R ] and solves
$$$$                                                  g      m  n
$$$$                                                             -1
$$$$ for multipoint constraint transformation matrix [G ] = -[R ]  [R ].
$$$$                                                   m       m     n
$$$$
MCE1     USET,RG/GM $   
****SBST   4
****CARD   1,  9, 24
****FILE 103
****RFMT 199-201,204-217
$$$$
$$$$ MCE2 partitions stiffness and mass matrices
$$$$ 
$$$$           +_      +                        +_      +
$$$$           |K  |K  |                        |M  |M  |
$$$$           | nn| nm|                        | nn| nm|
$$$$   [K  ] = |---+---|                [M  ] = |---+---|
$$$$     gg    |K  |K  |                  gg    |M  |M  |
$$$$           | mn| mm|                        | mn| mm|
$$$$           +       +                        +       +
$$$$ 
$$$$ and performs matrix reductions
$$$$ 
$$$$            _        T           T           T
$$$$   [K  ] = [K  ] + [G ][K  ] + [K  ][G ] + [G ][K  ][G ]
$$$$     nn      nn      m   mn      mn   m      m   mm   m
$$$$
$$$$            _        T           T           T
$$$$   [M  ] = [M  ] + [G ][M  ] + [M  ][G ] + [G ][M  ][G ]
$$$$     nn      nn      m   mn      mn   m      m   mm   m
$$$$
MCE2     USET,GM,KGG,MGG,,/KNN,MNN,, $  
****SBST   4
****CARD   1-  6,  8,  9, 13, 14, 24
****FILE 104
****RFMT 199-201,204-217
$$$$
LABEL    LBL2 $ 
****SBST   4
****CARD   1-  6,  8,  9, 13, 14, 24
****FILE 103,104
****RFMT 199-201,204-217
$$$$
$$$$ Equivalence [K  ] to [K  ] and [M  ] to [M  ] if no single-point
$$$$               nn       ff        nn       ff
$$$$ constraints exist.
EQUIV    KNN,KFF/SINGLE/MNN,MFF/SINGLE $
****SBST   4
****CARD   1-  6,  8- 10, 13, 14, 24
****FILE 105
****RFMT 199-201,204-217
$$$$
$$$$ Go to label LBL3 if no single-point constraints exist.
COND     LBL3,SINGLE $  
****SBST   4
****CARD   1-  6,  8- 10, 13, 14, 24
****FILE 105
****RFMT 199-201,204-217
$$$$
$$$$ SCE1 partitions out single-point constraints
$$$$ 
$$$$           +       +                        +       +
$$$$           |K  |K  |                        |M  |M  |
$$$$           | ff| fs|                        | ff| fs|
$$$$   [K  ] = |---+---|                [M  ] = |---+---|
$$$$     nn    |K  |K  |                  nn    |M  |M  |
$$$$           | sf| ss|                        | sf| ss|
$$$$           +       +                        +       +
$$$$
SCE1     USET,KNN,MNN,,/KFF,KFS,,MFF,, $
****SBST   4
****CARD   1-  6,  8- 10, 13, 14, 24
****FILE 105
****RFMT 199-201,204-217
$$$$
LABEL    LBL3 $ 
****SBST   4
****CARD   1-  6,  8- 10, 13, 14, 24
****FILE 105
****RFMT 199-201,204-217
$$$$
$$$$ Equivalence [K  ] to [K  ] and [M  ] to [M  ] if no omitted coordinates
$$$$               ff       aa        ff       aa
$$$$ exist.
EQUIV    KFF,KAA/OMIT/ MFF,MAA/OMIT $   
****SBST   4
****CARD   1-  6,  8- 11, 13, 14, 24
****FILE 123,142
****RFMT 199-201,204-217
$$$$
PURGE    GO/OMIT $  
****SBST   4
****CARD   1-  4,  6,  8- 11, 13, 24
****FILE 142
****RFMT 199-201,204-217
$$$$
$$$$ Go to label LBL5 if no omitted coordinates exist.
COND     LBL5,OMIT $
****SBST   4
****CARD   1-  6,  8- 11, 13, 14, 24
****FILE 106,123,142
****RFMT 199-201,204-217
$$$$
PARAM    //*PREC*/PREC $
****SBST   4
****CARD   1-  6,  8- 11, 13, 14, 24
****FILE 106
****RFMT 187-204,207-217
$$$$
$$$$ SMP1 partitions constrained stiffness matrix
$$$$
$$$$           +_      +
$$$$           |K  |K  |
$$$$           | aa| ao|
$$$$   [K  ] = |---+---|
$$$$     ff    |K  |K  |
$$$$           | oa| oo|
$$$$           +       +
$$$$           
$$$$                                               -1
$$$$ solves for transformation matrix [G ] = -[K  ]  [K  ]
$$$$                                    o       oo     oa
$$$$ 
$$$$                                        _        T
$$$$ and performs matrix reduction [K  ] = [K  ] + [K  ][G ].
$$$$                                 aa      aa      oa   o
$$$$
SMP1     USET,KFF,,,/GO,KAA,KOO,LOO,,,,, $  
****SBST   4
****CARD   1-  6,  8- 11, 13, 14, 24
****FILE 142
****RFMT 199-201,204-217
$$$$
$$$$ SMP2 partitions constrained mass matrix
$$$$
$$$$           +       +
$$$$           |_      |
$$$$           |M  |M  |
$$$$           | aa| ao|
$$$$   [M  ] = |---+---|
$$$$     ff    |   |   |
$$$$           |M  |M  |
$$$$           | oa| oo|
$$$$           +       +
$$$$ 
$$$$ and performs matrix reduction
$$$$
$$$$            _        T           T              T
$$$$   [M  ] = [M  ] + [M  ][G ] + [G ][M  ][G ]+ [G ][M  ]
$$$$     aa      aa      oa   o      o   oo   o     o   oa
$$$$
SMP2     USET,GO,MFF/MAA $  
****SBST   4
****CARD   1-  6,  8- 11, 13, 14, 24
****FILE 123
****RFMT 199-201,204-217
$$$$
LABEL    LBL5 $ 
****SBST   4
****CARD   1-  6,  8- 11, 13, 14, 24
****FILE 106,123,142
****RFMT 199-201,204-217
$$$$
$$$$ Go to label LBL6 if there are no free-body supports.
COND     LBL6,REACT $   
****SBST   4
****CARD   1-  6,  8- 14, 24
****FILE 107-110
****RFMT 199-201,204-217
$$$$
$$$$ RBMG1 partitions out single-point constraints
$$$$ 
$$$$           +       +                        +       +
$$$$           |K  |K  |                        |M  |M  |
$$$$           | ll| lr|                        | ll| lr|
$$$$   [K  ] = |---+---|                [M  ] = |---+---|
$$$$     aa    |K  |K  |                  aa    |M  |M  |
$$$$           | rl| rr|                        | rl| rr|
$$$$           +       +                        +       +
$$$$
RBMG1    USET,KAA,MAA/KLL,KLR,KRR,MLL,MLR,MRR $ 
****SBST   4
****CARD   1-  6,  8- 14, 24
****FILE 107
****RFMT 199-201,204-217
$$$$
$$$$ RBMG2 decomposes constrained stiffness matrix [K  ] = [L  ][U  ].
$$$$                                                 ll      ll   ll
$$$$
RBMG2    KLL/LLL/ $ 
****SBST   4
****CARD   1-  4,  6,  8- 13, 24
****FILE 108
****RFMT 199-201,204-217
$$$$
$$$$ RBMG3 forms rigid body transformation matrix
$$$$ 
$$$$               -1
$$$$   [D] = -[K  ]  [K  ]
$$$$            ll     lr
$$$$ 
$$$$ calculates rigid body check matrix
$$$$
$$$$                   T
$$$$   [X] = [K  ] + [K  ][D]
$$$$           rr      lr
$$$$ 
$$$$ and calculates rigid body error ratio
$$$$ 
$$$$ 
$$$$               ||X||
$$$$   epsilon = ---------
$$$$              ||K  ||
$$$$                 rr
$$$$
RBMG3    LLL,KLR,KRR/DM $   
****SBST   4
****CARD   1-  4,  6,  8- 13, 24
****FILE 109
****RFMT 199-201,204-217
$$$$
$$$$ RBMG4 forms rigid body mass matrix
$$$$ 
$$$$                    T          T           T
$$$$   [m ] = [M  ] + [M  ][D] + [D ][M  ] + [D ][M  ][D]
$$$$     r      rr      lr             lr          ll
$$$$
RBMG4    DM,MLL,MLR,MRR/MR $
****SBST   4
****CARD   1-  6,  8- 14, 24
****FILE 110
****RFMT 199-201,204-217
$$$$
LABEL    LBL6 $ 
****SBST   4
****CARD   1-  6,  8- 14, 24
****FILE 107-110
****RFMT 199-201,204-217
$$$$
$$$$ DPD generates flags defining members of various displacement sets used in 
$$$$ dynamic analysis (USETD), tables relating the internal and external grid 
$$$$ point numbers (GPLD), including extra points introduced for dynamic 
$$$$ analysis (SILD), and prepares Transfer Function Pool (TFPOOL) and 
$$$$ Eigenvalue Extraction Data (EED).
DPD      DYNAMICS,GPL,SIL,USET/GPLD,SILD,USETD,TFPOOL,,,,,,EED,EQDYN/   
         LUSET/S,N,LUSETD/NOTFL/NODLT/NOPSDL/NOFRL/ 
         NONLFT/NOTRL/S,N,NOEED/123/S,N,NOUE $  
****CARD   1,  9- 12, 40, 56, 58, 60
****FILE 111
****RFMT 199-201,204-217
$$$$
$$$$ Go to label ERROR2 and print Error Message No. 2 if there is no 
$$$$ Eigenvalue Extraction Data.
COND     ERROR2,NOEED $ 
****SBST   4
****CARD   1,  9- 12, 40, 56, 58, 60
****FILE 111
****RFMT 187-204,207-217
$$$$                       d                d
$$$$ Equivalence [G ] to [G ] and [G ] to [G ] if there are no extra points
$$$$               o       o        m       m
$$$$ introduced for dynamic analysis.
EQUIV    GO,GOD/NOUE/GM,GMD/NOUE $  
****SBST   4
****CARD   1-  4,  6,  9- 11, 13, 14, 24, 56
****FILE 115
****RFMT 199-201,204-217
$$$$
$$$$ READ extracts real eigenvalues and eigenvectors from the equation
$$$$ 
$$$$   [K   - lambda M  ]{u } = 0
$$$$     aa           aa   a
$$$$ 
$$$$ calculates rigid body modes by finding a square matrix [phi  ] such that
$$$$                                                            ro
$$$$              T
$$$$   [m ] = [phi  ][m ][phi  ]
$$$$     o        ro   r     ro
$$$$ 
$$$$ is diagonal and normalized, computes rigid body eigenvectors
$$$$ 
$$$$             +       +
$$$$             |Dphi   |
$$$$             |    ro |
$$$$   [phi  ] = |-------|
$$$$       ao    |phi    |
$$$$             |   ro  |
$$$$             +       +
$$$$ 
$$$$ calculates modal mass matrix
$$$$ 
$$$$             T
$$$$   [m] = [phi ][M  ][phi ]
$$$$             a   aa     a
$$$$ 
$$$$ and normalizes eigenvectors according to one of the following user 
$$$$ requests:
$$$$   1. Unit value of a selected component.
$$$$   2. Unit value of the largest component.
$$$$   3. Unit value of the generalized mass.
READ     KAA,MAA,MR,DM,EED,USET,CASECC/LAMA,PHIA,MI,OEIGS/*MODES*/S,N,  
         NEIGV $
****SBST   4
****CARD   1-  6,  8- 14, 24, 58, 59
****FILE 112
****RFMT 199-201,204-217
$$$$
$$$$ OFP formats the summary of eigenvalue extraction information (OEIGS) 
$$$$ prepared by READ and places it on the system output file for printing.
OFP      OEIGS,,,,,//S,N,CARDNO $   
****SBST   4
****CARD   1-  6,  8- 14, 24, 58, 59
****FILE 112
****RFMT 199-201,204-217
$$$$
$$$$ Go to label ERROR4 and print Error Message No. 4 if no eigenvalues were 
$$$$ found.
COND     ERROR4,NEIGV $ 
****SBST   4
****CARD   1-  6,  8- 14, 24, 58, 59
****FILE 112
****RFMT 187-204,207-217
$$$$
$$$$ OFP formats the eigenvalues (LAMA) prepared by READ and places them on 
$$$$ the system output file for printing.
OFP      LAMA,,,,,//S,N,CARDNO $
****SBST   4
****CARD   1-  6,  8- 14, 24, 58, 59
****FILE 112
****RFMT 199-201,204-217
$$$$                                            2      2          2
$$$$ MTRXIN selects the direct input matrices [K  ], [M  ], and [B  ].
$$$$                                            pp     pp         pp
$$$$
MTRXIN   CASECC,MATPOOL,EQDYN,,TFPOOL/K2PP,M2PP,B2PP/LUSETD/S,N,
         NOK2PP/S,N,NOM2PP/S,N,NOB2PP $ 
****SBST   5
****CARD   1, 40, 56, 57
****FILE 114
****RFMT 199-201,204-217
$$$$               2        2      2        2          2        2
$$$$ Equivalence [M  ] to [M  ], [B  ] to [B  ], and [K  ] to [K  ] if no
$$$$               pp       dd     pp       dd         pp       dd
$$$$ constraints are applied.
EQUIV    M2PP,M2DD/NOA/B2PP,B2DD/NOA/K2PP,K2DD/NOA $
****SBST   4
****CARD   1,  9- 11, 40, 56, 57
****FILE 139
****RFMT 199-201,204-217
$$$$                                                     2      2             
$$$$ GKAD applies constraints to direct input matrices [K  ], [M  ], and      
$$$$                                                     pp     pp            
$$$$   2              2      2          2
$$$$ [B  ], forming [K  ], [M  ], and [B  ], and forms [G  ] and [G  ].
$$$$   pp             dd     dd         dd               md        od
$$$$
GKAD     USETD,GM,GO,,,,,K2PP,M2PP,B2PP/,,,GMD,GOD,K2DD,M2DD,B2DD/  
         *CMPLEV*/*DISP*/*MODAL*/0.0/0.0/0.0/NOK2PP/
         NOM2PP/NOB2PP/MPCF1/SINGLE/OMIT/NOUE/  
         -1/-1/   -1/-1 $   
****SBST   4
****CARD   1-  4,  6,  8- 11, 13, 14, 24, 40, 56, 57
****FILE 115,139
****RFMT 199-201,204-217
$$$$
$$$$ GKAM selects eigenvectors to form [phi  ] and assembles stiffness, mass,
$$$$                                       dh
$$$$ and damping matrices in modal coordinates:
$$$$
$$$$            +       +
$$$$            |k  |   |
$$$$            | i | 0 |       T    2
$$$$   [K  ] =  |---+---| + [phi  ][K  ][phi  ]
$$$$     hh     | 0 | 0 |       dh   dd     dh
$$$$            |   |   |
$$$$            +       +
$$$$
$$$$            +       +
$$$$            |m  |   |
$$$$            | i | 0 |       T    2
$$$$   [M  ] =  |---+---| + [phi  ][M  ][phi  ]
$$$$     hh     | 0 | 0 |       dh   dd     dh
$$$$            |   |   |
$$$$            +       +
$$$$
$$$$            +       +
$$$$            |b  |   |
$$$$            | i | 0 |       T    2
$$$$   [B  ] =  |---+---| + [phi  ][B  ][phi  ]
$$$$     hh     | 0 | 0 |       dh   dd     dh
$$$$            |   |   |
$$$$            +       +
$$$$
$$$$ where
$$$$
$$$$   KDAMP = -1 (default)             KDAMP = 1
$$$$     m  = modal masses                m  = modal masses
$$$$      i                                i
$$$$     b  = m  2 pi f  g(f )            b  = 0
$$$$      i    i       i    i              i
$$$$                 2                                        2  2
$$$$     k  = m  4 pi  f                  k  = (1+ig(f )) 4 pi  f  m
$$$$      i    i        i                  i          i          i  i
$$$$
GKAM     USETD,PHIA,,LAMA,DIT,M2DD,B2DD,K2DD,CASECC/MHH,BHH,KHH,
         PHIDH/NOUE/C,Y,LMODES=0/C,Y,LFREQ=0./C,Y,HFREQ=-1.0/   
         NOM2PP/NOB2PP/NOK2PP/S,N,NONCUP/S,N,FMODE/C,Y,KDAMP $  
****SBST   4
****CARD   1-  6,  8- 14, 24, 40, 55- 59, 62
****FILE 116
****RFMT 199-201,204-217
$$$$
$$$$ APD processes the aerodynamic data cards from EDT. It adds the k points 
$$$$ and the SA points to USETD, making USETA. EQAERO, ECTA, BGPA, CSTMA, 
$$$$ GPLA, and SILA are updated to reflect the new elements. AERO and ACPT 
$$$$ reflect the aerodynamic parameters. SILGA is a special SIL for plotting.
APD      EDT,EQDYN,ECT,BGPDT,SILD,USETD,CSTM,GPLD/EQAERO,ECTA,BGPA,SILA,
         USETA,SPLINE,AERO,ACPT,FLIST,CSTMA,GPLA,SILGA/S,N,NK/S,N,NJ/   
         S,N,LUSETA/S,N,BOV $   
****CARD   1,  2,  4,  5,  9- 12, 16, 24, 29, 32, 34- 37, 56
****FILE 124
****RFMT 199-201,204-217
$$$$
PARAM    //*MPY*/PFILE/0/1 $
****SBST   7
****CARD  18
****FILE 118
****RFMT 199-201,204-217
$$$$
PURGE    PLTSETA,PLTPARA,GPSETSA,ELSETSA/JUMPPLOT $ 
****SBST   7
****CARD   1,  2,  4,  5,  9- 12, 16, 18, 24, 32, 35, 37, 56
****FILE 118,125
****RFMT 199-201,204-217
$$$$
$$$$ Go to lable SKPPLT if no plot output is requested.
COND     SKPPLT,JUMPPLOT $  
****SBST   7
****CARD   1,  2,  4,  5,  9- 12, 16, 18, 24, 32, 35, 37, 56
****FILE 118,125
****RFMT 199-201,204-217
$$$$
PARAM    //*MPY*/PLTFLG/0/1 $   
****SBST   7
****CARD   1,  2,  4,  5,  9- 12, 16, 18, 24, 32, 35, 37, 56
****FILE 118
****RFMT 199-201,204-217
$$$$
$$$$ PLTSET transforms user input into a form used to drive the structure 
$$$$ plotter.
PLTSET   PCDB,EQAERO,ECTA,/PLTSETA,PLTPARA,GPSETSA,ELSETSA/S,N,NSIL1/  
         S,N,JUMPPLOT $   
****SBST   7
****CARD   1,  2,  4,  5,  9- 12, 16, 18, 24, 32, 35, 37, 56
****FILE 125
****RFMT 199-201,204-217
$$$$
$$$$ PRTMSG prints error messages associated with the structure plotter.
PRTMSG   PLTSETA // $   
****SBST   7
****CARD   1,  2,  4,  5,  9- 12, 16, 18, 24, 32, 35, 37, 56
****FILE 125
****RFMT 199-201,204-217
$$$$
$$$$ Go to label SKPPLT if no undeformed aerodynamic or structural plot 
$$$$ elements are requested.
COND     SKPPLT,JUMPPLOT $  
****SBST   7
****CARD   1,  2,  4,  5,  9- 12, 16, 18, 24, 32, 35, 37, 56
****FILE 118,125
****RFMT 199-201,204-217
$$$$
$$$$ PLOT generates all requested undeformed aerodynamic and structural 
$$$$ element plots.
PLOT     PLTPARA,GPSETSA,ELSETSA,CASECC,BGPA,EQAERO,    ,,,,,,/PLOTX2/  
         NSIL1/LUSETA/S,N,JUMPPLOT/S,N,PLTFLG/S,N,PFILE $   
****SBST   7
****CARD   1,  2,  4,  5,  9- 12, 16, 18, 24, 32, 35, 37, 56
****FILE 118
****RFMT 199-201,204-217
$$$$
$$$$ PRTMSG prints plotter data and engineering data for each undeformed 
$$$$ aerodynamic and structural plot element generated.
PRTMSG   PLOTX2 // $
****SBST   7
****CARD   1,  2,  4,  5,  9- 12, 16, 18, 24, 32, 35, 37, 56
****FILE 118
****RFMT 199-201,204-217
$$$$
LABEL    SKPPLT $   
****SBST   7
****CARD   1,  2,  4,  5,  9- 12, 16, 18, 24, 32, 35, 37, 56
****FILE 118,125
****RFMT 199-201,204-217
$$$$
$$$$ Go to label ERROR2 and print Error Message No. 2 if there is no 
$$$$ Eigenvalue Extraction Data.
COND     ERROR2,NOEED $ 
****CARD  58, 60
****FILE 121
****RFMT 199-201,204-217
$$$$                                    T
$$$$ GI forms a transformation matrix [G  ] which interpolates between
$$$$                                    ka
$$$$ aerodynamic (k) and structural (a) degrees of freedom.
GI       SPLINE,USET ,CSTMA,BGPA,SIL , ,GM,GO/GTKA/NK/LUSET $   
****SBST   4
****CARD   1-  4,  6,  8- 11, 13, 24, 32, 35, 37
****FILE 126
****RFMT 199-201,204-217
$$$$
PARAM    //*ADD*/DESTRY/0/1/ $  
****SBST   6
****CARD  24, 29, 35, 37
****FILE 127
****RFMT 187-204,207-217
$$$$
$$$$ AMG forms the aerodynamic matrix list [A  ], the area matrix [S  ], and
$$$$                                         jj                     kj
$$$$                             1         2
$$$$ the downwash coefficients [D  ] and [D  ].
$$$$                             jk        jk
$$$$
AMG      AERO,ACPT/AJJL,SKJ,D1JK,D2JK/NK/NJ/S,N,DESTRY $
****SBST   6
****CARD  24, 29, 34, 35, 37
****FILE 127
****RFMT 199-201,204-217
$$$$
$$$$ Go to label NODJE if there are no user-supplied downwash coefficients.
COND     NODJE,    NODJE $  
****SBST   4
****CARD  17, 26, 37, 56
****FILE 128
****RFMT 199-201,204-217
$$$$
$$$$ INPUTT2 provides the user-supplied downwash factors due to extra points
$$$$    1      2
$$$$ ([D  ], [D  ]). PARAM NODJE must be set to enter these matrices. The
$$$$    je     je
$$$$ downwash w  onbox j due to the motion of an extra point, u , is given by
$$$$           j                                               e
$$$$             1       2
$$$$   {w  | = [D   + ikD  ]{u }
$$$$      j      je      je   e
$$$$
INPUTT2  /D1JE,D2JE,,,/C,Y,P1=0/C,Y,P2=11/C,Y,P3=XXXXXXXX $ 
****SBST   4
****CARD  17, 26, 37, 56
****FILE 128
****RFMT 199-201,204-217
$$$$
LABEL    NODJE $
****SBST   4
****CARD  17, 26, 37, 56
****FILE 128
****RFMT 199-201,204-217
$$$$
PARAM    //*ADD*/XQHHL/1/0 $
****SBST   4
****CARD   1-  6,  8- 14, 17, 24, 26, 29, 32, 35, 37, 54, 56, 58, 59, 62
****FILE 138
****RFMT 199-201,204-217
$$$$
$$$$ AMP computes the aerodynamic matrix list related to the modal coordinates 
$$$$ as follows
$$$$ 
$$$$              +           +
$$$$              |phi  |phi  |
$$$$              |   ai|   ae|                   T  T        
$$$$   [phi  ] =  |-----+-----|         [G  ] = [G  ]  [phi  ]
$$$$       dh     |phi  |phi  |           ki      ka       ai 
$$$$              |   ei|   ee|
$$$$              +           +
$$$$
$$$$     1        1     1                 1       1  T
$$$$   [D  ] <= [D   | D  ]             [D  ] = [D  ] [G  ]
$$$$     jh       jf    je                ji      jk    ki
$$$$
$$$$     2        2     2                 2       2  T
$$$$   [D  ] <= [D   | D  ]             [D  ] = [D  ] [G  ]
$$$$     jh       jf    je                ji      jk    ki
$$$$
$$$$ For each (m,k) pair:
$$$$
$$$$             1         2
$$$$   [D  ] = [D  ] + ik[D  ]
$$$$     jh      jh        jh
$$$$
$$$$ For each group:
$$$$
$$$$             T  -1
$$$$   [Q  ] = [A  ]      [D  ]
$$$$     jh      jj group   jh group
$$$$
$$$$   [Q  ] = [S  ][Q  ]
$$$$     kh      kj   jh
$$$$
$$$$                T
$$$$   [Q  ] = [G  ] [Q  ]
$$$$     ih      ki    kh
$$$$
$$$$             +     +
$$$$             | Q   |
$$$$             |  ih |
$$$$   [Q  ] <=  | ----|
$$$$     hh      | Q   |
$$$$             |  eh |
$$$$             +     +
$$$$
AMP      AJJL,SKJ,D1JK,D2JK,GTKA,PHIDH,D1JE,D2JE,USETD,AERO/QHHL,QKHL,  
         QHJL/NOUE/S,N,XQHHL/V,Y,GUSTAERO=-1 $  
****SBST   4
****CARD   1-  6,  8- 14, 17, 24, 26, 29, 32, 34, 35, 37, 54, 56, 58, 59
****CARD  62
****FILE 138
****RFMT 199-201,204-217
$$$$
$$$$ PARAM initializes the flutter loop counter (FLOOP) to zero.
PARAM    //*MPY*/FLOOP/V,Y,NODJE=-1/0 $ 
****SBST   4
****CARD   1-  6,  8- 14, 24, 26, 29, 32, 34- 40, 55- 62
****FILE 129
****RFMT 187-204,207-217
$$$$
$$$$ Beginning of loop for flutter.
LABEL    LOOPTOP $  
****SBST   3,  4
****CARD   1-  6,  8- 14, 24, 26, 29, 32, 34- 40, 55- 62
****FILE 129
****RFMT 187-204,207-217
$$$$                                                  x
$$$$ FA1 computes the total aerodynamic mass matrix [M  ], the total 
$$$$                                                  hh
$$$$                                x
$$$$ aerodynamic stiffness matrix [K  ], and the total aerodynamic damping
$$$$                                hh
$$$$          x
$$$$ matrix [B  ], as well as a looping table FSAVE. For the K method
$$$$          hh
$$$$ 
$$$$    x      2  2           ) Q
$$$$   M   = (k /b )M   + (p/2   hh
$$$$    hh           hh
$$$$ 
$$$$    x
$$$$   K   = K
$$$$    hh    hh
$$$$ 
$$$$    x
$$$$   B   = 0
$$$$    hh
$$$$
FA1      KHH,BHH,MHH,QHHL,CASECC,FLIST/FSAVE,KXHH,BXHH,MXHH/
         S,N,FLOOP/S,N,TSTART/S,N,NOCEAD $  
****SBST   4
****CARD   1-  6,  8- 14, 24, 26, 29, 32, 34- 40, 55- 62
****FILE 129
****RFMT 199-201,204-217
$$$$
$$$$ Set up equivalences for the KE and PK methods.
EQUIV    KXHH,PHIH/NOCEAD/BXHH,CLAMA/NOCEAD/KXHH,PHIHL/NOCEAD/BXHH, 
         CLAMAL/NOCEAD/CASECC,CASEYY/NOCEAD $   
****SBST   4
****CARD   1-  6,  8- 14, 24, 26, 29, 32, 34- 40, 55- 62
****FILE 117,130
****RFMT 199-201,204-217
$$$$
$$$$ Go to label VDR for the KE and PK methods.
COND     VDR,NOCEAD $   
****SBST   4
****CARD   1-  6,  8- 14, 18, 21, 24, 26, 29, 32, 34- 40, 55- 62
****FILE 117
****RFMT 199-201,204-217
$$$$
$$$$ CEAD extracts complex eigenvalues and eigenvectors from the equation
$$$$
$$$$     x  2    x      x
$$$$   [M  p  + B  p + K  ]{phi } = 0
$$$$     hh      hh     hh     h
$$$$
$$$$ and normalizes eigenvectors to unit magnitude of the largest component.
CEAD     KXHH,BXHH,MXHH,EED,CASECC/PHIH,CLAMA,OCEIGS,/S,N,EIGVS $   
****SBST   4
****CARD   1-  6,  8- 14, 24, 26, 29, 32, 34- 40, 55- 62
****FILE 117
****RFMT 199-201,204-217
$$$$
$$$$ Go to label LBLZAP if no complex eigenvalues were found.
COND     LBLZAP,EIGVS $ 
****SBST   4
****CARD   1-  6,  8- 14, 18, 21, 24, 26, 29, 32, 34- 40, 55- 62
****FILE 117
****RFMT 199-201,204-217
$$$$
LABEL    VDR $  
****SBST   4
****CARD   1-  6,  8- 14, 18, 21, 24, 26, 29, 32, 34- 40, 55- 62
****FILE 117
****RFMT 199-201,204-217
$$$$
$$$$ VDR prepares eigenvectors (OPHIH) for output, using only the extra points 
$$$$ introduced for dynamic analysis and modal coordinates.
VDR      CASECC,EQDYN ,USETD,PHIH,CLAMA,,/OPHIH,/*CEIGEN*/*MODAL*/  
         123/S,N,NOH/S,N,NOP/FMODE $
****SBST   4
****CARD  21
****FILE 119
****RFMT 199-201,204-217
$$$$
$$$$ Go to label LBL16 if there is no output request for the extra points 
$$$$ introduced for dynamic analysis or modal coordinates.
COND     LBL16,NOH $
****SBST   4
****CARD  21
****FILE 119
****RFMT 199-201,204-217
$$$$
$$$$ OFP formats the table of eigenvectors for extra points introduced for 
$$$$ dynamic analysis and modal coordinates prepared by VDR and places it on 
$$$$ the system output file for printing.
OFP      OPHIH,,,,,//S,N,CARDNO $   
****SBST   4
****CARD  21
****FILE 119
****RFMT 199-201,204-217
$$$$
LABEL    LBL16 $
****SBST   4
****CARD  18, 21
****FILE 119
****RFMT 199-201,204-217
$$$$
$$$$ FA2 appends eigenvectors to PHIHL, eigenvalues to CLAMAL, Case Control to 
$$$$ CASEYY, and V-g plot data to OVG.
FA2      PHIH,CLAMA,FSAVE/    PHIHL,CLAMAL,CASEYY,OVG/S,N,TSTART/   
         C,Y,VREF=1.0/C,Y,PRINT=YES $   
****SBST   3,  4
****CARD   1-  6,  8- 14, 18, 21, 24, 26, 29, 32, 34- 40, 55- 62
****FILE 130
****RFMT 199-201,204-217
$$$$
$$$$ Go to label CONTINUE if there is insufficient time for another flutter 
$$$$ loop.
COND     CONTINUE,TSTART $  
****SBST   3,  4
****CARD   1-  6,  8- 14, 18, 21, 24, 26, 29, 32, 34- 40, 55- 62
****RFMT 187-204,207-217
$$$$
LABEL    LBLZAP $   
****SBST   3,  4
****CARD   1-  6,  8- 14, 18, 21, 24, 26, 29, 32, 34- 40, 55- 62
****FILE 117
****RFMT 187-204,207-217
$$$$
$$$$ Go to label CONTINUE if the flutter loop is complete.
COND     CONTINUE,FLOOP $   
****SBST   3,  4
****CARD   1-  6,  8- 14, 18, 21, 24, 26, 29, 32, 34- 40, 55- 62
****RFMT 187-204,207-217
$$$$
$$$$ Go to label LOOPTOP for additional aerodynamic configuration triplet 
$$$$ values.
REPT     LOOPTOP,100 $  
****SBST   3,  4
****CARD   1-  6,  8- 14, 18, 21, 24, 26, 29, 32, 34- 40, 55- 62
****RFMT 187-204,207-217
$$$$
$$$$ Go to label ERROR3 and print Error Message No. 3 if the number of flutter 
$$$$ loops exceeds 100.
JUMP     ERROR3 $   
****SBST   3,  4
****CARD   1-  6,  8- 14, 18, 21, 24, 26, 29, 32, 34- 40, 55- 62
****RFMT 187-204,207-217
$$$$
LABEL    CONTINUE $ 
****SBST   3,  4
****CARD   1-  6,  8- 14, 18, 21, 24, 26, 29, 32, 34- 40, 55- 62
****RFMT 187-204,207-217
$$$$
PARAML   XYCDB//*PRES*////NOXYCDB $ 
****SBST   4
****CARD  20
****FILE 120
****RFMT 199-201,204-217
$$$$
$$$$ Go to label NOXYOUT if there are no X-Y plot requests.
COND     NOXYOUT,NOXYCDB $  
****SBST   4
****CARD  20
****FILE 120
****RFMT 199-201,204-217
$$$$
$$$$ XYTRAN prepares the input for requested X-Y plots.
XYTRAN   XYCDB,OVG,,,,/XYPLTCE/*VG*/*PSET*/S,N,PFILE/S,N,CARDNO/
         S,N,NOXYPL $   
****SBST   4
****CARD  20
****FILE 120
****RFMT 199-201,204-217
$$$$
$$$$ Go to label NOXYOUT if no plots are possible as requested.
COND     NOXYOUT,NOXYPL $   
****SBST   4
****CARD  20
****FILE 120
****RFMT 199-201,204-217
$$$$
$$$$ XYPLOT prepares the requested V-g plots.
XYPLOT   XYPLTCE// $
****SBST   4
****CARD  20
****FILE 120
****RFMT 199-201,204-217
$$$$
LABEL    NOXYOUT $  
****SBST   4
****CARD  20
****FILE 120
****RFMT 199-201,204-217
$$$$
PARAM    //*AND*/PJUMP/NOP=-1/JUMPPLOT $
****SBST   4
****CARD   1-  6,  8- 13, 20, 21, 24, 26, 29, 32, 34- 40, 55- 62
****FILE 121,122,131-134,136,137,144,145
****RFMT 199-201,204-217
$$$$
$$$$ Go to label FINIS and make normal exit if there are no output requests 
$$$$ involving dependent degrees of freedom or forces and stresses.
COND     FINIS,PJUMP $  
****SBST   4
****CARD   1-  6,  8- 13, 18- 21, 24- 26, 29, 32, 34- 40, 55- 62
****FILE 121,122,131-134,136,137,144,145
****RFMT 199-201,204-217
$$$$
$$$$ MODACC selects a list of eigenvalues and eigenvectors whose imaginary 
$$$$ parts (velocity in input units) are close to a user input list.
MODACC   CASEYY,CLAMAL,PHIHL,,,/CLAMAL1,CPHIH1,CASEZZ,,/*CEIGN* $   
****SBST   4
****CARD   1-  6,  8- 13, 21, 24, 26, 29, 32, 34- 40, 55- 62
****FILE 131
****RFMT 199-201,204-217
$$$$
$$$$ ADR builds a matrix of aerodynamic forces for each aerodynamic point and 
$$$$ prints requested aerodynamic forces for selected elements.
ADR      CPHIH1,CASEZZ,QKHL,CLAMAL1,SPLINE,SILA,USETA/PKF/BOV/  
         C,Y,MACH = 0.0/*FLUTTER* $ 
****SBST   4
****CARD  21, 25
****FILE 121
****RFMT 199-201,204-217
$$$$
$$$$ DDR1 transforms the complex eigenvectors from modal to physical 
$$$$ coordinates
$$$$ 
$$$$       c
$$$$   {phi } = {phi  }{phi }
$$$$       d        dh     h
$$$$
DDR1     CPHIH1,PHIDH/CPHID $   
****SBST   4
****CARD   1-  6,  8- 13, 24, 26, 29, 32, 34, 36- 40, 55- 62
****FILE 122
****RFMT 199-201,204-217
$$$$                 c         c
$$$$ Equivalence {phi } to {phi } if no constraints are applied.
$$$$                 d         p
$$$$
EQUIV    CPHID ,CPHIP/NOA $ 
****SBST   4
****CARD   1-  6,  8- 13, 24, 26, 29, 32, 34- 40, 55- 62
****FILE 144
****RFMT 199-201,204-217
$$$$
PURGE    QPC/NOA $  
****SBST   4
****CARD   1-  6,  8- 13, 24, 26, 29, 32, 34- 40, 55- 62
****FILE 144
****RFMT 199-201,204-217
$$$$
$$$$ Go to label LBL14 if no constraints are applied.
COND     LBL14,NOA $
****SBST   4
****CARD   1-  6,  8- 13, 24, 26, 29, 32, 34- 40, 55- 62
****FILE 144
****RFMT 199-201,204-217
$$$$
$$$$ SDR1 recovers dependent components of eigenvectors
$$$$ 
$$$$                                   phi
$$$$      c      d     c                  d        c      c
$$$$  {phi } = [G ]{phi }             {----} = {phi  + phi }
$$$$      o      o     d               phi         f      e
$$$$                                      o
$$$$       c      c
$$$$    phi  + phi
$$$$       f      e        c      c       c      d     c      c
$$$$   {-----------} = {phi  + phi }  {phi } = [G ]{phi  + phi } 
$$$$          c            n      e       m      m     n      e
$$$$       phi      
$$$$          s
$$$$ 
$$$$       c      c   
$$$$    phi  + phi    
$$$$       f      e        c
$$$$   {-----------} = {phi }
$$$$          c            p
$$$$       phi        
$$$$          m
$$$$                                                         T
$$$$ and recovers single-point forces of constraint {q } = [K  ] {phi }.
$$$$                                                  s      fs      f
$$$$   0       c
$$$$ {---} = {Q }.
$$$$   q       p
$$$$    s
$$$$
SDR1     USETD,,CPHID ,,,GOD,GMD,,KFS,,/CPHIP,,QPC/1 /*DYNAMICS* $  
****SBST   4
****CARD   1-  6,  8- 13, 24, 26, 29, 32, 34- 40, 55- 62
****FILE 144
****RFMT 199-201,204-217
$$$$
LABEL    LBL14 $
****SBST   4
****CARD   1-  6,  8- 13, 24, 26, 29, 32, 34- 40, 55- 62
****FILE 144
****RFMT 199-201,204-217
$$$$                 c         c
$$$$ Equivalence {phi } to {phi } if there are no extra points introduced for
$$$$                 d         a
$$$$ dynamic analysis.
EQUIV    CPHID ,CPHIA/NOUE $
****SBST   4
****CARD   1-  6,  8- 13, 24, 26, 29, 32, 34- 40, 55- 62
****FILE 122
****RFMT 199-201,204-217
$$$$
$$$$ Go to label LBLNOE if there are no extra points.
COND     LBLNOE,NOUE $  
****SBST   4
****CARD   1-  6,  8- 13, 24, 26, 29, 32, 34- 40, 55- 62
****FILE 132
****RFMT 199-201,204-217
$$$$
$$$$ VEC generates a d-size partitioning vector (RP) for the a and e sets
$$$$
$$$$   {u } -> {u } + {u }
$$$$     d       s      e
$$$$
VEC      USETA/RP/*D*/*A*/*E* $ 
****SBST   4
****CARD   1-  6,  8- 13, 24, 26, 29, 32, 34- 40, 55- 62
****FILE 132
****RFMT 199-201,204-217
$$$$                                 c
$$$$ PARTN performs partition of {phi } using RP
$$$$                                 d
$$$$                 c
$$$$              phi
$$$$       c         a
$$$$   {phi } => {----}
$$$$       d         c
$$$$              phi
$$$$                 e
$$$$
PARTN    CPHID ,,RP/CPHIA,,,/1/3 $  
****SBST   4
****CARD   1-  6,  8- 13, 24, 26, 29, 32, 34- 40, 55- 62
****FILE 122,132
****RFMT 199-201,204-217
$$$$
LABEL    LBLNOE $   
****SBST   4
****CARD   1-  6,  8- 13, 24, 26, 29, 32, 34- 40, 55- 62
****FILE 132
****RFMT 199-201,204-217
$$$$
$$$$ MPYAD recovers the displacements at the aerodynamic points (k)
$$$$ 
$$$$       c      T  T    c
$$$$   {phi } = [G  ] {phi }
$$$$       k      ka      a
$$$$
MPYAD    GTKA,CPHIA,/CPHIK/1/1/0/PREC $ 
****SBST   4
****CARD   1-  6,  8- 13, 24, 26, 29, 32, 34- 40, 55- 62
****FILE 133
****RFMT 199-201,204-217
$$$$                              c
$$$$ UMERGE is used to expand {phi } to the ps set.
$$$$                              p
$$$$
UMERGE   USETA,CPHIP,/CPHIPS/*PS*/*P*/*SA* $
****SBST   4
****CARD   1-  6,  8- 13, 24, 26, 29, 32, 34- 40, 55- 62
****FILE 134
****RFMT 199-201,204-217
$$$$                   c
$$$$ UMERGE places {phi } in its proper place in the displacement vector
$$$$                   k
$$$$
$$$$                  c
$$$$               phi
$$$$       c          ps
$$$$   {phi  } <= {-----}
$$$$       pa         c
$$$$               phi
$$$$                  k
$$$$
UMERGE   USETA,CPHIPS,CPHIK/CPHIPA/*PA*/*PS*/*K* $  
****SBST   4
****CARD   1-  6,  8- 13, 24, 26, 29, 32, 34- 40, 55- 62
****FILE 136
****RFMT 199-201,204-217
$$$$                            c
$$$$ UMERGE is used to expand {Q } to the pa set.
$$$$                            p
$$$$
UMERGE   USETA,QPC,/QPAC/*PA*/*P*/*K* $ 
****SBST   4
****CARD   1-  6,  8- 13, 24, 26, 29, 32, 34- 40, 55- 62
****FILE 136
****RFMT 199-201,204-217
$$$$
$$$$ SDR2 calculates element forces (OEFC1) and stresses (OESC1) and prepares 
$$$$ eigenvectors (OCPHIPA) and single-point forces of constraint (OQPAC1) for 
$$$$ output and PCPHIPA for deformed plotting.
SDR2     CASEZZ,CSTMA,MPT,DIT,EQAERO,SILA,,,BGPA,CLAMAL1,QPAC,CPHIPA,   
         EST,,,/,OQPAC1,OCPHIPA,OESC1,OEFC1,PCPHIPA,,/*CEIGN* $ 
****SBST   4
****CARD   4, 18, 19, 24
****FILE 137
$$$$
$$$$ OFP formats the tables prepared by SDR2 and places them on the system 
$$$$ output file for printing.
OFP      OCPHIPA,OQPAC1,OESC1,OEFC1,,//S,N,CARDNO $ 
****SBST   4
****CARD  19
****FILE 137
$$$$
$$$$ Go to label FINIS and make normal exit if no deformed aerodynamic or 
$$$$ structural element plots are requested.
COND     FINIS,JUMPPLOT $   
****SBST   4,  7
****CARD  18
****FILE 145
$$$$
$$$$ PLOT prepares all deformed aerodynamic and structural element plots.
PLOT     PLTPARA,GPSETSA,ELSETSA,CASEZZ,BGPA,EQAERO,SILGA,,PCPHIPA,,,,  
         /PLOTX3/NSIL1/LUSETA/JUMPPLOT/PLTFLG/S,N,  
         PFILE $
****SBST   4,  7
****CARD  18
****FILE 145
$$$$
$$$$ PRTMSG prints plotter data and engineering data for each deformed plot 
$$$$ generated.
PRTMSG   PLOTX3// $ 
****SBST   4,  7
****CARD  18
****FILE 145
$$$$
$$$$ Go to label FINIS and make normal exit.
JUMP     FINIS $
****CARD   1-  6,  8- 26, 29, 32, 34- 40, 55- 62
****FILE 145
****RFMT 187-204,207-217
$$$$
LABEL    ERROR3 $   
****SBST   4
****CARD   1-  6,  8- 14, 18, 21, 24, 26, 29, 32, 34- 40, 55- 62
****RFMT 187-204,207-217
$$$$
$$$$ Print Error Message No. 3 and terminate execution.
PRTPARM  //-3/*FLUTTER* $   
****SBST   4
****CARD   1-  6,  8- 14, 18, 21, 24, 26, 29, 32, 34- 40, 55- 62
****RFMT 187-204,207-217
$$$$
LABEL    ERROR2 $   
****CARD   1,  9- 12, 40, 56, 58, 60
****FILE 111
****RFMT 187-204,207-217
$$$$
$$$$ Print Error Message No. 2 and terminate execution.
PRTPARM  //-2/*FLUTTER* $   
****CARD   1,  9- 12, 40, 56, 58, 60
****FILE 111
****RFMT 187-204,207-217
$$$$
LABEL    ERROR1 $   
****CARD   1-  3,  5,  6,  8, 13, 14, 24
****FILE 147
****RFMT 187-204,207-217
$$$$
$$$$ Print Error Message No. 1 and terminate execution.
PRTPARM  //-1/*FLUTTER* $   
****CARD   1-  3,  5,  6,  8, 13, 14, 24
****FILE 147
****RFMT 187-204,207-217
$$$$
LABEL    ERROR4 $   
****SBST   4
****CARD   1-  6,  8- 14, 24, 58, 59
****FILE 112
****RFMT 187-204,207-217
$$$$
$$$$ Print Error Message No. 4 and terminate execution.
PRTPARM  //-4/*FLUTTER* $   
****SBST   4
****CARD   1-  6,  8- 14, 24, 58, 59
****FILE 112
****RFMT 187-204,207-217
$$$$
LABEL    ERROR5 $   
****CARD   1, 24
****FILE  94
****RFMT 187-204,207-217
$$$$
$$$$ Print Error Message No. 5 and terminate execution.
PRTPARM  //-5/*FLUTTER* $   
****CARD   1, 24
****FILE  94
****RFMT 187-204,207-217
$$$$
LABEL    FINIS $
****CARD   1-  6,  8- 26, 29, 32, 34- 40, 55- 62
****FILE 121,122,131-134,136,137,144,145
****RFMT 187-204,207-217
$$$$
PURGE    DUMMY/MINUS1 $ 
****CARD   1-  6,  8- 26, 29, 32, 34- 40, 55- 62
****FILE 121,122,131-134,136,137,144,145
****RFMT 187-204,207-217
$$$$
END      $  
****CARD   1-  6,  8- 26, 29, 32, 34- 40, 55- 62
****RFMT 187-204,207-217
$$$$
$*CARD BITS 
  1    AXIC     AXIF     AXSLOT 
  1    GRDSET   GRID     GRIDB  
  1    POINTAX  RINGAX   RINGFL   SECTAX
  1    SEQGP    SPOINT  
  1    CELAS1   CELAS2   CELAS3   CELAS4
  1    CMASS1   CMASS2   CMASS3   CMASS4
  2    ADUM1    ADUM2    ADUM3    ADUM4    ADUM5    ADUM6    ADUM7  
  2    ADUM8    ADUM9    BAROR  
  2    CAXIF2   CAXIF3   CAXIF4   CBAR     CBARAO   CCONEAX 
  2    CDUM1
  2    CDUM2    CDUM3    CDUM4    CDUM5    CDUM6    CDUM7    CDUM8  
  2    CDUM9
  2    CELBOW   CFLUID2  CFLUID3  CFLUID4  CHEXA1   CHEXA2  
  2    CIHEX1   CIHEX2   CIHEX3   CIS2D8
  2    CNGRNT   CONROD   CQUAD4   CTRIA3
  2    CQDMEM   CQDMEM1  CQDMEM2  CQDPLT   CQUAD1   CQUAD2  
  2    CROD     CSHEAR   CSLOT3   CSLOT4   CTETRA   CTRBSC   CTRAPAX
  2    CTRIAAX  CTRIARG  CTORDRG  CTRAPRG  CTRIA1   CTRIA2  
  2    CTRIM6   CTRMEM   CTRPLT   CTUBE    CTWIST   CWEDGE  
  3    PBAR     PCONEAX  PDUM1    PDUM2    PDUM3    PDUM4   
  3    PDUM5    PSHELL   PCOMP    PCOMP1   PCOMP2
  3    PDUM6    PDUM7    PDUM8    PDUM9    PELBOW   PHEX     PIS2D8 
  3    PQDMEM   PQDMEM1  PQDMEM2  PQDPLT   PQUAD1   PQUAD2  
  3    PROD     PSHEAR   PTORDRG  PTRAPAX  PTRBSC   PTRIA1  
  3    PTRIA2   PTRIM6   PTRIAAX  PTRMEM   PTRPLT   PTUBE    PTWIST 
  4    GENEL
  5    CONM1    CONM2   
  6    PELAS    PMASS   
  8    MAT1     MAT2     MAT3     MAT9     MATT1    MATT2    MATT3  
  8    MAT8     TABLEM1  TABLEM2  TABLEM3  TABLEM4   
  8    TEMPMT$  TEMPMX$ 
  9    CRIGD1   CRIGD2   CRIGD3   CRIGDR
  9    CRROD    CRBAR    CRTRPLT  CRBE1    CRBE2    CRBE3    CRSPLINE   
  9    MPC      MPCADD   MPC$     MPCAX 
 10    SPC      SPC1     SPCADD   SPCAX    SPC$ 
 11    ASET     ASET1   
 11    OMIT     OMIT1    OMITAX 
 11    SUPAX    SUPORT  
 13    TEMP     TEMPAX   TEMPD  
 13    TEMPP1   TEMPP2   TEMPP3   TEMPRB
 14    COUPMASS CPBAR    CPDPLT 
 14    CPQUAD1  CPQUAD2  CPROD    CPTRIA1  CPTRIA2  CPTRPLT  CPTUBE 
 14    WTMASS   
 15    GRDPNT   
 16    PLOTEL   
 17    P1       P2       P3 
 18    PLOT$
 19    POUT$
 20    XYOUT$   
 21    AOUT$
 22    ASETOUT  
 23    AUTOSPC  
 24    CORD1C   CORD1R   CORD1S   CORD2C   CORD2R   CORD2S  
 25    MACH 
 26    NODJE
 29    PAERO1   PAERO2   PAERO3   PAERO4   PAERO5   
 32    SET1     SET2
 32    SPLINE1  SPLINE2  SPLINE3
 34    MKAERO1  MKAERO2 
 35    AEFACT   
 36    FLFACT   FLUTTER 
 37    AERO 
 37    CAERO1   CAERO2   CAERO3   CAERO4   CAERO5   
 38    FMETHOD$ 
 39    PRINT    VREF  
 40    TF   
 54    GUSTAERO 
 55    SDAMP$   
 55    TABDMP1  
 56    EPOINT   SEQEP   
 57    K2PP$    M2PP$    B2PP$    TF$   
 57    DMIG 
 58    EIGR 
 59    METHOD$  
 60    EIGC     EIGP
 61    CMETHOD$ 
 62    HFREQ    LFREQ    LMODES   KDAMP 
$$$$
$*FILE BITS 
 94    GPL      EQEXIN   GPDT     CSTM     BGPDT    SIL 
 95    ECT  
 96    GPTT 
 97    EST      GEI      GPECT    MPTX     PCOMPS   EPTX
 98    KGGX 
 99    MGG  
100    KGG  
101    RG       USET     ASET     OGPST 
102    GPST 
103    GM   
104    KNN      MNN 
105    KFF      KFS      MFF
107    KLL      KLR      KRR      MLL      MLR      MRR 
108    LLL  
109    DM   
110    MR   
111    GPLD     SILD     USETD    TFPOOL   EED      EQDYN   
112    LAMA     PHIA     MI       OEIGS 
114    K2PP     M2PP     B2PP   
115    GMD      GOD 
116    MHH      BHH      KHH      PHIDH 
117    PHIH     CLAMA    OCEIGS 
118    PLOTX2   
119    OPHIH
120    XYPLTCE  
121    PKF  
122    CPHID
123    MAA  
124    EQAERO   ECTA     BGPA     SILA     USETA    SPLINE   AERO   
124    ACPT     FLIST    CSTMA    GPLA     SILGA
125    PLTSETA  PLTPARA  GPSETSA  ELSETSA   
126    GTKA 
127    AJJL     D1JK     D2JK     SKJ   
128    D1JE     D2JE
129    FSAVE    KXHH     BXHH     MXHH  
130    PHIHL    CLAMAL   CASEYY   OVG   
131    CLAMAL1  CPHIH1   CASEZZ 
132    RP   
133    CPHIK
134    CPHIPS   
136    CPHIPA   
137    OQPAC1   OCPHIPA  OESC1    OEFC1    PCPHIPA  
138    QHHL     QKHL     QHJL   
139    K2DD     M2DD     B2DD   
140    OGPWG
142    KOO      LOO      KAA
144    CPHIP    QPC 
145    PLOTX3   
147    KELM     KDICT    MELM     MDICT 
$*  
=PAGE=
AERO11

APR.93
$$$$$$$$
BEGIN    AERO 11 - MODAL AEROELASTIC RESPONSE - APR. 1993 $ 
****CARD   1-  6,  8- 26, 29, 32, 34- 40, 49- 62
****RFMT 187-204,207-217
$$$$
PRECHK   ALL $  
****CARD   1-  6,  8- 26, 29, 32, 34- 40, 49- 62
****RFMT 187-204,207-217
$$$$
FILE     AJJL=APPEND/QHHL=APPEND/QKHL=APPEND/QHJL=APPEND/SKJ=APPEND $   
****SBST   4
****CARD   1-  6,  8- 26, 29, 32, 34- 40, 49- 62
****FILE 127,138
****RFMT 187-204,207-217
$$$$
PARAM    //*MPY*/CARDNO/0/0 $   
****CARD   1-  6,  8- 14, 19- 21, 24, 25, 58, 59
****FILE 101,112,135,143,154
****RFMT 204-217
$$$$
$$$$ GP1 generates coordinate system transformation matrices, tables of grid 
$$$$ point locations, and tables relating the internal and external grid point 
$$$$ numbers.
GP1      GEOM1,GEOM2,/GPL,EQEXIN,GPDT,CSTM,BGPDT,SIL/S,N,LUSET/ 
         S,N,NOGPDT/MINUS1=-1 $ 
****CARD   1, 24
****FILE  94
****RFMT 204-217
$$$$
$$$$ Go to label ERROR1 and print Error Message No. 1 if no grid points are 
$$$$ defined.
COND     ERROR1,NOGPDT $
****CARD   1, 24
****FILE  94
****RFMT 187-204,207-217
$$$$
$$$$ GP2 generates Element Connection Table with internal indices.
GP2      GEOM2,EQEXIN/ECT $ 
****CARD   1,  2,  4,  5, 16
****FILE  95
****RFMT 204-217
$$$$
PARAML   PCDB//*PRES*/V,Y,NODJE=-1///JUMPPLOT $ 
****SBST   7
****CARD  18, 26
****FILE 122,125
****RFMT 204-217
$$$$
PARAML   XYCDB//*PRES*////NOXYCDB $ 
****SBST   4
****CARD  20, 22
****RFMT 204-217
$$$$
$$$$ GP3 generates Grid Point Temperature Table (element temperature).
GP3      GEOM3,EQEXIN,GEOM2/,GPTT/NOGRAV $  
****SBST   5
****CARD   1,  2, 13
****FILE  96
****RFMT 204-217
$$$$
$$$$ TA1 generates element tables for use in matrix assembly and stress 
$$$$ recovery.
TA1      ECT,EPT,BGPDT,SIL,GPTT,CSTM,MPT,EQEXIN/EST,GEI,GPECT,,,MPTX,
         PCOMPS,EPTX/LUSET/S,N,NOSIMP/1/S,N,NOGENL/GENEL/S,N,COMPS $   
****SBST   5
****CARD   1-  6, 13, 16, 24
****FILE  97
****RFMT 204-217
$$$$
EQUIV    MPTX,MPT/COMPS/EPTX,EPT/COMPS $
****SBST   5
****CARD   1-  6,  8- 26, 29, 32, 34- 40, 49- 62
****FILE  97
****RFMT 204-217
$$$$
$$$$ Go to label ERROR3 and print Error Message No. 3 if no structural 
$$$$ elements have been defined.
COND     ERROR3,NOSIMP $
****SBST   5
****CARD   1,  2,  4-  6, 16, 24
****FILE  97
****RFMT 187-204,207-217
$$$$
PARAM    //*ADD*/NOKGGX/1/0 $   
****SBST   4
****CARD   1-  3,  6,  8, 13, 14, 24
****FILE 113
****RFMT 204-217
$$$$
PARAM    //*ADD*/NOMGG /1/0 $   
****SBST   4
****CARD   1-  3,  5,  8, 13, 14, 24
****FILE 113
****RFMT 204-217
$$$$
$$$$ EMG generates structural element stiffness and mass matrix tables and 
$$$$ dictionaries for later assembly by the EMA module.
EMG      EST,CSTM,MPT,DIT,GEOM2,/KELM,KDICT,MELM,MDICT,,,/S,N,NOKGGX/   
         S,N,NOMGG////C,Y,COUPMASS/C,Y,CPBAR/C,Y,CPROD/ 
         C,Y,CPQUAD1/C,Y,CPQUAD2/C,Y,CPTRIA1/C,Y,CPTRIA2/C,Y,CPTUBE/
         C,Y,CPQDPLT/C,Y,CPTRPLT/C,Y,CPTRBSC/   
         C,Y,VOLUME/C,Y,SURFACE $   
****SBST   4
****CARD   1-  3,  5,  6,  8, 13, 14, 24
****FILE 113
****RFMT 204-217
$$$$
PURGE    KGGX/NOKGGX $  
****SBST   4
****CARD   1-  3,  6,  8, 13, 24
****FILE  98
****RFMT 204-217
$$$$
$$$$ Go to label JMPKGGX if no stiffness matrix is to be assembled.
COND     JMPKGGX,NOKGGX $   
****SBST   4
****CARD   1-  3,  6,  8, 13, 24
****FILE  98
****RFMT 204-217
$$$$                                  x
$$$$ EMA assembles stiffness matrix [K  ] and Grid Point Singularity Table.
$$$$                                  gg
$$$$
EMA      GPECT,KDICT,KELM/KGGX $
****SBST   4
****CARD   1-  3,  6,  8, 13, 24
****FILE  98
****RFMT 204-217
$$$$
PURGE    KDICT,KELM/MINUS1 $
****SBST   4
****CARD   1-  3,  6,  8, 13, 24
****FILE 113
****RFMT 204-217
$$$$
LABEL    JMPKGGX $  
****SBST   4
****CARD   1-  3,  6,  8, 13, 24
****FILE  98
****RFMT 204-217
$$$$
$$$$ Go to label ERROR1 and print Error Message No. 1 if no mass matrix is to 
$$$$ be assembled.
COND     ERROR1,NOMGG $ 
****SBST   4
****CARD   1-  3,  5,  8, 13, 14, 24
****FILE 113
****RFMT 204-217
$$$$
$$$$ EMA assembles mass matrix [M  ].
$$$$                             gg
$$$$
EMA      GPECT,MDICT,MELM/MGG/-1/C,Y,WTMASS=1.0 $   
****SBST   4
****CARD   1-  3,  5,  8, 13, 14, 24
****FILE  99
****RFMT 204-217
$$$$
PURGE    MDICT,MELM/MINUS1 $
****SBST   4
****CARD   1-  3,  5,  8, 13, 14, 24
****FILE 113
****RFMT 204-217
$$$$
$$$$ Go to label LGPWG if no weight and balance information is requested.
COND     LGPWG,GRDPNT $ 
****SBST   4,  8
****CARD   1-  3,  5,  8, 13- 15, 24
****FILE 154
****RFMT 204-217
$$$$
$$$$ GPWG generates weight and balance information.
GPWG     BGPDT,CSTM,EQEXIN,MGG/OGPWG/V,Y,GRDPNT=-1/C,Y,WTMASS $ 
****SBST   4,  8
****CARD   1-  3,  5,  8, 13- 15, 24
****FILE 154
****RFMT 204-217
$$$$
$$$$ OFP formats the weight and balance information prepared by GPWG and 
$$$$ places it on the system output file for printing.
OFP      OGPWG,,,,,//S,N,CARDNO $   
****SBST   4,  8
****CARD   1-  3,  5,  8, 13- 15, 24
****FILE 154
****RFMT 204-217
$$$$
LABEL    LGPWG $
****SBST   4,  8
****CARD   1-  3,  5,  8, 13- 15, 24
****FILE 154
****RFMT 204-217
$$$$               x
$$$$ Equivalence [K  ] to [K  ] if there are no general elements.
$$$$               gg       gg
$$$$
EQUIV    KGGX,KGG/NOGENL $  
****SBST   4
****CARD   1-  4,  6,  8, 13, 24
****FILE 100
****RFMT 204-217
$$$$
$$$$ Go to label LBL11 if there are no general elements.
COND     LBL11,NOGENL $ 
****SBST   4
****CARD   1-  4,  6,  8, 13, 24
****FILE 100
****RFMT 204-217
$$$$                                                   y
$$$$ SMA3 forms the general element stiffness matrix [K  ].
$$$$                                                   gg
$$$$
SMA3     GEI,KGGX/KGG/LUSET/NOGENL/NOSIMP $ 
****SBST   4
****CARD   1-  4,  6,  8, 13, 24
****FILE 100
****RFMT 204-217
$$$$
LABEL    LBL11 $
****SBST   4
****CARD   1-  4,  6,  8, 13, 24
****FILE 100
****RFMT 204-217
$$$$
GPSTGEN  KGG,SIL/GPST $ 
****SBST   4
****CARD   1-  4,  6,  8, 13, 24
****FILE 102
****RFMT 204-217
$$$$
$$$$ GP4 generates flags defining members of various displacement sets
$$$$ (USET), and forms multipoint constraint equations [R ] {u } = 0.
$$$$                                                     g    g
$$$$
GP4      CASECC,GEOM4,EQEXIN,GPDT,BGPDT,CSTM,GPST/RG,,USET, 
         ASET,OGPST/LUSET/S,N,MPCF1/S,N,MPCF2/S,N,SINGLE/S,N,OMIT/  
         S,N,REACT/0/S,N,REPEAT/S,N,NOSET/S,N,NOL/S,N,NOA/  
         C,Y,ASETOUT/C,Y,AUTOSPC $  
****CARD   1-  4,  6,  8- 13, 23- 25
****FILE 101
****RFMT 204-217
$$$$
$$$$ OFP formats the table of potential grid point similarities prepared by 
$$$$ GPSP and places it on the system output file for printing.
OFP      OGPST,,,,,//S,N,CARDNO $   
****SBST   4
****CARD   1-  4,  6,  8- 13, 23- 25
****FILE 101
****RFMT 204-217
$$$$
PURGE    GM/MPCF1/DM,MR/REACT $ 
****SBST   4
****CARD   1-  6,  8- 14, 24
****FILE 103,109,110
****RFMT 204-217
$$$$
$$$$ Equivalence [K  ] to [K  ] and [M  ] to [M  ] if no multipoint 
$$$$               gg       nn        gg       nn
$$$$ constraints exist.
EQUIV    KGG,KNN/MPCF1/MGG,MNN/MPCF1 $  
****SBST   4
****CARD   1-  6,  8,  9, 13, 14, 24
****FILE 104
****RFMT 204-217
$$$$
$$$$ Go to label LBL2 if no multipoint constraints exist.
COND     LBL2,MPCF1 $   
****SBST   4
****CARD   1-  6,  8,  9, 13, 14, 24
****FILE 103,104
****RFMT 204-217
$$$$
$$$$ MCE1 partitions multipoint constraint equation [R ] = [R |R ] and solves
$$$$                                                  g      m  n
$$$$                                                             -1
$$$$ for multipoint constraint transformation matrix [G ] = -[R ]  [R ].
$$$$                                                   m       m     n
$$$$
MCE1     USET,RG/GM $   
****SBST   4
****CARD   1,  9, 24
****FILE 103
****RFMT 204-217
$$$$
$$$$ MCE2 partitions stiffness and mass matrices
$$$$ 
$$$$           +_      +                        +_      +
$$$$           |K  |K  |                        |M  |M  |
$$$$           | nn| nm|                        | nn| nm|
$$$$   [K  ] = |---+---|                [M  ] = |---+---|
$$$$     gg    |K  |K  |                  gg    |M  |M  |
$$$$           | mn| mm|                        | mn| mm|
$$$$           +       +                        +       +
$$$$ 
$$$$ and performs matrix reductions
$$$$ 
$$$$            _        T           T           T
$$$$   [K  ] = [K  ] + [G ][K  ] + [K  ][G ] + [G ][K  ][G ]
$$$$     nn      nn      m   mn      mn   m      m   mm   m
$$$$
$$$$            _        T           T           T
$$$$   [M  ] = [M  ] + [G ][M  ] + [M  ][G ] + [G ][M  ][G ]
$$$$     nn      nn      m   mn      mn   m      m   mm   m
$$$$
MCE2     USET,GM,KGG,MGG,,/KNN,MNN,, $  
****SBST   4
****CARD   1-  6,  8,  9, 13, 14, 24
****FILE 104
****RFMT 204-217
$$$$
LABEL    LBL2 $ 
****SBST   4
****CARD   1-  6,  8,  9, 13, 14, 24
****FILE 103,104
****RFMT 204-217
$$$$
$$$$ Equivalence [K  ] to [K  ] and [M  ] to [M  ] if no single-point
$$$$               nn       ff        nn       ff
$$$$ constraints exist.
EQUIV    KNN,KFF/SINGLE/MNN,MFF/SINGLE $
****SBST   4
****CARD   1-  6,  8- 10, 13, 14, 24
****FILE 105
****RFMT 204-217
$$$$
$$$$ Go to label LBL3 if no single-point constraints exist.
COND     LBL3,SINGLE $  
****SBST   4
****CARD   1-  6,  8- 10, 13, 14, 24
****FILE 105
****RFMT 204-217
$$$$
$$$$ SCE1 partitions out single-point constraints
$$$$ 
$$$$           +       +                        +       +
$$$$           |K  |K  |                        |M  |M  |
$$$$           | ff| fs|                        | ff| fs|
$$$$   [K  ] = |---+---|                [M  ] = |---+---|
$$$$     nn    |K  |K  |                  nn    |M  |M  |
$$$$           | sf| ss|                        | sf| ss|
$$$$           +       +                        +       +
$$$$
SCE1     USET,KNN,MNN,,/KFF,KFS,,MFF,, $
****SBST   4
****CARD   1-  6,  8- 10, 13, 14, 24
****FILE 105
****RFMT 204-217
$$$$
LABEL    LBL3 $ 
****SBST   4
****CARD   1-  6,  8- 10, 13, 14, 24
****FILE 105
****RFMT 204-217
$$$$
$$$$ Equivalence [K  ] to [K  ] and [M  ] to [M  ] if no omitted coordinates
$$$$               ff       aa        ff       aa
$$$$ exist.
EQUIV    KFF,KAA/OMIT/ MFF,MAA/OMIT $   
****SBST   4
****CARD   1-  6,  8- 11, 13, 14, 24
****FILE 106,123
****RFMT 204-217
$$$$
PURGE    GO/OMIT $  
****SBST   4
****CARD   1-  4,  6,  8- 11, 13, 24
****FILE 106
****RFMT 204-217
$$$$
$$$$ Go to label LBL5 if no omitted coordinates exist.
COND     LBL5,OMIT $
****SBST   4
****CARD   1-  6,  8- 11, 13, 14, 24
****FILE 106,113,123
****RFMT 204-217
$$$$
PARAM    //*PREC*/PREC $
****SBST   4
****FILE 106,140
****RFMT 204-217
$$$$
$$$$ SMP1 partitions constrained stiffness matrix
$$$$
$$$$           +_      +
$$$$           |K  |K  |
$$$$           | aa| ao|
$$$$   [K  ] = |---+---|
$$$$     ff    |K  |K  |
$$$$           | oa| oo|
$$$$           +       +
$$$$           
$$$$                                               -1
$$$$ solves for transformation matrix [G ] = -[K  ]  [K  ]
$$$$                                    o       oo     oa
$$$$ 
$$$$                                        _        T
$$$$ and performs matrix reduction [K  ] = [K  ] + [K  ][G ].
$$$$                                 aa      aa      oa   o
$$$$
SMP1     USET,KFF,,,/GO,KAA,KOO,LOO,,,,, $  
****SBST   4
****CARD   1-  6,  8- 11, 13, 14, 24
****FILE 106
****RFMT 204-217
$$$$
$$$$ SMP2 partitions constrained mass matrix
$$$$
$$$$           +       +
$$$$           |_      |
$$$$           |M  |M  |
$$$$           | aa| ao|
$$$$   [M  ] = |---+---|
$$$$     ff    |   |   |
$$$$           |M  |M  |
$$$$           | oa| oo|
$$$$           +       +
$$$$ 
$$$$ and performs matrix reduction
$$$$
$$$$            _        T           T              T
$$$$   [M  ] = [M  ] + [M  ][G ] + [G ][M  ][G ]+ [G ][M  ]
$$$$     aa      aa      oa   o      o   oo   o     o   oa
$$$$
SMP2     USET,GO,MFF/MAA $  
****SBST   4
****CARD   1-  6,  8- 11, 13, 14, 24
****FILE 123
****RFMT 204-217
$$$$
LABEL    LBL5 $ 
****SBST   4
****CARD   1-  6,  8- 11, 13, 14, 24
****FILE 106,113,123
****RFMT 204-217
$$$$
$$$$ Go to label LBL6 if no free-body supports exist.
COND     LBL6,REACT $   
****SBST   4
****CARD   1-  6,  8- 14, 24
****FILE 107-110
****RFMT 204-217
$$$$
$$$$ RBMG1 partitions out free-body supports
$$$$ 
$$$$           +       +                        +       +
$$$$           |K  |K  |                        |M  |M  |
$$$$           | ll| lr|                        | ll| lr|
$$$$   [K  ] = |---+---|                [M  ] = |---+---|
$$$$     aa    |K  |K  |                  aa    |M  |M  |
$$$$           | rl| rr|                        | rl| rr|
$$$$           +       +                        +       +
$$$$
RBMG1    USET,KAA,MAA/KLL,KLR,KRR,MLL,MLR,MRR $ 
****SBST   4
****CARD   1-  6,  8- 14, 24
****FILE 107
****RFMT 204-217
$$$$
$$$$ RBMG2 decomposes constrained stiffness matrix [K  ] = [L  ][U  ].
$$$$                                                 ll      ll   ll
$$$$
RBMG2    KLL/LLL/ $ 
****SBST   4
****CARD   1-  4,  6,  8- 13, 24
****FILE 108
****RFMT 204-217
$$$$
$$$$ RBMG3 forms rigid body transformation matrix
$$$$ 
$$$$               -1
$$$$   [D] = -[K  ]  [K  ]
$$$$            ll     lr
$$$$ 
$$$$ calculates rigid body check matrix
$$$$
$$$$                   T
$$$$   [X] = [K  ] + [K  ][D]
$$$$           rr      lr
$$$$ 
$$$$ and calculates rigid body error ratio
$$$$ 
$$$$ 
$$$$               ||X||
$$$$   epsilon = ---------
$$$$              ||K  ||
$$$$                 rr
$$$$
RBMG3    LLL,KLR,KRR/DM $   
****SBST   4
****CARD   1-  4,  6,  8- 13, 24
****FILE 109
****RFMT 204-217
$$$$
$$$$ RBMG4 forms rigid body mass matrix
$$$$ 
$$$$                    T          T           T
$$$$   [m ] = [M  ] + [M  ][D] + [D ][M  ] + [D ][M  ][D]
$$$$     r      rr      lr             lr          ll
$$$$
RBMG4    DM,MLL,MLR,MRR/MR $
****SBST   4
****CARD   1-  6,  8- 14, 24
****FILE 110
****RFMT 204-217
$$$$
LABEL    LBL6 $ 
****SBST   4
****CARD   1-  6,  8- 14, 24
****FILE 107-110
****RFMT 204-217
$$$$
$$$$ DPD generates flags defining members of various displacement sets used in 
$$$$ dynamic analysis (USETD), tables relating the internal and external grid 
$$$$ point numbers (GPLD), including extra points introduced for dynamic 
$$$$ analysis (SILD), and prepares Transfer Function Pool (TFPOOL) and 
$$$$ Eigenvalue Extraction Data (EED).
DPD      DYNAMICS,GPL,SIL,USET/GPLD,SILD,USETD,TFPOOL,DLT,PSDL,FRL,,TRL,
         EED,EQDYN/LUSET/S,N,LUSETD/NOTFL/NODLT/S,N,NOPSDL/ 
         NOFRL/NONLFT/NOTRL/S,N,NOEED/123/S,N,NOUE $
****CARD   1,  9- 12, 40, 50, 52, 53, 56, 58
****FILE 111
****RFMT 204-217
$$$$
$$$$ Go to label ERROR2 and print Error Message No. 2 if there is no 
$$$$ Eigenvalue Extraction Data.
COND     ERROR2,NOEED $ 
****SBST   4
****CARD   1,  9- 12, 40, 50, 52, 53, 56, 58
****FILE 111
****RFMT 187-204,207-217
$$$$                       d                d
$$$$ Equivalence [G ] to [G ] and [G ] to [G ] if there are no extra points
$$$$               o       o        m       m
$$$$ introduced for dynamic analysis.
EQUIV    GO,GOD/NOUE/GM,GMD/NOUE $  
****SBST   4
****CARD   1-  4,  6,  9- 11, 13, 14, 24, 56
****FILE 115
****RFMT 204-217
$$$$
$$$$ READ extracts real eigenvalues and eigenvectors from the equation
$$$$ 
$$$$   [K   - lambda M  ]{phi } = 0
$$$$     aa           aa     a
$$$$ 
$$$$ calculates rigid body modes by finding a matrix [phi  ] such that
$$$$                                                     ro
$$$$              T
$$$$   [m ] = [phi  ][m ][phi  ]
$$$$     o        ro   r     ro
$$$$ 
$$$$ is diagonal and normalized, computes rigid body eigenvectors
$$$$ 
$$$$             +       +
$$$$             |Dphi   |
$$$$             |    ro |
$$$$   [phi  ] = |-------|
$$$$       ao    |phi    |
$$$$             |   ro  |
$$$$             +       +
$$$$ 
$$$$ calculates modal mass matrix
$$$$ 
$$$$             T
$$$$   [m] = [phi ][M  ][phi ]
$$$$             a   aa     a
$$$$ 
$$$$ and normalizes eigenvectors according to one of the following user 
$$$$ requests:
$$$$   1. Unit value of a selected component.
$$$$   2. Unit value of the largest component.
$$$$   3. Unit value of the generalized mass.
READ     KAA,MAA,MR,DM,EED,USET,CASECC/LAMA,PHIA,MI,OEIGS/*MODES*/S,N,  
         NEIGV $
****SBST   4
****CARD   1-  6,  8- 14, 24, 58, 59
****FILE 112
****RFMT 204-217
$$$$
$$$$ OFP formats the summary of eigenvalue extraction information (OEIGS) 
$$$$ prepared by READ and places it on the system output file for printing.
OFP      OEIGS,,,,,//S,N,CARDNO $   
****SBST   4
****CARD   1-  6,  8- 14, 24, 58, 59
****FILE 112
****RFMT 204-217
$$$$
$$$$ Go to label ERROR4 and print Error Message No. 4 if no eigenvalues were 
$$$$ found.
COND     ERROR4,NEIGV $ 
****SBST   4
****CARD   1-  6,  8- 14, 24, 58, 59
****FILE 112
****RFMT 187-204,207-217
$$$$
$$$$ OFP formats the eigenvalues (LAMA) prepared by READ and places them on 
$$$$ the system output file for printing.
OFP      LAMA,,,,,//S,N,CARDNO $
****SBST   4
****CARD   1-  6,  8- 14, 24, 58, 59
****FILE 112
****RFMT 204-217
$$$$                                            2      2          2
$$$$ MTRXIN selects the direct input matrices [K  ], [M  ], and [B  ].
$$$$                                            pp     pp         pp
$$$$
MTRXIN   CASECC,MATPOOL,EQDYN,,TFPOOL/K2PP,M2PP,B2PP/LUSETD/S,N,
         NOK2PP/S,N,NOM2PP/S,N,NOB2PP $ 
****SBST   5
****CARD   1, 40, 56, 57
****FILE 114
****RFMT 204-217
$$$$               2        2      2        2          2        2
$$$$ Equivalence [M  ] to [M  ], [B  ] to [B  ], and [K  ] to [K  ] if no
$$$$               pp       dd     pp       dd         pp       dd
$$$$ constraints are applied.
EQUIV    M2PP,M2DD/NOA/B2PP,B2DD/NOA/K2PP,K2DD/NOA $
****SBST   4
****CARD   1,  9- 11, 40, 56, 57
****FILE 139
****RFMT 204-217
$$$$                                                     2      2             
$$$$ GKAD applies constraints to direct input matrices [K  ], [M  ], and      
$$$$                                                     pp     pp            
$$$$   2              2      2          2
$$$$ [B  ], forming [K  ], [M  ], and [B  ], and forms [G  ] and [G  ].
$$$$   pp             dd     dd         dd               md        od
$$$$
GKAD     USETD,GM,GO,,,,,K2PP,M2PP,B2PP/,,,GMD,GOD,K2DD,M2DD,B2DD/  
         *CMPLEV*/*DISP*/*MODAL*/0.0/0.0/0.0/NOK2PP/
         NOM2PP/NOB2PP/MPCF1/SINGLE/OMIT/NOUE/  
         -1/-1/-1/-1 $  
****SBST   4
****CARD   1-  4,  6,  8- 11, 13, 14, 24, 40, 56, 57
****FILE 115,139
****RFMT 204-217
$$$$
$$$$ GKAM selects eigenvectors to form [phi  ] and assembles stiffness, mass,
$$$$                                       dh
$$$$ and damping matrices in modal coordinates:
$$$$
$$$$            +       +
$$$$            |k  |   |
$$$$            | i | 0 |       T    2
$$$$   [K  ] =  |---+---| + [phi  ][K  ][phi  ]
$$$$     hh     | 0 | 0 |       dh   dd     dh
$$$$            |   |   |
$$$$            +       +
$$$$
$$$$            +       +
$$$$            |m  |   |
$$$$            | i | 0 |       T    2
$$$$   [M  ] =  |---+---| + [phi  ][M  ][phi  ]
$$$$     hh     | 0 | 0 |       dh   dd     dh
$$$$            |   |   |
$$$$            +       +
$$$$
$$$$            +       +
$$$$            |b  |   |
$$$$            | i | 0 |       T    2
$$$$   [B  ] =  |---+---| + [phi  ][B  ][phi  ]
$$$$     hh     | 0 | 0 |       dh   dd     dh
$$$$            |   |   |
$$$$            +       +
$$$$
$$$$ where
$$$$
$$$$   KDAMP = -1 (default)             KDAMP = 1
$$$$     m  = modal masses                m  = modal masses
$$$$      i                                i
$$$$     b  = m  2 pi f  g(f )            b  = 0
$$$$      i    i       i    i              i
$$$$                 2                                        2  2
$$$$     k  = m  4 pi  f                  k  = (1+ig(f )) 4 pi  f  m
$$$$      i    i        i                  i          i          i  i
$$$$
GKAM     USETD,PHIA,,LAMA,DIT,M2DD,B2DD,K2DD,CASECC/MHH,BHH,KHH,
         PHIDH/NOUE/C,Y,LMODES=0/C,Y,LFREQ=0./C,Y,HFREQ=-1.0/   
         NOM2PP/NOB2PP/NOK2PP/S,N,NONCUP/S,N,FMODE/C,Y,KDAMP $  
****SBST   4
****CARD   1-  6,  8- 14, 24, 40, 55- 59, 62
****FILE 116
****RFMT 204-217
$$$$
$$$$ APD processes the aerodynamic data cards from EDT. It adds the k points 
$$$$ and the SA points to USETD, making USETA. EQAERO, ECTA, BGPA, CSTMA, 
$$$$ GPLA, and SILA are updated to reflect the new elements. AERO and ACPT 
$$$$ reflect the aerodynamic parameters. SILGA is a special SIL for plotting.
APD      EDT,EQDYN,ECT,BGPDT,SILD,USETD,CSTM,GPLD/EQAERO,ECTA,BGPA,SILA,
         USETA,SPLINE,AERO,ACPT,FLIST,CSTMA,GPLA,SILGA/S,N,NK/S,N,NJ/   
         S,N,LUSETA/S,N,BOV $   
****CARD   1,  2,  4,  5,  9- 12, 16, 24, 29, 32, 34, 35, 37, 56
****FILE 124
****RFMT 204-217
$$$$
PARAM    //*MPY*/PFILE/0/1 $
****SBST   7
****CARD  18, 20
****FILE 150
****RFMT 204-217
$$$$
PURGE    PLTSETA,PLTPARA,GPSETSA,ELSETSA/JUMPPLOT $ 
****SBST   7
****CARD   1,  2,  4,  5,  9- 12, 16, 18, 24, 32, 35, 37, 56
****FILE 125
****RFMT 204-217
$$$$
$$$$ Go to label SKPPLT if no plot output is requested.
COND     SKPPLT,JUMPPLOT $  
****SBST   7
****CARD   1,  2,  4,  5,  9- 12, 16, 18, 24, 32, 35, 37, 56
****FILE 125,150
****RFMT 204-217
$$$$
PARAM    //*MPY*/PLTFLG/0/1 $   
****SBST   7
****CARD   1,  2,  4,  5,  9- 12, 16, 18, 24, 32, 35, 37, 56
****FILE 150
****RFMT 204-217
$$$$
$$$$ PLTSET transforms user input into a form used to drive the structure 
$$$$ plotter.
PLTSET   PCDB,EQAERO,ECTA,/PLTSETA,PLTPARA,GPSETSA,ELSETSA/S,N,NSIL1/
         S,N,JUMPPLOT $ 
****SBST   7
****CARD   1,  2,  4,  5,  9- 12, 16, 18, 24, 32, 35, 37, 56
****FILE 125
****RFMT 204-217
$$$$
$$$$ PRTMSG prints error messages associated with the structure plotter.
PRTMSG   PLTSETA // $   
****SBST   7
****CARD   1,  2,  4,  5,  9- 12, 16, 18, 24, 32, 35, 37, 56
****FILE 125
****RFMT 204-217
$$$$
$$$$ Go to label SKPPLT if no undeformed aerodynamic or structural element 
$$$$ plots are requested.
COND     SKPPLT,JUMPPLOT $  
****SBST   7
****CARD   1,  2,  4,  5,  9- 12, 16, 18, 24, 32, 35, 37, 56
****FILE 125,150
****RFMT 204-217
$$$$
$$$$ PLOT generates all requested undeformed aerodynamic and structural 
$$$$ element plots.
PLOT     PLTPARA,GPSETSA,ELSETSA,CASECC,BGPA,EQAERO, ,,,,,,/PLOTX2/ 
         NSIL1/LUSETA/S,N,JUMPPLOT/S,N,PLTFLG/S,N,PFILE $   
****SBST   7
****CARD   1,  2,  4,  5,  9- 12, 16, 18, 24, 32, 35, 37, 56
****FILE 150
****RFMT 204-217
$$$$
$$$$ PRTMSG prints plotter data and engineering data for each undeformed 
$$$$ aerodynamic and structural plot element generated.
PRTMSG   PLOTX2 // $
****SBST   7
****CARD   1,  2,  4,  5,  9- 12, 16, 18, 24, 32, 35, 37, 56
****FILE 150
****RFMT 204-217
$$$$
LABEL    SKPPLT $   
****SBST   7
****CARD   1,  2,  4,  5,  9- 12, 16, 18, 24, 32, 35, 37, 56
****FILE 125,150
****RFMT 204-217
$$$$                                    T
$$$$ GI forms a transformation matrix [G  ] which interpolates between
$$$$                                    ka
$$$$ aerodynamic (k) and structural (a) degrees of freedom.
GI       SPLINE,USET ,CSTMA,BGPA,SIL , ,GM,GO/GTKA/NK/  
         LUSET $
****SBST   4
****CARD   1-  4,  6,  8- 11, 13, 24, 32, 35, 37
****FILE 126
****RFMT 204-217
$$$$
PARAM    //*ADD*/DESTRY/0/1/ $  
****SBST   6
****CARD  24, 29, 35, 37
****FILE 137
****RFMT 187-204,207-217
$$$$
$$$$ AMG forms the aerodynamic matrix list [A  ], the area matrix [S  ], and
$$$$                                         jj                     kj
$$$$                             1         2
$$$$ the downwash coefficients [D  ] and [D  ].
$$$$                             jk        jk
$$$$
AMG      AERO,ACPT/AJJL,SKJ,D1JK,D2JK/NK/NJ/S,N,DESTRY $
****SBST   6
****CARD  24, 29, 34, 35, 37
****FILE 127
****RFMT 204-217
$$$$
$$$$ Go to label NODJE if there are no user-supplied downwash coefficients.
COND     NODJE,NODJE $  
****SBST   4
****CARD  17, 26, 37, 56
****FILE 128
****RFMT 204-217
$$$$
$$$$ INPUTT2 provides the user-supplied downwash factors due to extra points
$$$$    1      2
$$$$ ([D  ], [D  ]). PARAM NODJE must be set to enter these matrices. The
$$$$    je     je
$$$$ downwash w  onbox j due to the motion of an extra point, u , is given by
$$$$           j                                               e
$$$$             1       2
$$$$   {w  | = [D   + ikD  ]{u }
$$$$      j      je      je   e
$$$$
INPUTT2  /D1JE,D2JE,,,/C,Y,P1=0/C,Y,P2=11/C,Y,P3=XXXXXXXX $ 
****SBST   4
****CARD  17, 26, 37, 56
****FILE 128
****RFMT 204-217
$$$$
LABEL    NODJE $
****SBST   4
****CARD  17, 26, 37, 56
****FILE 128
****RFMT 204-217
$$$$
PARAM    //*ADD*/XQHHL/1/0 $
****SBST   4
****CARD   1-  6,  8- 14, 17, 24, 26, 29, 32, 35, 37, 48, 56, 58, 59, 62
****FILE 138
****RFMT 204-217
$$$$
$$$$ AMP computes the aerodynamic matrix list related to the modal coordinates 
$$$$ as follows
$$$$ 
$$$$              +           +
$$$$              |phi  |phi  |
$$$$              |   ai|   ae|                   T  T        
$$$$   [phi  ] =  |-----+-----|         [G  ] = [G  ]  [phi  ]
$$$$       dh     |phi  |phi  |           ki      ka       ai 
$$$$              |   ei|   ee|
$$$$              +           +
$$$$
$$$$     1        1     1                 1       1  T
$$$$   [D  ] <= [D   | D  ]             [D  ] = [D  ] [G  ]
$$$$     jh       jf    je                ji      jk    ki
$$$$
$$$$     2        2     2                 2       2  T
$$$$   [D  ] <= [D   | D  ]             [D  ] = [D  ] [G  ]
$$$$     jh       jf    je                ji      jk    ki
$$$$
$$$$ For each (m,k) pair:
$$$$
$$$$             1         2
$$$$   [D  ] = [D  ] + ik[D  ]
$$$$     jh      jh        jh
$$$$
$$$$ For each group:
$$$$
$$$$             T  -1
$$$$   [Q  ] = [A  ]      [D  ]
$$$$     jh      jj group   jh group
$$$$
$$$$   [Q  ] = [S  ][Q  ]
$$$$     kh      kj   jh
$$$$
$$$$                T
$$$$   [Q  ] = [G  ] [Q  ]
$$$$     ih      ki    kh
$$$$
$$$$             +     +
$$$$             | Q   |
$$$$             |  ih |
$$$$   [Q  ] <=  | ----|
$$$$     hh      | Q   |
$$$$             |  eh |
$$$$             +     +
$$$$
AMP      AJJL,SKJ,D1JK,D2JK,GTKA,PHIDH,D1JE,D2JE,USETD,AERO/QHHL,QKHL,  
         QHJL/NOUE/S,N,XQHHL/V,Y,GUSTAERO=-1 $  
****SBST   4
****CARD   1-  6,  8- 14, 17, 24, 26, 29, 32, 34, 35, 37, 48, 56, 58, 59
****CARD  62
****FILE 138
****RFMT 204-217
$$$$
$$$$ FRLG forms the dynamic load vector {P } from the frequency response data
$$$$                                      h
$$$$ or transient data using a Fourier Transform.
FRLG     CASECC,USETD,DLT,FRL,GMD,GOD,DIT,PHIDH/PPF,PSF,PDF,FOL,PHF1/   
         *MODAL*/S,N,FREQY/S,N,APP $
****SBST   4
****CARD   1-  6,  8- 14, 24, 51, 52, 55, 56, 58, 59, 61, 62
****FILE 139
****RFMT 194,197,204-217
$$$$
PARAM    //*NOT*/NOFRY/FREQY $  
****SBST   4
****CARD   1-  6,  8- 14, 24, 40, 51, 52, 55, 56, 58, 59, 61, 62
****FILE 129
****RFMT 204-217
$$$$
PURGE    PPF/NOFRY $
****SBST   4
****CARD   1-  6,  8- 14, 24, 40, 51, 52, 55, 56, 58, 59, 61, 62
****FILE 129
****RFMT 204-217
$$$$
$$$$ GUST forms the loading due to gusts and adds to the direct loads.
GUST     CASECC,DLT,FRL,DIT,QHJL,,,ACPT,CSTMA,PHF1/PHF/ 
         S,N,NOGUST/BOV/C,Y,MACH/C,Y,Q $
****SBST   4
****CARD   1-  6,  8- 14, 24, 26, 28, 29, 32, 34, 37, 40, 49, 51, 52, 55
****CARD  56, 58, 59, 61, 62
****FILE 130
****RFMT 204-217
$$$$
$$$$ Equivalence {PHF1} to {PHF} if there are no gust loads.
EQUIV    PHF1,PHF/NOGUST $  
****SBST   4
****CARD   1-  6,  8- 14, 24, 26, 28, 29, 32, 34, 37, 40, 49, 51, 52, 55
****CARD  56, 58, 59, 61, 62
****FILE 130
****RFMT 204-217
$$$$
$$$$ FRRD2 solves for the modal displacements using
$$$$
$$$$             2
$$$$   [-M  omega  + iB  omega + K + qQ  (k)]U  = P (omega)
$$$$      hh           hh              hh     h    h
$$$$
FRRD2    KHH,BHH,MHH,QHHL,PHF,FOL/UHVF/BOV/C,Y,Q/C,Y,MACH $ 
****SBST   4
****CARD   1-  6,  8- 14, 24, 26, 28, 29, 32, 34, 37, 40, 49, 51, 52, 55
****CARD  56, 58, 59, 61, 62
****FILE 131
****RFMT 194,197,204-217
$$$$
$$$$ Equivalence {UHVF} to {UHVT} and FOL to TOL if it is a frequency response 
$$$$ formulation.
EQUIV    UHVF,UHVT/FREQY/FOL,TOL/FREQY $
****SBST   4
****CARD   1-  6,  8- 14, 24, 26, 28, 29, 32, 34, 37, 40, 49, 51, 52, 55
****CARD  56, 58, 59, 61, 62
****FILE 132
****RFMT 195,198,204-217
$$$$
$$$$ Go to label IFTSKP if it is a frequency response formulation.
COND     IFTSKP,FREQY $ 
****SBST   4
****CARD   1-  6,  8- 14, 24, 26- 29, 32, 34, 37, 40, 49- 52, 55, 56
****CARD  58- 62
****FILE 132
****RFMT 195,198,204-217
$$$$
$$$$ IFT performs Inverse Fourier Transform of the displacements for transient 
$$$$ formulation.
IFT      UHVF,CASECC,TRL,FOL/UHVT,TOL/C,Y,IFTM=0 $  
****SBST   4
****CARD   1-  6,  8- 14, 24, 26- 29, 32, 34, 37, 40, 49- 52, 55, 56
****CARD  58- 62
****FILE 132
****RFMT 195,198,204-217
$$$$
LABEL    IFTSKP $   
****SBST   4
****CARD   1-  6,  8- 14, 24, 26- 29, 32, 34, 37, 40, 49- 52, 55, 56
****CARD  58- 62
****FILE 132
****RFMT 195,198,204-217
$$$$
$$$$ MODACC uses the data from OFREQ or OTIME data cards to select solutions 
$$$$ for data recovery.
MODACC   CASECC,TOL,UHVT,,,/TOL1,UHVT1,,,/APP $ 
****SBST   4
****CARD   1-  6,  8- 14, 24, 26- 29, 32, 34, 37, 40, 49- 52, 55, 56
****CARD  58- 62
****FILE 133
****RFMT 204-217
$$$$
$$$$ ADR produces aerodynamic load output (PKF) for selected points in 
$$$$ frequency response only.
ADR      UHVT1,CASECC,QKHL,TOL1,SPLINE,SILA,USETA/PKF/BOV/  
         C,Y,MACH/APP $ 
****SBST   4
****CARD  21
****FILE 134
$$$$
$$$$ VDR prepares solution set displacements (OUHV1), sorted by frequency or 
$$$$ time, for output. The solution set includes mode amplitudes and extra 
$$$$ points.
VDR      CASECC,EQDYN,USETD,UHVT1,TOL1,XYCDB,/OUHV1,/APP/*MODAL*/   
         0/S,N,NOH/S,N,NOP/FMODE $  
****SBST   4
****CARD  21, 22
****FILE 135
$$$$
$$$$ Go to label NOH if the request is for output sorted by frequency or time 
$$$$ step.
COND     NOH,  NOH $
****SBST   4
****CARD  21, 22
****FILE 135,136
$$$$
$$$$ SDR3 prepares requested output sorted by solution set points.
SDR3     OUHV1,,,,,/OUHV2,,,,, $
****SBST   4
****CARD  21, 22
****FILE 135
$$$$
$$$$ OFP formats the table prepared by SDR3 for output sorted by solution set 
$$$$ point and places it on the system output file for printing.
OFP      OUHV2,,,,,//S,N,CARDNO $   
****SBST   4
****CARD  21
****FILE 135
$$$$
$$$$ Go to label NOH if no X-Y plots are requested.
COND     NOH,NOXYCDB $  
****SBST   4
****CARD  22
****FILE 136
$$$$
$$$$ XYTRAN prepares the input for X-Y plotting of solution set points versus 
$$$$ time or frequency.
XYTRAN   XYCDB,OUHV2,,,,/XYPTTA/APP/*HSET*/S,N,PFILE/S,N,CARDNO/
         S,N,NOXYPL $   
****SBST   4
****CARD  22
****FILE 136
$$$$
$$$$ Go to label NOH if no plots are possible as requested.
COND     NOH,NOXYPL $   
****SBST   4
****CARD  22
****FILE 136
$$$$
$$$$ XYPLOT prepares the requested X-Y plots of solution set points versus 
$$$$ time or frequency.
XYPLOT   XYPTTA  $  
****SBST   4
****CARD  22
****FILE 136
$$$$
LABEL    NOH $  
****SBST   4
****CARD  22
****FILE 135,136
$$$$
PARAM    //*AND*/PJUMP/NOP/JUMPPLOT $   
****SBST   4
****CARD   1-  6,  8- 14, 24, 26- 29, 32, 34, 37, 40, 49- 52, 55- 62
****FILE 137,140-147
****RFMT 204-217
$$$$
$$$$ Go to label FINIS if no output for physical points is requested.
COND     FINIS,PJUMP $  
****SBST   4
****CARD   1-  6,  8- 14, 18- 20, 24, 26- 29, 32, 34, 37, 40, 49- 52
****CARD  54- 62
****FILE 137,140-147
****RFMT 204-217
$$$$
$$$$ SDR2 recovers physical displacements (PHIP) and forces of constraint (QP) 
$$$$ for the real eigenvectors associated with the modes.
SDR1     USETD,,PHIDH,,,GOD,GMD,,KFS,,/PHIP,,QP/1/*DYNAMICS* $  
****SBST   4
****CARD   1-  6,  8- 14, 24, 26- 29, 32, 34, 37, 40, 49- 52, 55- 62
****FILE 137
****RFMT 204-217
$$$$
$$$$ Equivalence {phi  } to {phi  } if there are no extra points introduced 
$$$$                 dh         ah
$$$$ for dynamic analysis.
EQUIV    PHIDH,PHIAH/NOUE $ 
****SBST   4
****CARD   1-  6,  8- 14, 24, 26- 29, 32, 34, 37, 40, 49- 52, 55- 62
****FILE 140
****RFMT 204-217
$$$$
$$$$ Go to label NOUE1 if no extra points are present.
COND     NOUE1,NOUE $   
****SBST   4
****CARD   1-  6,  8- 14, 24, 26- 29, 32, 34, 37, 40, 49- 52, 55- 62
****FILE 140
****RFMT 204-217
$$$$
$$$$ VEC generates a d-size partitioning vector (EVEC) for the a and e sets
$$$$
$$$$   {u } -> {u } + {u }
$$$$     d       a      e
$$$$
VEC      USETD/EVEC/*D*/*A*/*E* $   
****SBST   4
****CARD   1-  6,  8- 14, 24, 26- 29, 32, 34, 37, 40, 49- 52, 55- 62
****FILE 140
****RFMT 204-217
$$$$
$$$$ PARTN performs partition of {phi  } using EVEC
$$$$                                 dh
$$$$ 
$$$$               phi
$$$$                  ah
$$$$   {phi  } => {-----}
$$$$       dh        0
$$$$
PARTN    PHIDH,,EVEC/PHIAH,,,/1 $   
****SBST   4
****CARD   1-  6,  8- 14, 24, 26- 29, 32, 34, 37, 40, 49- 52, 55- 62
****FILE 140
****RFMT 204-217
$$$$
LABEL    NOUE1 $
****SBST   4
****CARD   1-  6,  8- 14, 24, 26- 29, 32, 34, 37, 40, 49- 52, 55- 62
****FILE 140
****RFMT 204-217
$$$$
$$$$ MPYAD recovers the displacements at the aerodynamic points (k)
$$$$ 
$$$$              T  T
$$$$   {phi } = [G  ] {phi  }
$$$$       k      ka      ah
$$$$
MPYAD    GTKA,PHIAH,/PHIK/1/1/0/PREC $  
****SBST   4
****CARD   1-  6,  8- 14, 24, 26- 30, 32, 34, 37, 40, 49- 52, 55- 62
****FILE 140
****RFMT 204-217
$$$$
$$$$ UMERGE is used to expand {Q } to the ps set.
$$$$                            p
$$$$
UMERGE   USETA,PHIP,/PHIPS/*PS*/*P*/*SA* $  
****SBST   4
****CARD   1-  6,  8- 14, 24, 26- 29, 32, 34, 37, 40, 49- 52, 55- 62
****FILE 140
****RFMT 204-217
$$$$
$$$$ UMERGE places {phi } in its proper place in the displacement vector
$$$$                   k
$$$$
$$$$               phi
$$$$                  ps
$$$$   {phi  } <= {-----}
$$$$       pa      phi 
$$$$                  k
$$$$
UMERGE   USETA,PHIPS,PHIK/PHIPA/*PA*/*PS*/*K* $ 
****SBST   4
****CARD   1-  6,  8- 14, 24, 26- 29, 32, 34, 37, 40, 49- 52, 55- 62
****FILE 140
****RFMT 204-217
$$$$
$$$$ UMERGE is used to expand {Q } to the pa set.
$$$$                            p
$$$$
UMERGE   USETA,QP,/QPA/*PA*/*P*/*PS* $  
****SBST   4
****CARD   1-  6,  8- 14, 24, 26- 29, 32, 34, 37, 40, 49- 52, 55- 62
****FILE 140
$$$$
$$$$ SDR2 calculates element forces (MEF1) and stresses (MES1) and prepares 
$$$$ eigenvectors (MPHIPA1) and single-point forces of constraint (MQP1) for 
$$$$ output sorted by frequency or time.
SDR2     CASECC,CSTMA,MPT,DIT,EQAERO,SILA,,,BGPA,LAMA,QPA,PHIPA,
         EST,XYCDB,,/,MQP1,MPHIPA1,MES1,MEF1,,,/*MMREIG* $  
****SBST   4
****CARD  19, 20
****FILE 141
$$$$
$$$$ Go to label NOPF if it is not a frequency response formulation.
COND     NOPF,NOFRY $   
****SBST   4
****CARD  19, 20
****FILE 141,142
$$$$
$$$$ SDR2 prepares load vectors for output (OPP1) sorted by frequency.
SDR2     CASECC,,,,EQDYN,,,,,PPF,,,,XYCDB,,/OPP1,,,,,,,/*FREQ* $
****SBST   4
****CARD  19, 20
****FILE 141
$$$$
$$$$ SDR3 prepares requested load output sorted by point number.
SDR3     OPP1,,,,,/QPP2,,,,,/ $ 
****SBST   4
****CARD  19, 20
****FILE 142
$$$$
LABEL    NOPF $ 
****SBST   4
****CARD  19, 20
****FILE 141,142
$$$$
$$$$ SDR3 prepares requested modal quantities output sorted by point number.
SDR3     MPHIPA1,MES1,MEF1,MQP1,,/MPHIPA2,MES2,MEF2,MQP2,, $
****SBST   4
****CARD  19, 20
****FILE 147
$$$$
$$$$ DDRMM prepares a subset of the element forces (OEF2) and stresses (OES2), 
$$$$ displacement vectors (OUPV2), and single-point forces of constraint 
$$$$ (OQP2) for output sorted by point number or element number.
DDRMM    CASECC,UHVT1,TOL1,MPHIPA2,MQP2,MES2,MEF2,XYCDB,EST,MPT,DIT/
         OUPV2,OQP2,OES2,OEF2, $
****SBST   4
****CARD  19, 20
****FILE 143
$$$$
$$$$ OFP formats the requested physical output prepared by DDRMM and places it 
$$$$ on the system output file for printing.
OFP      OUPV2,,OES2,OEF2,OQP2,//S,N,CARDNO $   
****SBST   4
****CARD  19, 20
****FILE 143
$$$$
$$$$ SCAN examines the element stresses and forces calculated by DDRMM and 
$$$$ generates scanned output that meets the specifications set by the user.
SCAN     CASECC,OES2,OEF2/OESF2/C,N,*RF* $  
****CARD 19 
****FILE 143
$$$$
$$$$ OFP formats the scanned output table prepared by SCAN and places it on 
$$$$ the system output file for printing.
OFP      OESF2,,,,,//S,N,CARDNO $   
****CARD  19
****FILE 143
$$$$
$$$$ Go to label P2 if no deformed aerodynamic or structural element plots are 
$$$$ requested.
COND     P2,JUMPPLOT $  
****SBST   4,  7
****CARD  18
****FILE 144
$$$$
$$$$ MPYAD generates vectors for use by the SDR2 module.
MPYAD    PHIPA,UHVT1,/UVT1/0 $  
****SBST   4,  7
****CARD  18
****FILE 148
$$$$
$$$$ SDR2 prepares vectors for deformed plotting.
SDR2     CASECC,CSTMA,,,EQAERO,,,,BGPA,TOL,,UVT1,,,,/,,,,,PUVPAT,,/APP $
****SBST   4,  7
****CARD  18
****FILE 144
$$$$
$$$$ PLOT prepares all requested deformed aerodynamic and structural element 
$$$$ plots.
PLOT     PLTPARA,GPSETSA,ELSETSA,CASECC,BGPA,EQAERO,SILGA,,PUVPAT,,,,/  
         PLOTX3/NSIL1/LUSETA/JUMPPLOT/PLTFLG/PFILE $
****SBST   4,  7
****CARD  18
****FILE 144
$$$$
$$$$ PRTMSG prints plotter data and engineering data for each deformed plot 
$$$$ generated.
PRTMSG   PLOTX3// $ 
****SBST   4,  7
****CARD  18
****FILE 144
$$$$
LABEL    P2 $   
****SBST   4,  7
****CARD  18
****FILE 144
$$$$
$$$$ Go to label FINIS and make normal exit if no X-Y plots are requested.
COND     FINIS,NOXYCDB $
****SBST   4
****CARD  20
****FILE 145,146
$$$$
$$$$ XYTRAN prepares the input for physical point X-Y plots.
XYTRAN   XYCDB,,OQP2,OUPV2,OES2,OEF2/XYPLTT/APP/*PSET*/ 
         S,N,PFILE/S,N,CARDNO/S,N,NOXYPL $  
****SBST   4
****CARD  20
****FILE 145
$$$$
$$$$ Go to label NOXYPLTT if no plots are possible as requested.
COND     NOXYPLTT,NOXYPL $  
****SBST   4
****CARD  20
****FILE 145
$$$$
$$$$ XYPLOT prepares the requested X-Y plots of displacements, forces, 
$$$$ stresses, loads, and single-point forces of constraint versus frequency 
$$$$ or time.
XYPLOT   XYPLTT $   
****SBST   4
****CARD  20
****FILE 145
$$$$
LABEL    NOXYPLTT $ 
****SBST   4
****CARD  20
****FILE 145
$$$$
$$$$ Go to label FINIS and make normal exit if it is a transient response 
$$$$ formulation.
COND     FINIS,NOFRY $  
****SBST   4
****CARD  20
****FILE 145
$$$$
$$$$ Go to label FINIS and make normal exit if no power spectral density 
$$$$ functions or autocorrelation functions are requested.
COND     FINIS,NOPSDL $ 
****SBST   4
****CARD  20, 54
****FILE 146
$$$$
$$$$ RANDOM calculates power spectral density functions (PSDF) and 
$$$$ autocorrelation functions (AUTO) using the previously calculated 
$$$$ frequency response.
RANDOM   XYCDB,DIT,PSDL,OUPV2,,OQP2,OES2,OEF2,CASECC/PSDF,AUTO/ 
         S,N,NORN $ 
****SBST   4
****CARD  20, 54
****FILE 146
$$$$
$$$$ Go to label FINIS and make normal exit if no X-Y plots of RANDOM 
$$$$ calculations are requested.
COND     FINIS,NORN $   
****SBST   4
****CARD  20, 54
****FILE 146
$$$$
$$$$ XYTRAN prepares the input for requested X-Y plots of the RANDOM output.
XYTRAN   XYCDB,PSDF,AUTO,,,/XYPLTR/*RAND*/*PSET*/S,N,PFILE/ 
         S,N,CARDNO/S,N,NOXYPL $
****SBST   4
****CARD  20, 54
****FILE 146
$$$$
$$$$ Go to label FINIS and make normal exit if no plots are possible as 
$$$$ requested.
COND     FINIS,NOXYPL $ 
****SBST   4
****CARD  20, 54
****FILE 146
$$$$
$$$$ XYPLOT prepares the requested X-Y plots of autocorrelation functions and 
$$$$ power spectral density functions.
XYPLOT   XYPLTR $   
****SBST   4
****CARD  20, 54
****FILE 146
$$$$
$$$$ Go to label FINIS and make normal exit.
JUMP     FINIS $
****CARD   1-  6,  8- 26, 29, 32, 34- 40, 49- 62
****FILE 146
****RFMT 187-204,207-217
$$$$
LABEL    ERROR2 $   
****CARD   1,  9- 12, 40, 50, 52, 53, 56, 58
****FILE 111
****RFMT 187-204,207-217
$$$$
$$$$ Print Error Message No. 2 and terminate execution.
PRTPARM  //-2/*AERORESP* $  
****CARD   1,  9- 12, 40, 50, 52, 53, 56, 58
****FILE 111
****RFMT 187-204,207-217
$$$$
LABEL    ERROR1 $   
****CARD   1-  3,  5,  6,  8, 13, 14, 24
****FILE 113
****RFMT 187-204,207-217
$$$$
$$$$ Print Error Message No. 1 and terminate execution.
PRTPARM  //-1/*AERORESP* $  
****CARD   1-  3,  5,  6,  8, 13, 14, 24
****FILE 113
****RFMT 187-204,207-217
$$$$
LABEL    ERROR4 $   
****SBST   4
****CARD   1-  6,  8- 14, 24, 58, 59
****FILE 112
****RFMT 187-204,207-217
$$$$
$$$$ Print Error Message No. 4 and terminate execution.
PRTPARM  //-4/*AERORESP* $  
****SBST   4
****CARD   1-  6,  8- 14, 24, 58, 59
****FILE 112
****RFMT 187-204,207-217
$$$$
LABEL    ERROR3 $   
****CARD   1,  2,  4-  6, 16, 24
****FILE  97
****RFMT 187-204,207-217
$$$$
$$$$ Print Error Message No. 3 and terminate execution.
PRTPARM  //-3/*AERORESP* $  
****CARD   1,  2,  4-  6, 16, 24
****FILE  97
****RFMT 187-204,207-217
$$$$
LABEL    FINIS $
****CARD   1-  6,  8- 26, 29, 32, 34- 40, 49- 62
****FILE 137,140-147
****RFMT 187-204,207-217
$$$$
PURGE    DUMMY/MINUS1 $ 
****CARD   1-  6,  8- 26, 29, 32, 34- 40, 49- 62
****FILE 137,140-147
****RFMT 187-204,207-217
$$$$
END      $  
****CARD   1-  6,  8- 26, 29, 32, 34- 40, 49- 62
****RFMT 187-204,207-217
$$$$
$*CARD BITS 
  1    AXIC     AXIF
  1    CELAS1   CELAS2   CELAS3   CELAS4
  1    CMASS1   CMASS2   CMASS3   CMASS4
  1    GRDSET   GRID     GRIDB  
  1    POINTAX  RINGAX   RINGFL   SECTAX
  1    SEQGP    SPOINT  
  2    ADUM1    ADUM2    ADUM3    ADUM4    ADUM5    ADUM6    ADUM7  
  2    ADUM8    ADUM9    BAROR  
  2    CAXIF2   CAXIF3   CAXIF4   CBAR     CBEAM    CCONEAX  CDUM1  
  2    CDUM2
  2    CDUM3    CDUM4    CDUM5    CDUM6    CDUM7    CDUM8    CDUM9  
  2    CELBOW   CFLUID2  CFLUID3  CFLUID4  CHEXA1   CHEXA2  
  2    CIHEX1   CIHEX2   CIHEX3   CIS2D8
  2    CNGRNT   CONROD   CQUAD4   CTRIA3
  2    CQDMEM   CQDMEM1  CQDMEM2  CQDPLT   CQUAD1   CQUAD2  
  2    CROD     CSHEAR   CSLOT3   CSLOT4   CTETRA   CTORDRG  CTRAPRG
  2    CTRAPAX  CTRIAAX  CTRIA1   CTRIA2   CTRIARG  CTRIM6   CTRMEM 
  2    CTRBSC   CTRPLT   CTUBE    CTWIST   CWEDGE   
  3    PBAR     PCONEAX  PDUM1    PDUM2    PDUM3    PDUM4    PDUM5  
  3    PDUM6    PDUM7    PDUM8    PDUM9    PELBOW   PIHEX    PIS2D8 
  3    PQDMEM   PQDMEM1  PQDMEM2  PQDPLT   PQUAD1   PQUAD2  
  3    PSHEAR   PTORDRG  PTRAPAX  PTRBSC   PTRIA1   
  3    PTRIA2   PTRIAAX  PTRIM6   PTRMEM   PTRPLT   PTUBE    PTWIST
  3    PSHELL   PCOMP    PCOMP1   PCOMP2 
  4    GENEL
  5    CONM1    CONM2   
  6    PELAS
  7    PMASS
  8    TABLEM1  TABLEM2  TABLEM3  TABLEM4   
  8    MAT1     MAT2     MAT3     MAT9     MATT1    MATT2    MATT3  
  8    MAT8     TEMPMT$  TEMPMX$ 
  9    CRIGD1   CRIGD2   CRIGD3   CRIGDR
  9    CRROD    CRBAR    CRTRPLT  CRBE1    CRBE2    CRBE3    CRSPLINE   
  9    MPC      MPCADD   MPC$     MPCAX 
 10    SPC      SPC1     SPCADD   SPCAX    SPC$ 
 11    ASET     ASET1   
 11    OMIT     OMIT1    OMITAX 
 11    SUPAX    SUPORT  
 13    TEMP     TEMPAX   TEMPD  
 13    TEMPP1   TEMPP2   TEMPP3   TEMPRB
 14    COUPMASS CPBAR   
 14    CPQDPLT  CPQUAD1  CPQUAD2  CPROD    CPTRIA1  CPTRIA2  CPTRPLT
 14    CPTRBSC  CPTUBE  
 14    WTMASS   
 15    GRDPNT   
 16    PLOTEL   
 17    P1       P2       P3 
 18    PLOT$
 19    POUT$
 20    XYOUT$   
 21    AOUT$
 22    AXYOUT$  
 23    ASETOUT  
 24    CORD1C   CORD1R   CORD1S   CORD2C   CORD2R   CORD2S  
 25    AUTOSPC  
 26    NODJE
 27    IFTM 
 28    MACH     Q   
 29    PAERO1   PAERO2   PAERO3   PAERO4   PAERO5   
 32    SET1     SET2
 32    SPLINE1  SPLINE2  SPLINE3
 34    MKAERO1  MKAERO2 
 35    AEFACT   
 37    AERO 
 37    CAERO1   CAERO2   CAERO3   CAERO4   CAERO5   
 40    TF   
 48    GUSTAERO 
 49    GUST     GUST$   
 50    TSTEP
 51    TABLED1  TABLED2  TABLED3  TABLED4   
 52    DAREA    DELAY    DLOAD    DPHASE
 52    FREQ     FREQ1    FREQ2  
 52    RLOAD1   RLOAD2  
 52    TLOAD1   TLOAD2  
 53    RANDPS   RANDT1  
 54    RANDOM$  
 54    TABRND1  TABRNDG 
 55    SDAMP$   
 55    TABDMP1  
 56    EPOINT   SEQEP   
 57    K2PP$    M2PP$    B2PP$    TF$   
 57    DMIG 
 58    EIGR 
 59    METHOD$  
 60    TSTEP$   
 61    DLOAD$   FREQ$   
 62    HFREQ    LFREQ    LMODES   KDAMP 
$$$$
$*FILE BITS 
 94    GPL      EQEXIN   GPDT     CSTM     BGPDT    SIL 
 95    ECT  
 96    GPTT 
 97    EST      GEI      GPECT    MPTX     PCOMPS   EPTX
 98    KGGX 
 99    MGG  
100    KGG  
101    RG       USET     ASET     OGPST 
102    GPST 
103    GM   
104    KNN      MNN 
105    KFF      KFS      MFF
106    GO       KAA      KOO      LOO   
107    KLL      KLR      KRR      MLL      MLR      MRR 
108    LLL  
109    DM   
110    MR   
111    GPLD     SILD     USETD    TFPOOL   EED      EQDYN    DLT
111    PSDL     FRL      TRL
112    LAMA     PHIA     MI       OEIGS 
113    KELM     KDICT    MELM     MDICT 
114    K2PP     M2PP     B2PP   
115    GMD      GOD      K2DD     M2DD     B2DD 
116    MHH      BHH      KHH      PHIDH 
123    MAA  
124    EQAERO   ECTA     BGPA     SILA     USETA    SPLINE   AERO   
124    ACPT     FLIST    CSTMA    GPLA     SILGA
125    PLTSETA  PLTPARA  GPSETSA  ELSETSA   
126    GTKA 
127    AJJL     D1JK     D2JK     SKJ   
128    D1JE     D2JE
129    FOL      PDF      PHF1     PSF      PPF  
130    PHF  
131    UHVF 
132    UHVT     TOL 
133    TOL1     UHVT1   
134    PKF  
135    OUHV1    OUHV2   
136    XYPTTA   
137    PHIP     QP  
138    QHHL     QKHL     QHJL   
139    K2DD     M2DD     B2DD   
140    PHIAH    PHIK     PHIPA    PHIPS 
141    MQP1     MPHIPA1  MES1     MEF1     OPP1 
142    QPP2 
143    OUPV2    OQP2     OES2     OEF2     OESF2
144    PLOTX3   
145    XYPLTT   
146    PSDF     AUTO
147    QPP2 
148    UVT1 
150    PLOTX2   
154    OGPWG
$*  
=PAGE=
AERO9

APR.93
$$$$$$$$
BEGIN    AERO 09 - BLADE CYCLIC MODAL FLUTTER ANALYSIS - APR. 1993 $
****CARD   1- 16, 18- 24, 26, 29, 32, 34- 40, 55- 62
****RFMT 187-204,207-209
$$$$
PRECHK   ALL $  
****CARD   1- 16, 18- 24, 26, 29, 32, 34- 40, 55- 62
****RFMT 187-204,207-209
$$$$
FILE     PHIHL=APPEND/AJJL=APPEND/FSAVE=APPEND/CASEYY=APPEND/CLAMAL=
         APPEND/OVG=APPEND/QHHL=APPEND $
****CARD   1- 14, 19, 21- 24, 26, 29, 32, 34- 40, 55- 62
****RFMT 187-204,207-209
$$$$
PARAM    //*MPY*/CARDNO/0/0 $   
****CARD   1- 15, 19- 21, 24, 41, 43, 58, 59
****FILE 146
$$$$
$$$$ GP1 generates coordinate system transformation matrices, tables of grid 
$$$$ point locations, and tables relating the internal and external grid point 
$$$$ numbers.
GP1      GEOM1,GEOM2,/GPL,EQEXIN,GPDT,CSTM,BGPDT,SIL/S,N,LUSET/S,N, 
         NOGPDT/MINUS1=-1 $ 
****CARD   1
****FILE  94
$$$$
$$$$ Go to label ERROR5 and print Error Message No. 5 if no grid points are 
$$$$ defined.
COND     ERROR5,NOGPDT $
****CARD   1
****FILE  94
****RFMT 187-204,207-209
$$$$
$$$$ GP2 generates Element Connection Table with internal indices.
GP2      GEOM2,EQEXIN/ECT $ 
****CARD   1,  2,  4,  5, 16
****FILE  95
$$$$
$$$$ GP3 generates Static Loads Table and Grid Point Temperature Table.
GP3      GEOM3,EQEXIN,GEOM2/,GPTT/NOGRAV $  
****CARD   1,  2, 13
****FILE  96
$$$$
$$$$ TA1 generates element tables for use in matrix assembly and stress 
$$$$ recovery.
TA1      ECT,EPT,BGPDT,SIL,GPTT,CSTM,MPT,EQEXIN/EST,GEI,GPECT,,,MPTX,
         PCOMPS,EPTX/LUSET/S,N,NOSIMP/1/S,N,NOGENL/GENEL/S,N,COMPS $   
****CARD   1-  7, 13
****FILE  97
$$$$
EQUIV    MPTX,MPT/COMPS/EPTX,EPT/COMPS $
****CARD   1- 16, 18- 24, 26, 29, 32, 34- 40, 55- 62
****FILE  97
$$$$
$$$$ Go to label ERROR5 and print Error Message No. 5 if no structural 
$$$$ elements have been defined.
COND     ERROR5,NOSIMP $
****CARD   1-  7, 13, 14
****FILE  97
****RFMT 187-204,207-209
$$$$
PARAM    //*ADD*/NOKGGX/1/0 $   
****CARD   1-  3,  6,  8, 13
****FILE  98
$$$$
PARAM    //*ADD*/NOMGG/1/0 $
****CARD   1-  3,  5,  7,  8, 13, 14, 24
****FILE  99
****RFMT 187,190-192
$$$$
PARAM    //*NOP*/V,Y,KGGIN=-1 $ 
****CARD  43
$$$$
$$$$ Go to label JMPKGGIN if no stiffness matrix is supplied by the user on an 
$$$$ external file.
COND     JMPKGGIN,KGGIN $   
****CARD  43
****FILE  98,109
$$$$
$$$$ Set parameter NOKGGX = -1 so that the stiffness matrix will not be 
$$$$ generated by EMG.
PARAM    //*ADD*/NOKGGX/-1/0 $  
****CARD  43
****FILE  98,109
$$$$
$$$$ INPUTT1 reads the user-supplied stiffness matrix [KTOTAL] from an 
$$$$ external file (GINO file INPT).
INPUTT1  /KTOTAL,,,,/C,Y,LOCATION=-1/C,Y,INPTUNIT=0 $   
****CARD  43
****FILE 109
$$$$               x
$$$$ Equivalence [K  ] to [KTOTAL].
$$$$               gg
$$$$
EQUIV    KTOTAL,KGGX $  
****CARD  43
****FILE  98
$$$$
LABEL    JMPKGGIN $ 
****CARD  43
****FILE  98,109
$$$$
$$$$ EMG generates structural element stiffness and mass matrix tables and 
$$$$ dictionaries for later assembly by the EMA module.
EMG      EST,CSTM,MPT,DIT,GEOM2,/KELM,KDICT,MELM,MDICT,,,/S,N,NOKGGX/   
         S,N,NOMGG////C,Y,COUPMASS/C,Y,CPBAR/C,Y,CPROD/ 
         C,Y,CPQUAD1/C,Y,CPQUAD2/C,Y,CPTRIA1/C,Y,CPTRIA2/C,Y,CPTUBE/
         C,Y,CPQDPLT/C,Y,CPTRPLT/C,Y,CPTRBSC/   
         C,Y,VOLUME/C,Y,SURFACE $   
****CARD   1-  3,  5-  8, 13, 14, 24, 43
****FILE 122
****RFMT 187,190-192
$$$$
$$$$ Go to label JMPKGGX if no stiffness matrix is to be assembled.
COND     JMPKGGX,NOKGGX $   
****CARD   1-  3,  6,  8, 13, 43
****FILE  98
****RFMT 187,190-192
$$$$                                  x
$$$$ EMA assembles stiffness matrix [K  ] and Grid Point Singularity Table.
$$$$                                  gg
$$$$
EMA      GPECT,KDICT,KELM/KGGX $
****CARD   1-  3,  6,  8, 13, 43
****FILE  98
****RFMT 187,190-192
$$$$
PURGE    KDICT,KELM/MINUS1 $
****CARD   1-  3,  6,  8, 13, 43
****FILE 122
****RFMT 187,190-192
$$$$
LABEL    JMPKGGX $  
****CARD   1-  3,  6,  8, 13, 43
****FILE  98
****RFMT 187,190-192
$$$$
$$$$ Go to label ERROR1 and print Error Message No. 1 if no mass matrix is to 
$$$$ be assembled.
COND     ERROR1,NOMGG $ 
****CARD   1-  3,  5,  7,  8, 13, 14, 24
****FILE  99
****RFMT 187,190-192
$$$$
$$$$ EMA assembles mass matrix [M  ].
$$$$                             gg
$$$$ 
EMA      GPECT,MDICT,MELM/MGG/-1/C,Y,WTMASS=1.0 $   
****CARD   1-  3,  5,  7,  8, 13, 14, 24
****FILE  99
****RFMT 187,190-192
$$$$
PURGE    MDICT,MELM/MINUS1 $
****CARD   1-  3,  5,  7,  8, 13, 14, 24
****FILE 122
****RFMT 187,190-192
$$$$
$$$$ Go to label LGPWG if no weight and balance information is requested.
COND     LGPWG,GRDPNT $ 
****CARD   1-  3,  5,  7,  8, 13- 15, 24
****FILE 107
$$$$
$$$$ GPWG generates weight and balance information.
GPWG     BGPDT,CSTM,EQEXIN,MGG/OGPWG/V,Y,GRDPNT=-1/C,Y,WTMASS $ 
****CARD   1-  3,  5,  7,  8, 13- 15, 24
****FILE 107
$$$$
$$$$ OFP formats the weight and balance information prepared by GPWG and 
$$$$ places it on the system output file for printing.
OFP      OGPWG,,,,,//S,N,CARDNO $   
****CARD   1-  3,  5,  7,  8, 13- 15, 24
$$$$
LABEL    LGPWG $
****CARD   1-  3,  5,  7,  8, 13- 15, 24
****FILE 107
$$$$               x
$$$$ Equivalence [K  ] to [K  ] if there are no general elements.
$$$$               gg       gg
$$$$
EQUIV    KGGX,KGG/NOGENL $  
****CARD   1-  4,  6,  8, 13, 43
****FILE 100
$$$$
$$$$ Go to label LBL11 if there are no general elements.
COND     LBL11,NOGENL $ 
****CARD   1-  4,  6,  8, 13, 43
****FILE 100
$$$$                                 x
$$$$ SMA3 adds general elements to [K  ] to obtain stiffness matrix [K  ].
$$$$                                 gg                               gg
$$$$ 
SMA3     GEI,KGGX/KGG/LUSET/NOGENL/NOSIMP $ 
****CARD   1-  4,  6,  8, 13, 43
****FILE 100
$$$$
LABEL    LBL11 $
****CARD   1-  4,  6,  8, 13, 43
****FILE 100
$$$$
GPSTGEN  KGG,SIL/GPST $ 
****CARD   1-  4,  6,  8, 13, 43
****FILE 102
$$$$
$$$$ GP4 generates flags defining members of various displacement sets
$$$$ (USET), and forms multipoint constraint equations [R ] {u } = 0.
$$$$                                                     g    g
$$$$
GP4      CASECC,GEOM4,EQEXIN,GPDT,BGPDT,CSTM,GPST/RG,,USET, 
         ASET,OGPST/LUSET/S,N,MPCF1/S,N,MPCF2/S,N,SINGLE/S,N,OMIT/  
         S,N,REACT/0/S,N,REPEAT/S,N,NOSET/S,N,NOL/S,N,NOA/  
         C,Y,ASETOUT/C,Y,AUTOSPC $  
****CARD   1,  4,  6,  8, 13, 20- 22, 43
****FILE 101
$$$$
OFP      OGPST,,,,,//S,N,CARDNO $   
****CARD   1,  4,  6,  8, 13, 20- 22, 43
****FILE 101
$$$$
PARAM    //*NOT*/REACDATA/REACT $   
****CARD   1, 20- 22, 43
****FILE 101
$$$$
$$$$ Go to label ERROR6 and print Error Message No. 6 if free-body supports 
$$$$ are present.
COND     ERROR6,REACDATA $  
****CARD   1, 20- 22, 43
****FILE 101
$$$$
PURGE    GM,GMD/MPCF1/GO,GOD/OMIT/KFS,QPC/SINGLE $  
****CARD   1, 20- 22, 43
****FILE 103,105,113,115,120
$$$$
$$$$ GPCYC prepares segment boundary table.
GPCYC    GEOM4,EQEXIN,USET/CYCD/V,Y,CTYPE/S,N,NOGO $
****CARD   1,  9- 11, 41
****FILE 140
$$$$
$$$$ Go to label ERROR7 and print Error Message No. 7 if the CYJOIN data is 
$$$$ inconsistent.
COND     ERROR7,NOGO $  
****CARD   1,  9- 11, 41
****FILE 140
$$$$
EQUIV    KGG,KNN/MPCF1/MGG,MNN/MPCF1 $  
****CARD   1-  9, 14, 24, 43
****FILE 104
$$$$
$$$$ Go to label LBL2 if no multipoint constraints exist.
COND     LBL2,MPCF1 $   
****CARD   1-  9, 13, 14, 24, 43
****FILE 103,104
$$$$
$$$$ MCE1 partitions multipoint constraint equation [R ] = [R |R ] and solves
$$$$                                                  g      m  n
$$$$                                                             -1
$$$$ for multipoint constraint transformation matrix [G ] = -[R ]  [R ].
$$$$                                                   m       m     n
$$$$
MCE1     USET,RG/GM $   
****CARD   1,  9, 13, 43
****FILE 103
$$$$
$$$$ MCE2 partitions stiffness and mass matrices
$$$$ 
$$$$           +_      +                        +_      +
$$$$           |K  |K  |                        |M  |M  |
$$$$           | nn| nm|                        | nn| nm|
$$$$   [K  ] = |---+---|                [M  ] = |---+---|
$$$$     gg    |K  |K  |                  gg    |M  |M  |
$$$$           | mn| mm|                        | mn| mm|
$$$$           +       +                        +       +
$$$$ 
$$$$ and performs matrix reductions
$$$$ 
$$$$            _        T           T           T
$$$$   [K  ] = [K  ] + [G ][K  ] + [K  ][G ] + [G ][K  ][G ]
$$$$     nn      nn      m   mn      mn   m      m   mm   m
$$$$
$$$$            _        T           T           T
$$$$   [M  ] = [M  ] + [G ][M  ] + [M  ][G ] + [G ][M  ][G ]
$$$$     nn      nn      m   mn      mn   m      m   mm   m
$$$$
MCE2     USET,GM,KGG,MGG,,/KNN,MNN,, $  
****CARD   1-  9, 13, 14, 24, 43
****FILE 104
$$$$
LABEL    LBL2 $ 
****CARD   1-  9, 13, 14, 24, 43
****FILE 103,104
$$$$
$$$$ Equivalence [K  ] to [K  ] and [M  ] to [M  ] if no single-point 
$$$$               nn       ff        nn       ff
$$$$ constraints exist.
EQUIV    KNN,KFF/SINGLE/MNN,MFF/SINGLE $
****CARD   1- 10, 13, 14, 24, 43
****FILE 105
$$$$
$$$$ Go to label LBL3 if no single-point constraints exist.
COND     LBL3,SINGLE $  
****CARD   1- 10, 13, 14, 24, 43
****FILE 105
$$$$
$$$$ SCE1 partitions out single-point constraints
$$$$ 
$$$$           +       +                        +       +
$$$$           |K  |K  |                        |M  |M  |
$$$$           | ff| fs|                        | ff| fs|
$$$$   [K  ] = |---+---|                [M  ] = |---+---|
$$$$     nn    |K  |K  |                  nn    |M  |M  |
$$$$           | sf| ss|                        | sf| ss|
$$$$           +       +                        +       +
$$$$
SCE1     USET,KNN,MNN,,/KFF,KFS,,MFF,, $
****CARD   1- 10, 13, 14, 24, 43
****FILE 105
$$$$
LABEL    LBL3 $ 
****CARD   1- 10, 13, 14, 24, 43
****FILE 105
$$$$
$$$$ Equivalence [K  ] to [K  ] and [M  ] to [M  ] if no omitted coordinates
$$$$               ff       aa        ff       aa
$$$$ exist.
EQUIV    KFF,KAA/OMIT/MFF,MAA/OMIT $
****CARD   1- 11, 13, 14, 24, 43
****FILE 106,123
$$$$
$$$$ Go to label LBL5 if no omitted coordinates exist.
COND     LBL5,OMIT $
****CARD   1- 11, 13, 14, 24, 43
****FILE 106,113,123
$$$$
$$$$ SMP1 partitions constrained stiffness matrix
$$$$
$$$$           +_      +
$$$$           |K  |K  |
$$$$           | aa| ao|
$$$$   [K  ] = |---+---|
$$$$     ff    |K  |K  |
$$$$           | oa| oo|
$$$$           +       +
$$$$           
$$$$                                               -1
$$$$ solves for transformation matrix [G ] = -[K  ]  [K  ]
$$$$                                    o       oo     oa
$$$$ 
$$$$                                        _        T
$$$$ and performs matrix reduction [K  ] = [K  ] + [K  ][G ].
$$$$                                 aa      aa      oa   o
$$$$
SMP1     USET,KFF,,,/GO,KAA,KOO,LOO,,,,, $  
****CARD   1-  4,  6,  8- 11, 13, 43
****FILE 106,113
$$$$
$$$$ SMP2 partitions constrained mass matrix
$$$$
$$$$           +       +
$$$$           |_      |
$$$$           |M  |M  |
$$$$           | aa| ao|
$$$$   [M  ] = |---+---|
$$$$     ff    |   |   |
$$$$           |M  |M  |
$$$$           | oa| oo|
$$$$           +       +
$$$$ 
$$$$ and performs matrix reduction
$$$$
$$$$            _        T           T              T
$$$$   [M  ] = [M  ] + [M  ][G ] + [G ][M  ][G ]+ [G ][M  ]
$$$$     aa      aa      oa   o      o   oo   o     o   oa
$$$$
SMP2     USET,GO,MFF/MAA $  
****CARD   1- 11, 13, 14, 24
****FILE 123
$$$$
LABEL    LBL5 $ 
****CARD   1- 11, 13, 14, 24, 43
****FILE 106,113,123
$$$$
$$$$ DPD generates flags defining members of various displacement sets used in 
$$$$ dynamic analysis (USETD), and tables relating the internal and external
$$$$ grid point numbers (GPLD), including extra points introduced for dynamic 
$$$$ analysis (SILD), and prepares Transfer Function Pool (TFPOOL) and 
$$$$ Eigenvalue Extraction Data (EED).
DPD      DYNAMICS,GPL,SIL,USET/GPLD,SILD,USETD,TFPOOL,,,,,,EED,EQDYN/   
         LUSET/S,N,LUSETD/NOTFL/NODLT/NOPSDL/NOFRL/ 
         NONLFT/NOTRL/S,N,NOEED//S,N,NOUE $ 
****CARD   1,  9- 12, 40, 56, 58, 60
****FILE 111
$$$$
$$$$ Go to label ERROR2 and print Error Message No. 2 if there is no 
$$$$ Eigenvalue Extraction Data.
COND     ERROR2,NOEED $ 
****CARD   1,  9- 12, 40, 56, 58, 60
****FILE 111
****RFMT 187-204,207-209
$$$$                       d                d
$$$$ Equivalence [G ] to [G ] and [G ] to [G ] if there are no extra points
$$$$               o       o        m       m
$$$$ introduced for dynamic analysis.
EQUIV    GO,GOD/NOUE/GM,GMD/NOUE $  
****CARD   1-  7,  9- 12, 14, 22- 24, 56, 58
****FILE 115
$$$$
$$$$ CYCT2 transforms matrices from symmetric components to solution set.
CYCT2    CYCD,KAA,MAA,,,/KKK,MKK,,,/*FORE*/V,Y,NSEGS=-1/V,Y,
         KINDEX=-1/V,Y,CYCSEQ=-1/1/S,N,NOGO $   
****CARD   1- 11, 41, 43
****FILE 141
$$$$
$$$$ Go to label ERROR7 and print Error Message No. 7 if a CYCT2 error was 
$$$$ found.
COND     ERROR7,NOGO $  
****CARD   1- 11, 41, 43
****FILE 141
$$$$
$$$$ READ extracts real eigenvalues and eigenvectors from the equation
$$$$ 
$$$$   [K   - lambda M  ]{phi } = 0
$$$$     kk           kk     k
$$$$ 
$$$$ and normalizes eigenvectors according to one of the following user 
$$$$ requests:
$$$$   1. Unit value of a selected component.
$$$$   2. Unit value of the largest component.
$$$$   3. Unit value of the generalized mass.
READ     KKK,MKK,,,EED,,CASECC/LAMK,PHIK,  ,OEIGS/*MODES*/S,N,  
         NEIGV $
****CARD   1- 14, 24, 41, 43, 58, 59
****FILE 142
$$$$
$$$$ OFP formats the summary of eigenvalue extraction information (OEIGS) and 
$$$$ the eigenvalues (LAMK) prepared by READ and places them on the system 
$$$$ output file for printing.
OFP      OEIGS,LAMK,,,,//S,N,CARDNO $   
****CARD   1- 14, 24, 41, 43, 58, 59
$$$$
$$$$ Go to label ERROR4 and print Error Message No. 4 if no eigenvalues were 
$$$$ found.
COND     ERROR4,NEIGV $ 
****CARD   1- 14, 24, 41, 43, 58, 59
****FILE 142
****RFMT 187-204,207-209
$$$$
$$$$ CYCT2 finds symmetric components of eigenvectors from solution set 
$$$$ eigenvectors.
CYCT2    CYCD,,,,PHIK,LAMK/,,,PHIA,LAMA/*BACK*/V,Y,NSEGS/V,Y,   
         KINDEX/V,Y,CYCSEQ/1/S,N,NOGO $ 
****CARD   1- 11, 24, 41, 43, 58, 59
****FILE 112
$$$$
$$$$ Go to label ERROR7 and print Error Message No. 7 if a CYCT2 error was 
$$$$ found.
COND     ERROR7,NOGO $  
****CARD   1- 11, 24, 41, 43, 58, 59
****FILE 112
$$$$
$$$$ SDR1 recovers dependent components of eigenvectors
$$$$ 
$$$$                                   phi
$$$$                                      a
$$$$  {phi } = [G ]{phi }             {----} = {phi }
$$$$      o      o     a               phi         f
$$$$                                      o
$$$$
$$$$    phi       
$$$$       f
$$$$   {----} = {phi }                  {phi } = [G ]{phi }
$$$$    phi         n                       m      m     n
$$$$       s        
$$$$
$$$$    phi          
$$$$       n         
$$$$   {----} = {phi }
$$$$    phi         g 
$$$$       m         
$$$$
SDR1     USET,,PHIA,,,GO,GM,,KFS,,/PHIG,,/1/*REIG* $
****CARD   1- 11, 24, 41, 43, 58, 59
****FILE 143
$$$$
$$$$ SDR2 prepares the eigenvectors (OPHIG) for output and PPHIG for deformed 
$$$$ plotting.
SDR2     CASECC,CSTM,MPT,DIT,EQEXIN,SIL,,,BGPDT,LAMA,,PHIG,EST,,,/  
         ,,OPHIG,,,PPHIG,,/*REIG* $ 
****CARD  18, 19
****FILE 108
$$$$
$$$$ OFP formats the table prepared by SDR2 and places it on the system output 
$$$$ file for printing.
OFP      OPHIG,,,,,//S,N,CARDNO $   
****CARD  19
$$$$
PARAML   PCDB//*PRES*////JUMPPLOT $ 
****SBST   7
****CARD  18
****FILE 110,121
$$$$
PURGE    PLTSETZ,PLTPARZ,GPSETSZ,ELSETSZ/JUMPPLOT $ 
****SBST   7
****CARD  18
****FILE 110
$$$$
$$$$ Go to label PZZ if there are no structure plot requests.
COND     PZZ,JUMPPLOT $ 
****SBST   7
****CARD  18
****FILE 110,121
$$$$
$$$$ PLTSET transforms user input into a form used to drive the structure 
$$$$ plotter.
PLTSET   PCDB,EQEXIN,ECT,EPT/PLTSETZ,PLTPARZ,GPSETSZ,ELSETSZ/   
         S,N,NSILZ/S,N,JUMPZ=-1 $   
****SBST   7
****CARD  18
****FILE 110
$$$$
$$$$ PRTMSG prints messages associated with the structure plotter.
PRTMSG   PLTSETZ// $
****SBST   7
****CARD  18
$$$$
$$$$ Go to label PZZ if no deformed (modal) structure plots are requested.
COND     PZZ,JUMPZ $
****SBST   7
****CARD  18
****FILE 121
$$$$
$$$$ PLOT generates all requested deformed (modal) structure plots.
PLOT     PLTPARZ,GPSETSZ,ELSETSZ,CASECC,BGPDT,EQEXIN,SIL,,PPHIG,,,,/
         PLOTZ/NSILZ/LUSET/JUMPZ/PLTFLGZ=-1/S,N,PFILEZ=0 $  
****SBST   7
****CARD  18
****FILE 121
$$$$
$$$$ PRTMSG prints plotter data and engineering data for each deformed (modal) 
$$$$ structure plot generated.
PRTMSG   PLOTZ// $  
****SBST   7
****CARD  18
$$$$
LABEL    PZZ $  
****SBST   7
****CARD  18
****FILE 110,121
$$$$
$$$$ APDR processes the aerodynamic data cards from EDT. AERO and ACPT reflect 
$$$$ the aerodynamic parameters. PVECT is a partitioning vector and GTKA is a 
$$$$ transformation matrix between aerodynamic (K) and structural (a) degrees 
$$$$ of freedom.
APDB     EDT,USET,BGPDT,CSTM,EQEXIN,GM,GO/AERO,ACPT,FLIST,GTKA,PVECT/   
         S,N,NK/S,N,NJ/V,Y,MINMACH/V,Y,MAXMACH/V,Y,IREF/V,Y,MTYPE/  
         NEIGV/V,Y,KINDEX    $  
$$$$
****CARD   1,  2,  9- 12, 34- 37, 41- 43
****FILE 124,126,144
$$$$
$$$$ PARTN partitions the eigenvector into all sine or all cosine components.
PARTN    PHIA,PVECT,/PHIAX,,,/1 $   
****CARD   1,  2,  9- 12, 41, 43, 58, 59
****FILE 145
$$$$
$$$$ SMPYAD calculates the modal mass matrix
$$$$
$$$$             x T           x
$$$$   [M] = [phi ]  [M  ] [phi ]
$$$$             a     aa      a
$$$$
SMPYAD   PHIAX,MAA,PHIAX,,,/MI/3/1/1/0/1 $  
****CARD   1,  2,  9- 12, 41, 43, 58, 59
****FILE 136
$$$$                                            2      2          2
$$$$ MTRXIN selects the direct input matrices [K  ], [M  ], and [B  ].
$$$$                                            pp     pp         pp
$$$$
MTRXIN   CASECC,MATPOOL,EQDYN,,TFPOOL/K2PP,M2PP,B2PP/LUSETD/S,N,
         NOK2PP/S,N,NOM2PP/S,N,NOB2PP $ 
****CARD   1, 22, 23, 40, 56, 57
****FILE 114
$$$$
PURGE    K2DD/NOK2PP/M2DD/NOM2PP/B2DD/NOB2PP $  
****CARD   1,  2,  4, 22, 23, 40, 56, 57
****FILE 114,139
$$$$               2        2      2        2          2        2   
$$$$ Equivalence [M  ] to [M  ], [B  ] to [B  ], and [K  ] to [K  ] if no
$$$$               pp       dd     pp       dd         pp       dd  
$$$$ constraints are applied.
EQUIV    M2PP,M2DD/NOSET/B2PP,B2DD/NOSET/K2PP,K2DD/NOSET $  
****CARD   1,  2,  4,  9, 11, 22, 23, 40, 56, 57
****FILE 114,139
$$$$                                                     2      2
$$$$ GKAD applies constraints to direct input matrices [K  ], [M  ], and
$$$$                                                     pp     pp
$$$$   2              2      2          2
$$$$ [B  ], forming [K  ], [M  ], and [B  ], and forms [G  ] and [G  ].
$$$$   pp             dd     dd         dd               md        od
$$$$ 
GKAD     USETD,GM,GO,,,,,K2PP,M2PP,B2PP/,,,GMD,GOD,K2DD,M2DD,B2DD/  
         *CMPLEV*/*DISP*/*MODAL*/0.0/0.0/0.0/NOK2PP/
         NOM2PP/NOB2PP/MPCF1/SINGLE/OMIT/NOUE/  
         -1/-1/-1/-1 $  
****CARD   1-  4,  6,  8- 11, 13, 14, 22, 23, 40- 43, 56, 57
****FILE 115,139
$$$$
$$$$ GKAM selects eigenvectors to form [phi  ] and assembles stiffness, mass,
$$$$                                       dh
$$$$ and damping matrices in modal coordinates:
$$$$
$$$$            +       +
$$$$            |k  |   |
$$$$            | i | 0 |       T    2
$$$$   [K  ] =  |---+---| + [phi  ][K  ][phi  ]
$$$$     hh     | 0 | 0 |       dh   dd     dh
$$$$            |   |   |
$$$$            +       +
$$$$
$$$$            +       +
$$$$            |m  |   |
$$$$            | i | 0 |       T    2
$$$$   [M  ] =  |---+---| + [phi  ][M  ][phi  ]
$$$$     hh     | 0 | 0 |       dh   dd     dh
$$$$            |   |   |
$$$$            +       +
$$$$
$$$$            +       +
$$$$            |b  |   |
$$$$            | i | 0 |       T    2
$$$$   [B  ] =  |---+---| + [phi  ][B  ][phi  ]
$$$$     hh     | 0 | 0 |       dh   dd     dh
$$$$            |   |   |
$$$$            +       +
$$$$
$$$$ where
$$$$
$$$$   KDAMP = -1 (default)             KDAMP = 1
$$$$     m  = modal masses                m  = modal masses
$$$$      i                                i
$$$$     b  = m  2 pi f  g(f )            b  = 0
$$$$      i    i       i    i              i
$$$$                 2                                        2  2
$$$$     k  = m  4 pi  f                  k  = (1+ig(f )) 4 pi  f  m
$$$$      i    i        i                  i          i          i  i
$$$$
GKAM     USETD,PHIAX,MI,LAMK,DIT,M2DD,B2DD,K2DD,CASECC/MHH,BHH,KHH, 
         PHIDH/NOUE/C,Y,LMODES=999999/C,Y,LFREQ=0.0/C,Y,HFREQ=0.0/  
         NOM2PP/NOB2PP/NOK2PP/S,N,NONCUP/S,N,FMODE/C,Y, 
         KDAMP=-1 $ 
****CARD   1- 14, 22- 24, 40- 43, 55- 59, 62
****FILE 116
****RFMT 187,196-198
$$$$
PURGE    PLTSETX,PLTPAR,GPSETS,ELSETS/JUMPPLOT $
****SBST   7
****CARD  18
****FILE 125
$$$$
$$$$ Go to label P2 if no plot output is requested.
COND     P2,JUMPPLOT $  
****SBST   7
****CARD  18
****FILE 125,134
$$$$
$$$$ PLTSET transforms user input into a form used to drive the structure 
$$$$ plotter.
PLTSET   PCDB,EQDYN,ECT,/PLTSETX,PLTPAR,GPSETS,ELSETS/S,N,NSIL1/S,N, 
         JUMPPLOT $ 
****SBST   7
****CARD  18
****FILE 125
$$$$
$$$$ PRTMSG prints error messages associated with the structure plotter.
PRTMSG   PLTSETX//$ 
****SBST   7
****CARD  18
$$$$
PARAM    //*MPY*/PLTFLG/1/1 $   
****SBST   7
****CARD  18
****FILE 134
$$$$
PARAM    //*MPY*/PFILE/0/0 $
****SBST   7
****CARD  18
****FILE 134
$$$$
$$$$ Go to label P2 if no undeformed aerodynamic or structural element plots 
$$$$ are requested.
COND     P2,JUMPPLOT $  
****SBST   7
****CARD  18
****FILE 134
$$$$
$$$$ PLOT generates all requested undeformed aerodynamic and structural 
$$$$ element plots.
PLOT     PLTPAR,GPSETS,ELSETS,CASECC,BGPDT,EQDYN,,,,,,,/PLOTX1/NSIL1/   
         LUSET/S,N,JUMPPLOT/S,N,PLTFLG/S,N,PFILE $  
****SBST   7
****CARD  18
****FILE 134
$$$$
$$$$ PRTMSG prints plotter data and engineering data for each undeformed 
$$$$ aerodynamic and structural element plot generated.
PRTMSG   PLOTX1//$  
****SBST   7
****CARD  18
$$$$
LABEL    P2 $   
****SBST   7
****CARD  18
****FILE 125,134
$$$$
PARAM    //*ADD*/DESTRY/0/1 $   
****CARD   1, 29, 35, 37
****FILE 127
****RFMT 187-204,207-209
$$$$
$$$$ AMG forms the aerodynamic matrix list [A  ], the area matrix [S  ], and
$$$$                                         jj                     kj
$$$$                             1         2
$$$$ the downwash coefficients [D  ] and [D  ].
$$$$                             jk        jk
$$$$ 
AMG      AERO,ACPT/AJJL,SKJ,D1JK,D2JK/NK/NJ/S,N,DESTRY $
****CARD   1, 29, 34, 35, 37, 42, 43
****FILE 127
$$$$
PURGE    D1JE,D2JE/NODJE $  
****CARD  26, 37
****FILE 128
$$$$
$$$$ Go to label NODJE if there are no user-supplied downwash coefficients.
COND     NODJE,NODJE $  
****CARD  26, 37
****FILE 128
$$$$
$$$$ INPUTT2 provides the user-supplied downwash factors due to extra points
$$$$    1      2
$$$$ ([D  ], [D  ]). PARAM NODJE must be set to enter these matrices. The
$$$$    je     je
$$$$ downwash w  onbox j due to the motion of an extra point, u , is given by
$$$$           j                                               e
$$$$             1       2
$$$$   {w  | = [D   + ikD  ]{u }
$$$$      j      je      je   e
$$$$
INPUTT2  /D1JE,D2JE,,,/C,Y,POSITION=-1/C,Y,UNITNUM=11/C,Y,USRLABEL= 
         TAPEID $   
****CARD  26, 37
****FILE 128
$$$$
LABEL    NODJE $
****CARD  26, 37
****FILE 128
$$$$
PARAM    //*ADD*/XQHHL/1/0 $
****CARD   1- 13, 24, 26, 29, 32, 34, 35, 37, 41- 43, 56, 58, 59, 62
****FILE 138
$$$$
$$$$ AMP computes the aerodynamic matrix list related to the modal coordinates 
$$$$ as follows
$$$$ 
$$$$              +           +
$$$$              |phi  |phi  |
$$$$              |   ai|   ae|                   T  T        
$$$$   [phi  ] =  |-----+-----|         [G  ] = [G  ]  [phi  ]
$$$$       dh     |phi  |phi  |           ki      ka       ai 
$$$$              |   ei|   ee|
$$$$              +           +
$$$$
$$$$     1        1     1                 1       1  T
$$$$   [D  ] <= [D   | D  ]             [D  ] = [D  ] [G  ]
$$$$     jh       jf    je                ji      jk    ki
$$$$
$$$$     2        2     2                 2       2  T
$$$$   [D  ] <= [D   | D  ]             [D  ] = [D  ] [G  ]
$$$$     jh       jf    je                ji      jk    ki
$$$$
$$$$ For each (m,k) pair:
$$$$
$$$$             1         2
$$$$   [D  ] = [D  ] + ik[D  ]
$$$$     jh      jh        jh
$$$$
$$$$ For each group:
$$$$
$$$$             T  -1
$$$$   [Q  ] = [A  ]      [D  ]
$$$$     jh      jj group   jh group
$$$$
$$$$   [Q  ] = [S  ][Q  ]
$$$$     kh      kj   jh
$$$$
$$$$                T
$$$$   [Q  ] = [G  ] [Q  ]
$$$$     ih      ki    kh
$$$$
$$$$             +     +
$$$$             | Q   |
$$$$             |  ih |
$$$$   [Q  ] <=  | ----|
$$$$     hh      | Q   |
$$$$             |  eh |
$$$$             +     +
$$$$
AMP      AJJL,SKJ,D1JK,D2JK,GTKA,PHIDH,D1JE,D2JE,USETD,AERO/QHHL,,/ 
         NOUE/S,N,XQHHL $   
****CARD   1- 13, 24, 26, 29, 32, 34, 35, 37, 41- 43, 56, 58, 59, 62
****FILE 138
$$$$
PARAM    //*MPY*/NOP/1/1 $  
****CARD  21
$$$$
PARAM    //*MPY*/NOH/0/1 $  
****CARD  21
****RFMT 187-204,207-209
$$$$
$$$$ PARAM initializes the flutter loop counter (FLOOP) to zero.
PARAM    //*MPY*/FLOOP/V,Y,NODJE=-1/0 $ 
****CARD   1- 13, 24, 26, 29, 32, 34- 40, 55- 62
****RFMT 187-204,207-209
$$$$
$$$$ Beginning of loop for flutter.
LABEL    LOOPTOP $  
****CARD   1- 13, 24, 26, 29, 32, 34- 40, 55- 62
****RFMT 187-204,207-209
$$$$                                                  x
$$$$ FA1 computes the total aerodynamic mass matrix [M  ], the total 
$$$$                                                  hh
$$$$                                x
$$$$ aerodynamic stiffness matrix [K  ], and the total aerodynamic damping
$$$$                                hh
$$$$          x
$$$$ matrix [B  ], as well as a looping table FSAVE. For the K method
$$$$          hh
$$$$ 
$$$$    x      2  2           ) Q
$$$$   M   = (k /b )M   + (p/2   hh
$$$$    hh           hh
$$$$ 
$$$$    x
$$$$   K   = K
$$$$    hh    hh
$$$$ 
$$$$    x
$$$$   B   = 0
$$$$    hh
$$$$ 
FA1      KHH,BHH,MHH,QHHL,CASECC,FLIST/FSAVE,KXHH,BXHH,MXHH/S,N,FLOOP/  
         S,N,TSTART/S,N,NOCEAD $
****CARD   1- 13, 24, 26, 29, 32, 34- 43, 55- 62
****FILE 129
$$$$
$$$$ Set up the equivalences for the KE and PK methods.
EQUIV    KXHH,PHIH/NOCEAD/BXHH,CLAMA/NOCEAD/
         KXHH,PHIHL/NOCEAD/BXHH,CLAMAL/NOCEAD/  
         CASECC,CASEYY/NOCEAD $ 
****CARD   1- 13, 24, 26, 29, 32, 34- 43, 55- 62
****FILE 117,130
$$$$
$$$$ Go to label VDR for the KE and PK methods.
COND     VDR,NOCEAD $   
****CARD   1- 13, 24, 26, 29, 32, 34- 43, 55- 62
****FILE 117,119,130
$$$$
$$$$ CEAD extracts complex eigenvalues and eigenvectors from the equation
$$$$
$$$$     x  2    x      x
$$$$   [M  p  + B  p + K  ]{phi } = 0
$$$$     hh      hh     hh     h
$$$$
$$$$ and normalizes eigenvectors to unit magnitude of the largest component.
CEAD     KXHH,BXHH,MXHH,EED,CASECC/PHIH,CLAMA,OCEIGS,/S,N,EIGVS $   
****CARD   1- 13, 24, 26, 29, 32, 34- 43, 55- 62
****FILE 117
$$$$
$$$$ Go to label LBLZAP if no complex eigenvalues were found.
COND     LBLZAP,EIGVS $ 
****CARD   1- 13, 24, 26, 29, 32, 34- 43, 55- 62
****FILE 119,130
$$$$
LABEL    VDR $  
****CARD   1- 13, 24, 26, 29, 32, 34- 43, 55- 62
****FILE 117
$$$$
$$$$ VDR prepares eigenvectors (OPHIH) for output, using only the extra points 
$$$$ introduced for dynamic analysis and modal coordinates.
VDR      CASECC,EQDYN,USETD,PHIH,CLAMA,,/OPHIH,/*CEIGEN*/*MODAL*/   
         123/S,N,NOH/S,N,NOP/FMODE $
****CARD  21
****FILE 119
$$$$
$$$$ Go to label LBL16 if there is no output request for the extra points 
$$$$ introduced for dynamic analysis or modal coordinates.
COND     LBL16,NOH $
****CARD  21
****FILE 119
$$$$
$$$$ OFP formats the table of eigenvectors for extra points introduced for 
$$$$ dynamic analysis and modal coordinates prepared by VDR and places it on 
$$$$ the system output file for printing.
OFP      OPHIH,,,,,//S,N,CARDNO $   
****CARD  21
$$$$
LABEL    LBL16 $
****CARD  21
****FILE 119
$$$$
$$$$ FA2 appends eigenvectors to PHIHL, eigenvalues to CLAMAL, Case Control to 
$$$$ CASEYY, and V-g plot data to OVG.
FA2      PHIH,CLAMA,FSAVE/PHIHL,CLAMAL,CASEYY,OVG/S,N,TSTART/C,Y,VREF=  
         1.0/C,Y,PRINT=YESB $   
****CARD   1- 13, 24, 26, 29, 32, 34- 43, 55- 62
****FILE 130
$$$$
$$$$ Go to label CONTINUE if there is insufficient time for another flutter 
$$$$ loop.
COND     CONTINUE,TSTART $  
****CARD   1- 13, 24, 26, 29, 32, 34- 43, 55- 62
****FILE 130
****RFMT 187-204,207-209
$$$$
LABEL    LBLZAP $   
****CARD   1- 13, 24, 26, 29, 32, 34- 43, 55- 62
****FILE 119,130
****RFMT 187-204,207-209
$$$$
$$$$ Go to label CONTINUE if the flutter loop is complete.
COND     CONTINUE,FLOOP $   
****CARD   1- 13, 24, 26, 29, 32, 34- 43, 55- 62
****FILE 129
****RFMT 187-204,207-209
$$$$
$$$$ Go to label LOOPTOP for additional aerodynamic configuration triplet 
$$$$ values.
REPT     LOOPTOP,100  $ 
****CARD   1- 13, 24, 26, 29, 32, 34- 43, 55- 62
****RFMT 187-204,207-209
$$$$
$$$$ Go to label ERROR3 and print Error Message No. 3 if the number of flutter 
$$$$ loops exceeds 100.
JUMP     ERROR3 $   
****CARD   1- 13, 24, 26, 29, 32, 34- 43, 55- 62
****RFMT 187-204,207-209
$$$$
LABEL    CONTINUE $ 
****CARD   1- 13, 24, 26, 29, 32, 34- 43, 55- 62
****RFMT 187-204,207-209
$$$$
PARAML   XYCDB//*PRES*////NOXYCDB $ 
****SBST   7
****CARD  20
****FILE 146
$$$$
$$$$ Go to label NOXYOUT if there are no X-Y plot requests.
COND     NOXYOUT,NOXYCDB $  
****SBST   7
****CARD  20
****FILE 146
$$$$
$$$$ XYTRAN prepares the input for requested X-Y plots.
XYTRAN   XYCDB,OVG,,,,/XYPLTCE/*VG*/*PSET*/S,N,PFILE/S,N,CARDNO $   
****SBST   7
****CARD  20
****FILE 146
$$$$
$$$$ XYPLOT prepares the requested X-Y plots of displacements, velocities, 
$$$$ accelerations, forces, stresses, loads, and single-point forces of 
$$$$ constraint versus time.
XYPLOT   XYPLTCE//$ 
****SBST   7
****CARD  20
****FILE 146
$$$$
LABEL    NOXYOUT $  
****SBST   7
****CARD  20
****FILE 146
$$$$
PARAM    //*AND*/PJUMP/NOP=-1/JUMPPLOT $
****CARD   1- 13, 21, 24, 26, 29, 32, 34- 43, 55- 62
$$$$
$$$$ Go to label FINIS and make normal exit if there are no output requests 
$$$$ involving dependent degrees of freedom or forces and stresses.
COND     FINIS,PJUMP $  
****CARD   1- 13, 21, 24, 26, 29, 32, 34- 43, 55- 62
$$$$
$$$$ MODACC selects a list of eigenvalues and eigenvectors whose imaginary 
$$$$ parts (velocity in input units) are close to a user input list.
MODACC   CASEYY,CLAMAL,PHIHL,CASECC,,/CLAMAL1,CPHIH1,CASEZZ,,/  
         *CEIGN* $  
****CARD   1- 13, 24, 26, 29, 32, 34- 43, 55- 62
****FILE 131,133
$$$$
$$$$ DDR1 transforms the complex eigenvectors from modal to physical 
$$$$ coordinates
$$$$ 
$$$$       c
$$$$   {phi } = {phi  }{phi }
$$$$       d        dh     h
$$$$ 
DDR1     CPHIH1,PHIDH/CPHID $   
****CARD   1- 13, 24, 26, 29, 32, 34, 36- 43, 55- 62
****FILE 118
$$$$                 c         c
$$$$ Equivalence {phi } to {phi } if no constraints are applied.
$$$$                 d         p
$$$$
EQUIV    CPHID,CPHIP/NOA $  
****CARD   1- 13, 24, 26, 29, 32, 34- 43, 55- 62
****FILE 120
$$$$
$$$$ Go to label LBL14 if no constraints are applied.
COND     LBL14,NOA $
****CARD   1- 13, 24, 26, 29, 32, 34- 43, 55- 62
****FILE 120
$$$$
$$$$ SDR1 recovers dependent components of eigenvectors
$$$$ 
$$$$                                   phi
$$$$      c      d     c                  d        c      c
$$$$  {phi } = [G ]{phi }             {----} = {phi  + phi }
$$$$      o      o     d               phi         f      e
$$$$                                      o
$$$$       c      c
$$$$    phi  + phi
$$$$       f      e        c      c       c      d     c      c
$$$$   {-----------} = {phi  + phi }  {phi } = [G ]{phi  + phi } 
$$$$          c            n      e       m      m     n      e
$$$$       phi      
$$$$          s
$$$$ 
$$$$       c      c   
$$$$    phi  + phi    
$$$$       f      e      c
$$$$   {-----------} = {Q }
$$$$          c          p
$$$$       phi        
$$$$          m
$$$$                                                         T
$$$$ and recovers single-point forces of constraint {q } = [K  ] {phi }.
$$$$                                                  s      fs      f
$$$$  0      c
$$$$ --- = {Q }.
$$$$  q      p
$$$$   s
$$$$
SDR1     USETD,,CPHID,,,GOD,GMD,,KFS,,/CPHIP,,QPC/1/*DYNAMICS* $
****CARD   1- 13, 24, 26, 29, 32, 34- 43, 55- 62
****FILE 120
$$$$
LABEL    LBL14 $
****CARD   1- 13, 24, 26, 29, 32, 34- 43, 55- 62
****FILE 120
$$$$                 c         c
$$$$ Equivalence {phi } to {phi } if there are no extra points introduced for
$$$$                 d         a
$$$$ dynamic analysis.
EQUIV    CPHID,CPHIA/NOUE $ 
****CARD   1- 13, 24, 26, 29, 32, 34- 43, 55- 62
****FILE 132
$$$$
$$$$ Go to label LBLNOE if there are no extra points.
COND     LBLNOE,NOUE $  
****CARD   1- 13, 24, 26, 29, 32, 34- 43, 55- 62
****FILE 132,135
$$$$
$$$$ VEC generates a d-size partitioning vector (RP) for the a and e sets
$$$$
$$$$   {u } -> {u } + {u }
$$$$     d       s      e
$$$$
VEC      USETD/RP/*D*/*A*/*E* $ 
****CARD   1- 13, 24, 26, 29, 32, 34- 43, 55- 62
****FILE 135
$$$$                                 c
$$$$ PARTN performs partition of {phi } using RP
$$$$                                 d
$$$$                 c
$$$$              phi
$$$$       c         a
$$$$   {phi } => {----}
$$$$       d         c
$$$$              phi
$$$$                 e
$$$$
PARTN    CPHID,,RP/CPHIA,,,/1/3 $   
****CARD   1- 13, 24, 26, 29, 32, 34- 43, 55- 62
****FILE 132
$$$$
LABEL    LBLNOE $   
****CARD   1- 13, 24, 26, 29, 32, 34- 43, 55- 62
****FILE 132,135
$$$$
$$$$ SDR2 calculates element forces (OEFC1) and stresses (OESC1) and prepares 
$$$$ eigenvectors (OCPHIP) and single-point forces of constraint (OQPC1) for 
$$$$ output and PCPHIP for deformed plotting.
SDR2     CASEZZ,CSTM,MPT,DIT,EQDYN,SILD,,,BGPDT,CLAMAL1,QPC,CPHIP,  
         EST,,,/,OQPC1,OCPHIP,OESC1,OEFC1,PCPHIP,,/*CEIGN* $
****CARD   1- 13, 24, 26, 29, 32, 34- 43, 55- 62
****FILE 137
$$$$
$$$$ OFP formats the tables prepared by SDR2 and places them on the system 
$$$$ output file for printing.
OFP      OCPHIP,OQPC1,OESC1,OEFC1,,//S,N,CARDNO $   
****CARD  19
$$$$
$$$$ Go to label P3 if no deformed aerodynamic or structural element plots are 
$$$$ requested.
COND     P3,JUMPPLOT $  
****SBST   7
****CARD  18
****FILE 147
$$$$
$$$$ PLOT prepares all deformed aerodynamic and structural element plots.
PLOT     PLTPAR,GPSETS,ELSETS,CASEZZ,BGPDT,EQDYN,SILD,,PCPHIP,,,,/  
         PLOTX3/NSIL1/LUSET/JUMPPLOT/PLTFLG/PFILE $ 
****SBST   7
****CARD  18
****FILE 147
$$$$
$$$$ PRTMSG prints plotter data and engineering data for each deformed plot 
$$$$ generated.
PRTMSG   PLOTX3//$  
****SBST   7
****CARD  18
$$$$
LABEL    P3 $   
****SBST   7
****CARD  18
****FILE 147
$$$$
$$$$ Go to label FINIS and make normal exit.
JUMP     FINIS $
****CARD   1- 16, 18- 24, 26, 29, 32, 34- 43, 55- 62
****RFMT 187-204,207-209
$$$$
LABEL    ERROR1 $   
****CARD   1- 16, 18- 24, 26, 29, 32, 34- 43, 55- 62
****RFMT 187-204,207-209
$$$$
$$$$ Print Error Message No. 1 and terminate execution.
PRTPARM  //-1/*BLADEMDS* $  
****CARD   1- 16, 18- 24, 26, 29, 32, 34- 43, 55- 62
****RFMT 187-204,207-209
$$$$
LABEL    ERROR2 $   
****CARD   1- 16, 18- 24, 26, 29, 32, 34- 43, 55- 62
****RFMT 187-204,207-209
$$$$
$$$$ Print Error Message No. 2 and terminate execution.
PRTPARM  //-2/*BLADEMDS* $  
****CARD   1- 16, 18- 24, 26, 29, 32, 34- 43, 55- 62
****RFMT 187-204,207-209
$$$$
LABEL    ERROR3 $   
****CARD   1- 16, 18- 24, 26, 29, 32, 34- 43, 55- 62
****RFMT 187-204,207-209
$$$$
$$$$ Print Error Message No. 3 and terminate execution.
PRTPARM  //-3/*BLADEMDS* $  
****CARD   1- 16, 18- 24, 26, 29, 32, 34- 43, 55- 62
****RFMT 187-204,207-209
$$$$
LABEL    ERROR4 $   
****CARD   1- 16, 18- 24, 26, 29, 32, 34- 43, 55- 62
****RFMT 187-204,207-209
$$$$
$$$$ Print Error Message No. 4 and terminate execution.
PRTPARM  //-4/*BLADEMDS* $  
****CARD   1- 16, 18- 24, 26, 29, 32, 34- 43, 55- 62
****RFMT 187-204,207-209
$$$$
LABEL    ERROR5 $   
****CARD   1- 16, 18- 24, 26, 29, 32, 34- 43, 55- 62
****RFMT 187-204,207-209
$$$$
$$$$ Print Error Message No. 5 and terminate execution.
PRTPARM  //-5/*BLADEMDS* $  
****CARD   1- 16, 18- 24, 26, 29, 32, 34- 43, 55- 62
****RFMT 187-204,207-209
$$$$
LABEL    ERROR6 $   
****CARD   1- 16, 18- 24, 26, 29, 32, 34- 43, 55- 62
****RFMT 187-204,207-209
$$$$
$$$$ Print Error Message No. 6 and terminate execution.
PRTPARM  //-6/*BLADEMDS* $  
****CARD   1- 16, 18- 24, 26, 29, 32, 34- 43, 55- 62
****RFMT 187-204,207-209
$$$$
LABEL    ERROR7 $   
****CARD   1- 16, 18- 24, 26, 29, 32, 34- 43, 55- 62
****RFMT 187-204,207-209
$$$$
$$$$ Print Error Message No. 7 and terminate execution.
PRTPARM  //-7/*BLADEMDS* $  
****CARD   1- 16, 18- 24, 26, 29, 32, 34- 43, 55- 62
****RFMT 187-204,207-209
$$$$
LABEL    FINIS $
****CARD   1- 16, 18- 24, 26, 29, 32, 34- 43, 55- 62
****RFMT 187-204,207-209
$$$$
PURGE    DUMMY/MINUS1 $ 
****CARD   1- 16, 18- 24, 26, 29, 32, 34- 43, 55- 62
****RFMT 187-204,207-209
$$$$
END      $  
****CARD   1- 16, 18- 24, 26, 29, 32, 34- 43, 55- 62
****RFMT 187-204,207-209
$$$$
$*CARD BITS 
  1    ADUM1    ADUM2    ADUM3    ADUM4    ADUM5    ADUM6    ADUM7  
  1    ADUM8    ADUM9    AXIC     AXIF     AXSLOT   
  1    CELAS1   CELAS2   CELAS3   CELAS4
  1    CMASS1   CMASS2   CMASS3   CMASS4
  1    CORD1C   CORD1R   CORD1S   CORD2C   CORD2R   CORD2S  
  1    GRDSET   GRID     GRIDB  
  1    POINTAX  RINGAX   RINGFL 
  1    SECTAX   SEQGP    SPOINT 
  2    BAROR
  2    CAXIF2   CAXIF3   CAXIF4   CBAR     CCONEAX  CDUM1   
  2    CDUM2    CDUM3    CDUM4    CDUM5    CDUM6    CDUM7    CDUM8  
  2    CDUM9    CFLUID2  CFLUID3  CFLUID4  CHEXA1   CHEXA2  
  2    CIHEX1   CIHEX2   CIHEX3 
  2    CNGRNT   CONROD   CQUAD4   CTRIA3
  2    CQDMEM   CQDMEM1  CQDMEM2  CQDPLT   CQUAD1   CQUAD2  
  2    CROD     CSHEAR   CSLOT3   CSLOT4   CTETRA   CTRBSC   CTRAPAX
  2    CTRIAAX  CTRIARG  CTORDRG  CTRAPRG  CTRIA1   CTRIA2   CTRIM6 
  2    CTRMEM   CTRPLT   CTUBE    CTWIST   CWEDGE   
  3    PBAR     PCONEAX  PDUM1    PDUM2    PDUM3    PDUM4    PDUM5  
  3    PDUM6    PDUM7    PDUM8    PDUM9 
  3    PIHEX    PQDMEM   PQDMEM1  PQDMEM2  PQDPLT   PQUAD1   PQUAD2 
  3    PROD     PSHEAR   PTORDRG  PTRAPAX  PTRBSC   PTRIA1  
  3    PTRIA2   PTRIM6   PTRIAAX  PTRMEM   PTRPLT   PTUBE    PTWIST
  3    PSHELL   PCOMP    PCOMP1   PCOMP2 
  4    GENEL
  5    CONM1    CONM2   
  6    PELAS
  7    PMASS
  8    MAT1     MAT2     MAT3     MATT1    MATT2    MATT3    MAT8   
  8    TABLEM1  TABLEM2  TABLEM3  TABLEM4   
  8    TEMPMT$  TEMPMX$ 
  9    AXISYM   
  9    CRIGD1   CRIGD2   CRIGD3   CRIGDR
  9    CRROD    CRBAR    CRTRPLT  CRBE1    CRBE2    CRBE3    CRSPLINE   
  9    MPC      MPCADD   MPC$     MPCAX 
 10    SPC      SPC1     SPCADD   SPCAX    SPC$ 
 11    ASET     ASET1   
 11    OMIT     OMIT1    OMITAX 
 12    SUPAX    SUPORT  
 13    TEMP     TEMPAX   TEMPD  
 13    TEMPP1   TEMPP2   TEMPP3   TEMPRB
 15    GRDPNT   
 16    PLOTEL   
 18    PLOT$
 19    POUT$
 20    XYOUT$   
 21    AOUT$
 24    COUPMASS CPBAR    CPDPLT   CPTRBSC   
 24    CPQUAD1  CPQUAD2  CPROD    CPTRIA1  CPTRIA2  CPTRPLT  CPTUBE 
 24    WTMASS   
 26    NODJE
 29    PAERO1   PAERO2   PAERO3   PAERO4   PAERO5   
 32    SET1     SET2     SPLINE1  SPLINE2  SPLINE3  
 34    MKAERO1  MKAERO2 
 35    AEFACT   
 36    FLFACT   FLUTTER 
 37    AERO 
 37    CAERO1   CAERO2   CAERO3   CAERO4   CAERO5   
 38    FMETHOD$ 
 39    VREF 
 40    TF   
 41    CTYPE    CYCSEQ   CYJOIN 
 41    KINDEX   NSEGS   
 42    IREF 
 42    MAXMACH  MINMACH  MTYPE  
 42    STREAML  STREAML1 STREAML2   
 43    KGGIN
 55    SDAMP$   
 55    TABDMP1  
 56    EPOINT   
 56    SEQEP
 57    B2PP$
 57    DMIG 
 57    K2PP$
 57    M2PP$
 57    TF$  
 58    EIGR 
 59    METHOD$  
 60    EIGC     EIGP
 61    CMETHOD$ 
 62    HFREQ
 62    LFREQ    LMODES  
 62    KDAMP
$$$$
$*FILE BITS 
 94    BGPDT    CSTM     EQEXIN   GPDT     GPL      SIL 
 95    ECT  
 96    GPTT 
 97    EST      GEI      GPECT    MPTX     PCOMPS   EPTX
 98    KGGX 
 99    MGG  
100    KGG  
101    ASET     RG       USET     OGPST 
102    GPST 
103    GM   
104    KNN      MNN 
105    KFF      KFS      MFF
106    KAA  
107    OGPWG
108    OPHIG    PPHIG   
109    KTOTAL   
110    ELSETZ   GPSETZ   PLTPARZ  PLTSETZ   
111    EED      EQDYN    GPLD     SILD     TFPOOL   USETD   
112    LAMA     PHIA
113    GO       KOO      LOO
114    B2PP     K2PP     M2PP   
115    GMD      GOD 
116    PHIDH    MHH      KHH      BHH   
117    CLAMA    OCEIGS   PHIH   
118    CPHID
119    OPHIH
120    CPHIP    QPC 
121    PLOTZ
122    KDICT    KELM     MDICT    MELM  
123    MAA  
124    ACPT     AERO     FLIST  
125    ELSETS   GPSETS   PLTPAR   PLTSETX   
126    GTKA 
127    AJJL     D1JK     D2JK     SKJ   
128    D1JE     D2JE
129    BXHH 
129    FSAVE    KXHH     MXHH   
130    CASEYY   CLAMAL   OVG      PHIHL 
131    CLAMAL1  CPHIH1  
132    CPHIA
133    CASEZZ   
134    PLOTX1   
135    RP   
136    MI   
137    OCPHIP   OEFC1    OESC1    OQPC1   PCPHIP
138    QHHL 
139    B2DD     K2DD     M2DD   
140    CYCD 
141    KKK      MKK 
142    LAMK     OEIGS    PHIK   
143    PHIG 
144    PVECT
145    PHIAX
146    XYPLTCE  
147    PLOTX3   
$*  
=PAGE=
DISP0

APR.93    <== THE YEAR OF THIS DATE MUST MATCH THE NASTRAN RELEASE YEAR
$$$$$$$$  THIS BEGINS THE 1ST PART OF THE RIGID FORMAT  
BEGIN     DISP0 - DUMMY RIGID FORMAT TO ILLUSTRATE HOW TO WRITE A NEW R.F $
          REFERENCE: "THE DESIGN AND USAGE OF THE NEW DATA MANAGEMENT FEATURES
                      IN NASTRAN" BY P. R. PAMIDI AND W. K. BROWN, PP.11-25,
                      12TH NASTRAN USERS' COLLOQUIUM, MAY 1984 (NASA C.P. 2328)
          UPDATE SUBROUTINES XRGDFM AND XCSA, AND RELINK LINK1 TO INCLUDE NEW
                 SOLUTION NUMBER AND ITS ANALYSIS HEADING.
          NOTE:  IN THIS WRITE-UP, RIGID FORMAT CARDS ARE IN UPPER CASE LETTERS,
                 AND COMMENTS ARE IN LOWER CASE, OR AFTER <==, OR <<<
          WRITTEN BY G.CHAN/UNISYS 7/1990. (PLEASE INFORM ME IF ERROR IS FOUND)
$$$$      SYMBOL OF 4 OR MORE $ IS A COMMENT LINE. BLANK LINE IS NOT ALLOWED.
MODULE1   I1,,/O1//*P1* <== SEE NASTRAN USER'S MANUAL FOR DMAP RULES 
                        <   DMAP NAME BEGINS ON COLUMN 1 (VALID UP TO COL. 72)
                        <<< NEXT 7 CARDS BEGIN WITH '****'. THEY CAN BE SKIPPED
                        <   IF RESTART AND/OR SUBSTRUCTURE ARE NOT INVOLVED.
****CARD  1-20,30,40    <== RESTART INPUT DATA CHANGE INFORMATION
****FILE  100-103,110   <== RESTART DATA  FILE CHANGE INFORMATION
                        <   THE ABOVE CHANGE INFORMATION IS USED SUBSEQUENTLY
                        <   TO DETERMINE THE DMAP STATEMENTS TO BE FLAGGED FOR
                        <   EXECUTION IN MODIFIED RESTART.
****SBST  1,2,9         <== DMAP SEQUENCE SUBSET CONTROL (1 THRU 9).
                        <   THIS DMAP IS DELETED IF USER SPECIFIED A SUBSET NO.
                        <   ON SOL CARD THAT MATCHES THE NO. ON THIS SBST LINE. 
****RFMT  188,200-204   <== RESTART RIGID FORMAT SWITCH:
                        <   187-204 FOR APPROACH DISP, 207-209 FOR APP HEAT,
                        <   AND 214-215 FOR APPROACH AERO.
                        <   THIS DMAP IS FLAGGED FOR EXECUTION IN A MODIFIED 
                        <   RESTART IF THE PREVIOUS CHECKPOINT RUN HAD A R.F.
                        <   NO. LISTED ON THIS RFMT LINE.
****PHS1  I1            <== PHSI IS SUBSTRUCTURE PHASE NUMBER CONTROL (I=1,2,3).
****PHS2  DB5           <   MUST BE FOLLOWED BY IN, DN, DBN, OR DEN FLAGS, WHERE
****PHS3  D7            <   N=1 FOR PHASE 1, 5 OR 8 PHASE 2, 1 OR 7 PHASE 3
                        <   (REFERING TO ASCM01, 05, 07 OR 08 SUBROUTINES).
                        <   'I' IN 'IN' INDICATES INSERT AFTER THIS DMAP.
                        <   'D' IN 'DN' INDICATES DELETION OR REPLACEMENT BY A
                        <   DMAP ALTER. 'DBN' AND 'DEN' ARE BEGIN AND END OF
                        <   DELETION/REPLACEMENT BY GROUP OF CONTIGUOUS D.ALTER
                        <   (CURRENTLY SUBSTRUCTURE IN APP DISP1,2,3,8,9 ONLY)
$$$$      IMPORTANT. A COMMENT LINE IS NEEDED BEFORE A NEW DMAP LINE.
MODULES2  I2/O2/*P2* $  <== '$' ON DMAP LINE IS OPTIONAL
****CARD  1-40,45
****FILE  101,111
$$$$ *    '*' ON A 4-DOLLAR COMMENT LINE IS COSMETIC
           :
           :
$$$$ 
END
****CARD   ...
****RFMT   ...
$$$$
$$$$   THIS COMMENT IS NEEDED BEFORE THE 2ND PART OF THE RIGID FORMAT BY $*CARD
$*CARD BITS      <== CARD NAME TALBE, 1 THRU 93, FOR MODIFIED RESTART ONLY
$$$$
1         AXIC    AXIF    CELAS1   CELAS2   <== FREE FIELD, ALPHA-NUMERIC,
2         ADUM1   CDUM1   ETC               <   UP TO 8 CHARACTER CARD NAMES
:         SPC
           :
          SPC$   <== ITEM FOLLOWED BY $ INDICATES CASE CONTROL RELATED CARD NAME
           :
93         :
$$$$   THIS COMMENT IS NEEDED BEFORE THE 3RD PART OF THE RIGID FORMAT BY $*FILE
$*FILE BITS      <== FILE NAME TABLE, 94 THRU 186, FOR MODIFIED RESTART ONLY
$$$$
94        SLT     GPTT   <== FREE FIELD, ALPHA-NUMERIC, UP TO 8 CHARACTER GINO
95        KGGX    GPST   <   FILE NAMES
          ETC
           :
186        :
$*        THIS VERY LAST LINE IS NEEDED.       
=PAGE=
DISP1

APR.93  
$$$$$$$$
$$$$ NOTE: The DMAP sequence for static analysis involves use of parameters 
$$$$ INTERACT and SYS21. These parameters are of relevance only when the 
$$$$ primary purpose of the user is to make interactive restart runs. (The two 
$$$$ parameters are then specified via the PARAM card in the bulk data deck.) 
$$$$ However, these two parameters are not required for normal non-interactive 
$$$$ batch runs. Consequently, the rigid format DMAP listing shown here was 
$$$$ generated by not specifying those parameters (via the PARAM bulk data 
$$$$ card). As a result, the COMPOFF and COMPON instructions using those  
$$$$ parameters assume a value of 0 for these parameters (see Volume I, 
$$$$ Section 5.7). 
BEGIN    DISP 01 - STATIC ANALYSIS - APR. 1993 $   
****CARD   1- 20, 22- 24, 28, 31, 59- 62
****RFMT 188-204,207-209
$$$$
FILE     OPTP2=SAVE/EST1=SAVE $ 
****SBST   9
****CARD   1- 20, 22- 24, 28, 31, 59- 62
****RFMT 188-204,207-209
$$$$
FILE     QG=APPEND/PGG=APPEND/UGV=APPEND/GM=SAVE/KNN=SAVE $ 
****SBST   1,  3
****CARD   1- 20, 22- 24, 28, 31, 59- 62
****RFMT 188-204,207-209
$$$$
SETVAL   //V,Y,INTERACT/0/V,Y,SYS21/0 $ 
****CARD   1- 20, 22- 24, 28, 31, 59- 62
$$$$
PARAM    //*MPY*/CARDNO/0/0 $   
****CARD   1-  3,  5- 10, 14, 15, 18, 19, 22- 24, 28, 61
****FILE 101,114,119,121-125,127
****PHS1   I1   
$$$$
$$$$ COMPOFF causes the DMAP compiler to compile the next instruction as the 
$$$$ parameter INTERACT is 0. (See NOTE above.)
COMPOFF  1,INTERACT $   
****CARD   1- 20, 22- 24, 28, 31, 59- 62
$$$$
PRECHK   ALL $  
****CARD   1- 20, 22- 24, 28, 31, 59- 62
****RFMT 188-204,207-209
$$$$
$$$$ COMPON causes the DMAP compiler to skip the compilation of the next 
$$$$ instruction as the parameter INTERACT is 0. (See NOTE above.) 
COMPON   1,INTERACT $   
****CARD   1- 20, 22- 24, 28, 31, 59- 62
$$$$
PRECHK   BGPDT,EQEXIN,SIL,SIP,ECT,GPECT,
         OUGV1,OES1,OEF1,OPG1,OQG1,PUGV1,   
         OUGV2,OES2,OEF2,OPG2,OQG2,DUMMY,   
         OES1L,OEF1L,ONRGY1 $   
****CARD   1- 20, 22- 24, 28, 31, 59- 62
$$$$
$$$$ COMPOFF causes the DMAP compiler to compile all of the following 
$$$$ instructions through LABEL LBLINT02 as the parameter SYS21 is 0. (See 
$$$$ NOTE above.)
COMPOFF  LBLINT02,SYS21 $    
****CARD   1-20,22-24,28,31,59-62   
$$$$ GP1 generates coordinate system transformation matrices, tables of grid 
$$$$ point locations, and tables relating the internal and external grid point 
$$$$ numbers.
GP1      GEOM1,GEOM2,/GPL,EQEXIN,GPDT,CSTM,BGPDT,SIL/S,N,LUSET/ 
         NOGPDT/ALWAYS=-1 $ 
****CARD   1
****FILE  94
****PHS2   D5   
$$$$
$$$$ PLTTRAN modifies special scalar grid points in the BGPDT and SIL tables.
PLTTRAN  BGPDT,SIL/BGPDP,SIP/LUSET/S,N,LUSEP $  
****CARD   1
****FILE 129
$$$$
$$$$ GP2 generates Element Connection Table with internal indices.
GP2      GEOM2,EQEXIN/ECT $ 
****CARD   1,  2,  4,  5, 16
****FILE  95
$$$$
PARAML   PCDB//*PRES*////JUMPPLOT $ 
****SBST   7
****CARD  18
****FILE 115,120
****PHS2   DB5  
$$$$
PURGE    PLTSETX,PLTPAR,GPSETS,ELSETS/JUMPPLOT $
****SBST   7
****CARD  18
****FILE 115
$$$$
$$$$ Go to label P1 if there are no structure plot requests.
COND     P1,JUMPPLOT $  
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 115,120
$$$$
$$$$ PLTSET transforms user input into a form used to drive the structure 
$$$$ plotter.
PLTSET   PCDB,EQEXIN,ECT,EPT/PLTSETX,PLTPAR,GPSETS,ELSETS/S,N,NSIL/ 
         S,N,JUMPPLOT $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 115
$$$$
$$$$ PRTMSG prints error messages associated with the structure plotter.
PRTMSG   PLTSETX// $
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 115
$$$$
PARAM    //*MPY*/PLTFLG/1/1 $   
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 120
$$$$
PARAM    //*MPY*/PFILE/0/0 $
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 120
$$$$
$$$$ Go to label P1 if no undeformed structure plots are requested.
COND     P1,JUMPPLOT $  
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 120
$$$$
$$$$ PLOT generates all requested undeformed structure plots.
PLOT     PLTPAR,GPSETS,ELSETS,CASECC,BGPDT,EQEXIN,SIL,,ECT,,,,/PLOTX1/
         NSIL/LUSET/S,N,JUMPPLOT/S,N,PLTFLG/S,N,PFILE  $
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 120
$$$$
$$$$ PRTMSG prints plotter data and engineering data for each undeformed plot 
$$$$ generated.
PRTMSG   PLOTX1// $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 120
$$$$
LABEL    P1 $   
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 115,120
****PHS2   DE5  
$$$$
$$$$ GP3 generates Static Loads Table and Grid Point Temperature Table.
GP3      GEOM3,EQEXIN,GEOM2/SLT,GPTT/S,N,NOGRAV/NEVER=1 $   
****CARD   1,  2, 13, 60, 61
****FILE  96
$$$$
PARAM    //*AND*/NOMGG/NOGRAV/V,Y,GRDPNT=-1 $   
****CARD   1,  2, 15, 61
****FILE  96, 99
****RFMT 188-204,207-209
$$$$
$$$$ TA1 generates element tables for use in matrix assembly and stress 
$$$$ recovery.
TA1      ECT,EPT,BGPDT,SIL,GPTT,CSTM,MPT,EQEXIN/EST,GEI,GPECT,,,MPTX,
         PCOMPS,EPTX/LUSET/S,N,NOSIMP/1/S,N,NOGENL/GENEL/S,N,COMPS $   
****CARD   1-  6, 13, 16
****FILE  97
$$$$
EQUIV    MPTX,MPT/COMPS/EPTX,EPT/COMPS $
****CARD   1- 20, 22- 24, 28, 31, 59- 62
****FILE  97
$$$$
PARAM    //*AND*/NOELMT/NOGENL/NOSIMP $ 
****CARD   1,  2,  4-  6, 16
****FILE  97
****PHS2   DB5  
****RFMT 188-204,207-209
$$$$
$$$$ Go to label ERROR4 and print Error Message No. 4 if no elements have been 
$$$$ defined.
COND     ERROR4,NOELMT $
****CARD   1,  2,  4-  6, 16
****FILE  97
****PHS2   DE5  
****RFMT 188-204,207-209
$$$$
PURGE    KGGX/NOSIMP $  
****CARD   1,  2,  4-  6, 16
****FILE  98
$$$$
$$$$ OPTPR1 performs phase one property optimization and initialization check.
OPTPR1   MPT,EPT,ECT,DIT,EST/OPTP1/S,N,PRINT/S,N,TSTART/S,N,COUNT $ 
****SBST   9
****CARD   1-  6,  8, 13
****FILE 117
$$$$
$$$$ Beginning of loop for property optimization.
LABEL    LOOPTOP $  
****SBST   9
****CARD   1-  6
****FILE 117
$$$$
$$$$ Go to label LBL1 if there are no structural elements.
COND     LBL1,NOSIMP $  
****CARD   1-  3,  5,  6,  8, 13- 16, 24, 61
****FILE  98, 99,116,121
$$$$
PARAM    //*ADD*/NOKGGX/1/0 $   
****CARD   1-  3,  6,  8
****FILE  98
$$$$
EQUIV    OPTP1,OPTP2/NEVER/EST,EST1/NEVER $ 
****SBST   9
****CARD   1-  6, 13, 16
****FILE 118
****PHS2   D5   
$$$$
$$$$ EMG generates structural element stiffness and mass matrix tables and 
$$$$ dictionaries for later assembly by the EMA module.
EMG      EST,CSTM,MPT,DIT,GEOM2,/KELM,KDICT,MELM,MDICT,,,/S,N,NOKGGX/   
         S,N,NOMGG////C,Y,COUPMASS/C,Y,CPBAR/   
         C,Y,CPROD/C,Y,CPQUAD1/C,Y,CPQUAD2/C,Y,CPTRIA1/C,Y,CPTRIA2/ 
         C,Y,CPTUBE/C,Y,CPQDPLT/C,Y,CPTRPLT/C,Y,CPTRBSC/
         C,Y,VOLUME/C,Y,SURFACE $   
****CARD   1-  3,  5,  6,  8, 13, 15, 24, 61
****FILE 116
$$$$
$$$$ Go to label JMPKGG if no stiffness matrix is to be assembled.
COND     JMPKGG,NOKGGX $
****CARD   1-  3,  6,  8
****FILE  98
$$$$                                  x
$$$$ EMA assembles stiffness matrix [K  ] and Grid Point Singularity Table.
$$$$                                  gg
$$$$ 
EMA      GPECT,KDICT,KELM/KGGX $
****CARD   1-  3,  6,  8
****FILE  98
$$$$
LABEL    JMPKGG $   
****CARD   1-  3,  6,  8
****FILE  98
$$$$
PURGE    MGG/NOMGG $
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE  99
$$$$
$$$$ Go to label JMPMGG if no mass matrix is to be assembled.
COND     JMPMGG,NOMGG $ 
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE  99
$$$$
$$$$ EMA assembles mass matrix [M  ].
$$$$                             gg
$$$$ 
EMA      GPECT,MDICT,MELM/MGG/-1/C,Y,WTMASS=1.0 $   
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE  99
$$$$
PURGE    MDICT,MELM/ALWAYS $
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE 116
$$$$
LABEL    JMPMGG $   
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE  99
$$$$
$$$$ Go to label LBL1 if no weight and balance information is requested.
COND     LBL1,GRDPNT $  
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE 121
$$$$
$$$$ Go to label ERROR2 and print Error Message No. 2 if no mass matrix 
$$$$ exists.
COND     ERROR2,NOMGG $ 
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE 121
$$$$
$$$$ GPWG generates weight and balance information.
GPWG     BGPDP,CSTM,EQEXIN,MGG/OGPWG/V,Y,GRDPNT/C,Y,WTMASS $
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE 121
$$$$
$$$$ OFP formats the weight and balance information prepared by GPWG and 
$$$$ places it on the system output file for printing.
OFP      OGPWG,,,,,//S,N,CARDNO $   
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE 121
$$$$
LABEL    LBL1 $ 
****CARD   1-  3,  5,  8, 13- 16, 24, 61
****FILE  98, 99,116,121
$$$$               x
$$$$ Equivalence [K  ] to [K  ] if no general elements exist.
$$$$               gg       gg
$$$$ 
EQUIV    KGGX,KGG/NOGENL $  
****CARD   1-  4,  6,  8
****FILE 100
****PHS2   DB5  
$$$$
$$$$ Go to label LBL11A if no general elements exist.
COND     LBL11A,NOGENL $
****CARD   1-  4,  6,  8
****FILE 100
$$$$                                 x
$$$$ SMA3 adds general elements to [K  ] to obtain stiffness matrix [K  ].
$$$$                                 gg                               gg
$$$$ 
SMA3     GEI,KGGX/KGG/LUSET/NOGENL/NOSIMP $ 
****CARD   1-  4,  6,  8
****FILE 100
$$$$
LABEL    LBL11A $   
****CARD   1-  4,  6,  8
****FILE 100
$$$$
GPSTGEN  KGG,SIL/GPST $ 
****CARD   1-  4,  6,  8
****FILE 102
****PHS2   DE5  
$$$$
PARAM    //*MPY*/NSKIP/0/0 $
****CARD   1,  9- 12, 22, 23, 31, 59
****FILE 101
$$$$
$$$$ Beginning of loop for multiple constraint sets.
LABEL    LBL11 $
****SBST   1,  3
****CARD  22, 23
****FILE 101
$$$$
$$$$ GP4 generates flags defining members of various displacement sets 
$$$$ (USET), forms multipoint constraint equations [R ] {u } = 0, and forms 
$$$$                                                 g    g
$$$$ enforced displacement vector {Y }.
$$$$                                s
$$$$ 
GP4      CASECC,GEOM4,EQEXIN,GPDT,BGPDT,CSTM,GPST/RG,YS,USET,   
         ASET,OGPST/LUSET/S,N,MPCF1/S,N,MPCF2/S,N,SINGLE/S,N,OMIT/  
         S,N,REACT/S,N,NSKIP/S,N,REPEAT/S,N,NOSET/S,N,NOL/S,N,NOA/  
         C,Y,ASETOUT/C,Y,AUTOSPC $  
****CARD   1-  4,  6,  8- 12, 20, 22, 23, 28, 31, 59
****FILE 101
$$$$
OFP      OGPST,,,,,//S,N,CARDNO $   
****CARD   1-  4,  6,  8- 12, 22, 23, 28
****FILE 101
$$$$
$$$$ Go to label ERROR3 and print Error Message No. 3 if no independent 
$$$$ degrees of freedom are defined.
COND     ERROR3,NOL $   
****CARD   1,  9- 12, 22, 23, 59
****FILE 101
****RFMT 188-204,207-209
****PHS1   I1   
$$$$
PARAM    //*AND*/NOSR/SINGLE/REACT $
****CARD   1,  9- 12, 22, 23, 59
****FILE 101
$$$$
PURGE    KRR,KLR,QR,DM/REACT/GM/MPCF1/GO,KOO,LOO,PO,UOOV,RUOV/OMIT/PS,  
         KFS,KSS/SINGLE/QG/NOSR $   
****CARD   1,  9- 12, 22, 23, 59
****FILE 103,105-107,109,111,113
$$$$
$$$$ Equivalence [K  ] to [K  ] if no multipoint constraints exist.
$$$$               gg       nn
EQUIV    KGG,KNN/MPCF1 $
****CARD   1-  4,  6,  8,  9, 22, 23
****FILE 104
$$$$
$$$$ Go to label LBL2 if the MPC set for the current pass is unchanged from 
$$$$ that of the previous pass.
COND     LBL2,MPCF2 $   
****CARD   1-  4,  6,  8,  9, 22, 23
****FILE 103,104
$$$$
$$$$ MCE1 partitions multipoint constraint equations [R ] = [R |R ] and
$$$$                                                   g      m  n
$$$$ solves for multipoint constraint transformation matrix [G ] =
$$$$       -1                                                 m
$$$$ - [R ]   [R ].
$$$$     m      n
$$$$ 
MCE1     USET,RG/GM $   
****CARD   1,  9, 22, 23
****FILE 103
$$$$
$$$$ MCE2 partitions stiffness matrix
$$$$ 
$$$$           +_      +
$$$$           |K  |K  |
$$$$           | nn| nm|
$$$$   [K  ] = |---+---|
$$$$     gg    |K  |K  |
$$$$           | mn| mm|
$$$$           +       +
$$$$           
$$$$ and performs the matrix reduction
$$$$ 
$$$$            _        T           T           T
$$$$   [K  ] = [K  ] + [G ][K  ] + [K  ][G ] + [G ][K  ][G ]
$$$$     nn      nm      m   mn      mn   m      m   mm   m
$$$$ 
MCE2     USET,GM,KGG,,,/KNN,,, $
****CARD   1-  4,  6,  8,  9, 22, 23
****FILE 104
$$$$
LABEL    LBL2 $ 
****CARD   1-  4,  6,  8,  9, 22, 23
****FILE 103,104
$$$$
$$$$ Equivalence [K  ] to [K  ] if no single-point constraints exist.
$$$$               nn       ff
$$$$ 
EQUIV    KNN,KFF/SINGLE $   
****CARD   1-  4,  6,  8- 10, 22, 23
****FILE 105
$$$$
$$$$ Go to label LBL3 if no single-point constraints exist.
COND     LBL3,SINGLE $  
****CARD   1-  4,  6,  8- 10, 22, 23
****FILE 105
$$$$
$$$$ SCE1 partitions out single-point constraints.
$$$$ 
$$$$           +       +
$$$$           |K  |K  |
$$$$           | ff| fs|
$$$$   [K  ] = |---+---|
$$$$     nn    |K  |K  |
$$$$           | sf| ss|
$$$$           +       +
$$$$ 
SCE1     USET,KNN,,,/KFF,KFS,KSS,,, $   
****CARD   1-  4,  6,  8- 10, 22, 23
****FILE 105
$$$$
LABEL    LBL3 $ 
****CARD   1-  4,  6,  8- 10, 22, 23
****FILE 105
$$$$
$$$$ Equivalence [K  ] to [K  ] if no omitted coordinates exist.
$$$$               ff       aa
$$$$ 
EQUIV    KFF,KAA/OMIT $ 
****CARD   1-  4,  6,  8- 11, 22, 23
****FILE 106
$$$$
$$$$ Go to label LBL5 if no omitted coordinates exist.
COND     LBL5,OMIT $
****CARD   1-  4,  6,  8- 11, 22, 23
****FILE 106
$$$$
$$$$ SMP1 partitions constrained stiffness matrix
$$$$
$$$$           +_      +
$$$$           |K  |K  |
$$$$           | aa| ao|
$$$$   [K  ] = |---+---|
$$$$     ff    |K  |K  |
$$$$           | oa| oo|
$$$$           +       +
$$$$           
$$$$                                                -1
$$$$ solves for transformation matrix [G ] = -[[K  ]  [K  ]
$$$$                                    o        oo     oa
$$$$ 
$$$$                                        _        T
$$$$ and performs matrix reduction [K  ] = [K  ] + [K  ]{G ].
$$$$                                 aa      aa      oa   o
$$$$ 
SMP1     USET,KFF,,,/GO,KAA,KOO,LOO,,,,, $  
****CARD   1-  4,  6,  8- 11, 22, 23
****FILE 106
$$$$
LABEL    LBL5 $ 
****CARD   1-  4,  6,  8- 11, 22, 23
****FILE 106
$$$$
$$$$ Equivalence [K  ] to [K  ] if no free-body supports exist.
$$$$               aa       ll
$$$$ 
EQUIV    KAA,KLL/REACT $
****CARD   1-  4,  6,  8- 12, 22, 23
****FILE 107
****PHS1   DB1  
****PHS3   DB1  
$$$$
$$$$ Go to label LBL6 if no free-body supports exist.
COND     LBL6,REACT $   
****CARD   1-  4,  6,  8- 12, 22, 23
****FILE 107
$$$$
$$$$ RBMG1 partitions out free-body supports
$$$$ 
$$$$           +       +
$$$$           |K  |K  |
$$$$           | ll| lr|
$$$$   [K  ] = |---+---|
$$$$     aa    |K  |K  |
$$$$           | rl| rr|
$$$$           +       +
$$$$ 
RBMG1    USET,KAA,/KLL,KLR,KRR,,, $ 
****CARD   1-  4,  6,  8- 12, 22, 23
****FILE 107
$$$$
LABEL    LBL6 $ 
****CARD   1-  4,  6,  8- 12, 22, 23
****FILE 107
$$$$
$$$$ RBMG2 decomposes constrained stiffness matrix [K  ] = [L  ][U  ].
$$$$                                                 ll      ll   ll
$$$$ 
RBMG2    KLL/LLL $  
****CARD   1-  4,  6,  8- 12, 22, 23
****FILE 108
$$$$
$$$$ Go to label LBL7 if no free-body supports exist.
COND     LBL7,REACT $   
****CARD   1-  4,  6,  8- 12, 22, 23
****FILE 109
$$$$
$$$$ RBMG3 forms rigid body transformation matrix
$$$$ 
$$$$               -1
$$$$   [D] = -[K  ]  [K  ]
$$$$            ll     lr
$$$$ 
$$$$ calculates rigid body check matrix
$$$$
$$$$                   T
$$$$   [X] = [K  ] + [K  ][D]
$$$$           rr      lr
$$$$ 
$$$$ and calculates rigid body error ratio
$$$$ 
$$$$ 
$$$$               ||X||
$$$$   epsilon = ---------
$$$$              ||K  ||
$$$$                 rr
$$$$ 
RBMG3    LLL,KLR,KRR/DM $   
****CARD   1-  4,  6,  8- 12, 22, 23
****FILE 109
$$$$
LABEL    LBL7 $ 
****CARD   1-  4,  6,  8- 12, 22, 23
****FILE 109
****PHS1   DE1  
****PHS3   DE1  
$$$$
$$$$ SSG1 generates static load vectors {P }.
$$$$                                      g
$$$$ 
SSG1     SLT,BGPDT,CSTM,SIL,EST,MPT,GPTT,EDT,MGG,CASECC,DIT,PCOMPS/ 
         PG,,,,/LUSET/NSKIP/COMPS $ 
****CARD   1-  3,  5,  6,  8, 13, 22, 23, 59- 62
****FILE 110
$$$$
$$$$ Equivalence {P } to {P } if no constraints are applied.
$$$$               g       l
$$$$ 
EQUIV    PG,PL/NOSET $  
****CARD   1-  3,  5,  6,  8- 13, 22, 23, 59- 62
****FILE 111
****PHS1   DB1  
$$$$
$$$$ Go to label LBL10 if no constraints are applied.
COND     LBL10,NOSET $  
****CARD   1-  3,  5,  6,  8- 13, 22, 23, 59- 62
****FILE 111
****PHS3   DB7  
$$$$
$$$$ SSG2 applies constraints to static load vectors
$$$$ 
$$$$           _
$$$$           P
$$$$            n            _       T
$$$$   {P } = {--} , {P } = {P } + [G ]{P }
$$$$     g     P       n      n      m   m
$$$$            m
$$$$ 
$$$$           _
$$$$           P
$$$$            f            _      
$$$$   {P } = {--} , {P } = {P } - [K  ]{Y }
$$$$     n     P       f      f      fs   s
$$$$            s
$$$$ 
$$$$           _
$$$$           P
$$$$            a            _       T
$$$$   {P } = {--} , {P } = {P } + [G ]{P }
$$$$     f     P       a      a      o   o
$$$$            o
$$$$ 
$$$$           P
$$$$            l
$$$$   {P } = {--}
$$$$     a     P
$$$$            r
$$$$                                                                T
$$$$ and calculates determinate forces of reaction {q } = -{P } - [D ]{P }.
$$$$                                                 r       r          l
$$$$ 
SSG2     USET,GM,YS,KFS,GO,DM,PG/QR,PO,PS,PL $  
****CARD   1-  3,  5,  6,  8- 13, 22, 23, 59- 62
****FILE 111
$$$$
LABEL    LBL10 $
****CARD   1-  3,  5,  6,  8- 13, 22, 23, 59- 62
****FILE 111
$$$$
$$$$ SSG3 solves for displacements of independent coordinates
$$$$ 
$$$$               -1
$$$$   {u } = [K  ]  {P }
$$$$     l      ll     l
$$$$ 
$$$$ solves for displacements of omitted coordinates
$$$$ 
$$$$     o         -1
$$$$   {u } = [K  ]  {P }
$$$$     o      oo     o
$$$$ 
$$$$ calculates residual vector (RULV) and residual vector error ratio for 
$$$$ independent coordinates
$$$$ 
$$$$   {deltaP } = {P } - [K  ]{u }
$$$$          l      l      ll   l
$$$$ 
$$$$                T
$$$$              {u }{deltaP }
$$$$                l        l
$$$$   epsilon  = -------------
$$$$          l       T
$$$$                {P }{u }
$$$$                  l   l
$$$$ 
$$$$ and calculates residual vector (RUOV) and residual vector error ratio for
$$$$ omitted coordinates
$$$$                             o
$$$$   {deltaP } = {P } - [K  ]{u }
$$$$          o      o      oo   o
$$$$ 
$$$$                T
$$$$              {u }{deltaP }
$$$$                o        o
$$$$   epsilon  = -------------
$$$$          o       T   o
$$$$                {P }{u }
$$$$                  o   o
$$$$ 
SSG3     LLL,KLL,PL,LOO,KOO,PO/ULV,UOOV,RULV,RUOV/OMIT/V,Y,IRES=-1/ 
         NSKIP/S,N,EPSI $   
****CARD   1-  6,  8- 13, 17, 22, 23, 59- 62
****FILE 112
****RFMT 188
$$$$
$$$$ Go to label LBL9 if residual vectors are not to be printed.
COND     LBL9,IRES $
****CARD   1-  6,  8- 13, 17, 22, 23, 59- 62
****RFMT 188-204,207-209
$$$$
$$$$ MATGPR prints the residual vector for independent coordinates (RULV).
MATGPR   GPL,USET,SIL,RULV//*L* $   
****CARD   1-  6,  8- 13, 17, 22, 23, 59- 62
****RFMT 188-204,207-209
$$$$
$$$$ MATGPR prints the residual vector for omitted coordinates (RUOV).
MATGPR   GPL,USET,SIL,RUOV//*O* $   
****CARD   1-  6,  8- 13, 17, 22, 23, 59- 62
****RFMT 188-204,207-209
$$$$
LABEL    LBL9 $ 
****CARD   1-  6,  8- 13, 17, 22, 23, 59- 62
****RFMT 188-204,207-209
****PHS3   DE7  
$$$$
$$$$ SDR1 recovers dependent displacements
$$$$ 
$$$$    u
$$$$     l                                  o
$$$$   {--} = {u }  ,   {u } = [G ]{u ] + {u }  ,
$$$$    u       a         o      o   a      o
$$$$     r
$$$$ 
$$$$    u                u            
$$$$     a                f           
$$$$   {--} = {u }  ,   {--} = {u }  ,
$$$$    u       f        Y       n    
$$$$     o                s           
$$$$                                  
$$$$                        u         
$$$$                         n        
$$$$  {u } = [G ]{u ]  ,   {--} = {u }
$$$$    m      m   n        u       g 
$$$$                         m        
$$$$ 
$$$$ and recovers single-point forces of constraint
$$$$ 
$$$$                    T
$$$$   {q } = -{P } + [K  ]{u } + [K  ]{Y }
$$$$     s       s      fs   f      ss   s
$$$$ 
SDR1     USET,PG,ULV,UOOV,YS,GO,GM,PS,KFS,KSS,QR/UGV,PGG,QG/NSKIP/  
         *STATICS* $
****CARD   1-  6,  8- 13, 22, 23, 59- 62
****FILE 113
****RFMT 188-204,207-209
****PHS3   I7     
$$$$
$$$$ Go to label LBL8 if all constraint sets have been processed.
COND     LBL8,REPEAT $  
****SBST   1,  3
****CARD  22, 23
****RFMT 188-204,207-209
$$$$
$$$$ Go to label LBL11 if additional sets of constraints need to be processed.
REPT     LBL11,360 $
****SBST   1,  3
****CARD  22, 23
****RFMT 188-204,207-209
$$$$
$$$$ Go to label ERROR1 and print Error Message No. 1 if the number of 
$$$$ constraint sets exceeds 360.
JUMP     ERROR1 $   
****SBST   1,  3
****CARD  22, 23
****RFMT 188-204,207-209
$$$$
PARAM    //*NOT*/TEST/REPEAT $  
****CARD  22, 23
****RFMT 188-204,207-209
$$$$
$$$$ Go to label ERROR5 and print Error Message No. 5 if multiple boundary 
$$$$ conditions are attempted with an improper subset.
COND     ERROR5,TEST $  
****CARD  22, 23
****RFMT 188-204,207-209
$$$$
LABEL    LBL8 $ 
****SBST   1,  3
****CARD  22, 23
****RFMT 188-204,207-209
$$$$
$$$$ GPFDR calculates the grid point force balance (OGPFB1) and element strain 
$$$$ energy (ONRGY1) for requested sets.
GPFDR    CASECC,UGV,KELM,KDICT,ECT,EQEXIN,GPECT,PGG,QG/ONRGY1,OGPFB1/   
         *STATICS* $
****CARD  18, 19
****FILE 119
****PHS2   DB5  
$$$$
PURGE    KDICT,KELM/REPEAT $
****CARD   1-  3,  6,  8, 18, 19
****FILE 116
$$$$
$$$$ OFP formats the tables prepared by GPFDR and places them on the system 
$$$$ output file for printing.
OFP      ONRGY1,OGPFB1,,,,//S,N,CARDNO $
****CARD  18, 19
****FILE 119
$$$$
$$$$ Go to label NOMPCF if no multipoint constraint force balance is 
$$$$ requested.
COND     NOMPCF,GRDEQ $ 
****CARD   7
****FILE 127
$$$$
$$$$ EQMCK calculates the force and moment equilibrium check and prepares the 
$$$$ multipoint constraint force balance (OQM1) for output.
EQMCK    CASECC,EQEXIN,GPL,BGPDT,SIL,USET,KGG,GM,UGV,PGG,QG,CSTM/   
         OQM1/V,Y,OPT=0/V,Y,GRDEQ/NSKIP $   
****CARD   7
****FILE 127
$$$$
$$$$ OFP formats the table prepared by EQMCK and places it on the system 
$$$$ output file for printing.
OFP      OQM1,,,,,//S,N,CARDNO $
****CARD   7
****FILE 127
$$$$
LABEL    NOMPCF $   
****CARD   7
****FILE 127
$$$$
$$$$ SDR2 calculates the element forces (OEF1) and stresses (OES1) and 
$$$$ prepares load vectors (OPG1), displacement vectors (OUGV1), and single-
$$$$ point forces of constraint (OQG1) for output and translation components 
$$$$ of the displacement vectors (PUGV1).
SDR2     CASECC,CSTM,MPT,DIT,EQEXIN,SIL,GPTT,EDT,BGPDP,,QG,UGV,EST, 
         XYCDB,PGG,PCOMPS/OPG1,OQG1,OUGV1,OES1,OEF1,PUGV1,OES1L,
         OEF1L/*STATICS*/S,N,NOSORT2/-1/S,N,STRNFLG/COMPS $ 
****CARD  18, 19
****FILE 114
$$$$
$$$$ Go to label LBLSTRS if element stresses in material coordinate system and 
$$$$ stresses at the connected grid points are not to be calculated.
COND     LBLSTRS,STRESS $   
****CARD  18, 19
****FILE 122
$$$$
$$$$ CURV calculates element stresses in material coordinate system (OES1M) 
$$$$ and stresses at the connected grid points (OES1G).
CURV     OES1,MPT,CSTM,EST,SIL,GPL/OES1M,OES1G/V,Y,STRESS/  
         V,Y,NINTPTS $  
****CARD  18, 19
****FILE 122
$$$$
LABEL    LBLSTRS $  
****CARD  18, 19
****FILE 122
$$$$
PURGE    OES1M/STRESS $ 
****CARD  18, 19
****FILE 122
$$$$
$$$$ Go to label LBLSTRN if element strains/curvatures are not to be 
$$$$ calculated.
COND     LBLSTRN,STRNFLG $  
****CARD  18, 19
****FILE 123,124
$$$$
$$$$ SDR2 calculates element strains/curvatures (OES1A).
SDR2     CASECC,CSTM,MPT,DIT,EQEXIN,SIL,GPTT,EDT,BGPDT,,,UGV,EST,,,/
         ,,,OES1A,,,,/*STATICS*//1 $
****CARD  18, 19
****FILE 123
$$$$
$$$$ Go to label LBLSTRN if element strains/curvatures in material coordinate 
$$$$ system and strains/curvatures at the connected grid points are not to be 
$$$$ calculated.
COND     LBLSTRN,STRAIN $   
****CARD  18, 19
****FILE 124
$$$$
$$$$ CURV calculates element strains/curvatures in material coordinate system 
$$$$ (OES1AM) and strains/curvatures at the connected grid points (OES1AG).
CURV     OES1A,MPT,CSTM,EST,SIL,GPL/OES1AM,OES1AG/V,Y,STRAIN/   
         V,Y,NINTPTS $  
****CARD  18, 19
****FILE 124
$$$$
LABEL    LBLSTRN $  
****CARD  18, 19
****FILE 123,124
$$$$
PURGE    OES1A/STRNFLG $
****CARD  18, 19
****FILE 123,124
$$$$
$$$$ Go to label LBL17 if there are no requests for output sorted by grid 
$$$$ point number or element number.
COND     LBL17,NOSORT2 $
****CARD  18, 19, 29
****FILE 125,126
$$$$
$$$$ SDR3 prepares requested output sorted by grid point number or element 
$$$$ number.
SDR3     OUGV1,OPG1,OQG1,OEF1,OES1,/OUGV2,OPG2,OQG2,OEF2,OES2, $
****CARD  18, 19
****FILE 125
$$$$
PARAM    //*SUB*/PRTSORT2/NOSORT2/1 $   
****CARD  18, 19
****FILE 125
$$$$
$$$$ Go to label LBLSORT1 if printed output sorted by grid point number or 
$$$$ element number is not required.
COND     LBLSORT1,PRTSORT2 $
****CARD  18, 19
****FILE 125
$$$$
$$$$ OFP formats the tables prepared by SDR3 for output sorted by grid point 
$$$$ number or element number and places them on the system output file for 
$$$$ printing.
OFP      OUGV2,OPG2,OQG2,OEF2,OES2,//S,N,CARDNO $   
****CARD  18, 19
****FILE 125
$$$$
$$$$ SCAN examines the element stresses and forces calculated by SDR3 and 
$$$$ generates scanned output that meets the specifications set by the user.
SCAN     CASECC,OES2,OEF2/OESF2/*RF* $  
****CARD 19 
****FILE 125
$$$$
$$$$ OFP formats the scanned output table prepared by SCAN and places it on 
$$$$ the system output file for printing.
OFP      OESF2,,,,,//S,N,CARDNO $   
****CARD  19
****FILE 125
$$$$
$$$$ Go to label LBLXYPLT.
JUMP     LBLXYPLT $ 
****CARD  18, 19
****FILE 125
$$$$
LABEL    LBLSORT1 $ 
****CARD  18, 19
****FILE 125
$$$$
$$$$ OFP formats the tables prepared by SDR2 for output sorted by subcase 
$$$$ number and places them on the system output file for printing.
OFP      OUGV1,OPG1,OQG1,OEF1,OES1,//S,N,CARDNO $   
****CARD  18, 19
****FILE 114
$$$$
OFP      OEF1L,OES1L,,,,//S,N,CARDNO $  
****CARD  18, 19
****FILE 114
$$$$
$$$$ SCAN examines the element stresses and forces calculated by SDR2 and 
$$$$ generates scanned output that meets the specifications set by the user.
SCAN     CASECC,OES1,OEF1/OESF1/*RF* $  
****CARD 19 
****FILE 114
$$$$
$$$$ OFP formats the scanned output table prepared by SCAN and places it on 
$$$$ the system output file for printing.
OFP      OESF1,,,,,//S,N,CARDNO $   
****CARD  19
****FILE 114
$$$$
LABEL    LBLXYPLT $ 
****CARD  18, 19
****FILE 125
$$$$
$$$$ OFP formats the tables prepared by CURV and SDR2 for output sorted by 
$$$$ subcase number and places them on the system output file for printing.
OFP      OES1M,OES1G,OES1A,OES1AM,OES1AG,//S,N,CARDNO $ 
****CARD  18, 19
****FILE 114
$$$$
$$$$ XYTRAN prepares the input for requested X-Y plots.
XYTRAN   XYCDB,OPG2,OQG2,OUGV2,OES2,OEF2/XYPLTT/*TRAN*/*PSET*/S,N,  
         PFILE/S,N,CARDNO $ 
****SBST   7
****CARD  29
****FILE 126
$$$$
$$$$ XYPLOT prepares the requested X-Y plots of displacements, forces, 
$$$$ stresses, loads, and single-point forces of constraint vs. subcase.
XYPLOT   XYPLTT// $ 
****SBST   7
****CARD  29
****FILE 126
$$$$
$$$$ Go to label DPLOT.
JUMP     DPLOT $
****SBST   7
****CARD  29
****FILE 126
$$$$
LABEL    LBL17 $
****CARD  18, 19, 29
****FILE 125,126
$$$$
PURGE    OUGV2/NOSORT2 $
****CARD  18, 19
****FILE 125,126
$$$$
$$$$ Go to label LBLOFP if there is no phase two property optimization.
COND     LBLOFP,COUNT $ 
****SBST   9
****CARD  18, 19
****FILE 118
$$$$
$$$$ OPTPR2 performs phase two property optimization.
OPTPR2   OPTP1,OES1,EST/OPTP2,EST1/S,N,PRINT/TSTART/S,N,COUNT/S,N,  
         CARDNO $   
****SBST   9
****CARD  18, 19
****FILE 118
$$$$
$$$$ Equivalence EST2 to EST and OPTP2 to OPTP1.
EQUIV    EST1,EST/ALWAYS/OPTP2,OPTP1/ALWAYS $   
****SBST   9
****CARD  18, 19
****FILE  97,117
$$$$
$$$$ Go to label LOOPEND if no additional output is to be printed for this 
$$$$ loop.
COND     LOOPEND,PRINT $
****SBST   9
****CARD  18, 19
****FILE 118,128
$$$$
LABEL    LBLOFP $   
****SBST   9
****CARD  18, 19
****FILE 118
$$$$
$$$$ OFP formats the tables prepared by SDR2 for output sorted by subcase 
$$$$ number and places them on the system output file for printing.
OFP      OUGV1,OPG1,OQG1,OEF1,OES1,//S,N,CARDNO $   
****CARD  19
****FILE 114
$$$$
OFP      OEF1L,OES1L,,,,//S,N,CARDNO $  
****CARD  19
****FILE 114
$$$$
$$$$ SCAN examines the element stresses and forces calculated by SDR2 and 
$$$$ generates scanned output that meets the specifications set by the user.
SCAN     CASECC,OES1,OEF1/OESF1X/*RF* $ 
****CARD 19 
****FILE 114
$$$$
$$$$ OFP formats the scanned output table prepared by SCAN and places it on 
$$$$ the system output file for printing.
OFP      OESF1X,,,,,//S,N,CARDNO $  
****CARD  19
****FILE 114
$$$$
$$$$ OFP formats the tables prepared by CURV and SDR2 for output sorted by 
$$$$ sucbcase number and places them on the system output file for printing.
OFP      OES1M,OES1G,OES1A,OES1AM,OES1AG,//S,N,CARDNO $ 
****CARD  19
****FILE 122-124
$$$$
LABEL    DPLOT $
****SBST   7
****CARD  18, 29
****FILE 126
$$$$
$$$$ Go to label P2 if no deformed structure plots are requested.
COND     P2,JUMPPLOT $  
****SBST   7
****CARD  18
****FILE 128
$$$$
$$$$ PLOT generates all requested deformed structure and contour plots.
PLOT     PLTPAR,GPSETS,ELSETS,CASECC,BGPDT,EQEXIN,SIP,PUGV1,,GPECT,OES1,
         OES1L,ONRGY1/PLOTX2/NSIL/LUSEP/JUMPPLOT/PLTFLG/S,N,PFILE $ 
****SBST   7
****CARD  18, 29
****FILE 128
$$$$
$$$$ PRTMSG prints plotter data, engineering data, and contour data for each 
$$$$ deformed plot generated.
PRTMSG   PLOTX2// $ 
****SBST   7
****CARD  18, 29
****FILE 128
$$$$
LABEL    P2 $   
****SBST   7
****CARD  18
****FILE 128
$$$$
LABEL    LOOPEND $  
****SBST   9
****CARD  18, 22, 23
****FILE 128
****PHS1   DE1  
****PHS2   DE5  
$$$$
$$$$ Go to label FINIS and make normal exit if property optimization is 
$$$$ complete.
COND     FINIS,COUNT $  
****SBST   9
****CARD  18, 22, 23
$$$$
$$$$ Go to label LOOPTOP if additional loops for property optimization are 
$$$$ needed.
REPT     LOOPTOP,360 $  
****SBST   9
****CARD  18, 22, 23
$$$$
$$$$ Go to label FINIS and make normal exit.
JUMP     FINIS $
****CARD   1- 20, 22- 24, 28, 29, 31, 59- 62
****RFMT 188-204,207-209
$$$$
LABEL    ERROR1 $   
****SBST   1,  3
****CARD  22, 23
****RFMT 188-204,207-209
$$$$
$$$$ Print Error Message No. 1 and terminate execution.
PRTPARM  //-1/*STATICS* $   
****SBST   1,  3
****CARD  22, 23
****RFMT 188-204,207-209
$$$$
LABEL    ERROR2 $   
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE 121
****RFMT 188-204,207-209
$$$$
$$$$ Print Error Message No. 2 and terminate execution.
PRTPARM  //-2/*STATICS* $   
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE 121
****RFMT 188-204,207-209
$$$$ 
LABEL    ERROR3 $   
****CARD   1,  9- 12, 22, 23, 59
****FILE 101
****RFMT 188-204,207-209
$$$$
$$$$ Print Error Message No. 3 and terminate execution.
PRTPARM  //-3/*STATICS* $   
****CARD   1,  9- 12, 22, 23, 59
****FILE 101
****RFMT 188-204,207-209
$$$$
LABEL    ERROR4 $   
****CARD   1,  2,  4-  6, 16
****FILE  97
****RFMT 188-204,207-209
$$$$ Print Error Message No. 4 and terminate execution.
PRTPARM  //-4/*STATICS* $   
****CARD   1,  2,  4-  6, 16
****FILE  97
****RFMT 188-204,207-209
$$$$
LABEL    ERROR5 $   
****CARD  22, 23
****RFMT 188-204,207-209
$$$$
$$$$ Print Error Message No. 5 and terminate execution.
PRTPARM  //-5/*STATICS* $   
****CARD  22, 23
****RFMT 188-204,207-209
$$$$
LABEL    FINIS $
****CARD   1- 24, 28, 29, 31, 59- 62
****RFMT 188-204,207-209
$$$$
PURGE    DUMMY/ALWAYS $ 
****CARD   1- 24, 28, 29, 31, 59- 62
****RFMT 188-204,207-209
$$$$
LABEL    LBLINT02 $ 
****CARD   1-20,22-24,28,31,59-62   
$$$$
$$$$ COMPON causes the DMAP compiler to skip the compilation of all of the 
$$$$ following instructions through label LBLINT01 as the parameter SYS21 is 0 
$$$$ (see NOTE at the beginning).
COMPON   LBLINT01,SYS21 $   
****CARD   1- 20, 22- 24, 28, 31, 59- 62
$$$$
PARAM    //*SYST*//86/1 $   
****CARD   1-20,22-24,28,31,59-62   
$$$$
SETVAL   //V,N,PFILE/0 $
****CARD   1-20,22-24,28,31,59-62   
$$$$
LABEL    AGAIN $
****CARD   1- 20, 22- 24, 28, 31, 59- 62
$$$$
PROMPT1  //S,N,PEXIT/S,N,PLOT1/S,N,PLOT2/S,N,XYPLOT/
         S,N,SCAN1/S,N,SCAN2 $  
****CARD   1-20,22-24,28,31,59-62   
$$$$
COND     LBLINT1,PEXIT $
****CARD   1-20,22-24,28,31,59-62   
$$$$
PARAM    //*OR*/V,N,PLOTZ/V,N,PLOT1/V,N,PLOT2 $ 
****CARD   1-20,22-24,28,31,59-62   
$$$$
PARAM    //*NOT*/V,N,NOPLOTZ/V,N,PLOTZ $
****CARD   1-20,22-24,28,31,59-62   
$$$$
COND     STEPPLOT,NOPLOTZ $ 
****CARD   1- 20, 22- 24, 28, 31, 59- 62
$$$$
PURGE    PLTSETR,PLTPARR,GPSETR,ELSETR/NOPLOTZ $
****CARD   1-20,22-24,28,31,59-62   
$$$$
PLTSET   PCDB,EQEXIN,ECT,/PLTSETR,PLTPARR,GPSETR,ELSETR/S,N,NSIL/
         S,N,JUMPPLOT $ 
****CARD   1- 20, 22- 24, 28, 31, 59- 62
$$$$
PRTMSG   PLTSETR $  
****CARD   1-20,22-24,28,31,59-62   
$$$$
COND     LBLINT2,PLOT2 $
****CARD   1-20,22-24,28,31,59-62   
$$$$
SETVAL   //S,N,PLTFG1/1 $   
****CARD   1-20,22-24,28,31,59-62   
$$$$
PLOT     PLTPARR,GPSETR,ELSETR,CASECC,BGPDT,EQEXIN,SIL,,ECT,,,,/
         PLOTX3/NSIL/LUSET/JUMPPLOT/PLTFG1/S,N,PFILE $  
****CARD   1-20,22-24,28,31,59-62   
$$$$
PRTMSG   PLOTX3 $   
****CARD   1-20,22-24,28,31,59-62   
$$$$
SITEPLOT   $
****CARD   1-20,22-24,28,31,59-62   
$$$$
PURGE    PLTSETR,PLTPARR,GPSETR,ELSETR $
****CARD   1-20,22-24,28,31,59-62   
$$$$
JUMP     LBLINTEX $ 
****CARD   1-20,22-24,28,31,59-62   
$$$$
LABEL    LBLINT2 $  
****CARD   1-20,22-24,28,31,59-62   
$$$$
SETVAL   //S,N,PLTFG2/-1 $  
****CARD   1-20,22-24,28,31,59-62   
$$$$
PLOT     PLTPARR,GPSETR,ELSETR,CASECC,BGPDT,EQEXIN,SIP,PUGV1,,GPECT,
         OES1,OES1L,ONRGY1/PLOTX4/NSIL/LUSEP/JUMPPLOT/PLTFG2/S,N,PFILE $
****CARD   1-20,22-24,28,31,59-62   
$$$$
PRTMSG   PLOTX4// $ 
****CARD   1-20,22-24,28,31,59-62   
$$$$
SITEPLOT  $ 
****CARD   1-20,22-24,28,31,59-62   
$$$$
PURGE    PLTSETR,PLTPARR,GPSETR,ELSETR $
****CARD   1-20,22-24,28,31,59-62   
$$$$
JUMP     LBLINTEX $ 
****CARD   1-20,22-24,28,31,59-62   
$$$$
LABEL    STEPPLOT $ 
****CARD   1- 20, 22- 24, 28, 31, 59- 62
$$$$
PARAM    //*OR*/V,N,SCANZ/V,N,SCAN1/V,N,SCAN2 $ 
****CARD   1-20,22-24,28,31,59-62   
$$$$
PARAM    //*NOT*/V,N,NOSCANZ/V,N,SCANZ $
****CARD   1-20,22-24,28,31,59-62   
$$$$
COND     STEPSCAN,NOSCANZ $ 
****CARD   1-20,22-24,28,31,59-62   
$$$$
PURGE    OESF1I,OESF2I/NOSCANZ $
****CARD   1-20,22-24,28,31,59-62   
$$$$
COND     LBLINT3,SCAN2 $
****CARD   1-20,22-24,28,31,59-62   
$$$$
SCAN     CASECC,OES1,OEF1/OESF1I/*OL1* $
****CARD   1-20,22-24,28,31,59-62   
$$$$
OFP      OESF1I,,,,,//S,N,CARDNO $  
****CARD   1-20,22-24,28,31,59-62   
$$$$
PURGE    OESF1I $   
****CARD   1-20,22-24,28,31,59-62   
$$$$
JUMP     LBLINTEX $ 
****CARD   1-20,22-24,28,31,59-62   
$$$$
LABEL    LBLINT3 $  
****CARD   1-20,22-24,28,31,59-62   
$$$$
SCAN     CASECC,OES2,OEF2/OESF2I/*OL2* $
****CARD   1-20,22-24,28,31,59-62   
$$$$
OFP      OESF2I,,,,,//S,N,CARDNO $  
****CARD   1-20,22-24,28,31,59-62   
$$$$
PURGE    OESF2I $   
****CARD   1-20,22-24,28,31,59-62   
$$$$
JUMP     LBLINTEX $ 
****CARD   1-20,22-24,28,31,59-62   
$$$$
LABEL    STEPSCAN $ 
****CARD   1-20,22-24,28,31,59-62   
$$$$
PARAM    //*NOT*/V,N,NOXYPT/V,N,XYPLOT $
****CARD   1-20,22-24,28,31,59-62   
$$$$
COND     LBLINTEX,NOXYPT $  
****CARD   1-20,22-24,28,31,59-62   
$$$$
PURGE    XYPLTI/NOXYPT $
****CARD   1-20,22-24,28,31,59-62   
$$$$
XYTRAN   XYCDB,OPG2,OQG2,OUGV2,OES2,OEF2/XYPLTI/*TRAN*/ 
         *PSET*/S,N,PFILE/S,N,CARDNO $  
****CARD   1-20,22-24,28,31,59-62   
$$$$
XYPLOT   XYPLTI// $ 
****CARD   1-20,22-24,28,31,59-62   
$$$$
SITEPLOT   $
****CARD   1-20,22-24,28,31,59-62   
$$$$
PURGE    XYPLTI $   
****CARD   1-20,22-24,28,31,59-62   
$$$$
JUMP     LBLINTEX $ 
****CARD   1-20,22-24,28,31,59-62   
$$$$
LABEL    LBLINTEX $ 
****CARD   1-20,22-24,28,31,59-62   
$$$$
REPT     AGAIN,400 $
****CARD   1- 20, 22- 24, 28, 31, 59- 62
$$$$
PRTPARM  //1/*STATICS* $
****CARD   1- 20, 22- 24, 28, 31, 59- 62
$$$$
LABEL    LBLINT1 $  
****CARD   1- 20, 22- 24, 28, 31, 59- 62
$$$$
LABEL    LBLINT01 $ 
****CARD   1- 20, 22- 24, 28, 31, 59- 62
$$$$
END      $  
****CARD   1- 24, 28, 29, 31, 59- 62
****RFMT 188-204,207-209
$$$$
$*CARD BITS 
  1    AXIC     AXIF     CELAS1   CELAS2   CELAS3   CELAS4   CMASS1 
  1    CMASS2   
  1    CMASS3   CMASS4   CORD1C   CORD1R   CORD1S   CORD2C   CORD2R 
  1    CORD2S   
  1    GRDSET   GRID     GRIDB    POINTAX  RINGAX   RINGFL   SECTAX 
  1    SEQGP    SPOINT  
  2    ADUM1    ADUM2    ADUM3    ADUM4    ADUM5    ADUM6    ADUM7  
  2    ADUM8
  2    ADUM9    BAROR    CBAR     CCONEAX  CDUM1    CDUM2    CDUM3  
  2    CDUM4
  2    CDUM5    CDUM6    CDUM7    CDUM8    CDUM9    CHBDY    CHEXA1 
  2    CHEXA2   
  2    CIHEX1   CIHEX2   CIHEX3   CONROD   CQDMEM   CQDMEM1  CQDMEM2
  2    CQDPLT   CQUAD1   CQUAD2   CROD     CSHEAR   CTETRA   CTORDRG
  2    CTRAPAX  CQUAD4   CTRIA3
  2    CTRAPRG  CTRBSC   CTRIA1   CTRIA2   CTRIAAX  CTRIARG  CTRIM6 
  2    CTRMEM   
  2    CTRPLT   CTRPLT1  CTRSHL   CTUBE    CTWIST   CWEDGE  
  3    PBAR     PCONEAX  PDUM1    PDUM2    PDUM3    PDUM4    PDUM5  
  3    PDUM6
  3    PDUM7    PDUM8    PDUM9    PHBDY    PIHEX    PQDMEM   PQDMEM1
  3    PQDMEM2  PQDPLT   PQUAD1   PQUAD2   PROD     PSHEAR   PTORDRG
  3    PTRAPAX  PSHELL   PCOMP    PCOMP1   PCOMP2
  3    PTRBSC   PTRIA1   PTRIA2   PTRIAAX  PTRIM6   PTRMEM   PTRPLT 
  3    PTRPLT1  PTRSHL   PTUBE    PTWIST
  4    GENEL
  5    CONM1    CONM2    PMASS  
  6    PELAS
  7    AOUT$
  8    MAT1     MAT2     MAT3     MAT4     MAT5     MATT1    MATT2  
  8    MATT3    MAT8
  8    MATT4    MATT5    TABLEM1  TABLEM2  TABLEM3  TABLEM4  TEMPMT$
  8    TEMPMX$  
  9    AXISYM$  CRIGD1   CRIGD2   CRIGD3   CRIGDR   CRROD    CRBAR  
  9    CRTRPLT  CRBE1    CRBE2    CRBE3    CRSPLINE MPC      MPCADD 
  9    MPCAX    MPC$
 10    SPC      SPC1     SPCADD   SPCAX    SPC$ 
 11    ASET     ASET1    OMIT     OMIT1    OMITAX   SUPAX    SUPORT 
 13    TEMP     TEMPAX   TEMPD    TEMPP1   TEMPP2   TEMPP3   TEMPRB 
 14    WTMASS   
 15    GRDPNT   
 16    PLOTEL   
 17    IRES 
 18    PLOT$
 19    POUT$
 20    ASETOUT  
 21    OPT      GRDEQ   
 22    LOOP$
 23    LOOP1$   
 24    COUPMASS CPBAR    CPQDPLT  CPQUAD1  CPQUAD2  CPROD    CPTRBSC
 24    CPTRIA1  CPTRIA2  CPTRPLT  CPTUBE
 25    STRESS   
 26    STRAIN   
 27    NINTPTS  
 28    AUTOSPC  
 29    XYOUT$   
 31    NOLOOP$  
 59    DEFORM   DEFORM$  LOAD$    SPCD  
 60    FORCE    FORCE1   FORCE2   FORCEAX  LOAD     MOMAX    MOMENT 
 60    MOMENT1  PLOAD4
 60    MOMENT2  PLOAD    PLOAD1   PLOAD2   PLOAD3   PRESAX   SLOAD  
 61    GRAV     RFORCE  
 62    TEMPLD$  
$$$$
$*FILE BITS 
 94    BGPDT    CSTM     EQEXIN   GPDT     GPL      SIL 
 95    ECT  
 96    GPTT     SLT 
 97    GPECT    EST      GEI      MPTX     PCOMPS   EPTX
 98    KGGX 
 99    MGG  
100    KGG  
101    RG       USET     YS       ASET     OGPST
102    GPST 
103    GM   
104    KNN  
105    KFF      KFS      KSS
106    GO       KAA      KOO      LOO   
107    KLL      KLR      KRR
108    LLL  
109    DM   
110    PG   
111    PL       PO       PS       QR
112    RULV     RUOV     ULV      UOOV  
113    PGG      QG       UGV
114    OEF1     OES1     OPG1     OQG1     OUGV1    PUGV1   
114    OEF1L    OES1L    OESF1    OESF1X
115    ELSETS   GPSETS   PLTPAR   PLTSETX   
116    KDICT    KELM     MDICT    MELM  
117    OPTP1
118    OPTP2    EST1
119    OGPFB1   ONRGY1  
120    PLOTX1   
121    OGPWG
122    OES1M    OES1G   
123    OES1A
124    OES1AM   OES1AG  
125    OUGV2    OPG2     OQG2     OEF2     OES2     OESF2
126    XYPLTT   
127    OQM1 
128    PLOTX2   
129    BGPDP    SIP 
$*  
=PAGE=
DISP10

APR.93
$$$$$$$$
BEGIN    DISP 10 - MODAL COMPLEX EIGENVALUE ANALYSIS - APR. 1993 $  
****CARD   1-  6,  8- 25, 56- 62
****RFMT 187-195,197-204,207-209
$$$$
PRECHK   ALL $  
****CARD   1-  6,  8- 25, 56- 62
****RFMT 187-195,197-204,207-209
$$$$
FILE     GOD=SAVE/GMD=SAVE/LAMA=APPEND/PHIA=APPEND $
****CARD   1-  6,  8- 25, 56- 62
****RFMT 187-195,197-204,207-209
$$$$
PARAM    //*MPY*/CARDNO/0/0 $   
****CARD   1-  6,  8- 12, 14, 15, 19- 24, 56- 62
****FILE 101,112,117,118,121,126
$$$$ GP1 generates coordinate system transformation matrices, tables of grid 
$$$$ point locations, and tables relating the internal and external grid point 
$$$$ numbers.
GP1      GEOM1,GEOM2,/GPL,EQEXIN,GPDT,CSTM,BGPDT,SIL/S,N,LUSET/ 
         NOGPDT/MINUS1=-1 $ 
****CARD   1
****FILE  94
$$$$ PLTTRAN modifies special scalar grid points in the BGPDT and SIL tables.
PLTTRAN  BGPDT,SIL/BGPDP,SIP/LUSET/S,N,LUSEP $  
****CARD   1
****FILE 127
$$$$ GP2 generates Element Connection Table with internal indices.
GP2      GEOM2,EQEXIN/ECT $ 
****CARD   1,  2,  4,  5, 16
****FILE  95
$$$$
PARAML   PCDB//*PRES*////JUMPPLOT $ 
****SBST   7
****CARD  18
****FILE 122,125
$$$$
PURGE    PLTSETX,PLTPAR,GPSETS,ELSETS/JUMPPLOT $
****SBST   7
****CARD  18
****FILE 122
$$$$ Go to label P1 if there are no structure plot requests.
COND     P1,JUMPPLOT $  
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 122,125
$$$$ PLTSET transforms user input into a form used to drive the structure 
$$$$ plotter.
PLTSET   PCDB,EQEXIN,ECT,EPT/PLTSETX,PLTPAR,GPSETS,ELSETS/S,N,NSIL/ 
         S,N,JUMPPLOT $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 122
$$$$ PRTMSG prints error messages associated with the structure plotter.
PRTMSG   PLTSETX// $
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 122
$$$$
PARAM    //*MPY*/PLTFLG/1/1 $   
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 125
$$$$
PARAM    //*MPY*/PFILE/0/0 $
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 125
$$$$ Go to label P1 if no undeformed structure plots are requested.
COND     P1,JUMPPLOT $  
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 125
$$$$ PLOT generates all requested undeformed structure plots.
PLOT     PLTPAR,GPSETS,ELSETS,CASECC,BGPDT,EQEXIN,SIL,,ECT,,,,/PLOTX1/  
         NSIL/LUSET/JUMPPLOT/PLTFLG/S,N,PFILE $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 125
$$$$ PRTMSG prints plotter data and engineering data for each undeformed plot 
$$$$ generated.
PRTMSG   PLOTX1// $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 125
$$$$
LABEL    P1 $   
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 122,125
$$$$ GP3 generates Grid Point Temperature Table.
GP3      GEOM3,EQEXIN,GEOM2/,GPTT/NOGRAV $  
****CARD   1,  2, 13
****FILE  96
$$$$ TA1 generates element tables for use in matrix assembly and stress 
$$$$ recovery.
TA1      ECT,EPT,BGPDT,SIL,GPTT,CSTM,MPT,EQEXIN/EST,GEI,GPECT,,,MPTX,
         PCOMPS,EPTX/LUSET/S,N,NOSIMP/1/S,N,NOGENL/GENEL/S,N,COMPS $   
****CARD   1-  6, 13, 16
****FILE  97
$$$$
EQUIV    MPTX,MPT/COMPS/EPTX,EPT/COMPS $
****CARD   1-  6,  8- 25, 56- 62
****FILE  97
$$$$ Go to label ERROR5 and print Error Message No. 5 if there are no 
$$$$ structural elements.
COND     ERROR5,NOSIMP $
****CARD   1,  2,  5,  6,  8, 16
****FILE  97
$$$$
PARAM    //*ADD*/NOKGGX/1/0 $   
****CARD   1-  3,  6,  8, 24
****FILE 124
$$$$
PARAM    //*ADD*/NOMGG/1/0 $
****CARD   1-  3,  5,  8, 24
****FILE 124
****RFMT 187,190-192
$$$$ EMG generates structural element stiffness, mass and damping matrix 
$$$$ tables, and dictionaries for later assembly by the EMA module.
EMG      EST,CSTM,MPT,DIT,GEOM2,/KELM,KDICT,MELM,MDICT,,,/S,N,NOKGGX/   
         S,N,NOMGG////C,Y,COUPMASS/C,Y,CPBAR/   
         C,Y,CPROD/C,Y,CPQUAD1/C,Y,CPQUAD2/C,Y,CPTRIA1/C,Y,CPTRIA2/ 
         C,Y,CPTUBE/C,Y,CPQDPLT/C,Y,CPTRPLT/C,Y,CPTRBSC/
         C,Y,VOLUME/C,Y,SURFACE $   
****CARD   1-  3,  5,  6,  8, 13, 24
****FILE 124
****RFMT 187,190-192
$$$$
PURGE    KGGX/NOKGGX $  
****CARD   1-  3,  6,  8
****FILE  98
$$$$ Go to label JMPKGGX if no stiffness matrix is to be assembled.
COND     JMPKGGX,NOKGGX $   
****CARD   1-  3,  6,  8
****FILE  98
$$$$                                  x
$$$$ EMA assembles stiffness matrix [K  ] and Grid Point Singularity Table.
$$$$                                  gg
$$$$
EMA      GPECT,KDICT,KELM/KGGX $
****CARD   1-  3,  6,  8
****FILE  98
$$$$
PURGE    KDICT,KELM/MINUS1 $
****CARD   1-  3,  6,  8
****FILE 124
$$$$
LABEL    JMPKGGX $  
****CARD   1-  3,  6,  8
****FILE  98
$$$$ Go to label ERROR1 and print Error Message No. 1 if no mass matrix is to 
$$$$ be assembled.
COND     ERROR1,NOMGG $ 
****CARD   1-  3,  5,  8, 14, 24
****FILE 124
****RFMT 187,190-192
$$$$ EMA assembles mass matrix [M  ].
$$$$                             gg
$$$$
EMA      GPECT,MDICT,MELM/MGG/-1/C,Y,WTMASS=1.0 $   
****CARD   1-  3,  5,  8, 14, 24
****FILE  99
****RFMT 187,190-192
$$$$
PURGE    MDICT,MELM/MINUS1 $
****CARD   1-  3,  5,  8, 14, 24
****FILE 124
$$$$ Go to label LGPWG if no weight and balance information is requested.
COND     LGPWG,GRDPNT $ 
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 126
$$$$ GPWG generates weight and balance information.
GPWG     BGPDP,CSTM,EQEXIN,MGG/OGPWG/V,Y,GRDPNT=-1/C,Y,WTMASS $ 
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 126
$$$$ OFP formats the weight and balance information prepared by GPWG and 
$$$$ places it on the system output file for printing.
OFP      OGPWG,,,,,//S,N,CARDNO $   
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 126
$$$$
LABEL    LGPWG $
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 126
$$$$               x
$$$$ Equivalence [K  ] to [K  ] if there are no general elements.
$$$$               gg       gg
$$$$
EQUIV    KGGX,KGG/NOGENL $  
****CARD   1-  4,  6,  8
****FILE 100
$$$$ Go to label LBL11 if there are no general elements.
COND     LBL11,NOGENL $ 
****CARD   1-  4,  6,  8
****FILE 100
$$$$                                 x
$$$$ SMA3 adds general elements to [K  ] to obtain stiffness matrix [K  ].
$$$$                                 gg                               gg
$$$$
SMA3     GEI,KGGX/KGG/LUSET/NOGENL/NOSIMP $ 
****CARD   1-  4,  6,  8
****FILE 100
$$$$
LABEL    LBL11 $
****CARD   1-  4,  6,  8
****FILE 100
$$$$
GPSTGEN  KGG,SIL/GPST   
****CARD   1-  4,  6,  8
****FILE 102
$$$$
PARAM    //*MPY*/NSKIP/0/0 $
****CARD   1,  9- 12
****FILE 101
$$$$ GP4 generates flags defining members of various displacement sets (USET) 
$$$$ and forms multipoint constraint equations [R ] {u } = 0.
$$$$                                             g    g
$$$$
GP4      CASECC,GEOM4,EQEXIN,GPDT,BGPDT,CSTM,GPST/RG,,USET, 
         ASET,OGPST/LUSET/S,N,MPCF1/S,N,MPCF2/S,N,SINGLE/S,N,OMIT/  
         S,N,REACT/S,N,NSKIP/S,N,REPEAT/S,N,NOSET/S,N,NOL/S,N,NOA/  
         C,Y,ASETOUT/C,Y,AUTOSPC $  
****CARD   1-  4,  6,  8- 12, 17, 20
****FILE 101
$$$$ OFP formats the table of potential grid point similarities prepared by 
$$$$ GPSP and places it on the system output file for printing.
OFP      OGPST,,,,,//S,N,CARDNO $   
****CARD   1-  4,  6,  8- 10, 20
****FILE 101
$$$$
PARAM    //*AND*/NOSR/REACT/SINGLE $
****CARD   1,  9- 12
****FILE 101
$$$$
PURGE    GM,GMD/MPCF1/GO,GOD/OMIT/KFS/SINGLE/QPC/NOSR/KLR,KRR,MLR,MRR,  
         DM,MR/REACT $  
****CARD   1,  9- 12
****FILE 103,105-107,109,110,115,120
$$$$ Equivalence [K  ] to [K  ] and [M  ] to [M  ] if no multipoint 
$$$$               gg       nn        gg       nn
$$$$ constraints exist.
EQUIV    KGG,KNN/MPCF1/MGG,MNN/MPCF1 $  
****CARD   1-  6,  8,  9, 14, 24
****FILE 104
$$$$ Go to label LBL2 if no multipoint constraints exist.
COND     LBL2,MPCF1 $   
****CARD   1-  6,  8,  9, 14, 24
****FILE 103,104
$$$$ MCE1 partitions multipoint constraint equation [R ] = [R |R ] and solves
$$$$                                                  g      m  n
$$$$                                                             -1
$$$$ for multipoint constraint transformation matrix [G ] = -[R ]  [R ].
$$$$                                                   m       m     n
$$$$
MCE1     USET,RG/GM $   
****CARD   1,  9
****FILE 103
$$$$ MCE2 partitions stiffness and mass matrices
$$$$ 
$$$$           +_      +                        +_      +
$$$$           |K  |K  |                        |M  |M  |
$$$$           | nn| nm|                        | nn| nm|
$$$$   [K  ] = |---+---|                [M  ] = |---+---|
$$$$     gg    |K  |K  |                  gg    |M  |M  |
$$$$           | mn| mm|                        | mn| mm|
$$$$           +       +                        +       +
$$$$ 
$$$$ and performs the matrix reductions
$$$$ 
$$$$            _        T           T           T
$$$$   [K  ] = [K  ] + [G ][K  ] + [K  ][G ] + [G ][K  ][G ]
$$$$     nn      nm      m   mn      mn   m      m   mm   m
$$$$ 
$$$$            _        T           T           T
$$$$   [M  ] = [M  ] + [G ][M  ] + [M  ][G ] + [G ][M  ][G ]
$$$$     nn      nm      m   mn      mn   m      m   mm   m
$$$$
MCE2     USET,GM,KGG,MGG,,/KNN,MNN,, $  
****CARD   1-  6,  8,  9, 14, 24
****FILE 104
$$$$
LABEL    LBL2 $ 
****CARD   1-  6,  8,  9, 14, 24
****FILE 103,104
$$$$ Equivalence [K  ] to [K  ] and [M  ] to [M  ] if no single-point
$$$$               nn       ff        nn       ff
$$$$ constraints exist.
$$$$
EQUIV    KNN,KFF/SINGLE/MNN,MFF/SINGLE $
****CARD   1-  6,  8- 10, 14, 24
****FILE 105
$$$$ Go to label LBL3 if no single-point constraints exist.
COND     LBL3,SINGLE $  
****CARD   1-  6,  8- 10, 14, 24
****FILE 105
$$$$ SCE1 partitions out single-point constraints
$$$$ 
$$$$           +       +                        +       +
$$$$           |K  |K  |                        |M  |M  |
$$$$           | ff| fs|                        | ff| fs|
$$$$   [K  ] = |---+---|                [M  ] = |---+---|
$$$$     nn    |K  |K  |                  nn    |M  |M  |
$$$$           | sf| ss|                        | sf| ss|
$$$$           +       +                        +       +
$$$$
SCE1     USET,KNN,MNN,,/KFF,KFS,,MFF,, $
****CARD   1-  6,  8- 10, 14, 24
****FILE 105
$$$$
LABEL    LBL3 $ 
****CARD   1-  6,  8- 10, 14, 24
****FILE 105
$$$$ Equivalence {K  ] to [K  ] if no omitted coordinates exist.
$$$$               ff       aa
$$$$
EQUIV    KFF,KAA/OMIT $ 
****CARD   1-  4,  6,  8- 11
****FILE 106
$$$$ Equivalence [M  ] to [M  ] if no omitted coordinates exist.
$$$$               ff       aa
$$$$
EQUIV    MFF,MAA/OMIT $ 
****CARD   1-  5,  8- 11, 14, 24
****FILE 123
$$$$ Go to label LBL5 if no omitted coordinates exist.
COND     LBL5,OMIT $
****CARD   1-  6,  8- 11, 14, 24
****FILE 106,123
$$$$ SMP1 partitions constrained stiffness matrix
$$$$
$$$$           +_      +
$$$$           |K  |K  |
$$$$           | aa| ao|
$$$$   [K  ] = |---+---|
$$$$     ff    |K  |K  |
$$$$           | oa| oo|
$$$$           +       +
$$$$           
$$$$                                               -1
$$$$ solves for transformation matrix [G ] = -[K  ]  [K  ]
$$$$                                    o       oo     oa
$$$$ 
$$$$                                        _        T
$$$$ and performs matrix reduction [K  ] = [K  ] + [K  ][G ].
$$$$                                 aa      aa      oa   o
$$$$
SMP1     USET,KFF,,,/GO,KAA,KOO,LOO,,,,, $  
****CARD   1-  4,  6,  8- 11
****FILE 106
$$$$ SMP2 partitions constrained mass matrix
$$$$
$$$$           +_      +
$$$$           |M  |M  |
$$$$           | aa| ao|
$$$$   [M  ] = |---+---|
$$$$     ff    |M  |M  |
$$$$           | oa| oo|
$$$$           +       +
$$$$           
$$$$ and performs matrix reduction
$$$$
$$$$            _        T           T           T                   
$$$$   [M  ] = [M  ] + [M  ][G ] + [G ][M  ] + [G ][M  ][G ]         
$$$$     aa      aa      oa   o      o   ao      o   oo   o          
$$$$
SMP2     USET,GO,MFF/MAA $  
****CARD   1-  6,  8- 11, 14, 24
****FILE 123
$$$$
LABEL    LBL5 $ 
****CARD   1-  6,  8- 11, 14, 24
****FILE 106,123
$$$$ Go to label LBL6 if there are no free-body supports.
COND     LBL6,REACT $   
****CARD   1-  6,  8- 12, 14, 24
****FILE 107-110
$$$$ RBMG1 partitions out free-body supports
$$$$ 
$$$$           +       +                        +       +
$$$$           |K  |K  |                        |M  |M  |
$$$$           | ll| lr|                        | ll| lr|
$$$$   [K  ] = |---+---|                [M  ] = |---+---|
$$$$     aa    |K  |K  |                  aa    |M  |M  |
$$$$           | rl| rr|                        | rl| rr|
$$$$           +       +                        +       +
$$$$
RBMG1    USET,KAA,MAA/KLL,KLR,KRR,MLL,MLR,MRR $ 
****CARD   1-  6,  8- 12, 14, 24
****FILE 107
$$$$ RBMG2 decomposes constrained stiffness matrix [K  ] = [L  ][U  ].
$$$$                                                 ll      ll   ll
$$$$
RBMG2    KLL/LLL $  
****CARD   1-  4,  6,  8- 12
****FILE 108
$$$$ RBMG3 forms rigid body transformation matrix
$$$$ 
$$$$               -1
$$$$   [D] = -[K  ]  [K  ]
$$$$            ll     lr
$$$$ 
$$$$ calculates rigid body check matrix
$$$$
$$$$                   T
$$$$   [X] = [K  ] + [K  ][D]
$$$$           rr      lr
$$$$ 
$$$$ and calculates rigid body error ratio
$$$$ 
$$$$ 
$$$$               ||X||
$$$$   epsilon = ---------
$$$$              ||K  ||
$$$$                 rr
$$$$
RBMG3    LLL,KLR,KRR/DM $   
****CARD   1-  4,  6,  8- 12
****FILE 109
$$$$ RBMG4 forms rigid body mass matrix
$$$$ 
$$$$                    T          T           T
$$$$   [m ] = [M  ] + [M  ][D] + [D ][M  ] + [D ][M  ][D]
$$$$     r      rr      lr             lr          ll
$$$$
RBMG4    DM,MLL,MLR,MRR/MR $
****CARD   1-  6,  8- 12, 14, 24
****FILE 110
$$$$
LABEL    LBL6 $ 
****CARD   1-  6,  8- 12, 14, 24
****FILE 107-110
$$$$ DPD generates flags defining members of various displacement sets used in 
$$$$ dynamic analysis (USETD), tables relating the internal and external grid 
$$$$ point numbers (GPLD), including extra points introduced for dynamic 
$$$$ analysis (SILD), and prepares Transfer Function Pool (TFPOOL), and 
$$$$ Eigenvalue Extraction Data (EED).
DPD      DYNAMICS,GPL,SIL,USET/GPLD,SILD,USETD,TFPOOL,,,,,,EED,EQDYN/   
         LUSET/S,N,LUSETD/NOTFL/NODLT/NOPSDL/   
         NOFRL/NONLFT/NOTRL/S,N,NOEED//S,N,NOUE $   
****CARD   1,  9- 12, 56, 58, 60
****FILE 111
$$$$ Go to label ERROR2 and print Error Message No. 2 if there is no 
$$$$ Eigenvalue Extraction Data.
COND     ERROR2,NOEED $ 
****CARD   1,  9- 12, 56, 58, 60
****FILE 111
****RFMT 187-195,197-204,207-209
$$$$                       d                d
$$$$ Equivalence [G ] to [G ] and [G ] to [G ] if there are no extra points
$$$$               o       o        m       m
$$$$ introduced for dynamic analysis.
EQUIV    GO,GOD/NOUE/GM,GMD/NOUE  $ 
****CARD   1-  6,  8- 12, 14, 22- 24, 56- 59
****FILE 115
$$$$
PARAM    //*MPY*/NEIGV/1/-1 $   
****CARD   1-  6,  8- 12, 14, 24
****FILE 112
$$$$ READ extracts real eigenvalues and eigenvectors from the equation
$$$$ 
$$$$   [K   - lambda M  ]{u } = 0
$$$$     aa           aa   a
$$$$ 
$$$$ calculates rigid body modes by finding a square matrix [phi  ] such that
$$$$                                                            ro
$$$$              T
$$$$   [m ] = [phi  ][m ][phi  ]
$$$$     o        ro   r     ro
$$$$ 
$$$$ is diagonal and normalized, computes rigid body eigenvectors
$$$$ 
$$$$             +       +
$$$$             |Dphi   |
$$$$             |    ro |
$$$$   [phi  ] = |-------|
$$$$       ao    |phi    |
$$$$             |   ro  |
$$$$             +       +
$$$$ 
$$$$ calculates modal mass matrix
$$$$ 
$$$$             T
$$$$   [m] = [phi ][M  ][phi ]
$$$$             a   aa     a
$$$$ 
$$$$ and normalizes eigenvectors according to one of the following user 
$$$$ requests:
$$$$   1. Unit value of a selected component.
$$$$   2. Unit value of the largest component.
$$$$   3. Unit value of the generalized mass.
READ     KAA,MAA,MR,DM,EED,USET,CASECC/LAMA,PHIA,MI,OEIGS/*MODES*/S,N,  
         NEIGV $
****CARD   1-  6,  8- 12, 14, 24, 58, 59
****FILE 112
$$$$ OFP formats the summary of eigenvalue extraction information (OEIGS) 
$$$$ prepared by READ and places it on the system output file for printing.
OFP      OEIGS,,,,,//S,N,CARDNO $   
****CARD   1-  6,  8- 12, 14, 24, 58, 59
****FILE 112
$$$$ Go to label ERROR4 and print Error Message No. 4 if no eigenvalues were 
$$$$ found.
COND     ERROR4,NEIGV $ 
****CARD   1-  6,  8- 12, 14, 24, 58, 59
****FILE 112
****RFMT 187-195,197-204,207-209
$$$$ OFP formats the eigenvalues (LAMA) prepared by READ and places them on 
$$$$ the system output file for printing.
OFP      LAMA,,,,,//S,N,CARDNO $
****CARD   1-  6,  8- 12, 14, 24, 58, 59
****FILE 112
$$$$
PARAM    //*ADD*/NEVER/1/0 $
****CARD  22, 23
****FILE 117-121
$$$$
PARAM    //*MPY*/REPEATE/1/-1 $ 
****CARD   1-  6,  8- 14, 16, 22, 23, 56- 62
****FILE 113
****RFMT 187-195,197-204,207-209
$$$$ Beginning of loop for additional sets of direct input matrices.
LABEL    LBL13 $
****SBST   1,  3
****CARD   1-  6,  8- 16, 18, 19, 21- 23, 56- 62
****FILE 113
****RFMT 187-195,197-204,207-209
$$$$
PURGE    PHIH,CLAMA,OPHIH,CPHID,CPHIP,QPC,OQPC1,OCPHIP,OESC1,OEFC1, 
         K2PP,M2PP,B2PP,K2DD,M2DD,B2DD/NEVER $  
****CARD  22, 23
****FILE 117-121
$$$$ CASE extracts the appropriate record from CASECC corresponding to the 
$$$$ current loop and copies it into CASEXX.
CASE     CASECC,/CASEXX/*CEIGN*/S,N,REPEATE/S,N,NOLOOP $
****CARD   1-  6,  8- 14, 16, 19, 21- 23, 25, 56- 62
****FILE 113
****RFMT 187-195,197-204,207-209
$$$$                                            2      2          2
$$$$ MTRXIN selects the direct input matrices [K  ], [M  ], and [B  ] for the
$$$$                                            pp     pp         pp
$$$$ current loop.
MTRXIN   CASEXX,MATPOOL,EQDYN,,TFPOOL/K2PP,M2PP,B2PP/LUSETD/S,N,
         NOK2PP/S,N,NOM2PP/S,N,NOB2PP $ 
****CARD   1, 22, 23, 56, 57
****FILE 114
$$$$
PURGE    K2DD/NOK2PP/M2DD/NOM2PP/B2DD/NOB2PP $  
****CARD   1-  6,  8- 11, 22, 23, 56, 57
****FILE 114
$$$$               2        2      2        2          2        2
$$$$ Equivalence [M  ] to [M  ], [B  ] to [B  ], and [K  ] to [K  ] if no
$$$$               pp       dd     pp       dd         pp       dd
$$$$ constraints are applied.
EQUIV    M2PP,M2DD/NOSET/B2PP,B2DD/NOSET/K2PP,K2DD/NOSET $  
****CARD   1-  6,  8- 11, 22, 23, 56, 57
****FILE 114
$$$$                                                     2      2             
$$$$ GKAD applies constraints to direct input matrices [K  ], [M  ], and      
$$$$                                                     pp     pp            
$$$$   2              2      2          2
$$$$ [B  ], forming [K  ], [M  ], and [B  ].
$$$$   pp             dd     dd         dd
$$$$ 
GKAD     USETD,GM,GO,,,,,K2PP,M2PP,B2PP/,,,GMD,GOD,K2DD,
         M2DD,B2DD/*CMPLEV*/*DISP*/*MODAL*/0.0/ 
         0.0/0.0/NOK2PP/NOM2PP/NOB2PP/  
         MPCF1/SINGLE/OMIT/NOUE/-1/-1/  
         -1/-1 $
****CARD   1-  6,  8- 11, 14, 22- 24, 56, 57
****FILE 115
$$$$ GKAM assembles stiffness, mass, and damping matrices in modal coordinates 
$$$$ for use in Complex Eigenvalue Analysis:
$$$$ 
$$$$                     T    2
$$$$   [K  ] = [k] + [phi  ][K  ][phi  ]
$$$$     hh              dh   dd     dh
$$$$ 
$$$$                     T    2
$$$$   [M  ] = [m] + [phi  ][M  ][phi  ]
$$$$     hh              dh   dd     dh
$$$$ 
$$$$                     T    2
$$$$   [B  ] = [b] + [phi  ][B  ][phi  ]
$$$$     hh              dh   dd     dh
$$$$ 
$$$$ where
$$$$ 
$$$$   m  = modal masses
$$$$    i
$$$$ 
$$$$   b  = m  2 pi f  g(f )
$$$$    i    i       i    i
$$$$ 
$$$$               2  2
$$$$   k  = m  4 pi  f
$$$$    i    i        i
$$$$ 
$$$$ Direct input matrices may be complex.
GKAM     USETD,PHIA,MI,LAMA,DIT,M2DD,B2DD,K2DD,CASEXX/MHH,BHH,KHH,PHIDH/
         NOUE/C,Y,LMODES=0/C,Y,LFREQ=0.0/C,Y,HFREQ=-1.0/
         NOM2PP/NOB2PP/NOK2PP/S,N,NONCUP/S,N,FMODE $
****CARD   1-  6,  8- 12, 14, 22- 24, 56- 59, 62
****FILE 116
$$$$ CEAD extracts complex eigenvalues and eigenvectors from the equation
$$$$
$$$$        2
$$$$   [M  p  + B  p + K  ] {u } = 0
$$$$     hh      hh     hh    h
$$$$
$$$$ and normalizes eigenvectors according to one of the following user 
$$$$ requests:
$$$$   1. Unit magnitude of a selected component.
$$$$   2. Unit magnitude of the largest component.
CEAD     KHH,BHH,MHH,EED,CASEXX/PHIH,CLAMA,OCEIGS,/S,N,EIGVS $  
****CARD   1-  6,  8- 12, 14, 22- 24, 56- 62
****FILE 117
$$$$ OFP formats the summary of eigenvalue extraction information (OCEIGS) 
$$$$ prepared by CEAD and places it on the system output file for printing.
OFP      OCEIGS,,,,,//S,N,CARDNO $  
****CARD  56- 62
****FILE 117
$$$$ Go to label LBL17 if no complexe eigenvalues were found.
COND     LBL17,EIGVS $  
****CARD   1-  6,  8- 12, 14, 19, 21- 24, 56- 62
****FILE 117,118
$$$$ OFP formats the complex eigenvalues (CLAMA) prepared by CEAD and places 
$$$$ them on the system output file for printing.
OFP      CLAMA,,,,,//S,N,CARDNO $   
****CARD   1-  6,  8- 12, 14, 22- 24
****FILE 117
$$$$ VDR prepares eigenvectors (OPHIH) for output, using only the extra points 
$$$$ introduced for dynamic analysis and modal coordinates.
VDR      CASEXX,EQDYN,USETD,PHIH,CLAMA,,/OPHIH,/*CEIGEN*/*MODAL*/   
         NOSORT2/S,N,NOH/S,N,NOP/FMODE $
****CARD  19, 21
****FILE 118
$$$$ Go to label LBL16 if there is no output request for the extra points 
$$$$ introduced for dynamic analysis or modal coordinates.
COND     LBL16,NOH $
****CARD  21
****FILE 118
$$$$ OFP formats the table of eigenvectors for extra points introduced for 
$$$$ dynamic analysis and modal coordinates prepared by VDR and places it on 
$$$$ the system output file for printing.
OFP      OPHIH,,,,,//S,N,CARDNO $   
****CARD  21
****FILE 118
$$$$ Go to label LBL16 if there is no output request involving dependent 
$$$$ degrees of freedom
LABEL    LBL16 $
****CARD  21
****FILE 117,118
$$$$ Go to label LBL17 if there is no output request involving dependent 
$$$$ degrees of freedom or forces and stresses.
COND     LBL17,NOP $
****CARD   1-  6,  8- 12, 14, 22- 24, 56- 62
****FILE 119
$$$$ DDR1 transforms the complex eigenvectors from modal to physical 
$$$$ coordinates
$$$$
$$$$   [phi ] = [phi  ][phi ]
$$$$       d        dh     h
$$$$ 
DDR1     PHIH,PHIDH/CPHID $ 
****CARD   1-  6,  8- 12, 14, 22- 24, 56- 62
****FILE 119
$$$$ Equivalence [phi ] to [phi ] if no constraints are applied.
$$$$                 d         p
$$$$
EQUIV    CPHID,CPHIP/NOA $  
****CARD   1-  6,  8- 12, 14, 22- 24, 56- 62
****FILE 120
****RFMT 187-195,197-204,207-209
$$$$ Go to label LBLNOA if no constraints are applied.
COND     LBLNOA,NOA $   
****CARD   1-  6,  8- 12, 14, 22- 24, 56- 62
****FILE 120
****RFMT 187-195,197-204,207-209
$$$$ SDR1 recovers dependent components of eigenvectors
$$$$ 
$$$$                                     phi
$$$$             phi                        d
$$$$  {phi } = [G   ]{phi }             {----} = {phi  + phi }
$$$$      o      o       d               phi         f      e
$$$$                                        o
$$$$
$$$$    phi +phi      
$$$$       f    e                                  d
$$$$   {---------} = {phi +phi }        {phi } = [G ]{phi  + phi }
$$$$      phi            n    e             m      m     n      e
$$$$         s        
$$$$
$$$$    phi +phi                
$$$$       n    e               
$$$$   {---------} = {phi }
$$$$      phi            p 
$$$$         m                  
$$$$                                                         T
$$$$ and recovers single-point forces of constraint {q } = [K  ] {phi }.
$$$$                                                  s      fs      f
$$$$
SDR1     USETD,,CPHID,,,GOD,GMD,,KFS,,/CPHIP,,QPC/1/*DYNAMICS* $
****CARD   1-  6,  8- 12, 14, 22- 24, 56- 62
****FILE 120
****RFMT 187-195,197-204,207-209
$$$$
LABEL    LBLNOA $   
****CARD   1-  6,  8- 12, 14, 22- 24, 56- 62
****FILE 120
****RFMT 187-195,197-204,207-209
$$$$ SDR2 calculates element forces (OEFC1) and stresses (OESC1) and prepares 
$$$$ eigenvectors (OCPHIP) and single-point forces of constraint (OQPC1) for 
$$$$ output.
SDR2     CASEXX,CSTM,MPT,DIT,EQDYN,SILD,,,,CLAMA,QPC,CPHIP,EST,,,/  
         ,OQPC1,OCPHIP,OESC1,OEFC1,,,/*CEIGEN* $
****CARD  19
****FILE 121
$$$$ OFP formats the tables prepared by SDR2 and places them on the system 
$$$$ output file for printing.
OFP      OCPHIP,OQPC1,OEFC1,OESC1,,//S,N,CARDNO $   
****CARD  19
****FILE 121
$$$$
LABEL    LBL17 $
****CARD   1-  6,  8- 12, 14, 19, 21- 24, 56- 62
$$$$ Go to label FINIS and make normal exit if no additional sets of direct 
$$$$ input matrices need to be processed.
COND     FINIS,REPEATE $
****SBST   1,  3
****CARD  22, 23
****RFMT 187-195,197-204,207-209
$$$$ Go to label LBL13 if additional sets of direct input matrices need to be 
$$$$ processed.
REPT     LBL13,100 $
****SBST   1,  3
****CARD  22, 23
****RFMT 187-195,197-204,207-209
$$$$ Print Error Message No. 3 and terminate execution.
PRTPARM  //-3/*MDLCEAD* $   
****SBST   1,  3
****CARD  22, 23
****RFMT 187-195,197-204,207-209
$$$$ Go to label FINIS and make normal exit.
JUMP     FINIS $
****CARD   1-  6,  8- 25, 56- 62
****RFMT 187-195,197-204,207-209
$$$$
LABEL    ERROR2 $   
****CARD   1,  9- 12, 56, 58, 60
****FILE 101
****RFMT 187-195,197-204,207-209
$$$$ Print Error Message No. 2 and terminate execution.
PRTPARM  //-2/*MDLCEAD* $   
****CARD   1,  9- 12, 56, 58, 60
****FILE 101
****RFMT 187-195,197-204,207-209
$$$$
LABEL    ERROR1 $   
****CARD   1-  3,  5,  8, 14, 24
****FILE 124
****RFMT 187-195,197-204,207-209
$$$$ Print Error Message No. 1 and terminate execution.
PRTPARM  //-1/*MDLCEAD* $   
****CARD   1-  3,  5,  8, 14, 24
****FILE 124
****RFMT 187-195,197-204,207-209
$$$$
LABEL    ERROR4 $   
****CARD   1-  6,  8- 12, 14, 24, 58, 59
****FILE 112
****RFMT 187-195,197-204,207-209
$$$$ Print Error Message No. 4 and terminate execution.
PRTPARM  //-4/*MDLCEAD* $   
****CARD   1-  6,  8- 12, 14, 24, 58, 59
****FILE 112
****RFMT 187-195,197-204,207-209
$$$$
LABEL    ERROR5 $   
****CARD   1,  2,  5,  6,  8, 16
****FILE  97
****RFMT 187-195,197-204,207-209
$$$$ Print Error Message No. 5 and terminate execution.
PRTPARM  //-5/*MDLCEAD* $   
****CARD   1,  2,  5,  6,  8, 16
****FILE  97
****RFMT 187-195,197-204,207-209
$$$$
LABEL    FINIS $
****CARD   1-  6,  8- 25, 56- 62
****RFMT 187-195,197-204,207-209
$$$$
PURGE    DUMMY/MINUS1 $ 
****CARD   1-  6,  8- 25, 56- 62
****RFMT 187-195,197-204,207-209
$$$$
END      $  
****CARD   1-  6,  8- 25, 56- 62
****RFMT 187-195,197-204,207-209
$$$$
$*CARD BITS 
  1    AXIC     AXIF     CELAS1   CELAS2   CELAS3   CELAS4   CMASS1 
  1    CMASS2   
  1    CMASS3   CMASS4   CORD1C   CORD1R   CORD1S   CORD2C   CORD2R 
  1    CORD2S   
  1    GRDSET   GRID     GRIDB    POINTAX  RINGAX   RINGFL   SECTAX 
  1    SEQGP    SPOINT  
  2    ADUM1    ADUM2    ADUM3    ADUM4    ADUM5    ADUM6    ADUM7  
  2    ADUM8
  2    ADUM9    BAROR    CBAR     CCONEAX  CDUM1    CDUM2    CDUM3  
  2    CDUM4
  2    CDUM5    CDUM6    CDUM7    CDUM8    CDUM9    CELBOW   CHEXA1 
  2    CHEXA2   
  2    CIHEX1   CIHEX2   CIHEX3   CIS2D8   CONROD   CQDMEM   CQDMEM1
  2    CQDMEM2  CQDPLT   CQUAD1   CQUAD2   CROD     CSHEAR   CTETRA 
  2    CTORDRG  CQUAD4   CTRIA3
  2    CTRAPAX  CTRAPRG  CTRBSC   CTRIA1   CTRIA2   CTRIAAX  CTRIARG
  2    CTRIM6   
  2    CTRMEM   CTRPLT   CTRPLT1  CTRSHL   CTUBE    CTWIST   CWEDGE 
  3    PBAR     PCONEAX  PDUM1    PDUM2    PDUM3    PDUM4    PDUM5  
  3    PDUM6
  3    PDUM7    PDUM8    PDUM9    PELBOW   PIHEX    PIS2D8   PQDMEM 
  3    PQDMEM1  PQDMEM2  PQDPLT   PQUAD1   PQUAD2   PROD     PSHEAR 
  3    PTORDRG  PSHELL   PCOMP    PCOMP1   PCOMP2
  3    PTRAPAX  PTRBSC   PTRIA1   PTRIA2   PTRIAAX  PTRIM6   PTRMEM 
  3    PTRPLT   PTRPLT1  PTRSHL   PTUBE    PTWIST   
  4    GENEL
  5    CONM1    CONM2    PMASS  
  6    PELAS
  8    MAT1     MAT2     MAT3     MATT1    MATT2    MATT3    MAT8
  8    TABLEM1  TABLEM2  TABLEM3  TABLEM4  TEMPMT$  TEMPMX$  
  9    AXISYM$  CRIGD1   CRIGD2   CRIGD3   CRIGDR   CRROD    CRBAR  
  9    CRTRPLT  CRBE1    CRBE2    CRBE3    CRSPLINE MPC      MPCADD 
  9    MPCAX    MPC$
 10    SPC      SPC1     SPCADD   SPCAX    SPC$ 
 11    ASET     ASET1    OMIT     OMIT1    OMITAX   SUPAX    SUPORT 
 13    TEMP     TEMPAX   TEMPD    TEMPP1   TEMPP2   TEMPP3   TEMPRB 
 14    WTMASS   
 15    GRDPNT   
 16    PLOTEL   
 17    ASETOUT  
 18    PLOT$
 19    POUT$
 20    AUTOSPC  
 21    AOUT$
 22    LOOP$
 23    LOOP1$   
 24    COUPMASS CPBAR    CPDPLT   CPQUAD1  CPQUAD2  CPROD    CPTRBSC
 24    CPTRIA1  CPTRIA2  CPTRPLT  CPTUBE
 25    NOLOOP$  
 56    EPOINT   SEQEP    TF 
 57    DMIG     DMIAX    B2PP$    K2PP$    M2PP$    TF$ 
 58    EIGR 
 59    METHOD$  
 60    EIGC     EIGP
 61    CMETHOD$ 
 62    LFREQ    LMODES   HFREQ    SDAMP$   TABDMP1  
$$$$
$*FILE BITS 
 94    BGPDT    CSTM     EQEXIN   GPDT     GPL      SIL 
 95    ECT  
 96    GPTT 
 97    EST      GEI      GPECT    MPTX     PCOMPS   EPTX
 98    KGGX 
 99    MGG  
100    KGG  
101    ASET     RG       USET     OGPST 
102    GPST 
103    GM   
104    KNN      MNN 
105    KFF      KFS      MFF
106    GO       KOO      LOO      KAA   
107    KLL      KLR      KRR      MLL      MLR      MRR 
108    LLL  
109    DM   
110    MR   
111    EED      EQDYN    GPLD     SILD     TFPOOL   USETD   
112    LAMA     MI       PHIA     OEIGS 
113    CASEXX   
114    B2PP     K2PP     M2PP   
115    GMD      GOD      B2DD     K2DD     M2DD 
116    BHH      KHH      MHH      PHIDH 
117    CLAMA    OCEIGS   PHIH   
118    OPHIH
119    CPHID
120    CPHIP    QPC 
121    OCPHIP   OEFC1    OESC1    OQPC1 
122    ELSETS   GPSETS   PLTPAR   PLTSETX   
123    MAA  
124    KDICT    KELM     MDICT    MELM  
125    PLOTX1   
126    OGPWG
127    BGPDP    SIP 
$*  
=PAGE=
DISP11

APR.93
$$$$$$$$
BEGIN    DISP 11 - MODAL FREQUENCY/RANDOM RESPONSE ANALYSIS-APR. 1993 $ 
****CARD   1-  6,  8- 16, 18- 29, 53- 62
****RFMT 187-196,198-204,207-209
$$$$
PRECHK   ALL $  
****CARD   1-  6,  8- 16, 18- 29, 53- 62
****RFMT 187-196,198-204,207-209
$$$$
FILE     GOD=SAVE/GMD=SAVE/LAMA=APPEND/PHIA=APPEND $
****CARD   1-  6,  8- 16, 18- 29, 53- 62
****RFMT 187-196,198-204,207-209
$$$$
PARAM    //*MPY*/CARDNO/0/0 $   
****CARD   1-  6,  8- 12, 14, 15, 19, 21, 24, 29, 59, 60
****FILE 101,112,118,119,122,123,136
$$$$ GP1 generates coordinate system transformation matrices, tables of grid 
$$$$ point locations, and tables relating the internal and external grid point 
$$$$ numbers.
GP1      GEOM1,GEOM2,/GPL,EQEXIN,GPDT,CSTM,BGPDT,SIL/S,N,LUSET/ 
         NOGPDT/MINUS1=-1 $ 
****CARD   1
****FILE  94
$$$$ PLTTRAN modifies special scalar grid points in the BGPDT and SIL tables.
PLTTRAN  BGPDT,SIL/BGPDP,SIP/LUSET/S,N,LUSEP $  
****CARD   1
****FILE 141
$$$$ GP2 generates Element Connection Table with internal indices.
GP2      GEOM2,EQEXIN/ECT $ 
****CARD   1,  2,  4,  5, 16
****FILE  95
$$$$
PARAML   PCDB//*PRES*////JUMPPLOT $ 
****SBST   7
****CARD  18
****FILE 126,135
$$$$
PURGE    PLTSETX,PLTPAR,GPSETS,ELSETS/JUMPPLOT $
****SBST   7
****CARD  18
****FILE 126
$$$$ Go to label P1 if there are no structure plot requests.
COND     P1,JUMPPLOT $  
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 126,135
$$$$ PLTSET transforms user input into a form used to drive the structure 
$$$$ plotter.
PLTSET   PCDB,EQEXIN,ECT,EPT/PLTSETX,PLTPAR,GPSETS,ELSETS/S,N,NSIL/ 
         S,N,JUMPPLOT $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 126
$$$$ PRTMSG prints error messages associated with the structure plotter.
PRTMSG   PLTSETX// $
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 126
$$$$
PARAM    //*MPY*/PLTFLG/1/1 $   
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 135
$$$$
PARAM    //*MPY*/PFILE/0/0 $
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 135
$$$$ Go to label P1 if no undeformed structure plots are requested.
COND     P1,JUMPPLOT $  
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 135
$$$$ PLOT generates all requested undeformed structure plots.
PLOT     PLTPAR,GPSETS,ELSETS,CASECC,BGPDT,EQEXIN,SIL,,ECT,,,,/PLOTX1/  
         NSIL/LUSET/S,N,JUMPPLOT/S,N,PLTFLG/S,N,PFILE $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 135
$$$$ PRTMSG prints plotter data and engineering data for each undeformed plot 
$$$$ generated.
PRTMSG   PLOTX1// $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 135
$$$$
LABEL    P1 $   
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 126,135
$$$$ GP3 generates Grid Point Temperature Table.
GP3      GEOM3,EQEXIN,GEOM2/,GPTT/NOGRAV $  
****CARD   1,  2, 13
****FILE  96
$$$$ TA1 generates element tables for use in matrix assembly and stress 
$$$$ recovery.
TA1      ECT,EPT,BGPDT,SIL,GPTT,CSTM,MPT,EQEXIN/EST,GEI,GPECT,,,MPTX,
         PCOMPS,EPTX/LUSET/S,N,NOSIMP/1/S,N,NOGENL/GENEL/S,N,COMPS $   
****CARD   1-  6, 13, 16
****FILE  97
$$$$
EQUIV    MPTX,MPT/COMPS/EPTX,EPT/COMPS $
****CARD   1-  6,  8- 16, 18- 29, 53- 62
****FILE  97
$$$$ Go to label ERROR7 and print Error Message No. 7 if there are no 
$$$$ structural elements.
COND     ERROR7,NOSIMP $
****CARD   1,  2,  4-  6,  8, 16
****FILE  97
$$$$
PARAM    //*ADD*/NOKGGX/1/0 $   
****CARD   1-  3,  6,  8, 24
****FILE 128
$$$$
PARAM    //*ADD*/NOMGG/1/0 $
****CARD   1-  3,  5,  8, 24
****FILE 128
****RFMT 187,190-192
$$$$ EMG generates structural element stiffness, mass and damping matrix 
$$$$ tables, and dictionaries for later assembly by the EMA module.
EMG      EST,CSTM,MPT,DIT,GEOM2,/KELM,KDICT,MELM,MDICT,,,/S,N,NOKGGX/   
         S,N,NOMGG////C,Y,COUPMASS/C,Y,CPBAR/   
         C,Y,CPROD/C,Y,CPQUAD1/C,Y,CPQUAD2/C,Y,CPTRIA1/C,Y,CPTRIA2/ 
         C,Y,CPTUBE/C,Y,CPQDPLT/C,Y,CPTRPLT/C,Y,CPTRBSC/
         C,Y,VOLUME/C,Y,SURFACE $   
****CARD   1-  3,  5,  6,  8, 13, 24
****FILE 128
****RFMT 187,190-192
$$$$
PURGE    KGGX/NOKGGX $  
****CARD   1-  3,  6,  8
****FILE  98
$$$$ Go to label JMPKGGX if no stiffness matrix is to be assembled.
COND     JMPKGGX,NOKGGX $   
****CARD   1-  3,  6,  8
****FILE  98
$$$$                                  x
$$$$ EMA assembles stiffness matrix [K  ] and Grid Point Singularity Table.
$$$$                                  gg
$$$$
EMA      GPECT,KDICT,KELM/KGGX $
****CARD   1-  3,  6,  8
****FILE  98
$$$$
PURGE    KDICT,KELM/MINUS1 $
****CARD   1-  3,  6,  8
****FILE 128
$$$$
LABEL    JMPKGGX $  
****CARD   1-  3,  6,  8
****FILE  98
$$$$ Go to label ERROR1 and print Error Message No. 1 if no mass matrix is to 
$$$$ be assembled.
COND     ERROR1,NOMGG $ 
****CARD   1-  3,  5,  8, 24
****FILE 128
****RFMT 187,190-192
$$$$ EMA assembles mass matrix [M  ].
$$$$                             gg
$$$$
EMA      GPECT,MDICT,MELM/MGG/-1/C,Y,WTMASS=1.0 $   
****CARD   1-  3,  5,  8, 14, 24
****FILE  99
****RFMT 187,190-192
$$$$
PURGE    MDICT,MELM/MINUS1 $
****CARD   1-  3,  5,  8, 14, 24
****FILE 128
$$$$ Go to label LGPWG if no weight and balance information is requested.
COND     LGPWG,GRDPNT $ 
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 136
$$$$ GPWG generates weight and balance information.
GPWG     BGPDP,CSTM,EQEXIN,MGG/OGPWG/V,Y,GRDPNT=-1/C,Y,WTMASS $ 
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 136
$$$$ OFP formats the weight and balance information prepared by GPWG and 
$$$$ places it on the system output file for printing.
OFP      OGPWG,,,,,//S,N,CARDNO $   
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 136
$$$$
LABEL    LGPWG $
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 136
$$$$               x
$$$$ Equivalence [K  ] to [K  ] if no general elements exist.
$$$$               gg       gg
$$$$
EQUIV    KGGX,KGG/NOGENL $  
****CARD   1-  4,  6,  8
****FILE 100
$$$$ Go to LBL11 if no general elements exist.
COND     LBL11,NOGENL $ 
****CARD   1-  4,  6,  8
****FILE 100
$$$$                                 x
$$$$ SMA3 adds general elements to [K  ] to obtain stiffness matrix [K  ].
$$$$                                 gg                               gg
$$$$
SMA3     GEI,KGGX/KGG/LUSET/NOGENL/NOSIMP $ 
****CARD   1-  4,  6,  8
****FILE 100
$$$$
LABEL    LBL11 $
****CARD   1-  4,  6,  8
****FILE 100
$$$$
GPSTGEN  KGG,SIL/GPST $ 
****CARD   1-  4,  6,  8
****FILE 102
$$$$
PARAM    //*MPY*/NSKIP/0/0 $
****CARD   1,  9- 12
****FILE 101
$$$$ GP4 generates flags defining members of various displacement sets (USET) 
$$$$ and forms multipoint constraint equations [R ] {u } = 0.
$$$$                                             g    g
$$$$
GP4      CASECC,GEOM4,EQEXIN,GPDT,BGPDT,CSTM,GPST/RG,,USET, 
         ASET,OGPST/LUSET/S,N,MPCF1/S,N,MPCF2/S,N,SINGLE/S,N,OMIT/  
         S,N,REACT/S,N,NSKIP/S,N,REPEAT/S,N,NOSET/S,N,NOL/S,N,NOA/  
         C,Y,ASETOUT/C,Y,AUTOSPC $  
****CARD   1-  4,  6,  8- 12, 28, 29
****FILE 101
$$$$ OFP formats the table of potential grid point similarities prepared by 
$$$$ GPSP and places it on the system output file for printing.
OFP      OGPST,,,,,//S,N,CARDNO $   
****CARD   1-  4,  6,  8- 10, 29
****FILE 101
$$$$
PARAM    //*AND*/NOSR/REACT/SINGLE $
****CARD   1,  9- 12
****FILE 101
$$$$
PURGE    GM,GMD/MPCF1/GO,GOD/OMIT/KFS,PSF/SINGLE/QPC/NOSR/KLR,KRR,MLR,  
         MRR,DM,MR/REACT/MDD/MODACC $   
****CARD   1,  9- 12
****FILE 103,105-107,109,110,115,117,121
$$$$ Equivalence [K  ] to [K  ] and [M  ] to [M  ] if no multipoint 
$$$$               gg       nn        gg       nn
$$$$ constraints exist.
EQUIV    KGG,KNN/MPCF1/MGG,MNN/MPCF1 $  
****CARD   1-  6,  8,  9, 14, 24
****FILE 104
$$$$ Go to label LBL2 if no multipoint constraints exist.
COND     LBL2,MPCF1 $   
****CARD   1-  6,  8,  9, 14, 24
****FILE 103,104
$$$$ MCE1 partitions multipoint constraint equation [R ] = [R |R ] and solves
$$$$                                                  g      m  n
$$$$                                                             -1
$$$$ for multipoint constraint transformation matrix [G ] = -[R ]  [R ].
$$$$                                                   m       m     n
$$$$
MCE1     USET,RG/GM $   
****CARD   1,  9
****FILE 103
$$$$ MCE2 partitions stiffness and mass matrices
$$$$ 
$$$$           +_      +                        +_      +
$$$$           |K  |K  |                        |M  |M  |
$$$$           | nn| nm|                        | nn| nm|
$$$$   [K  ] = |---+---|                [M  ] = |---+---|
$$$$     gg    |K  |K  |                  gg    |M  |M  |
$$$$           | mn| mm|                        | mn| mm|
$$$$           +       +                        +       +
$$$$ 
$$$$ and performs the matrix reductions
$$$$ 
$$$$            _        T           T           T
$$$$   [K  ] = [K  ] + [G ][K  ] + [K  ][G ] + [G ][K  ][G ]
$$$$     nn      nm      m   mn      mn   m      m   mm   m
$$$$ 
$$$$            _        T           T           T
$$$$   [M  ] = [M  ] + [G ][M  ] + [M  ][G ] + [G ][M  ][G ]
$$$$     nn      nm      m   mn      mn   m      m   mm   m
$$$$
MCE2     USET,GM,KGG,MGG,,/KNN,MNN,, $  
****CARD   1-  6,  8,  9, 14, 24
****FILE 104
$$$$
LABEL    LBL2 $ 
****CARD   1-  6,  8,  9, 14, 24
****FILE 103,104
$$$$ Equivalence [K  ] to [K  ] and [M  ] to [M  ] if no single-point
$$$$               nn       ff        nn       ff
$$$$ constraints exist.
$$$$
EQUIV    KNN,KFF/SINGLE/MNN,MFF/SINGLE $
****CARD   1-  6,  8- 10, 14, 24
****FILE 105
$$$$ Go to label LBL3 if no single-point constraints exist.
COND     LBL3,SINGLE $  
****CARD   1-  6,  8- 10, 14, 24
****FILE 105
$$$$ SCE1 partitions out single-point constraints
$$$$ 
$$$$           +       +                        +       +
$$$$           |K  |K  |                        |M  |M  |
$$$$           | ff| fs|                        | ff| fs|
$$$$   [K  ] = |---+---|                [M  ] = |---+---|
$$$$     nn    |K  |K  |                  nn    |M  |M  |
$$$$           | sf| ss|                        | sf| ss|
$$$$           +       +                        +       +
$$$$
SCE1     USET,KNN,MNN,,/KFF,KFS,,MFF,, $
****CARD   1-  6,  8- 10, 14, 24
****FILE 105
$$$$
LABEL    LBL3 $ 
****CARD   1-  6,  8- 10, 14, 24
****FILE 105
$$$$ Equivalence {K  ] to [K  ] if no omitted coordinates exist.
$$$$               ff       aa
$$$$
EQUIV    KFF,KAA/OMIT $ 
****CARD   1-  4,  6,  8- 11
****FILE 106
$$$$ Equivalence [M  ] to [M  ] if no omitted coordinates exist.
$$$$               ff       aa
$$$$
EQUIV    MFF,MAA/OMIT $ 
****CARD   1-  5,  8- 11, 14, 24
****FILE 127
$$$$ Go to label LBL5 if no omitted coordinates exist.
COND     LBL5,OMIT $
****CARD   1-  6,  8- 11, 14, 24
****FILE 106,127
$$$$ SMP1 partitions constrained stiffness matrix
$$$$
$$$$           +_      +
$$$$           |K  |K  |
$$$$           | aa| ao|
$$$$   [K  ] = |---+---|
$$$$     ff    |K  |K  |
$$$$           | oa| oo|
$$$$           +       +
$$$$           
$$$$                                               -1
$$$$ solves for transformation matrix [G ] = -[K  ]  [K  ]
$$$$                                    o       oo     oa
$$$$ 
$$$$                                        _        T
$$$$ and performs matrix reduction [K  ] = [K  ] + [K  ][G ].
$$$$                                 aa      aa      oa   o
$$$$
SMP1     USET,KFF,,,/GO,KAA,KOO,LOO,,,,, $  
****CARD   1-  4,  6,  8- 11
****FILE 106
$$$$ SMP2 partitions constrained mass matrix
$$$$
$$$$           +_      +
$$$$           |M  |M  |
$$$$           | aa| ao|
$$$$   [M  ] = |---+---|
$$$$     ff    |M  |M  |
$$$$           | oa| oo|
$$$$           +       +
$$$$           
$$$$ and performs matrix reduction
$$$$
$$$$            _        T           T           T                   
$$$$   [M  ] = [M  ] + [M  ][G ] + [G ][M  ] + [G ][M  ][G ]         
$$$$     aa      aa      oa   o      o   ao      o   oo   o          
$$$$
SMP2     USET,GO,MFF/MAA $  
****CARD   1-  6,  8- 11, 14, 24
****FILE 127
$$$$
LABEL    LBL5 $ 
****CARD   1-  6,  8- 11, 14, 24
****FILE 106,127
$$$$ Equivalence [K  ] to [K  ] if no free-body supports exist.
$$$$               aa       ll
$$$$
EQUIV    KAA,KLL/REACT $
****CARD   1-  6,  8- 12, 14, 24
****FILE 107
$$$$ Go to label LBL6 if no free-body supports exist.
COND     LBL6,REACT $   
****CARD   1-  6,  8- 12, 14, 24
****FILE 107
$$$$ RBMG1 partitions out free-body supports
$$$$ 
$$$$           +       +                        +       +
$$$$           |K  |K  |                        |M  |M  |
$$$$           | ll| lr|                        | ll| lr|
$$$$   [K  ] = |---+---|                [M  ] = |---+---|
$$$$     aa    |K  |K  |                  aa    |M  |M  |
$$$$           | rl| rr|                        | rl| rr|
$$$$           +       +                        +       +
$$$$
RBMG1    USET,KAA,MAA/KLL,KLR,KRR,MLL,MLR,MRR $ 
****CARD   1-  6,  8- 12, 14, 24
****FILE 107
$$$$ Go to label LBL8.
JUMP     LBL8 $ 
****CARD   1-  4,  6,  8- 12
****FILE 107
$$$$
LABEL    LBL6 $ 
****CARD   1-  6,  8- 12, 14, 24
****FILE 107
$$$$ Go to label LBL7 if there is no request for mode acceleration data 
$$$$ recovery.
COND     LBL7,MODACC $  
****CARD   1-  6,  8- 12, 14, 24
****FILE 108-110
$$$$
LABEL    LBL8 $ 
****CARD   1-  4,  6,  8- 12
****FILE 107
$$$$ RBMG2 decomposes constrained stiffness matrix [K  ] = [L  ][U  ].
$$$$                                                 ll      ll   ll
$$$$
RBMG2    KLL/LLL $  
****CARD   1-  4,  6,  8- 12
****FILE 108
$$$$ Go to label LBL7 if no free-body supports exist.
COND     LBL7,REACT $   
****CARD   1-  4,  6,  8- 12, 14, 24
****FILE 109
$$$$ RBMG3 forms rigid body transformation matrix
$$$$ 
$$$$               -1
$$$$   [D] = -[K  ]  [K  ]
$$$$            ll     lr
$$$$ 
$$$$ calculates rigid body check matrix
$$$$
$$$$                   T
$$$$   [X] = [K  ] + [K  ][D]
$$$$           rr      lr
$$$$ 
$$$$ and calculates rigid body error ratio
$$$$ 
$$$$ 
$$$$               ||X||
$$$$   epsilon = ---------
$$$$              ||K  ||
$$$$                 rr
$$$$
RBMG3    LLL,KLR,KRR/DM $   
****CARD   1-  4,  6,  8- 12
****FILE 109
$$$$ RBMG4 forms rigid body mass matrix
$$$$ 
$$$$                    T          T           T
$$$$   [m ] = [M  ] + [M  ][D] + [D ][M  ] + [D ][M  ][D]
$$$$     r      rr      lr             lr          ll
$$$$
RBMG4    DM,MLL,MLR,MRR/MR $
****CARD   1-  6,  8- 12, 14, 24
****FILE 110
$$$$
LABEL    LBL7 $ 
****CARD   1-  6,  8- 12, 14, 24
****FILE 108-110
$$$$ DPD generates flags defining members of various displacement sets used in 
$$$$ dynamic analysis (USETD), tables relating the internal and external grid 
$$$$ point numbers (GPLD), including extra points introduced for dynamic 
$$$$ analysis (SILD), and prepares Transfer Function Pool (TFPOOL), Dynamic 
$$$$ Loads Table (DLT), Power Spectral Density List (PSDL), Frequency Response 
$$$$ List (FRL), and Eigenvalue Extraction Data (EED).
DPD      DYNAMICS,GPL,SIL,USET/GPLD,SILD,USETD,TFPOOL,DLT,PSDL,FRL,,,   
         EED,EQDYN/LUSET/S,N,LUSETD/NOTFL/S,N,NODLT/S,N,NOPSDL/ 
         S,N,NOFRL/NONLFT/NOTRL/S,N,NOEED//S,N,NOUE $   
****CARD   1,  9- 12, 55, 56, 58, 59
****FILE 111
$$$$ Go to label ERROR2 and print Error Message No. 2 if there is no 
$$$$ Eigenvalue Extraction Data.
COND     ERROR2,NOEED $ 
****CARD   1,  9- 12, 55, 56, 58, 59
****FILE 111
****RFMT 187-196,198-204,207-209
$$$$
PURGE    UEVF/NOUE $
****CARD   1,  9- 12, 55, 56, 58, 59
****FILE 120
$$$$                       d                d
$$$$ Equivalence [G ] to [G ] and [G ] to [G ] if there are no extra points
$$$$               o       o        m       m
$$$$ introduced for dynamic analysis.
EQUIV    GO,GOD/NOUE/GM,GMD/NOUE $  
****CARD   1-  6,  8- 12, 14, 22- 24, 53, 56, 57, 59, 60
****FILE 115
$$$$
PARAM    //*MPY*/NEIGV/1/-1 $   
****CARD   1-  6,  8- 12, 14, 24
****FILE 112
$$$$ READ extracts real eigenvalues and eigenvectors from the equation
$$$$ 
$$$$   [K   - lambda M  ]{u } = 0
$$$$     aa           aa   a
$$$$ 
$$$$ calculates rigid body modes by finding a square matrix [phi  ] such that
$$$$                                                            ro
$$$$              T
$$$$   [m ] = [phi  ][m ][phi  ]
$$$$     o        ro   r     ro
$$$$ 
$$$$ is diagonal and normalized, computes rigid body eigenvectors
$$$$ 
$$$$             +       +
$$$$             |Dphi   |
$$$$             |    ro |
$$$$   [phi  ] = |-------|
$$$$       ao    |phi    |
$$$$             |   ro  |
$$$$             +       +
$$$$ 
$$$$ calculates modal mass matrix
$$$$ 
$$$$             T
$$$$   [m] = [phi ][M  ][phi ]
$$$$             a   aa     a
$$$$ 
$$$$ and normalizes eigenvectors according to one of the following user 
$$$$ requests:
$$$$   1. Unit value of a selected component.
$$$$   2. Unit value of the largest component.
$$$$   3. Unit value of the generalized mass.
READ     KAA,MAA,MR,DM,EED,USET,CASECC/LAMA,PHIA,MI,OEIGS/*MODES*/S,N,  
         NEIGV $
****CARD   1-  6,  8- 12, 14, 24, 59, 60
****FILE 112
$$$$ OFP formats the summary of eigenvalue extraction information (OEIGS) 
$$$$ prepared by READ and places it on the system output file for printing.
OFP      OEIGS,,,,,//S,N,CARDNO $   
****CARD   1-  6,  8- 12, 14, 24, 59, 60
****FILE 112
$$$$ Go to label ERROR4 and print Error Message No. 4 if no eigenvalues were 
$$$$ found.
COND     ERROR4,NEIGV $ 
****CARD   1-  6,  8- 12, 14, 24, 59, 60
****FILE 112
****RFMT 187-196,198-204,207-209
$$$$ OFP formats the eigenvalues (LAMA) prepared by READ and places them on 
$$$$ the system output file for printing.
OFP      LAMA,,,,,//S,N,CARDNO $
****CARD   1-  6,  8- 12, 14, 24, 59, 60
****FILE 112
$$$$
PARAM    //*ADD*/NEVER/1/0 $
****CARD  22, 23
****FILE 114,115,122,123,125,130-134,137,138,140
$$$$
PARAM    //*MPY*/REPEATF/1/-1 $ 
****CARD   1-  6,  8- 14, 16, 19- 23, 27, 53- 62
****FILE 113
****RFMT 187-196,198-204,207-209
$$$$ Beginning of loop for additional sets of direct input matrices.
LABEL    LBL13 $
****SBST   1,  3
****CARD   1-  6,  8- 16, 18- 23, 53- 62
****FILE 113
****RFMT 187-196,198-204,207-209
$$$$
PURGE    OUHVC1,OUHVC2,XYPLTFA,OPPC1,OQPC1,OUPVC1,OESC1,OEFC1,OPPC2,
         OQPC2,OUPVC2,OESC2,OEFC2,XYPLTF,PSDF,AUTO,XYPLTR,K2PP,M2PP,
         B2PP,K2DD,M2DD,B2DD,OPPCA,IQP1,IPHIP1,IES1,IEF1,OPPCB,IQP2,
         IPHIP2,IES2,IEF2,ZQPC2,ZUPVC2,ZESC2,ZEFC2,ZQPC1,ZUPVC1,ZESC1,  
         ZEFC1/NEVER $  
****CARD  19- 23, 27
****FILE 114,115,118,119,122,123,125,130-134,137,138,140
$$$$ CASE extracts the appropriate record from CASECC corresponding to the 
$$$$ current loop and copies it into CASEXX.
CASE     CASECC,PSDL/CASEXX/*FREQ*/S,N,REPEATF/S,N,NOLOOP $ 
****CARD   1-  6,  8- 14, 16, 19- 23, 25, 27, 53- 62
****FILE 113
****RFMT 187-196,198-204,207-209
$$$$                                            2      2          2
$$$$ MTRXIN selects the direct input matrices [K  ], [M  ], and [B  ] for the
$$$$                                            pp     pp         pp
$$$$ current loop.
MTRXIN   CASEXX,MATPOOL,EQDYN,,TFPOOL/K2PP,M2PP,B2PP/LUSETD/S,N,
         NOK2PP/S,N,NOM2PP/S,N,NOB2PP $ 
****CARD   1, 22, 23, 56, 57
****FILE 114
$$$$
PURGE    K2DD/NOK2PP/M2DD/NOM2PP/B2DD/NOB2PP $  
****CARD   1-  6,  8- 11, 22, 23, 56, 57
****FILE 115
$$$$
PARAM    //*AND*/MDEMA/NOUE/NOM2PP $
****CARD   1-  6,  8- 11, 22, 23, 56, 57
****FILE 115
$$$$               2        2      2        2          2        2
$$$$ Equivalence [M  ] to [M  ], [B  ] to [B  ], and [K  ] to [K  ] if no
$$$$               pp       dd     pp       dd         pp       dd
$$$$ 
$$$$ constraints are applied, and [M  ] to [M  ] if there are no direct input
$$$$                                aa       dd
$$$$ mass matrices and no extra points introduced for dynamic analysis.
EQUIV    M2PP,M2DD/NOA/B2PP,B2DD/NOA/K2PP,K2DD/NOA/MAA,MDD/MDEMA $  
****CARD   1-  6,  8- 11, 22, 23, 56, 57
****FILE 115
$$$$                                                     2      2             
$$$$ GKAD applies constraints to direct input matrices [K  ], [M  ], and      
$$$$                                                     pp     pp            
$$$$   2              2      2          2
$$$$ [B  ], forming [K  ], [M  ], and [B  ].
$$$$   pp             dd     dd         dd
$$$$
GKAD     USETD,GM,GO,,,MAA,,K2PP,M2PP,B2PP/,,MDD,GMD,   
         GOD,K2DD,M2DD,B2DD/*FREQRESP*/*DISP*/*MODAL*/0.0/  
         0.0/0.0/NOK2PP/NOM2PP/NOB2PP/  
         MPCF1/SINGLE/OMIT/NOUE/-1/-1/  
         1/V,Y,MODACC = -1 $
****CARD   1-  6,  8- 11, 14, 22- 24, 53, 56, 57, 60
****FILE 115
$$$$ GKAM assembles stiffness, mass, and damping matrices in modal coordinates 
$$$$ for use in Frequency Response:
$$$$ 
$$$$                     T    2
$$$$   [K  ] = [k] + [phi  ][K  ][phi  ]
$$$$     hh              dh   dd     dh
$$$$ 
$$$$                     T    2
$$$$   [M  ] = [m] + [phi  ][M  ][phi  ]
$$$$     hh              dh   dd     dh
$$$$ 
$$$$                     T    2
$$$$   [B  ] = [b] + [phi  ][B  ][phi  ]
$$$$     hh              dh   dd     dh
$$$$ 
$$$$ where
$$$$ 
$$$$   m  = modal masses
$$$$    i
$$$$ 
$$$$   b  = m  2 pi f  g(f )
$$$$    i    i       i    i
$$$$ 
$$$$               2  2
$$$$   k  = m  4 pi  f
$$$$    i    i        i
$$$$ 
$$$$ Direct input matrices may be complex.
GKAM     USETD,PHIA,MI,LAMA,DIT,M2DD,B2DD,K2DD,CASEXX/MHH,BHH,KHH,PHIDH/
         NOUE/C,Y,LMODES=0/C,Y,LFREQ=0.0/C,Y,HFREQ=-1.0/
         NOM2PP/NOB2PP/NOK2PP/S,N,NONCUP/S,N,FMODE $
****CARD   1-  6,  8- 12, 14, 22- 24, 56, 57, 59, 60, 62
****FILE 116
$$$$ Go to label ERROR5 and print Error Message No. 5 if there is no Frequency 
$$$$ Response List.
COND     ERROR5,NOFRL $ 
****CARD   1-  6,  8- 12, 14, 22- 24, 56- 62
****FILE 111
****RFMT 187-196,198-204,207-209
$$$$ Go to label ERROR6 and print Error Message No. 6 if there is no Dynamic 
$$$$ Loads Table.
COND     ERROR6,NODLT $ 
****CARD   1-  6,  8- 12, 14, 22- 24, 56- 62
****FILE 111
****RFMT 187-196,198-204,207-209
$$$$ FRRD forms the dynamic load vectors {P } and solves for displacements
$$$$                                       h
$$$$ using the following equation:
$$$$
$$$$             2
$$$$   [-M  omega + iB  omega + K  ] {u } = {P }
$$$$      hh          hh         hh    h      h
$$$$
FRRD     CASEXX,USETD,DLT,FRL,GMD,GOD,KHH,BHH,MHH,PHIDH,DIT/UHVF,PSF,   
         PDF,PPF/*DISP*/*MODAL*/LUSETD/MPCF1/SINGLE/
         OMIT/NONCUP/S,N,FRQSET $   
****CARD   1-  6,  8- 12, 14, 22- 24, 56- 62
****FILE 117
$$$$ Equivalence {P } to {P } if no constraints are applied.
$$$$               p       d
$$$$
EQUIV    PPF,PDF/NOSET $
****CARD   1-  6,  8- 12, 14, 22- 24, 56- 62
****FILE 117
$$$$ VDR prepares displacements (OUHVC1), sorted by frequency, for output 
$$$$ using only the extra points introduced for dynamic analysis and modal 
$$$$ coordinates (solution points).
VDR      CASEXX,EQDYN,USETD,UHVF,PPF,XYCDB,/OUHVC1,/*FREQRESP*/ 
         *MODAL*/S,N,NOSORT2/S,N,NOH/S,N,NOP/FMODE $
****CARD  19- 21, 27
****FILE 118
$$$$ Go to label LBL16 if there is no output request for solution points.
COND     LBL16,NOH $
****CARD  21, 27
****FILE 118,119,137
$$$$ Go to label LBL16A if there is no output request for solution points 
$$$$ sorted by extra point or mode number.
COND     LBL16A,NOSORT2 $   
****CARD  21, 27
****FILE 118,119,137
$$$$ SDR3 sorts the solution point displacements by extra point or mode 
$$$$ number.
SDR3     OUHVC1,,,,,/OUHVC2,,,,, $  
****CARD  21, 27
****FILE 119
$$$$ OFP formats the requested solution point displacements prepared by SDR3 
$$$$ and places them on the system output file for printing.
OFP      OUHVC2,,,,,//S,N,CARDNO $  
****CARD  21
****FILE 119
$$$$ XYTRAN prepares the input for requested X-Y plots of the solution point 
$$$$ displacement vs. frequency.
XYTRAN   XYCDB,OUHVC2,,,,/XYPLTFA/*FREQ*/*HSET*/S,N,PFILE/  
         S,N,CARDNO $   
****SBST   7
****CARD  27
****FILE 137
$$$$ XYPLOT prepares the requested X-Y plots of the solution point 
$$$$ displacements vs. frequency.
XYPLOT   XYPLTFA // $   
****SBST   7
****CARD  27
****FILE 137
$$$$ Go to label LBL16.
JUMP     LBL16 $
****CARD  21, 27
****FILE 137
$$$$
LABEL    LBL16A $   
****CARD  21, 27
****FILE 118,119,137
$$$$ OFP formats the requested solution point displacements prepared by VDR 
$$$$ and places them on the system output file for printing.
OFP      OUHVC1,,,,,//S,N,CARDNO $  
****CARD  21
****FILE 118
$$$$
LABEL    LBL16 $
****CARD  20, 21, 27
****FILE 118,119,137
$$$$ Go to label LBL14 if there is no output request involving dependent 
$$$$ degrees of freedom or forces and stresses.
COND     LBL14,NOP $
****CARD   1-  6,  8- 12, 14, 19, 20, 22- 24, 26, 53- 62
****FILE 120-125,129-134,138-140
$$$$
PARAM    //*NOT*/NOMOD/V,Y,MODACC $ 
****CARD   1-  6,  8- 12, 14, 22- 24, 53, 56- 62
$$$$ Go to label LBDDRM if the mode acceleration technique is not requested.
COND     LBDDRM,MODACC $
****CARD   1-  6,  8- 12, 14, 19, 20, 22- 24, 53, 56- 62
****FILE 120-124
$$$$ DDR1 transforms the solution vector of displacements from modal to 
$$$$ physical coordinates
$$$$
$$$$   [u ] = [phi  ]{u }
$$$$     d        dh   h
$$$$
DDR1     UHVF,PHIDH/UDV1F $ 
****CARD   1-  6,  8- 12, 14, 22- 24, 53, 56- 62
****FILE 124
$$$$ DDR2 calculates an improved displacement vector using the mode 
$$$$ acceleration technique.
DDR2     USETD,UDV1F,PDF,K2DD,B2DD,MDD,PPF,LLL,DM/UDV2F,UEVF,PAF/   
         *FREQRESP*/NOUE/REACT/FRQSET $ 
****SBST   2,  3
****CARD   1-  6,  8- 12, 14, 22- 24, 53, 56- 62
****FILE 120
$$$$ Equivalence {u } to the improved displacement vector. (Flag NOMOD is 
$$$$               d
$$$$ negative since the mode acceleration technique is requested.)
EQUIV    UDV2F,UDV1F/NOMOD $
****SBST   2,  3
****CARD   1-  6,  8- 12, 14, 22- 24, 53, 56- 62
****FILE 124
$$$$ Equivalence {u } to {u } if no constraints are applied.
$$$$               d       p
$$$$
EQUIV    UDV1F,UPVC/NOA $   
****CARD   1-  6,  8- 12, 14, 22- 24, 53, 56- 62
****FILE 121
****RFMT 187-196,198-204,207-209
$$$$ Go to label LBLNOA if no constraints are applied.
COND     LBLNOA,NOA $   
****CARD   1-  6,  8- 12, 14, 22- 24, 53, 56- 62
****FILE 121
****RFMT 187-196,198-204,207-209
$$$$ SDR1 recovers dependent components of displacements
$$$$ 
$$$$                                 u
$$$$           d                      d
$$$$  {u } = [G ]{u }               {----} = {u  + u  }
$$$$    o      o   d                 u         f     e
$$$$                                   o
$$$$
$$$$    u + u      
$$$$     f   e                              d
$$$$   {-------} = {u + u }        {u } = [G ]{u  + u }
$$$$      u          n   e           m      m   f    e
$$$$       s        
$$$$
$$$$    u + u                
$$$$     n   e               
$$$$   {---------} = {u }
$$$$       u           p 
$$$$        m                  
$$$$                                                                 T
$$$$ and recovers single-point forces of constraint {q } = -{P } + [K  ]{u }.
$$$$                                                  s       s      fs   f
$$$$
SDR1     USETD,,UDV1F,,,GOD,GMD,PSF,KFS,,/UPVC,,QPC/1/*DYNAMICS* $  
****CARD   1-  6,  8- 12, 14, 22- 24, 53, 56- 62
****FILE 121
****RFMT 187-196,198-204,207-209
$$$$
LABEL    LBLNOA $   
****CARD   1-  6,  8- 12, 14, 22- 24, 53, 56- 62
****FILE 121
****RFMT 187-196,198-204,207-209
$$$$ SDR2 calculates element forces (OEFC1) and stresses (OESC1) and 
$$$$ prepares load vectors (OPPC1), displacement, velocity, and acceleration
$$$$ vectors (OUPVC1), and single-point forces of constraint (OQPC1) for output 
$$$$ and translation components of the displacement vector (PUGV), sorted by 
$$$$ frequency. 
SDR2     CASEXX,CSTM,MPT,DIT,EQDYN,SILD,,,BGPDP,PPF,QPC,UPVC,EST,   
         XYCDB,PPF,/OPPC1,OQPC1,OUPVC1,OESC1,OEFC1,PUGV,,/*FREQ*/   
         S,N,NOSORT2 $  
****CARD  19, 20
****FILE 122
$$$$ Go to label LBL18 if there are no requests for output sorted by point 
$$$$ number or element number.
COND     LBL18,NOSORT2 $
****CARD   1-  6,  8- 12, 14, 19, 20, 22- 24, 26, 53- 62
****FILE 122,123,125,129-134,138-140
$$$$ SDR3 prepares the requested output sorted by point number or element 
$$$$ number.
SDR3     OPPC1,OQPC1,OUPVC1,OESC1,OEFC1,/OPPC2,OQPC2,OUPVC2,OESC2,  
         OEFC2, $   
****CARD  19, 20
****FILE 123
$$$$ Go to label P2A.
JUMP     P2A $  
****CARD  19, 20
****FILE 123
$$$$
LABEL    LBDDRM $   
****CARD   1-  6,  8- 12, 14, 19, 20, 22- 24, 53, 56- 62
****FILE 120-124
$$$$ SDR1 recovers dependent components of eigenvectors
$$$$ 
$$$$                                   phi
$$$$             d                        n
$$$$  {phi } = [G ]{phi }             {----} = {phi  + phi }
$$$$      o      o     n               phi         f      e
$$$$                                      o
$$$$
$$$$    phi +phi      
$$$$       f    e                                  d
$$$$   {---------} = {phi +phi }        {phi } = [G ]{phi  + phi }
$$$$      phi            n    e             m      m     n      e
$$$$         s        
$$$$
$$$$    phi +phi                
$$$$       n    e               
$$$$   {---------} = {phi  + u } = {phi }
$$$$      phi            g    e        p
$$$$         m                  
$$$$                                                         T
$$$$ and recovers single-point forces of constraint {q } = [K  ] {phi }.
$$$$                                                  s      fs      f
$$$$
SDR1     USETD,,PHIDH,,,GOD,GMD,,KFS,,/PHIPH,,QPH/1/*DYNAMICS* $
****CARD   1-  6,  8- 12, 14, 22- 24, 53, 56- 62
****FILE 129
****RFMT 187-196,198-204,207-209
$$$$ SDR2 calculates element forces (IEF1) and stresses (IES1) and prepares 
$$$$ eigenvectors (IPHIP1) and single-point forces of constraint (IQP1) for 
$$$$ output sorted by frequency.
SDR2     CASEXX,CSTM,MPT,DIT,EQDYN,SILD,,,,LAMA,QPH,PHIPH,EST,XYCDB,,/  
         ,IQP1,IPHIP1,IES1,IEF1,,,/*MMREIG*/S,N,NOSORT2 $   
****CARD  19, 20
****FILE 130
$$$$ SDR2 prepares load vectors for output (OPPCA) sorted by frequency.
SDR2     CASEXX,CSTM,MPT,,EQDYN,SILD,,,,PPF,,,EST,XYCDB,PPF,/OPPCA, 
         ,,,,,,/*FREQ* $
****CARD  19, 20
****FILE 131
$$$$ Equivalence OPPCA to OPPC1. (Flag MODACC is negative since the 
$$$$ mode acceleration technique is not requested.)
EQUIV    OPPCA,OPPC1/MODACC $   
****CARD  19, 20
****FILE 122
$$$$ Go to label LBLSORT if there are no requests for output sorted by point 
$$$$ number or element number.
COND     LBLSORT,NOSORT2 $  
****CARD  19, 20
****FILE 123,132,133
$$$$ SDR3 prepares the requested output sorted by point number or element 
$$$$ number.
SDR3     IQP1,IPHIP1,IES1,IEF1,OPPCA,/IQP2,IPHIP2,IES2,IEF2,OPPCB, $
****CARD  19, 20
****FILE 132
$$$$ Equivalence OPPCB to OPPC2. (Flag MODACC is negative since the 
$$$$ mode acceleration technique is not requested.)
EQUIV    OPPCB,OPPC2/MODACC $   
****CARD  19, 20
****FILE 123
$$$$ DDRMM prepares a subset of the element forces (ZEFC2) and stresses 
$$$$ (ZESC2), and displacement vectors (ZUPVC2) and single-point forces of 
$$$$ constraint (ZQPC2) for output sorted by point number or element number.
DDRMM    CASEXX,UHVF,PPF,IPHIP2,IQP2,IES2,IEF2,XYCDB,EST,MPT,DIT/   
         ZUPVC2,ZQPC2,ZESC2,ZEFC2,  $   
****CARD  19, 20
****FILE 133
$$$$ Equivalence ZUPVC2 to OUPVC2, ZQPC2 to OQPC2, ZESC2 to OESC2, and ZEFC2 
$$$$ to OEFC2. (Flag MODACC is negative since the mode acceleration technique 
$$$$ is not requested.) 
EQUIV    ZUPVC2,OUPVC2/MODACC/ZQPC2,OQPC2/MODACC/ZESC2,OESC2/MODACC/
         ZEFC2,OEFC2/MODACC $   
****CARD  19, 20
****FILE 123
$$$$ Go to label P2A.
JUMP     P2A $  
****CARD  19, 20
****FILE 123
$$$$
LABEL    LBLSORT $  
****CARD  19, 20
****FILE 123,132,133
$$$$ DDRMM prepares a subset of the element forces (ZEFC2) and stresses 
$$$$ (ZESC2), and displacement vectors (ZUPVC2) and single-point forces of 
$$$$ constraint (ZQPC2) for output sorted by frequency.
DDRMM    CASEXX,UHVF,PPF,IPHIP1,IQP1,IES1,IEF1,,EST,MPT,DIT/
         ZUPVC1,ZQPC1,ZESC1,ZEFC1, $
****CARD  19, 20
****FILE 134
$$$$ Equivalence ZUPVC1 to OUPVC1, ZQPC1 to OQPC1, ZESC1 to OESC1, and ZEFC1 
$$$$ to OEFC1. (Flag MODACC is negative since the mode acceleration technique 
$$$$ is not requested.) 
EQUIV    ZUPVC1,OUPVC1/MODACC/ZQPC1,OQPC1/MODACC/ZESC1,OESC1/MODACC/
         ZEFC1,OEFC1/MODACC $   
****CARD  19, 20
****FILE 122
$$$$ Go to label LBL18.
JUMP     LBL18 $
****CARD  19, 20
****FILE 134
$$$$
LABEL    P2A $  
****CARD  19, 20
****FILE 123
$$$$ OFP formats the requested output prepared by SDR3 (with mode 
$$$$ acceleration) or DDRMM (no mode acceleration) and places it on the system 
$$$$ output file for printing.
OFP      OPPC2,OQPC2,OUPVC2,OEFC2,OESC2,//S,N,CARDNO $  
****CARD  19
****FILE 123
$$$$ XYTRAN prepares the input for requested X-Y plots.
XYTRAN   XYCDB,OPPC2,OQPC2,OUPVC2,OESC2,OEFC2/XYPLTF/*FREQ*/*PSET*/ 
         S,N,PFILE/S,N,CARDNO $ 
****SBST   7
****CARD  20
****FILE 138
$$$$ XYPLOT prepares the requested X-Y plots of displacements, forces, 
$$$$ stresses, loads, and single-point forces of constraint vs. frequency.
XYPLOT   XYPLTF// $ 
****SBST   7
****CARD  20
****FILE 138
$$$$ Go to label LBL21 if no deformed structure plots are requested.
COND     LBL21,JUMPPLOT $   
****SBST   7
****CARD  20
****FILE 139
$$$$ PLOT generates all requested deformed structure and contour plots.
PLOT     PLTPAR,GPSETS,ELSETS,CASEXX,BGPDT,EQEXIN,SIP,,PUGV,,,,/
         PLOTX2/NSIL/LUSEP/JUMPPLOT/PLTFLG/S,N,PFILE $  
****SBST   7
****CARD  20
****FILE 139
$$$$ PRTMSG prints plotter data, engineering data, and contour data for each 
$$$$ deformed plot generated.
PRTMSG   PLOTX2// $ 
****SBST   7
****CARD  20
****FILE 139
$$$$
LABEL    LBL21 $
****SBST   7
****CARD  20
****FILE 139
$$$$ Go to label LBL14 if no power spectral density functions or 
$$$$ autocorrelation functions are requested.
COND     LBL14,NOPSDL $ 
****CARD  20, 26, 54, 55
****FILE 125
****RFMT 187-196,198-204,207-209
$$$$ RANDOM calculates the power spectral density functions (PSDF) and 
$$$$ autocorrelation functions (AUTO) using the previously calculated 
$$$$ frequency response.
RANDOM   XYCDB,DIT,PSDL,OUPVC2,OPPC2,OQPC2,OESC2,OEFC2,CASEXX/PSDF,AUTO/
         S,N,NORD $ 
****CARD  26, 54, 55
****FILE 125
****RFMT 187-196,198-204,207-209
$$$$ Go to label LBL14 if no X-Y plots of RANDOM calculations are requested.
COND     LBL14,NORD $   
****CARD  26, 54, 55
****FILE 140
$$$$ XYTRAN prepares the input for requested X-Y plots of the RANDOM output.
XYTRAN   XYCDB,PSDF,AUTO,,,/XYPLTR/*RAND*/*PSET*/S,N,PFILE/ 
         S,N,CARDNO $   
****SBST   7
****CARD  20
****FILE 140
$$$$ XYPLOT prepares the requested X-Y plots of autocorrelation functions and 
$$$$ power spectral density functions.
XYPLOT   XYPLTR// $ 
****SBST   7
****CARD  20
****FILE 140
$$$$ Go to label LBL14.
JUMP     LBL14 $
****CARD  20
****FILE 140
$$$$
LABEL    LBL18 $
****CARD   1-  6,  8- 12, 14, 19, 20, 22- 24, 26, 53- 62
****FILE 122,123,125,129-134,138-140
$$$$ OFP formats the frequency response output requests prepared by SDR2 (with 
$$$$ mode acceleration) or DDRMM (no mode acceleration) and places them on the 
$$$$ system output file for printing.
OFP      OUPVC1,OPPC1,OQPC1,OEFC1,OESC1,//S,N,CARDNO $  
****CARD  19
****FILE 122
$$$$
LABEL    LBL14 $
****CARD   1-  6,  8- 12, 14, 19, 20, 22- 24, 26, 53- 62
****FILE 120-125,129-134,138-140
$$$$ Go to label FINIS and make normal exit if no additional sets of direct 
$$$$ input matrices need to be processed.
COND     FINIS,REPEATF $
****SBST   1,  3
****CARD  22, 23
****RFMT 187-196,198-204,207-209
$$$$ Go to label LBL13 if additional sets of direct input matrices need to be 
$$$$ processed.
REPT     LBL13,100 $
****SBST   1,  3
****CARD  22, 23
****RFMT 187-196,198-204,207-209
$$$$ Print Error Message No. 3 and terminate execution.
PRTPARM  //-3/*MDLFRRD* $   
****SBST   1,  3
****CARD  22, 23
****RFMT 187-196,198-204,207-209
$$$$ Go to label FINIS and make normal exit.
JUMP     FINIS $
****CARD   1-  6,  8- 16, 18- 29, 53- 62
****RFMT 187-196,198-204,207-209
$$$$
LABEL    ERROR2 $   
****CARD   1,  9- 12, 55, 56, 58, 59
****FILE 111
****RFMT 187-196,198-204,207-209
$$$$ Print Error Message No. 2 and terminate execution.
PRTPARM  //-2/*MDLFRRD* $   
****CARD   1,  9- 12, 55, 56, 58, 59
****FILE 111
****RFMT 187-196,198-204,207-209
$$$$
LABEL    ERROR1 $   
****CARD   1-  3,  5,  6,  8, 13, 24
****FILE 128
****RFMT 187-196,198-204,207-209
$$$$ Print Error Message No. 1 and terminate execution.
PRTPARM  //-1/*MDLFRRD* $   
****CARD   1-  3,  5,  6,  8, 13, 24
****FILE 128
****RFMT 187-196,198-204,207-209
$$$$
LABEL    ERROR4 $   
****CARD   1-  6,  8- 12, 14, 24, 59, 60
****FILE 112
****RFMT 187-196,198-204,207-209
$$$$ Print Error Message No. 4 and terminate execution.
PRTPARM  //-4/*MDLFRRD* $   
****CARD   1-  6,  8- 12, 14, 24, 59, 60
****FILE 112
****RFMT 187-196,198-204,207-209
$$$$
LABEL    ERROR5 $   
****CARD   1-  6,  8- 12, 14, 22- 24, 56- 62
****FILE 111
****RFMT 187-196,198-204,207-209
$$$$ Print Error Message No. 5 and terminate execution.
PRTPARM  //-5/*MDLFRRD* $   
****CARD   1-  6,  8- 12, 14, 22- 24, 56- 62
****FILE 111
****RFMT 187-196,198-204,207-209
$$$$
LABEL    ERROR6 $   
****CARD   1-  6,  8- 12, 14, 22- 24, 56- 62
****FILE 111
****RFMT 187-196,198-204,207-209
$$$$ Print Error Message No. 6 and terminate execution.
PRTPARM  //-6/*MDLFRRD* $   
****CARD   1-  6,  8- 12, 14, 22- 24, 56- 62
****FILE 111
****RFMT 187-196,198-204,207-209
$$$$
LABEL    ERROR7 $   
****CARD   1,  2,  4-  6,  8, 16
****FILE  97
****RFMT 187-196,198-204,207-209
$$$$ Print Error Message No. 7 and terminate execution.
PRTPARM  //-7/*MDLFRRD* $   
****CARD   1,  2,  4-  6,  8, 16
****FILE  97
****RFMT 187-196,198-204,207-209
$$$$
LABEL    FINIS $
****CARD   1-  6,  8- 16, 18- 29, 53- 62
****RFMT 187-196,198-204,207-209
$$$$
PURGE    DUMMY/MINUS1 $ 
****CARD   1-  6,  8- 16, 18- 29, 53- 62
****RFMT 187-196,198-204,207-209
$$$$
END      $  
****CARD   1-  6,  8- 16, 18- 29, 53- 62
****RFMT 187-196,198-204,207-209
$$$$
$*CARD BITS 
  1    AXIC     AXIF     CELAS1   CELAS2   CELAS3   CELAS4   CMASS1 
  1    CMASS2   
  1    CMASS3   CMASS4   CORD1C   CORD1R   CORD1S   CORD2C   CORD2R 
  1    CORD2S   
  1    GRDSET   GRID     GRIDB    POINTAX  RINGAX   RINGFL   SECTAX 
  1    SEQGP    SPOINT  
  2    ADUM1    ADUM2    ADUM3    ADUM4    ADUM5    ADUM6    ADUM7  
  2    ADUM8
  2    ADUM9    BAROR    CBAR     CCONEAX  CDUM1    CDUM2    CDUM3  
  2    CDUM4
  2    CDUM5    CDUM6    CDUM7    CDUM8    CDUM9    CELBOW   CHEXA1 
  2    CHEXA2   
  2    CIHEX1   CIHEX2   CIHEX3   CIS2D8   CONROD   CQDMEM   CQDMEM1
  2    CQDMEM2  CQDPLT   CQUAD1   CQUAD2   CROD     CSHEAR   CTETRA 
  2    CTORDRG  CQUAD4   CTRIA3
  2    CTRAPAX  CTRAPRG  CTRBSC   CTRIA1   CTRIA2   CTRIAAX  CTRIARG
  2    CTRIM6   
  2    CTRMEM   CTRPLT   CTRPLT1  CTRSHL   CTUBE    CTWIST   CWEDGE 
  3    PBAR     PCONEAX  PDUM1    PDUM2    PDUM3    PDUM4    PDUM5  
  3    PDUM6
  3    PDUM7    PDUM8    PDUM9    PELBOW   PIHEX    PIS2D8   PQDMEM 
  3    PQDMEM1  PQDMEM2  PQDPLT   PQUAD1   PQUAD2   PROD     PSHEAR 
  3    PTORDRG  PSHELL   PCOMP    PCOMP1   PCOMP2
  3    PTRAPAX  PTRBSC   PTRIA1   PTRIA2   PTRIAAX  PTRIM6   PTRMEM 
  3    PTRPLT   PTRPLT1  PTRSHL   PTUBE    PTWIST   
  4    GENEL
  5    CONM1    CONM2    PMASS  
  6    PELAS
  8    MAT1     MAT2     MAT3     MATT1    MATT2    MATT3    MAT8
  8    TABLEM1  TABLEM2  TABLEM3  TABLEM4  TEMPMT$  TEMPMX$  
  9    AXISYM$  CRIGD1   CRIGD2   CRIGD3   CRIGDR   CRROD    CRBAR  
  9    CRTRPLT  CRBE1    CRBE2    CRBE3    CRSPLINE MPC      MPCADD 
  9    MPCAX    MPC$
 10    SPC      SPC1     SPCADD   SPCAX    SPC$ 
 11    ASET     ASET1    OMIT     OMIT1    OMITAX   SUPAX    SUPORT 
 13    TEMP     TEMPAX   TEMPD    TEMPP1   TEMPP2   TEMPP3   TEMPRB 
 14    WTMASS   
 15    GRDPNT   
 16    PLOTEL   
 18    PLOT$
 19    POUT$
 20    XYOUT$   
 21    AOUT$
 22    LOOP$
 23    LOOP1$   
 24    COUPMASS CPBAR    CPQDPLT  CPQUAD1  CPQUAD2  CPROD    CPTRBSC
 24    CPTRIA1  CPTRIA2  CPTRPLT  CPTUBE
 25    NOLOOP$  
 26    RANDOM$  
 27    AXYOUT$  
 28    ASETOUT  
 29    AUTOSPC  
 53    MODACC   
 54    TABRND1  TABRND2  TABRND3  TABRND4   
 55    RANDPS   RANDT1   RANDT2 
 56    EPOINT   SEQEP    TF 
 57    DMIAX    DMIG     B2PP$    K2PP$    M2PP$    TF$ 
 58    DAREA    DELAY    DLOAD    DPHASE   FREQ     FREQ1    FREQ2  
 58    RLOAD1   RLOAD2   TABLED1  TABLED2  TABLED3  TABLED4 
 59    EIGR 
 60    METHOD$  
 61    DECOMOPT DLOAD$   FREQ$  
 62    HFREQ    LFREQ    LMODES   TABDMP1  SDAMP$   
$$$$
$*FILE BITS 
 94    BGPDT    CSTM     EQEXIN   GPDT     GPL      SIL 
 95    ECT  
 96    GPTT 
 97    EST      GEI      GPECT    MPTX     PCOMPS   EPTX
 98    KGGX 
 99    MGG  
100    KGG  
101    ASET     RG       USET     OGPST 
102    GPST 
103    GM   
104    KNN      MNN 
105    KFF      KFS      MFF
106    GO       KOO      LOO      KAA   
107    KLL      KLR      KRR      MLL      MLR      MRR 
108    LLL  
109    DM   
110    MR   
111    DLT      EED      EQDYN    FRL      GPLD     PSDL     SILD   
111    TFPOOL   USETD   
112    LAMA     MI       OEIGS    PHIA  
113    CASEXX   
114    B2PP     K2PP     M2PP   
115    B2DD     GMD      GOD      K2DD     M2DD     MDD 
116    BHH      KHH      MHH      PHIDH 
117    PDF      PPF      PSF      UHVF  
118    OUHVC1   
119    OUHVC2   
120    PAF      UDV2F    UEVF   
121    QPC      UPVC
122    OEFC1    OESC1    OPPC1    OQPC1    OUPVC1   
123    OEFC2    OESC2    OPPC2    OQPC2    OUPVC2   
124    UDV1F
125    AUTO     PSDF
126    ELSETS   GPSETS   PLTPAR   PLTSETX   
127    MAA  
128    KDICT    KELM     MDICT    MELM  
129    PHIPH    QPH 
130    IEF1     IES1     IPHIP1   IQP1  
131    OPPCA
132    IEF2     IES2     IPHIP2   OPPCB    IQP2 
133    ZEFC2    ZESC2    ZQPC2    ZUPVC2
134    ZEFC1    ZESC1    ZQPC1    ZUPVC1
135    PLOTX1   
136    OGPWG
137    XYPLTFA  
138    XYPLTF   
139    PLOTX2   
140    XYPLTR   
141    BGPDP    SIP 
$*  
=PAGE=
DISP12

APR.93
$$$$$$$$
BEGIN    DISP 12 - MODAL TRANSIENT RESPONSE ANALYSIS - APR. 1993 $  
****CARD   1-  6,  8- 28, 55- 62
****RFMT 187-197,199-204,207-209
$$$$
PRECHK   ALL $  
****SBST   6
****CARD   1-  6,  8- 28, 55- 62
****RFMT 187-197,199-204,207-209
$$$$
FILE     LAMA=APPEND/PHIA=APPEND/UHVT=APPEND/TOL=APPEND $   
****CARD   1-  6,  8- 28, 55- 62
****RFMT 187-197,199-204,207-209
$$$$
PARAM    //*MPY*/CARDNO/0/0 $   
****CARD   1-  6,  8- 12, 14, 15, 19, 21, 24, 28, 59, 60
****FILE 101,112,119,123,134
$$$$ GP1 generates coordinate system transformation matrices, tables of grid 
$$$$ point locations, and tables relating the internal and external grid point 
$$$$ numbers.
GP1      GEOM1,GEOM2,/GPL,EQEXIN,GPDT,CSTM,BGPDT,SIL/S,N,LUSET/ 
         NOGPDT/MINUS1=-1 $ 
****CARD   1
****FILE  94
$$$$ PLTTRAN modifies special scalar grid points in the BGPDT and SIL tables.
PLTTRAN  BGPDT,SIL/BGPDP,SIP/LUSET/S,N,LUSEP $  
****CARD   1
****FILE 138
$$$$ GP2 generates Element Connection Table with internal indices.
GP2      GEOM2,EQEXIN/ECT $ 
****CARD   1,  2,  4,  5, 16
****FILE  95
$$$$
PARAML   PCDB//*PRES*////JUMPPLOT $ 
****SBST   7
****CARD  18
****FILE 125,133
$$$$
PURGE    PLTSETX,PLTPAR,GPSETS,ELSETS/JUMPPLOT $
****SBST   7
****CARD  18
****FILE 125
$$$$ Go to label P1 if there are no structure plot requests.
COND     P1,JUMPPLOT $  
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 125,133
$$$$ PLTSET transforms user input into a form used to drive the structure 
$$$$ plotter.
PLTSET   PCDB,EQEXIN,ECT,EPT/PLTSETX,PLTPAR,GPSETS,ELSETS/S,N,NSIL/ 
         S,N,JUMPPLOT $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 125
$$$$ PRTMSG prints error messages associated with the structure plotter.
PRTMSG   PLTSETX// $
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 125
$$$$
PARAM    //*MPY*/PLTFLG/1/1 $   
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 133
$$$$
PARAM    //*MPY*/PFILE/0/0 $
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 133
$$$$ Go to label P1 if no undeformed structure plots are requested.
COND     P1,JUMPPLOT $  
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 133
$$$$ PLOT generates all requested undeformed structure plots.
PLOT     PLTPAR,GPSETS,ELSETS,CASECC,BGPDT,EQEXIN,SIL,,ECT,,,,/PLOTX1/  
         NSIL/LUSET/S,N,JUMPPLOT/S,N,PLTFLG/S,N,PFILE $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 133
$$$$ PRTMSG prints plotter data and engineering data for each undeformed plot 
$$$$ generated.
PRTMSG   PLOTX1// $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 133
$$$$
LABEL    P1 $   
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 125,133
$$$$ GP3 generates Grid Point Temperature Table.
GP3      GEOM3,EQEXIN,GEOM2/SLT,GPTT/NOGRAV $   
****CARD   1,  2, 13, 61
****FILE  96
$$$$ TA1 generates element tables for use in matrix assembly and stress 
$$$$ recovery.
TA1      ECT,EPT,BGPDT,SIL,GPTT,CSTM,MPT,EQEXIN/EST,GEI,GPECT,,,MPTX,
         PCOMPS,EPTX/LUSET/S,N,NOSIMP/1/S,N,NOGENL/GENEL/S,N,COMPS $   
****CARD   1-  6, 13, 16
****FILE  97
$$$$
EQUIV    MPTX,MPT/COMPS/EPTX,EPT/COMPS $
****CARD   1-  6,  8- 28, 55- 62
****FILE  97
$$$$ Go to label ERROR6 and print Error Message No. 6 if there are no 
$$$$ structural elements.
COND     ERROR6,NOSIMP $
****CARD   1,  2,  4-  6, 16
****FILE  97
$$$$
PARAM    //*ADD*/NOKGGX/1/0 $   
****CARD   1-  3,  6,  8
****FILE 127
$$$$
PARAM    //*ADD*/NOMGG/1/0 $
****CARD   1-  3,  5,  8
****FILE 127
****RFMT 187,190-192
$$$$ EMG generates structural element stiffness, mass and damping matrix 
$$$$ tables, and dictionaries for later assembly by the EMA module.
EMG      EST,CSTM,MPT,DIT,GEOM2,/KELM,KDICT,MELM,MDICT,,,/S,N,NOKGGX/   
         S,N,NOMGG////C,Y,COUPMASS/C,Y,CPBAR/   
         C,Y,CPROD/C,Y,CPQUAD1/C,Y,CPQUAD2/C,Y,CPTRIA1/C,Y,CPTRIA2/ 
         C,Y,CPTUBE/C,Y,CPQDPLT/C,Y,CPTRPLT/C,Y,CPTRBSC/
         C,Y,VOLUME/C,Y,SURFACE $   
****CARD   1-  3,  5,  6,  8, 24
****FILE 127
****RFMT 187,190-192
$$$$
PURGE    KGGX/NOKGGX $  
****CARD   1-  3,  6,  8
****FILE  98
$$$$ Go to label JMPKGGX if no stiffness matrix is to be assembled.
COND     JMPKGGX,NOKGGX $   
****CARD   1-  3,  6,  8
****FILE  98
$$$$                                  x
$$$$ EMA assembles stiffness matrix [K  ] and Grid Point Singularity Table.
$$$$                                  gg
$$$$
EMA      GPECT,KDICT,KELM/KGGX $
****CARD   1-  3,  6,  8
****FILE  98
$$$$
PURGE    KDICT,KELM/MINUS1 $
****CARD   1-  3,  6,  8
****FILE 127
$$$$
LABEL    JMPKGGX $  
****CARD   1-  3,  6,  8
****FILE  98
$$$$ Go to label ERROR1 and print Error Message No. 1 if no mass matrix is to 
$$$$ be assembled.
COND     ERROR1,NOMGG $ 
****CARD   1-  3,  5,  8, 14, 24
****FILE 127
****RFMT 187,190-192
$$$$ EMA assembles mass matrix [M  ].
$$$$                             gg
$$$$
EMA      GPECT,MDICT,MELM/MGG/-1/C,Y,WTMASS=1.0 $   
****CARD   1-  3,  5,  8, 14, 24
****FILE  99
****RFMT 187,190-192
$$$$
PURGE    MDICT,MELM/MINUS1 $
****CARD   1-  3,  5,  8, 14, 24
****FILE 127
$$$$ Go to label LGPWG if no weight and balance information is requested.
COND     LGPWG,GRDPNT $ 
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 134
$$$$ GPWG generates weight and balance information.
GPWG     BGPDP,CSTM,EQEXIN,MGG/OGPWG/V,Y,GRDPNT=-1/C,Y,WTMASS $ 
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 134
$$$$ OFP formats the weight and balance information prepared by GPWG and 
$$$$ places it on the system output file for printing.
OFP      OGPWG,,,,,//S,N,CARDNO $   
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 134
$$$$
LABEL    LGPWG $
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 134
$$$$               x
$$$$ Equivalence [K  ] to [K  ] if no general elements exist.
$$$$               gg       gg
$$$$
EQUIV    KGGX,KGG/NOGENL $  
****CARD   1-  4,  8
****FILE 100
$$$$ Go to label LBL11 if no general elements exist.
COND     LBL11,NOGENL $ 
****CARD   1-  4,  6,  8
****FILE 100
$$$$                                 x
$$$$ SMA3 adds general elements to [K  ] to obtain stiffness matrix [K  ].
$$$$                                 gg                               gg
$$$$
SMA3     GEI,KGGX/KGG/LUSET/NOGENL/NOSIMP $ 
****CARD   1-  4,  6,  8
****FILE 100
$$$$
LABEL    LBL11 $
****CARD   1-  4,  6,  8
****FILE 100
$$$$
GPSTGEN  KGG,SIL/GPST   
****CARD   1-  4,  6,  8
****FILE 102
$$$$
PARAM    //*MPY*/NSKIP/0/0 $
****CARD   1,  9- 12
****FILE 101
$$$$ GP4 generates flags defining members of various displacement sets (USET) 
$$$$ and forms multipoint constraint equations [R ] {u } = 0.
$$$$                                             g    g
$$$$
GP4      CASECC,GEOM4,EQEXIN,GPDT,BGPDT,CSTM,GPST/RG,,USET, 
         ASET,OGPST/LUSET/S,N,MPCF1/S,N,MPCF2/S,N,SINGLE/S,N,OMIT/  
         S,N,REACT/S,N,NSKIP/S,N,REPEAT/S,N,NOSET/S,N,NOL/S,N,NOA/  
         C,Y,ASETOUT/C,Y,AUTOSPC $  
****CARD   1-  4,  6,  8- 12, 26, 28
****FILE 101
$$$$ OFP formats the table of potential grid point singularities prepared by 
$$$$ GPSP and places it on the system output file for printing.
OFP      OGPST,,,,,//S,N,CARDNO $   
****CARD   1-  4,  6,  8- 10, 28
****FILE 101
$$$$
PARAM    //*AND*/NOSR/REACT/SINGLE $
****CARD   1,  9- 12
****FILE 121
$$$$
PURGE    GM,GMD/MPCF1/GO,GOD/OMIT/KFS,PST/SINGLE/QP/NOSR/KLR,KRR,MLR,MR,
         MRR,DM/REACT $ 
****CARD   1,  9- 12
****FILE 103,105-107,109,110,114,117,121
$$$$ Equivalence [K  ] to [K  ] and [M  ] to [M  ] if no multipoint 
$$$$               gg       nn        gg       nn
$$$$ constraints exist.
EQUIV    KGG,KNN/MPCF1/MGG,MNN/MPCF1 $  
****CARD   1-  6,  8,  9, 14, 24
****FILE 104
$$$$ Go to label LBL2 if no multipoint constraints exist.
COND     LBL2,MPCF1 $   
****CARD   1-  6,  8,  9, 14, 24
****FILE 103,104
$$$$ MCE1 partitions multipoint constraint equation [R ] = [R |R ] and solves
$$$$                                                  g      m  n
$$$$                                                             -1
$$$$ for multipoint constraint transformation matrix [G ] = -[R ]  [R ].
$$$$                                                   m       m     n
$$$$
MCE1     USET,RG/GM $   
****CARD   1,  9
****FILE 103
$$$$ MCE2 partitions stiffness and mass matrices
$$$$ 
$$$$           +_      +                        +_      +
$$$$           |K  |K  |                        |M  |M  |
$$$$           | nn| nm|                        | nn| nm|
$$$$   [K  ] = |---+---|                [M  ] = |---+---|
$$$$     gg    |K  |K  |                  gg    |M  |M  |
$$$$           | mn| mm|                        | mn| mm|
$$$$           +       +                        +       +
$$$$ 
$$$$ and performs the matrix reductions
$$$$ 
$$$$            _        T           T           T
$$$$   [K  ] = [K  ] + [G ][K  ] + [K  ][G ] + [G ][K  ][G ]
$$$$     nn      nm      m   mn      mn   m      m   mm   m
$$$$ 
$$$$            _        T           T           T
$$$$   [M  ] = [M  ] + [G ][M  ] + [M  ][G ] + [G ][M  ][G ]
$$$$     nn      nm      m   mn      mn   m      m   mm   m
$$$$
MCE2     USET,GM,KGG,MGG,,/KNN,MNN,, $  
****CARD   1-  6,  8,  9, 14, 24
****FILE 104
$$$$
LABEL    LBL2 $ 
****CARD   1-  6,  8,  9, 14, 24
****FILE 103,104
$$$$ Equivalence [K  ] to [K  ] and [M  ] to [M  ] if no single-point
$$$$               nn       ff        nn       ff
$$$$ constraints exist.
EQUIV    KNN,KFF/SINGLE/MNN,MFF/SINGLE $
****CARD   1-  6,  8- 10, 14, 24
****FILE 105
$$$$ Go to label LBL3 if no single-point constraints exist.
COND     LBL3,SINGLE $  
****CARD   1-  6,  8- 10, 14, 24
****FILE 105
$$$$ SCE1 partitions out single-point constraints
$$$$ 
$$$$           +       +                        +       +
$$$$           |K  |K  |                        |M  |M  |
$$$$           | ff| fs|                        | ff| fs|
$$$$   [K  ] = |---+---|                [M  ] = |---+---|
$$$$     nn    |K  |K  |                  nn    |M  |M  |
$$$$           | sf| ss|                        | sf| ss|
$$$$           +       +                        +       +
$$$$
SCE1     USET,KNN,MNN,,/KFF,KFS,,MFF,, $
****CARD   1-  6,  8- 10, 14, 24
****FILE 105
$$$$
LABEL    LBL3 $ 
****CARD   1-  6,  8- 10, 14, 24
****FILE 105
$$$$ Equivalence [K  ] to [K  ] if no omitted coordinates exist.
$$$$               ff       aa
$$$$
EQUIV    KFF,KAA/OMIT $ 
****CARD   1-  4,  6,  8- 11
****FILE 106
$$$$ Equivalence [M  ] to [M  ] if no omitted coordinates exist.
$$$$               ff       aa
$$$$
EQUIV    MFF,MAA/OMIT $ 
****CARD   1-  5,  8- 11, 14, 24
****FILE 126
$$$$ Go to label LBL5 if no omitted coordinates exist.
COND     LBL5,OMIT $
****CARD   1-  6,  8- 11, 14, 24
****FILE 106,126
$$$$ SMP1 partitions constrained stiffness matrix
$$$$
$$$$           +_      +
$$$$           |K  |K  |
$$$$           | aa| ao|
$$$$   [K  ] = |---+---|
$$$$     ff    |K  |K  |
$$$$           | oa| oo|
$$$$           +       +
$$$$           
$$$$                                               -1
$$$$ solves for transformation matrix [G ] = -[K  ]  [K  ]
$$$$                                    o       oo     oa
$$$$ 
$$$$                                        _        T
$$$$ and performs matrix reduction [K  ] = [K  ] + [K  ][G ].
$$$$                                 aa      aa      oa   o
$$$$
SMP1     USET,KFF,,,/GO,KAA,KOO,LOO,,,,, $  
****CARD   1-  4,  6,  8- 11
****FILE 106
$$$$ SMP2 partitions constrained mass matrix
$$$$
$$$$           +_      +
$$$$           |M  |M  |
$$$$           | aa| ao|
$$$$   [M  ] = |---+---|
$$$$     ff    |M  |M  |
$$$$           | oa| oo|
$$$$           +       +
$$$$           
$$$$ and performs matrix reduction
$$$$
$$$$            _        T           T           T                   
$$$$   [M  ] = [M  ] + [M  ][G ] + [G ][M  ] + [G ][M  ][G ]         
$$$$     aa      aa      oa   o      o   ao      o   oo   o          
$$$$
SMP2     USET,GO,MFF/MAA $  
****CARD   1-  6,  8- 11, 14, 24
****FILE 126
$$$$
LABEL    LBL5 $ 
****CARD   1-  6,  8- 11, 14, 24
****FILE 106,126
$$$$ Equivalence [K  ] to [K  ] if no free-body supports exist.
$$$$               aa       ll
$$$$
EQUIV    KAA,KLL/REACT $
****CARD   1-  6,  8- 12, 14, 24
****FILE 107
$$$$ Go to label LBL6 if no free-body supports exist.
COND     LBL6,REACT $   
****CARD   1-  6,  8- 12, 14, 24
****FILE 107
$$$$ RBMG1 partitions out free-body supports
$$$$ 
$$$$           +       +                        +       +
$$$$           |K  |K  |                        |M  |M  |
$$$$           | ll| lr|                        | ll| lr|
$$$$   [K  ] = |---+---|                [M  ] = |---+---|
$$$$     aa    |K  |K  |                  aa    |M  |M  |
$$$$           | rl| rr|                        | rl| rr|
$$$$           +       +                        +       +
$$$$
RBMG1    USET,KAA,MAA/KLL,KLR,KRR,MLL,MLR,MRR $ 
****CARD   1-  6,  8- 12, 14, 24
****FILE 107
$$$$ Go to label LBL8.
JUMP     LBL8 $ 
****CARD   1-  4,  6,  8- 12
****FILE 107
$$$$
LABEL    LBL6 $ 
****CARD   1-  6,  8- 12, 14, 24
****FILE 107
$$$$ Go to label LBL7 if there is no request for mode acceleration data 
$$$$ recovery.
COND     LBL7,MODACC $  
****CARD   1-  6,  8- 12, 14, 24
****FILE 108-110
$$$$
LABEL    LBL8 $ 
****CARD   1-  4,  6,  8- 12
****FILE 107
$$$$ RBMG2 decomposes constrained stiffness matrix [K  ] = [L  ][U  ].
$$$$                                                 ll      ll   ll
$$$$
RBMG2    KLL/LLL $  
****CARD   1-  4,  6,  8- 12
****FILE 108
$$$$ Go to label LBL7 if no free-body supports exist.
COND     LBL7,REACT $   
****CARD   1-  4,  6,  8- 12
****FILE 109
$$$$ RBMG3 forms rigid body transformation matrix
$$$$ 
$$$$               -1
$$$$   [D] = -[K  ]  [K  ]
$$$$            ll     lr
$$$$ 
$$$$ calculates rigid body check matrix
$$$$
$$$$                   T
$$$$   [X] = [K  ] + [K  ][D]
$$$$           rr      lr
$$$$ 
$$$$ and calculates rigid body error ratio
$$$$ 
$$$$ 
$$$$               ||X||
$$$$   epsilon = ---------
$$$$              ||K  ||
$$$$                 rr
$$$$
RBMG3    LLL,KLR,KRR/DM $   
****CARD   1-  4,  6,  8- 12
****FILE 109
$$$$ RBMG4 forms rigid body mass matrix
$$$$ 
$$$$                    T          T           T
$$$$   [m ] = [M  ] + [M  ][D] + [D ][M  ] + [D ][M  ][D]
$$$$     r      rr      lr             lr          ll
$$$$
RBMG4    DM,MLL,MLR,MRR/MR $
****CARD   1-  6,  8- 12, 14, 24
****FILE 110
$$$$
LABEL    LBL7 $ 
****CARD   1-  6,  8- 12, 14, 24
****FILE 108-110
$$$$ DPD generates flags defining members of various displacement sets used in 
$$$$ dynamic analysis (USETD), tables relating the internal and external grid 
$$$$ point numbers (GPLD), including extra points introduced for dynamic 
$$$$ analysis (SILD), and prepares Transfer Function Pool (TFPOOL), Dynamic 
$$$$ Loads Table (DLT), Nonlinear Function Table (NLFT), and Transient 
$$$$ Response List (TRL).
DPD      DYNAMICS,GPL,SIL,USET/GPLD,SILD,USETD,TFPOOL,DLT,,,NLFT,TRL,   
         EED ,EQDYN/LUSET/S,N,LUSETD/NOTFL/S,N,NODLT/NOPSDL/
         NOFRL/S,N,NONLFT/S,N,NOTRL/S,N,NOEED//S,N,NOUE $   
****CARD   1,  9- 12, 56, 58, 59
****FILE 111
$$$$ Go to label ERROR2 and print Error Message No. 2 if there is no 
$$$$ Eigenvalue Extraction Data.
COND     ERROR2,NOEED $ 
****CARD   1,  9- 12, 56, 58, 59
****FILE 111
****RFMT 187-197,199-204,207-209
$$$$
PURGE    UEVT/NOUE/PNLH/NONLFT $
****CARD   1,  9- 12, 56, 58, 59
****FILE 120,128
$$$$                       d                d
$$$$ Equivalence [G ] to [G ] and [G ] to [G ] if there are no extra points
$$$$               o       o        m       m
$$$$ introduced for dynamic analysis.
EQUIV    GO,GOD/NOUE/GM,GMD/NOUE  $ 
****CARD   1-  6,  8- 12, 14, 22- 24, 55- 57, 59, 60
****FILE 114
$$$$
PARAM    //*MPY*/NEIGV/1/-1 $   
****CARD   1-  6,  8- 12, 14, 24
****FILE 112
$$$$ READ extracts real eigenvalues and eigenvectors from the equation
$$$$ 
$$$$   [K   - lambda M  ]{u } = 0
$$$$     aa           aa   a
$$$$ 
$$$$ calculates rigid body modes by finding a square matrix [phi  ] such that
$$$$                                                            ro
$$$$              T
$$$$   [m ] = [phi  ][m ][phi  ]
$$$$     o        ro   r     ro
$$$$ 
$$$$ is diagonal and normalized, computes rigid body eigenvectors
$$$$ 
$$$$             +       +
$$$$             |Dphi   |
$$$$             |    ro |
$$$$   [phi  ] = |-------|
$$$$       ao    |phi    |
$$$$             |   ro  |
$$$$             +       +
$$$$ 
$$$$ calculates modal mass matrix
$$$$ 
$$$$             T
$$$$   [m] = [phi ][M  ][phi ]
$$$$             a   aa     a
$$$$ 
$$$$ and normalizes eigenvectors according to one of the following user 
$$$$ requests:
$$$$   1. Unit value of a selected component.
$$$$   2. Unit value of the largest component.
$$$$   3. Unit value of the generalized mass.
READ     KAA,MAA,MR,DM,EED,USET,CASECC/LAMA,PHIA,MI,OEIGS/*MODES*/S,N,  
         NEIGV $
****CARD   1-  6,  8- 12, 14, 24, 59, 60
****FILE 112
$$$$ OFP formats the summary of eigenvalue extraction information (OEIGS) 
$$$$ prepared by READ and places it on the system output file for printing.
OFP      OEIGS,,,,,//S,N,CARDNO $   
****CARD   1-  6,  8- 12, 14, 24, 59, 60
****FILE 112
$$$$ Go to label ERROR4 and print Error Message No. 4 if no eigenvalues were 
$$$$ found.
COND     ERROR4,NEIGV $ 
****CARD   1-  6,  8- 12, 14, 24, 59, 60
****FILE 112
****RFMT 187-197,199-204,207-209
$$$$ OFP formats the eigenvalues (LAMA) prepared by READ and places them on 
$$$$ the system output file for printing.
OFP      LAMA,,,,,//S,N,CARDNO $
****CARD   1-  6,  8- 12, 14, 24, 59, 60
****FILE 112
$$$$                                            2      2          2
$$$$ MTRXIN selects the direct input matrices [K  ], [M  ], and [B  ].
$$$$                                            pp     pp         pp
$$$$
MTRXIN   CASECC,MATPOOL,EQDYN,,TFPOOL/K2PP,M2PP,B2PP/LUSETD/S,N,
         NOK2PP/S,N,NOM2PP/S,N,NOB2PP $ 
****CARD   1, 22, 23, 56, 57
****FILE 113
$$$$
PURGE    K2DD/NOK2PP/M2DD/NOM2PP/B2DD/NOB2PP $  
****CARD   1-  6,  8- 11, 22, 23, 56, 57
****FILE 113
$$$$
PARAM    //*AND*/MDEMA/NOUE/NOM2PP $
****CARD   1-  6,  8- 11, 22, 23, 56, 57
****FILE 113
$$$$               2        2      2        2          2        2
$$$$ Equivalence [M  ] to [M  ], [B  ] to [B  ], and [K  ] to [K  ] if no
$$$$               pp       dd     pp       dd         pp       dd
$$$$ 
$$$$ constraints are applied, and [M  ] to [M  ] if there are no direct input
$$$$                                aa       dd
$$$$ mass matrices and no extra points introduced for dynamic analysis.
EQUIV    M2PP,M2DD/NOA/B2PP,B2DD/NOA/K2PP,K2DD/NOA/MAA,MDD/MDEMA $  
****CARD   1-  6,  8- 11, 22, 23, 56, 57
****FILE 113
$$$$                                                     2      2             
$$$$ GKAD applies constraints to direct input matrices [K  ], [M  ], and      
$$$$                                                     pp     pp            
$$$$   2              2      2          2
$$$$ [B  ], forming [K  ], [M  ], and [B  ].
$$$$   pp             dd     dd         dd
$$$$
GKAD     USETD,GM,GO,,,MAA,,K2PP,M2PP,B2PP/,,MDD,GMD,   
         GOD,K2DD,M2DD,B2DD/*TRANRESP*/*DISP*/*MODAL*/0.0/  
         0.0/0.0/NOK2PP/NOM2PP/NOB2PP/  
         MPCF1/SINGLE/OMIT/NOUE/-1/-1/  
         1/V,Y,MODACC = -1 $
****CARD   1-  6,  8- 11, 14, 22- 24, 55- 57, 60
****FILE 114
$$$$ GKAM assembles stiffness, mass, and damping matrices in modal coordinates 
$$$$ for use in Transient Response:
$$$$ 
$$$$                     T    2
$$$$   [K  ] = [k] + [phi  ][K  ][phi  ]
$$$$     hh              dh   dd     dh
$$$$ 
$$$$                     T    2
$$$$   [M  ] = [m] + [phi  ][M  ][phi  ]
$$$$     hh              dh   dd     dh
$$$$ 
$$$$                     T    2
$$$$   [B  ] = [b] + [phi  ][B  ][phi  ]
$$$$     hh              dh   dd     dh
$$$$ 
$$$$ where
$$$$ 
$$$$   m  = modal masses
$$$$    i
$$$$ 
$$$$   b  = m  2 pi f  g(f )
$$$$    i    i       i    i
$$$$ 
$$$$               2  2
$$$$   k  = m  4 pi  f
$$$$    i    i        i
$$$$ 
$$$$ All matrices are real.
GKAM     USETD,PHIA,MI,LAMA,DIT,M2DD,B2DD,K2DD,CASECC/MHH,BHH,KHH,PHIDH/
         NOUE/C,Y,LMODES=0/C,Y,LFREQ=0.0/C,Y,HFREQ=-1.0/
         NOM2PP/NOB2PP/NOK2PP/S,N,NONCUP/S,N,FMODE $
****CARD   1-  6,  8- 12, 14, 22- 24, 56, 57, 59, 60, 62
****FILE 115
$$$$ Go to label ERROR5 and print Error Message No. 5 if there is no Transient 
$$$$ Response List.
COND     ERROR5,NOTRL $ 
****CARD   1-  6,  8- 12, 14, 22- 24, 56, 57, 59, 60, 62
****FILE 117
****RFMT 187-197,199-204,207-209
$$$$
PARAM    //*ADD*/NEVER/1/0 $
****CARD  22, 23
****FILE 118,119,122,123,128,130-132
$$$$
PARAM    //*MPY*/REPEATT/1/-1 $ 
****CARD   1-  6,  8- 14, 16, 19- 24, 27, 55- 62
****FILE 116
****RFMT 187-197,199-204,207-209
$$$$ Beginning of loop for additional dynamic load sets.
LABEL    LBL13 $
****SBST   1,  3
****CARD   1-  6,  8- 16, 18- 24, 55- 62
****RFMT 187-197,199-204,207-209
$$$$
PURGE    PNLH,OUHV1,OPNL1,OUHV2,OPNL2,XYPLTTA,OPP1,OQP1,OUPV1,OES1,OEF1,
         OPP2,OQP2,OUPV2,OES2,OEF2,PLOTX2,XYPLTT,OPPA,IQP1,IPHIP1,IES1, 
         IEF1,OPPB,IQP2,IPHIP2,IES2,IEF2,ZQP2,ZUPV2,ZES2,ZEF2/NEVER $   
****CARD  19- 23, 27
****FILE 118,119,122,123,128,130-132,135-137,139
$$$$ CASE extracts the appropriate record from CASECC corresponding to the 
$$$$ current loop and copies it into CASEXX.
CASE     CASECC,/CASEXX/*TRAN*/S,N,REPEATT/S,N,NOLOOP $ 
****CARD   1-  6,  8- 14, 16, 19- 25, 27, 55- 62
****FILE 116
****RFMT 187-197,199-204,207-209
$$$$
PARAM    //*MPY*/NCOL/0/1 $ 
****SBST   4
****CARD   1-  6,  8- 12, 14, 24, 56, 57, 59, 60
****FILE 117
$$$$                                                 t     t         t
$$$$ TRLG generates matrices of loads versus time. {P }, {P }, and {P } are
$$$$                                                 p     s         d
$$$$ generated with one column per output time step. {P } and {P } are 
$$$$                                                   d        h
$$$$ generated with one column per solution time step, and the Transient 
$$$$ Output List (TOL) is a list of output time steps.
TRLG     CASEXX,USETD,DLT,SLT,BGPDT,SIL,CSTM,TRL,DIT,GMD,GOD,PHIDH, 
         EST,MGG,/PPT,PST,PDT,PD,PH,TOL/S,N,NOSET/NCOL $
****CARD   1-  6,  8- 12, 14, 22- 24, 56- 62
****FILE 117
$$$$               t       t
$$$$ Equivalence {P } to {P } if the d and p sets are the same.
$$$$               d       p
$$$$
EQUIV    PPT,PDT/NOSET $
****CARD   1-  6,  8- 12, 14, 22- 24, 56- 62
****FILE 117
$$$$                                              nl
$$$$ TRD forms the linear, {P }, and nonlinear, {P  }, dynamic load vectors
$$$$                         d                    d
$$$$ and integrates the equations of motion over specified time periods to
$$$$ solve for the displacements, velocities, and accelerations, using the 
$$$$ following equation:
$$$$
$$$$        2                             nl
$$$$   [M  p + B  p + K  ]{u } = {P } + {P  }
$$$$     hh     hh     hh   h      h      h
$$$$
TRD      CASEXX,TRL,NLFT,DIT,KHH,BHH,MHH,PH/UHVT,PNLH/*MODAL*/  
         NOUE/NONCUP/S,N,NCOL/C,Y,ISTART $  
****CARD   1-  6,  8- 12, 14, 17, 22- 24, 56- 62
****FILE 128
$$$$ VDR prepares displacements, velocities, and accelerations, sorted by time 
$$$$ step, for output using only the extra points introduced for dynamic 
$$$$ analysis and modal coordinates (solution points).
VDR      CASEXX,EQDYN,USETD,UHVT,TOL,XYCDB,PNLH/OUHV1,OPNL1/
         *TRANRESP*/*MODAL*/0/S,N,NOH/S,N,NOP/FMODE $   
****CARD  19- 21, 27
****FILE 118
$$$$ Go to label LBL16 if there is no output request for the solution points.
COND     LBL16,NOH $
****CARD  21, 27
****FILE 119,135
$$$$ SDR3 sorts the solution point displacements, velocities, accelerations, 
$$$$ and nonlinear load vectors by extra point or mode number.
SDR3     OUHV1,OPNL1,,,,/OUHV2,OPNL2,,,, $  
****CARD  21, 27
****FILE 119
$$$$ OFP formats the requested solution point displacements, velocities, 
$$$$ accelerations, and nonlinear load vectors prepared by SDR3 and places 
$$$$ them on the system output file for printing.
OFP      OUHV2,OPNL2,,,,//S,N,CARDNO $  
****CARD  21
****FILE 119
$$$$ XYTRAN prepares the input for X-Y plotting of the solution point 
$$$$ displacements, velocities, accelerations, and nonlinear load vectors vs. 
$$$$ time.
XYTRAN   XYCDB,OUHV2,OPNL2,,,/XYPLTTA/*TRAN*/*HSET*/S,N,PFILE/  
         S,N,CARDNO $   
****SBST   7
****CARD  27
****FILE 135
$$$$ XYPLOT prepares the requested X-Y plots of the solution point 
$$$$ displacements, velocities, accelerations, and nonlinear load vectors vs. 
$$$$ time.
XYPLOT   XYPLTTA// $
****SBST   7
****CARD  27
****FILE 135
$$$$
LABEL    LBL16 $
****CARD  21, 27
****FILE 119,135
$$$$
PARAM    //*AND*/PJUMP/NOP/JUMPPLOT $   
****CARD   1-  6,  8- 12, 14, 22- 24, 55- 62
****FILE 120
$$$$ Go to label LBL15 if there is no output request involving dependent 
$$$$ degrees of freedom, forces and stresses, or deformed structure plots.
COND     LBL15,PJUMP $  
****CARD   1-  6,  8- 12, 14, 18- 20, 22- 24, 55- 62
****FILE 120-124,129-132,136,137,139
$$$$
PARAM    //*NOT*/NOMOD/V,Y,MODACC $ 
****CARD   1-  6,  8- 12, 14, 22- 24, 55- 62
****FILE 124
$$$$
PARAM    //*AND*/MPJUMP/V,Y,MODACC/JUMPPLOT $   
****CARD   1-  6,  8- 12, 14, 22- 24, 55- 62
****FILE 120-124
$$$$ Go to label LBDDRM if the mode acceleration technique is not requested 
$$$$ and if there are no requests for deformed structure plots.
COND     LBDDRM,MPJUMP $
****CARD   1-  6,  8- 12, 14, 22- 24, 55- 62
****FILE 120-124
$$$$ DDR1 transforms the solution vector of displacements from modal to 
$$$$ physical coordinates
$$$$
$$$$   [u ] = [phi  ]{u }
$$$$     d        dh   h
$$$$
DDR1     UHVT,PHIDH/UDV1T $ 
****CARD   1-  6,  8- 12, 14, 22- 24, 56- 62
****FILE 124
$$$$ Go to label LBLMOD if the mode acceleration technique is not requested.
COND     LBLMOD,MODACC $
****SBST   2,  3
****CARD   1-  6,  8- 12, 14, 22- 24, 55- 62
****FILE 120
$$$$ DDR2 calculates an improved displacement vector using the mode 
$$$$ acceleration technique.
DDR2     USETD,UDV1T,PDT,K2DD,B2DD,MDD,,LLL,DM/UDV2T,UEVT,PAF/  
         *TRANRESP*/NOUE/REACT/0 $  
****SBST   2,  3
****CARD   1-  6,  8- 12, 14, 22- 24, 55- 62
****FILE 120
$$$$ Equivalence {u } to the improved displacement vector. (Flag NOMOD is 
$$$$               d
$$$$ negative since the mode acceleration technique is requested.)
EQUIV    UDV2T,UDV1T/NOMOD $
****SBST   2,  3
****CARD   1-  6,  8- 12, 14, 22- 24, 55- 62
****FILE 124
$$$$
LABEL    LBLMOD $   
****SBST   2,  3
****CARD   1-  6,  8- 12, 14, 22- 24, 55- 62
****FILE 120
$$$$ Equivalence {u } to {u } if no constraints are applied.
$$$$               d       p
$$$$
EQUIV    UDV1T,UPV/NOA $
****CARD   1-  6,  8- 12, 14, 22- 24, 55- 62
****FILE 121
$$$$ Go to label LBL14 if no constraints are applied.
COND     LBL14,NOA $
****CARD   1-  6,  8- 12, 14, 22- 24, 55- 62
****FILE 121
****RFMT 187-197,199-204,207-209
$$$$ SDR1 recovers dependent components of displacements
$$$$ 
$$$$                                 u
$$$$           d                      d
$$$$  {u } = [G ]{u }               {----} = {u  + u  }
$$$$    o      o   d                 u         f     e
$$$$                                   o
$$$$
$$$$    u + u      
$$$$     f   e                              d
$$$$   {-------} = {u + u }        {u } = [G ]{u  + u }
$$$$      u          n   e           m      m   f    e
$$$$       s        
$$$$
$$$$    u + u                
$$$$     n   e               
$$$$   {---------} = {u }
$$$$       u           p 
$$$$        m                  
$$$$                                                                 T
$$$$ and recovers single-point forces of constraint {q } = -{P } + [K  ]{u }.
$$$$                                                  s       s      fs   f
$$$$
SDR1     USETD,,UDV1T,,,GOD,GMD,PST,KFS,,/UPV,,QP/1/*DYNAMICS* $
****CARD   1-  6,  8- 12, 14, 22- 24, 55- 62
****FILE 121
****RFMT 187-197,199-204,207-209
$$$$
LABEL    LBL14 $
****CARD   1-  6,  8- 12, 14, 22- 24, 55- 62
****FILE 121
****RFMT 187-197,199-204,207-209
$$$$ SDR2 calculates element forces (OEF1) and stresses (OES1) and 
$$$$ prepares load vectors (OPP1), displacement, velocity, and acceleration
$$$$ vectors (OUPV1), and single-point forces of constraint (OQP1) for output 
$$$$ and translation components of the displacement vector (PUGV), sorted by 
$$$$ time step.
SDR2     CASEXX,CSTM,MPT,DIT,EQDYN,SILD,,,BGPDP,TOL,QP,UPV,EST,XYCDB,   
         PPT,/OPP1,OQP1,OUPV1,OES1,OEF1,PUGV,,/*TRANRESP* $ 
****CARD  18- 20
****FILE 122
$$$$ SDR3 prepares requested output sorted by point number or element number.
SDR3     OPP1,OQP1,OUPV1,OES1,OEF1,/OPP2,OQP2,OUPV2,OES2,OEF2, $
****CARD  18- 20
****FILE 123
$$$$ Go to label P2A.
JUMP     P2A $  
****CARD  18- 20
****FILE 123
$$$$
LABEL    LBDDRM $   
****CARD   1-  6,  8- 12, 14, 18- 20, 22- 24, 55- 62
****FILE 120-124
$$$$ SDR1 recovers dependent components of eigenvectors
$$$$ 
$$$$                                   phi
$$$$             d                        h
$$$$  {phi } = [G ]{phi }             {----} = {phi  + phi }
$$$$      o      o     n               phi         f      e
$$$$                                      o
$$$$
$$$$    phi +phi      
$$$$       f    e                                  d
$$$$   {---------} = {phi +phi }        {phi } = [G ]{phi  + phi }
$$$$      phi            n    e             m      m     n      e
$$$$         s        
$$$$
$$$$    phi +phi                
$$$$       n    e               
$$$$   {---------} = {phi  + u } = {phi }
$$$$      phi            g    e        p
$$$$         m                  
$$$$                                                            T
$$$$ and recovers single-point forces of constraint {q } = [K  ] {phi }.
$$$$                                                  s      fs      f
$$$$
SDR1     USETD,,PHIDH,,,GOD,GMD,,KFS,,/PHIPH,,QPH/1/*DYNAMICS* $
****CARD   1-  6,  8- 12, 14, 22- 24, 55- 62
****FILE 129
****RFMT 187-197,199-204,207-209
$$$$ SDR2 calculates element forces (IEF1) and stresses (IES1) and prepares 
$$$$ eigenvectors (IPHIP1) and single-point forces of constraint (IQP1) for 
$$$$ output sorted by time step.
SDR2     CASEXX,CSTM,MPT,DIT,EQDYN,SILD,,,,LAMA,QPH,PHIPH,EST,XYCDB,,/  
         ,IQP1,IPHIP1,IES1,IEF1,,,/*MMREIG* $   
****CARD  18- 20
****FILE 130
$$$$ SDR2 prepares load vectors for output (OPPCA) sorted by time step.
SDR2     CASEXX,,,,EQDYN,SILD,,,,TOL,,,,XYCDB,PPT,/OPPA,,,,,,,/ 
         *TRANRESP* $   
****CARD  18- 20
****FILE 139
$$$$ SDR3 prepares the requested output sorted by point number or element 
$$$$ number.
SDR3     OPPA,IQP1,IPHIP1,IES1,IEF1,/OPPB,IQP2,IPHIP2,IES2,IEF2, $  
****CARD  18- 20
****FILE 131
$$$$ Equivalence OPPB to OPP2. (Flag MODACC is negative since the 
$$$$ mode acceleration technique is not requested.)
EQUIV    OPPB,OPP2/MODACC $ 
****CARD  18- 20
****FILE 123
$$$$ DDRMM prepares a subset of the element forces (ZEF2) and stresses (ZES2), 
$$$$ and displacement vectors (ZUPV2) and single-point forces of constraint 
$$$$ (ZQP2) for output sorted by point number or element number.
DDRMM    CASEXX,UHVT,TOL,IPHIP2,IQP2,IES2,IEF2,,EST,MPT,DIT/
         ZUPV2,ZQP2,ZES2,ZEF2, $
****CARD  18- 20
****FILE 132
$$$$ Equivalence ZUPV2 to OUPV2, ZQP2 to OQP2, ZES2 to OES2, and ZEF2 
$$$$ to OEF2. (Flag MODACC is negative since the mode acceleration technique 
$$$$ is not requested.) 
EQUIV    ZUPV2,OUPV2/MODACC/ZQP2,OQP2/MODACC/ZEF2,OEF2/MODACC/ZES2,OES2/
         MODACC $   
****CARD  18- 20
****FILE 123
$$$$
LABEL    P2A $  
****CARD  18- 20
****FILE 123
$$$$ OFP formats the requested output prepared by SDR3 (with mode 
$$$$ acceleration) or DDRMM (no mode acceleration) and places it on the system 
$$$$ output file for printing.
OFP      OUPV2,OPP2,OQP2,OEF2,OES2,//S,N,CARDNO $   
****CARD  19
****FILE 123
$$$$ SCAN examines the element stresses and forces calculated by SDR3 or DDRMM 
$$$$ and generates scanned output that meets the specifications set by the 
$$$$ user.
SCAN     CASECC,OES2,OEF2/OESF2/*RF* $  
****CARD 19 
****FILE 123
$$$$ OFP formats the scanned output table prepared by SCAN and places it on 
$$$$ the system output file for printing.
OFP      OESF2,,,,,//S,N,CARDNO $   
****CARD  19
****FILE 123
$$$$ Go to label P2 if no deformed structure plots are requested.
COND     P2,JUMPPLOT $  
****SBST   7
****CARD  18
****FILE 136
$$$$ PLOT generates all requested deformed structure and contour plots.
PLOT     PLTPAR,GPSETS,ELSETS,CASEXX,BGPDT,EQEXIN,SIP,,PUGV,,,,/PLOTX2/ 
         NSIL/LUSEP/JUMPPLOT/PLTFLG/S,N,PFILE $ 
****SBST   7
****CARD  18
****FILE 136
$$$$ PRTMSG prints plotter data, engineering data, and contour data for each 
$$$$ deformed plot generated.
PRTMSG   PLOTX2// $ 
****SBST   7
****CARD  18
****FILE 136
$$$$
LABEL    P2 $   
****SBST   7
****CARD  18
****FILE 136
$$$$ XYTRAN prepares the input for requested X-Y plots.
XYTRAN   XYCDB,OPP2,OQP2,OUPV2,OES2,OEF2/XYPLTT/*TRAN*/*PSET*/  
         S,N,PFILE/S,N,CARDNO $ 
****SBST   7
****CARD  20
****FILE 137
$$$$ XYPLOT prepares the requested X-Y plots of displacements, velocities, 
$$$$ accelerations, forces, stresses, loads, and single-point forces of 
$$$$ constraint vs. time.
XYPLOT   XYPLTT// $ 
****SBST   7
****CARD  20
****FILE 137
$$$$
LABEL    LBL15 $
****CARD   1-  6,  8- 12, 14, 18- 20, 22- 24, 55- 62
****FILE 120-124,129-132,136,137,139
$$$$ Go to label FINIS and make normal exit if no additional dynamic load sets 
$$$$ need to be processed.
COND     FINIS,REPEATT $
****SBST   1,  3
****CARD  22, 23
****RFMT 187-197,199-204,207-209
$$$$ Go to label LBL13 if additional dynamic load sets need to be processed.
REPT     LBL13,100 $
****SBST   1,  3
****CARD  22, 23
****RFMT 187-197,199-204,207-209
$$$$ Print Error Message No. 3 and terminate execution.
PRTPARM  //-3/*MDLTRD* $
****SBST   1,  3
****CARD  22, 23
****RFMT 187-197,199-204,207-209
$$$$ Go to label FINIS and make normal exit.
JUMP     FINIS $
****CARD   1-  6,  8- 28, 55- 62
****RFMT 187-197,199-204,207-209
$$$$
LABEL    ERROR2 $   
****CARD   1,  9- 12, 56, 58, 59
****FILE 101
****RFMT 187-197,199-204,207-209
$$$$ Print Error Message No. 2 and terminate execution.
PRTPARM  //-2/*MDLTRD* $
****CARD   1,  9- 12, 56, 58, 59
****FILE 101
****RFMT 187-197,199-204,207-209
$$$$
LABEL    ERROR1 $   
****CARD   1-  3,  5,  6,  8, 24
****FILE 127
****RFMT 187-197,199-204,207-209
$$$$ Print Error Message No. 1 and terminate execution.
PRTPARM  //-1/*MDLTRD* $
****CARD   1-  3,  5,  6,  8, 24
****FILE 127
****RFMT 187-197,199-204,207-209
$$$$
LABEL    ERROR4 $   
****CARD   1-  6,  8- 12, 14, 24, 59, 60
****FILE 112
****RFMT 187-197,199-204,207-209
$$$$ Print Error Message No. 4 and terminate execution.
PRTPARM  //-4/*MDLTRD* $
****CARD   1-  6,  8- 12, 14, 24, 59, 60
****FILE 112
****RFMT 187-197,199-204,207-209
$$$$
LABEL    ERROR5 $   
****CARD   1-  6,  8- 12, 14, 22- 24, 56, 57, 59, 60, 62
****RFMT 187-197,199-204,207-209
$$$$ Print Error Message No. 5 and terminate execution.
PRTPARM  //-5/*MDLTRD* $
****CARD   1-  6,  8- 12, 14, 22- 24, 56, 57, 59, 60, 62
****RFMT 187-197,199-204,207-209
$$$$
LABEL    ERROR6 $   
****CARD   1,  2,  4-  6, 16
****FILE  97
****RFMT 187-197,199-204,207-209
$$$$ Print Error Message No. 6 and terminate execution.
PRTPARM  //-6/*MDLTRD* $
****CARD   1,  2,  4-  6, 16
****FILE  97
****RFMT 187-197,199-204,207-209
$$$$
LABEL    FINIS $
****CARD   1-  6,  8- 28, 55- 62
****RFMT 187-197,199-204,207-209
$$$$
PURGE    DUMMY/MINUS1 $ 
****CARD   1-  6,  8- 28, 55- 62
****RFMT 187-197,199-204,207-209
$$$$
END      $  
****CARD   1-  6,  8- 28, 55- 62
****RFMT 187-197,199-204,207-209
$$$$
$*CARD BITS 
  1    AXIC     AXIF     CELAS1   CELAS2   CELAS3   CELAS4   CMASS1 
  1    CMASS2   
  1    CMASS3   CMASS4   CORD1C   CORD1R   CORD1S   CORD2C   CORD2R 
  1    CORD2S   
  1    GRDSET   GRID     GRIDB    POINTAX  RINGAX   RINGFL   SECTAX 
  1    SEQGP    SPOINT  
  2    ADUM1    ADUM2    ADUM3    ADUM4    ADUM5    ADUM6    ADUM7  
  2    ADUM8
  2    ADUM9    BAROR    CBAR     CCONEAX  CDUM1    CDUM2    CDUM3  
  2    CDUM4
  2    CDUM5    CDUM6    CDUM7    CDUM8    CDUM9    CELBOW   CHEXA1 
  2    CHEXA2   
  2    CIHEX1   CIHEX2   CIHEX3   CIS2D8   CONROD   CQDMEM   CQDMEM1
  2    CQDMEM2  CQDPLT   CQUAD1   CQUAD2   CROD     CSHEAR   CTETRA 
  2    CTORDRG  CQUAD4   CTRIA3
  2    CTRAPAX  CTRAPRG  CTRBSC   CTRIA1   CTRIA2   CTRIAAX  CTRIARG
  2    CTRIM6   
  2    CTRMEM   CTRPLT   CTRPLT1  CTRSHL   CTUBE    CTWIST   CWEDGE 
  3    PBAR     PCONEAX  PDUM1    PDUM2    PDUM3    PDUM4    PDUM5  
  3    PDUM6
  3    PDUM7    PDUM8    PDUM9    PELBOW   PIHEX    PIS2D8   PQDMEM 
  3    PQDMEM1  PQDMEM2  PQDPLT   PQUAD1   PQUAD2   PROD     PSHEAR 
  3    PTORDRG  PSHELL   PCOMP    PCOMP1   PCOMP2
  3    PTRAPAX  PTRBSC   PTRIA1   PTRIA2   PTRIAAX  PTRIM6   PTRMEM 
  3    PTRPLT   PTRPLT1  PTRSHL   PTUBE    PTWIST   
  4    GENEL
  5    CONM1    CONM2    PMASS  
  6    PELAS
  8    MAT1     MAT2     MAT3     MATT1    MATT2    MATT3    MAT8
  8    TABLEM1  TABLEM2  TABLEM3  TABLEM4  TEMPMT$  TEMPMX$  
  9    AXISYM$  CRIGD1   CRIGD2   CRIGD3   CRIGDR   CRROD    CRBAR  
  9    CRTRPLT  CRBE1    CRBE2    CRBE3    CRSPLINE MPC      MPCADD 
  9    MPCADD   MPCAX    MPC$   
 10    SPC      SPC1     SPCADD   SPCAX    SPC$ 
 11    ASET     ASET1    OMIT     OMIT1    OMITAX   SUPAX    SUPORT 
 13    TEMP     TEMPAX   TEMPD    TEMPP1   TEMPP2   TEMPP3   TEMPRB 
 14    WTMASS   
 15    GRDPNT   
 16    PLOTEL   
 17    ISTART   
 18    PLOT$
 19    POUT$
 20    XYOUT$   
 21    AOUT$
 22    LOOP$
 23    LOOP1$   
 24    COUPMASS CPBAR    CPQDPLT  CPQUAD1  CPQUAD2  CPROD    CPTRBSC
 24    CPTRIA1  CPTRIA2  CPTRPLT  CPTUBE
 25    NOLOOP$  
 26    ASETOUT  
 27    AXYOUT$  
 28    AUTOSPC  
 55    MODACC   
 56    EPOINT   SEQEP    TF 
 57    DMIAX    DMIG     B2PP$    K2PP$    M2PP$    TF$ 
 58    DAREA    DELAY    DLOAD    FORCE    FORCE1   FORCE2   GRAV   
 58    MOMENT   
 58    MOMENT1  MOMENT2  NOLIN1   NOLIN2   NOLIN3   NOLIN4   NOLIN6 
 58    PLOAD    PLOAD4
 58    PLOAD1   PLOAD2   SLOAD    TABLED1  TABLED2  TABLED3  TABLED4
 58    TLOAD1   TLOAD2   TSTEP  
 59    EIGR 
 60    METHOD$  
 61    DLOAD$   NLFORCE  TSTEP$ 
 62    HFREQ    LFREQ    LMODES   TABDMP1  SDAMP$   
$$$$
$*FILE BITS 
 94    BGPDT    CSTM     EQEXIN   GPDT     GPL      SIL 
 95    ECT  
 96    GPTT     SLT 
 97    EST      GEI      GPECT    MPTX     PCOMPS   EPTX
 98    KGGX 
 99    MGG  
100    KGG  
101    ASET     RG       USET     OGPST 
102    GPST 
103    GM   
104    KNN      MNN 
105    KFF      KFS      MFF
106    GO       KOO      LOO      KAA   
107    KLL      KLR      KRR      MLL      MLR      MRR 
108    LLL  
109    DM   
110    MR   
111    DLT      EED      EQDYN    GPLD     NLFT     SILD     TFPOOL 
111    TRL      USETD   
112    LAMA     MI       OEIGS    PHIA  
113    B2PP     K2PP     M2PP   
114    B2DD     GMD      GOD      K2DD     M2DD     MDD 
115    BHH      KHH      MHH      PHIDH 
116    CASEXX   
117    PD       PDT      PH       PPT      PST      TOL 
118    OPNL1    OUHV1   
119    OPNL2    OUHV2   
120    PAF      UDV2T    UEVT   
121    QP       UPV 
122    OEF1     OES1     OPP1     OQP1     OUPV1    PUGV
123    OEF2     OES2     OPP2     OQP2     OUPV2    OESF2
124    UDV1T
125    ELSETS   GPSETS   PLTPAR   PLTSETX   
126    MAA  
127    KDICT    KELM     MDICT    MELM  
128    PNLH     UHVT
129    PHIPH    QPH 
130    IEF1     IES1     IPHIP1   IQP1  
131    IEF2     IES2     IPHIP2   IQP2     OPPB 
132    ZEF2     ZES2     ZQP2     ZUPV2 
133    PLOTX1   
134    OGPWG
135    XYPLTTA  
136    PLOTX2   
137    XYPLTT   
138    BGPDP    SIP 
$*  
=PAGE=
DISP13

APR.93
$$$$$$$$
BEGIN    DISP 13 - NORMAL MODES WITH DIFFERENTIAL STIFFNESS - APR. 1993 $ 
****CARD   1-  6,  8- 11, 13- 16, 18, 19, 21- 24, 57- 62
****RFMT 187-198,200,201-204,207-209
$$$$
PRECHK   ALL $  
****CARD   1-  6,  8- 11, 13- 16, 18, 19, 21- 24, 57- 62
****RFMT 187-198,200,201-204,207-209
$$$$
FILE     LAMA=APPEND/PHIA=APPEND $  
****CARD   1-  6,  8- 11, 13- 16, 18, 19, 21- 24, 57- 62
****RFMT 187-198,200,201-204,207-209
$$$$
PARAM    //*MPY*/CARDNO/0/0 $   
****CARD   1-  6,  8- 11, 14- 16, 19, 21, 23, 24, 57- 62
****FILE 101,112,118,120,130,132
$$$$ GP1 generates coordinate system transformation matrices, tables of grid 
$$$$ point locations, and tables relating the internal and external grid point 
$$$$ numbers.
GP1      GEOM1,GEOM2,/GPL,EQEXIN,GPDT,CSTM,BGPDT,SIL/S,N,LUSET/ 
         NOGPDT/MINUS1=-1 $ 
****CARD   1
****FILE  94
$$$$ PLTTRAN modifies special scalar grid points in the BGPDT and SIL tables.
PLTTRAN  BGPDT,SIL/BGPDP,SIP/LUSET/S,N,LUSEP $  
****CARD   1
****FILE 135
$$$$ GP2 generates Element Connection Table with internal indices.
GP2      GEOM2,EQEXIN/ECT $ 
****CARD   1,  2,  4,  5, 16
****FILE  95
$$$$
PARAML   PCDB//*PRES*////JUMPPLOT $ 
****SBST   7
****CARD  18
****FILE 121,131
$$$$
PURGE    PLTSETX,PLTPAR,GPSETS,ELSETS/JUMPPLOT $
****SBST   7
****CARD  18
****FILE 121
$$$$ Go to label P1 if there are no structure plot requests.
COND     P1,JUMPPLOT $  
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 121,131
$$$$ PLTSET transforms user input into a form used to drive the structure 
$$$$ plotter.
PLTSET   PCDB,EQEXIN,ECT,EPT/PLTSETX,PLTPAR,GPSETS,ELSETS/S,N,NSIL/ 
         S,N,JUMPPLOT $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 121
$$$$ PRTMSG prints error messages associated with the structure plotter.
PRTMSG   PLTSETX// $
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 121
$$$$
PARAM    //*MPY*/PLTFLG/1/1 $   
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 131
$$$$
PARAM    //*MPY*/PFILE/0/0 $
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 131
$$$$ Go to label P1 if no undeformed structure plots are requested.
COND     P1,JUMPPLOT $  
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 131
$$$$ PLOT generates all requested undeformed structure plots.
PLOT     PLTPAR,GPSETS,ELSETS,CASECC,BGPDT,EQEXIN,SIL,,ECT,,,,/PLOTX1/  
         NSIL/LUSET/S,N,JUMPPLOT/S,N,PLTFLG/S,N,PFILE $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 131
$$$$ PRTMSG prints plotter data and engineering data for each undeformed plot 
$$$$ generated.
PRTMSG   PLOTX1// $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 131
$$$$
LABEL    P1 $   
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 121,131
$$$$ GP3 generates Static Loads Table and Grid Point Temperature Table.
GP3      GEOM3,EQEXIN,GEOM2/SLT,GPTT/NOGRAV $   
****CARD   1,  2, 13, 57, 60
****FILE  96
$$$$ TA1 generates element tables for use in matrix assembly and stress 
$$$$ recovery.
TA1      ECT,EPT,BGPDT,SIL,GPTT,CSTM,MPT,EQEXIN/EST,GEI,GPECT,,,MPTX,
         PCOMPS,EPTX/LUSET/S,N,NOSIMP/1/S,N,NOGENL/S,N,GENEL/S,N,COMPS $   
****CARD   1-  6, 13, 16
****FILE  97
$$$$
EQUIV    MPTX,MPT/COMPS/EPTX,EPT/COMPS $
****CARD   1-  6,  8- 11, 13- 16, 18, 19, 21- 24, 57- 62
****FILE  97
$$$$ Go to label ERROR1 and print Error Message No. 1 if no structural 
$$$$ elements have been defined.
COND     ERROR1,NOSIMP $
****CARD   1,  2,  4-  6,  8, 16
****FILE  97
****RFMT 187-198,200,201-204,207-209
$$$$
PARAM    //*ADD*/NOKGGX/1/0 $   
****CARD   1-  3,  6,  8
****FILE 123
$$$$
PARAM    //*ADD*/NOMGG/1/0 $
****CARD   1-  3,  6,  8
****FILE 123
$$$$ EMG generates structural element stiffness and mass matrix tables and 
$$$$ dictionaries for later assembly by the EMA module. 
EMG      EST,CSTM,MPT,DIT,GEOM2,/KELM,KDICT,MELM,MDICT,,,/S,N,NOKGGX/   
         S,N,NOMGG////C,Y,COUPMASS/C,Y,CPBAR/   
         C,Y,CPROD/C,Y,CPQUAD1/C,Y,CPQUAD2/C,Y,CPTRIA1/C,Y,CPTRIA2/ 
         C,Y,CPTUBE/C,Y,CPQDPLT/C,Y,CPTRPLT/C,Y,CPTRBSC/
         C,Y,VOLUME/C,Y,SURFACE $   
****CARD   1-  3,  5,  6,  8, 24, 57
****FILE 123
$$$$
PURGE    KGGX/NOKGGX $  
****CARD   1-  3,  6,  8
****FILE  98
$$$$ Go to label JMPKGG if no stiffness matrix is to be assembled.
COND     JMPKGG,NOKGGX $
****CARD   1-  3,  6,  8
****FILE  98
$$$$                                  x
$$$$ EMA assembles stiffness matrix [K  ] and Grid Point Singularity Table.
$$$$                                  gg
$$$$
EMA      GPECT,KDICT,KELM/KGGX $
****CARD   1-  3,  6,  8
****FILE  98
$$$$
PURGE    KDICT,KELM/MINUS1 $
****CARD   1-  3,  6,  8
****FILE 123
$$$$
LABEL    JMPKGG $   
****CARD   1-  3,  6,  8
****FILE  98
$$$$ Go to label ERROR5 and print Error Message No. 5 if no mass matrix is to 
$$$$ be assembled.
COND     ERROR5,NOMGG $ 
****CARD   1-  3,  5,  8, 24, 57
****FILE 123
$$$$ EMA assembles mass matrix [M  ].
$$$$                             gg
$$$$
EMA      GPECT,MDICT,MELM/MGG/-1/C,Y,WTMASS=1.0 $   
****CARD   1-  3,  5,  8, 14, 15, 57
****FILE  99
$$$$
PURGE    MDICT,MELM/MINUS1 $
****CARD   1-  3,  5,  8, 14, 15, 57
****FILE 123
$$$$ Go to label LBL1 if no weight and balance information is requested.
COND     LBL1,GRDPNT $  
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 132
$$$$ GPWG generates weight and balance information.
GPWG     BGPDP,CSTM,EQEXIN,MGG/OGPWG/V,Y,GRDPNT/C,Y,WTMASS $
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 132
$$$$ OFP formats the weight and balance information prepared by GPWG and 
$$$$ places it on the system output file for printing.
OFP      OGPWG,,,,,//S,N,CARDNO $   
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 132
$$$$
LABEL    LBL1 $ 
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 132
$$$$               x
$$$$ Equivalence [K  ] to [K  ] if no general elements exist.
$$$$               gg       gg
$$$$
EQUIV    KGGX,KGG/NOGENL $  
****CARD   1-  4,  6,  8
****FILE 100
$$$$ Go to label LBL11 if no general elements exist.
COND     LBL11,NOGENL $ 
****CARD   1-  4,  6,  8
****FILE 100
$$$$                                 x
$$$$ SMA3 adds general elements to [K  ] to obtain stiffness matrix [K  ].
$$$$                                 gg                               gg
$$$$
SMA3     GEI,KGGX/KGG/LUSET/NOGENL/NOSIMP $ 
****CARD   1-  4,  6,  8
****FILE 100
$$$$
LABEL    LBL11 $
****CARD   1-  4,  6,  8
****FILE 100
$$$$
GPSTGEN  KGG,SIL/GPST $ 
****CARD   1-  4,  6,  8
****FILE 102
$$$$
PARAM    //*MPY*/NSKIP/0/0 $
****CARD   1,  9- 11
****FILE 101
$$$$ GP4 generates flags defining members of various displacement sets (USET), 
$$$$ forms multipoint constraint equations [R ] {u } = 0, and forms enforced
$$$$                                         g    g
$$$$ displacement vector {Y }.
$$$$                       s
$$$$
GP4      CASECC,GEOM4,EQEXIN,GPDT,BGPDT,CSTM,GPST/RG,YS,USET,   
         ASET,OGPST/LUSET/S,N,MPCF1/S,N,MPCF2/S,N,SINGLE/S,N,OMIT/  
         S,N,REACT/S,N,NSKIP/S,N,REPEAT/S,N,NOSET/S,N,NOL/S,N,NOA/  
         C,Y,ASETOUT/C,Y,AUTOSPC $  
****CARD   1-  4,  6,  8- 11, 22, 23, 59
****FILE 101
$$$$ OFP formats the table of potential grid point singularities prepared by 
$$$$ GPSP and places it on the system output file for printing.
OFP      OGPST,,,,,//S,N,CARDNO $   
****CARD   1-  4,  6,  8- 10, 23
****FILE 101
$$$$ Go to label ERROR6 and print Error Message No. 6 if no independent 
$$$$ degrees of freedom are defined.
COND     ERROR6,NOL $   
****CARD   1,  9- 11, 22, 23, 59
****FILE 101
****RFMT 187-198,200,201-204,207-209
$$$$ Go to label LBL4D if there are no support cards.
COND     LBL4D,REACT $  
****CARD   1, 11
****RFMT 187-189,193-198
$$$$ Go to label ERROR2 and print Error Message No. 2.
JUMP     ERROR2 $   
****CARD   1, 11
****RFMT 187-189,193-198
$$$$
LABEL    LBL4D $
****CARD   1, 11
****RFMT 187-189,193-198
$$$$
PARAM    //*AND*/NOSR/SINGLE/REACT $
****CARD   1, 10, 11
****FILE 111
$$$$
PURGE    GM/MPCF1/GO,KOO,LOO,PO,UOOV,RUOV/OMIT/PS,KFS,KSS/SINGLE/   
         QG/NOSR $  
****CARD   1,  9- 11, 59
****FILE 103,105,106,109-111
$$$$ Equivalence [K  ] to [K  ] if no multipoint constraints exist.
$$$$               gg       nn
$$$$
EQUIV    KGG,KNN/MPCF1 $
****CARD   1-  4,  6,  8,  9
****FILE 104
$$$$ Go to label LBL2 if no multipoint constraints exist.
COND     LBL2,MPCF1 $   
****CARD   1-  4,  6,  8,  9
****FILE 103,104
$$$$ MCE1 partitions multipoint constraint equation [R ] = [R |R ] and solves
$$$$                                                  g      m  n
$$$$                                                             -1
$$$$ for multipoint constraint transformation matrix [G ] = -[R ]  [R ].
$$$$                                                   m       m     n
$$$$
MCE1     USET,RG/GM $   
****CARD   1,  9
****FILE 103
$$$$ MCE2 partitions stiffness matrix
$$$$ 
$$$$           +_      +
$$$$           |K  |K  |
$$$$           | nn| nm|
$$$$   [K  ] = |---+---|
$$$$     gg    |K  |K  |
$$$$           | mn| mm|
$$$$           +       +
$$$$ 
$$$$ and performs matrix reduction
$$$$ 
$$$$            _        T           T           T
$$$$   [K  ] = [K  ] + [G ][K  ] + [K  ][G ] + [G ][K  ][G ]
$$$$     nn      nm      m   mn      mn   m      m   mm   m
$$$$
MCE2     USET,GM,KGG,,,/KNN,,, $
****CARD   1-  4,  6,  8,  9
****FILE 104
$$$$
LABEL    LBL2 $ 
****CARD   1-  4,  6,  8,  9
****FILE 103,104
$$$$ Equivalence [K  ] to [K  ] if no single-point constraints exist.
$$$$               nn       ff
$$$$
EQUIV    KNN,KFF/SINGLE $   
****CARD   1-  4,  6,  8- 10
****FILE 105
$$$$ Go to label LBL3 if no single-point constraints exist.
COND     LBL3,SINGLE $  
****CARD   1-  4,  6,  8- 10
****FILE 105
$$$$ SCE1 partitions out single-point constraints
$$$$ 
$$$$           +       +
$$$$           |K  |K  |
$$$$           | ff| fs|
$$$$   [K  ] = |---+---|
$$$$     nn    |K  |K  |
$$$$           | sf| ss|
$$$$           +       +
$$$$
SCE1     USET,KNN,,,/KFF,KFS,KSS,,, $   
****CARD   1-  4,  6,  8- 10
****FILE 105
$$$$
LABEL    LBL3 $ 
****CARD   1-  4,  6,  8- 10
****FILE 105
$$$$ Equivalence [K  ] to [K  ] if no omitted coordinates exist.
$$$$               ff       aa
$$$$
EQUIV    KFF,KAA/OMIT $ 
****CARD   1-  4,  6,  8- 11
****FILE 106
$$$$ Go to label LBL5 if no omitted coordinates exist.
COND     LBL5,OMIT $
****CARD   1-  4,  6,  8- 11
****FILE 106
$$$$ SMP1 partitions constrained stiffness matrix
$$$$
$$$$           +_      +
$$$$           |K  |K  |
$$$$           | aa| ao|
$$$$   [K  ] = |---+---|
$$$$     ff    |K  |K  |
$$$$           | oa| oo|
$$$$           +       +
$$$$           
$$$$                                               -1
$$$$ solves for transformation matrix [G ] = -[K  ]  [K  ]
$$$$                                    o       oo     oa
$$$$ 
$$$$                                        _        T
$$$$ and performs matrix reduction [K  ] = [K  ] + [K  ][G ].
$$$$                                 aa      aa      oa   o
$$$$
SMP1     USET,KFF,,,/GO,KAA,KOO,LOO,,,,, $  
****CARD   1-  4,  6,  8- 11
****FILE 106
$$$$
LABEL    LBL5 $ 
****CARD   1-  4,  6,  8- 11
****FILE 106
$$$$ RBMG2 decomposes constrained stiffness matrix [K  ] = [L  ][U  ].
$$$$                                                 ll      ll   ll
$$$$
RBMG2    KAA/LLL $  
****CARD   1-  4,  6,  8- 11
****FILE 107
$$$$ SSG1 generates static load vectors {P }.
$$$$                                      g
$$$$ 
SSG1     SLT,BGPDT,CSTM,SIL,EST,MPT,GPTT,EDT,MGG,CASECC,DIT,PCOMPS/ 
         PG,,,,/LUSET/1/COMPS $ 
****CARD   1-  3,  5,  6,  8, 57- 60
****FILE 108
$$$$ Equivalence {P } to {P } if no constraints are applied.
$$$$               g       l
$$$$
EQUIV    PG,PL/NOSET $  
****CARD   1-  3,  5,  6,  8- 11, 57- 60
****FILE 109
$$$$ Go to label LBL10 if no constraints are applied.
COND     LBL10,NOSET $  
****CARD   1-  3,  5,  6,  8- 11, 57- 60
****FILE 109
$$$$ SSG2 applies constraints to static load vectors
$$$$ 
$$$$           _
$$$$           P
$$$$            n            _       T
$$$$   {P } = {--} , {P } = {P } + [G ]{P }
$$$$     g     P       n      n      m   m
$$$$            m
$$$$ 
$$$$           _
$$$$           P
$$$$            f            _      
$$$$   {P } = {--} , {P } = {P } - [K  ]{Y }
$$$$     n     P       f      f      fs   s
$$$$            s
$$$$ 
$$$$           P
$$$$            a                    T
$$$$   {P } = {--} , {P } = {P } + [G ]{P }
$$$$     f     P       l      a      o   o
$$$$            o
$$$$
SSG2     USET,GM,YS,KFS,GO,,PG/,PO,PS,PL $  
****CARD   1-  3,  5,  6,  8- 11, 57- 60
****FILE 109
$$$$
LABEL    LBL10 $
****CARD   1-  3,  5,  6,  8- 11, 57- 60
****FILE 109
$$$$ SSG3 solves for displacements of independent coordinates
$$$$ 
$$$$               -1  
$$$$   {u } = [K  ]  {P }
$$$$     l      ll     l
$$$$ 
$$$$ solves for displacements of omitted coordinates
$$$$ 
$$$$     o         -1  
$$$$   {u } = [K  ]  {P }
$$$$     o      oo     o
$$$$ 
$$$$ calculates residual vector (RULV) and residual vector error ratio for 
$$$$ independent coordinates
$$$$                
$$$$   {deltaP } = {P } - [K  ]{u }
$$$$          l      l      ll   l
$$$$ 
$$$$                T        
$$$$              {u }{deltaP }
$$$$                l        l
$$$$   epsilon  = -------------
$$$$          l       T 
$$$$                {P }{u }
$$$$                  l   l
$$$$ 
$$$$ and calculates residual vector (RUOV) and residual vector error ratio for
$$$$ omitted coordinates
$$$$ 
$$$$                             o
$$$$   {deltaP } = {P } - [K  ]{u }
$$$$          o      o      oo   o
$$$$ 
$$$$                T        
$$$$              {u }{deltaP }
$$$$                o        o
$$$$   epsilon  = -------------
$$$$          o       T   o
$$$$                {P }{u }
$$$$                  o   o
$$$$
SSG3     LLL,KAA,PL,LOO,KOO,PO/ULV,UOOV,RULV,RUOV/OMIT/V,Y,IRES=-1/ 
         1/S,N,EPSI $   
****CARD   1-  6,  8- 11, 57- 60
****FILE 110
****RFMT 188
$$$$ Go to label LBL9 if residual vectors are not to be printed.
COND     LBL9,IRES $
****CARD   1-  6,  8- 11, 17, 57- 60
****RFMT 188,189,192-198
$$$$ MATGPR prints the residual vector for independent coordinates (RULV).
MATGPR   GPL,USET,SIL,RULV//*L* $   
****CARD   1-  6,  8- 11, 17, 57- 60
****RFMT 188,189,192-198
$$$$ MATGPR prints the residual vector for omitted coordinates (RUOV).
MATGPR   GPL,USET,SIL,RUOV//*O* $   
****CARD   1-  6,  8- 11, 17, 57- 60
****RFMT 188,189,192-198
$$$$
LABEL    LBL9 $ 
****CARD   1-  6,  8- 11, 17, 57- 60
****RFMT 188,189,192-198
$$$$ SDR1 recovers dependent displacements for current loop
$$$$ 
$$$$                       o
$$$$   {u } = [G ]{u ] + {u }  ,
$$$$     o      o   l      o
$$$$ 
$$$$    u                u            
$$$$     a                f           
$$$$   {--} = {u }  ,   {--} = {u }  ,
$$$$    u       f        Y       n    
$$$$     o                s           
$$$$                                  
$$$$                        u         
$$$$                         n        
$$$$  {u } = [G ]{u ]  ,   {--} = {u }
$$$$    m      m   n        u       g 
$$$$                         m        
$$$$ 
$$$$ and recovers single-point forces of constraint
$$$$ 
$$$$                    T
$$$$   {q } = -{P } + [K  ]{u } +[K  ]{Y }
$$$$     s       s      fs   f     ss   s
$$$$
SDR1     USET,PG,ULV,UOOV,YS,GO,GM,PS,KFS,KSS,/UGV,PGG,QG/1/
         *BKL0* $   
****CARD   1-  6,  8- 11, 57- 60
****FILE 111
$$$$ SDR2 calculates element forces (OEF1) and stresses (OES1)
$$$$ and prepares load vectors (OPG1), displacement vectors (OUGV1), 
$$$$ and single-point forces of constraint (OQG1) for output and translation 
$$$$ components of the displacement vector (PUGV1) for the static solution.
SDR2     CASECC,CSTM,MPT,DIT,EQEXIN,SIL,GPTT,EDT,BGPDP,,QG,UGV,EST, 
         ,PGG,PCOMPS/OPG1,OQG1,OUGV1,OES1,OEF1,PUGV1,OES1L,OEF1L/
         *BKL0*////COMPS $  
****CARD  19
****FILE 112
$$$$ OFP formats the tables prepared by SDR2 and places them on the system 
$$$$ output file for printing.
OFP      OUGV1,OPG1,OQG1,OEF1,OES1,//S,N,CARDNO $   
****CARD  19
****FILE 112
$$$$
OFP      OEF1L,OES1L,,,,//S,N,CARDNO $   
****CARD  19
****FILE 112
$$$$ SCAN examines the element stresses and forces calculated by SDR2 and 
$$$$ generates scanned output that meets the specifications set by the user.
SCAN     CASECC,OES1,OEF1/OESF1/C,N,*RF* $  
****FILE 112
****CARD 19 
$$$$ OFP formats the scanned output table prepared by SCAN and places it on 
$$$$ the system output file for printing.
OFP      OESF1,,,,,//S,N,CARDNO $   
****FILE 112
****CARD  19
$$$$ Go to label P2 if no static solution deformed structure plots are 
$$$$ requested.
COND     P2,JUMPPLOT $  
****SBST   7
****CARD  18
****FILE 133
$$$$ PLOT generates all requested static solution deformed structure and 
$$$$ contour plots.
PLOT     PLTPAR,GPSETS,ELSETS,CASECC,BGPDT,EQEXIN,SIP,PUGV1,,   
         GPECT,OES1,OES1L,/PLOTX2/NSIL/LUSEP/JUMPPLOT/PLTFLG/S,N,PFILE $
****SBST   7
****CARD  18
****FILE 133
$$$$ PRTMSG prints plotter data, engineering data, and contour data for each 
$$$$ static solution deformed plot generated.
PRTMSG   PLOTX2// $ 
****SBST   7
****CARD  18
****FILE 133
$$$$
LABEL    P2 $   
****SBST   7
****CARD  18
****FILE 133
$$$$ TA1 generates element tables for use in differential stiffness matrix 
$$$$ assembly.
TA1      ECT,EPT,BGPDT,SIL,GPTT,CSTM,,/X1,X2,X3,ECPT,GPCT,,,/LUSET/  
         NOSIMP/0/NOGENL/GENEL $
****CARD   1-  6,  8- 10, 57- 60
****FILE 113
$$$$                                                 d
$$$$ DSMG1 generates differential stiffness matrix [K  ].
$$$$                                                 gg
$$$$ 
DSMG1    CASECC,GPTT,SIL,EDT,UGV,CSTM,MPT,ECPT,GPCT,DIT/KDGG/   
         S,N,DSCOSET $  
****CARD   1-  6,  8- 11, 57- 60
****FILE 113
$$$$               d        d
$$$$ Equivalence [K  ] to [K  ] and [M  ] to [M  ] if no multipoint 
$$$$               gg       nn        gg       nn
$$$$ constraints exist.
EQUIV    KDGG,KDNN/MPCF2 / MGG,MNN/MPCF2 $  
****CARD   1-  6,  8- 11, 57- 60
****FILE 104,114
$$$$ Go to label LBL2D if no multipoint constraints exist.
COND     LBL2D,MPCF2 $  
****CARD   1-  6,  8- 11, 57- 60
****FILE 114
$$$$ MCE2 partitions differential stiffness matrix
$$$$ 
$$$$           +_d   d +
$$$$           |K  |K  |
$$$$     d     | nn| nm|
$$$$   [K  ] = |---+---|
$$$$     gg    | d | d |
$$$$           |K  |K  |
$$$$           + mn| mm+
$$$$ 
$$$$ and performs matrix reduction
$$$$ 
$$$$     d      _d       T   d       d           T   d
$$$$   [K  ] = [K  ] + [G ][K  ] + [K  ][G ] + [G ][K  ][G ]
$$$$     nn      nm      m   mn      mn   m      m   mm   m
$$$$
MCE2     USET,GM,KDGG,MGG,,/KDNN,MNN,, $
****CARD   1-  6,  8- 11, 57- 60
****FILE 114
$$$$
LABEL    LBL2D $
****CARD   1-  6,  8- 11, 57- 60
****FILE 114
$$$$               d        d
$$$$ Equivalence [K  ] to [K  ] and [M  ] to [M  ] if no single-point 
$$$$               nn       ff        nn       ff
$$$$ constraints exist.
EQUIV    KDNN,KDFF/SINGLE / MNN,MFF/SINGLE $
****CARD   1-  6,  8- 11, 57- 60
****FILE 105,115
$$$$ Go to label LBL3D if no single-point constraints exist.
COND     LBL3D,SINGLE $ 
****CARD   1-  6,  8- 11, 57- 60
****FILE 115
$$$$ SCE1 partitions out single-point constraints
$$$$ 
$$$$           + d   d +                        +       +
$$$$           |K  |K  |                        |M  |M  |
$$$$     d     | ff| fs|                        | ff| fs|
$$$$   [K  ] = |---+---|                [M  ] = |---+---|
$$$$     nn    | d | d |                  nn    |M  |M  |
$$$$           |K  |K  |                        | sf| ss|
$$$$           + sf| ss+                        +       +
$$$$
SCE1     USET,KDNN,MNN,,/KDFF,KDFS,KDSS,MFF,, $ 
****CARD   1-  6,  8- 11, 57- 60
****FILE 115
$$$$
LABEL    LBL3D $
****CARD   1-  6,  8- 11, 57- 60
****FILE 115
$$$$               d        d
$$$$ Equivalence [K  ] to [K  ] and [M  ] to [M  ] if no omitted coordinates
$$$$               ff       aa        ff       aa
$$$$ exist.
EQUIV    KDFF,KDAA/OMIT / MFF,MAA/OMIT $
****CARD   1-  6,  8- 11, 57- 60
****FILE 116,122
$$$$ Go to label LBL5D if no omitted coordinates exist.
COND     LBL5D,OMIT $   
****CARD   1-  6,  8- 11, 57- 60
****FILE 116,122
$$$$ SMP2 partitions constrained differential stiffness matrix
$$$$
$$$$           +       +
$$$$           |_d   d |
$$$$           |K  |K  |
$$$$     d     | aa| ao|
$$$$   [K  ] = |---+---|
$$$$     ff    | d | d |
$$$$           |K  |K  |
$$$$           | oa| oo|
$$$$           +       +
$$$$ 
$$$$ and performs matrix reduction
$$$$
$$$$     d      _d       d
$$$$   [K  ] = [K  ] + [K  ][G ]
$$$$     aa      aa      oa   o
$$$$
SMP2     USET,GO,KDFF/KDAA $
****CARD   1-  6,  8- 11, 57- 60
****FILE 116
$$$$ SMP2 partitions constrained mass matrix
$$$$
$$$$           +_      +
$$$$           |M  |M  |
$$$$           | aa| ao|
$$$$   [M  ] = |---+---|
$$$$     ff    |M  |M  |
$$$$           | oa| oo|
$$$$           +       +
$$$$           
$$$$ and performs matrix reduction
$$$$
$$$$            _        T           T           T                   
$$$$   [M  ] = [M  ] + [M  ][G ] + [G ][M  ] + [G ][M  ][G ]         
$$$$     aa      aa      oa   o      o   ao      o   oo   o          
$$$$
SMP2     USET,GO,MFF/MAA $  
****CARD   1-  6,  8- 11, 57- 60
****FILE 122
$$$$
LABEL    LBL5D $
****CARD   1-  6,  8- 11, 57- 60
****FILE 116,122
$$$$
PARAM    //*ADD*/DSCOSET/-1/0 $ 
****CARD   1-  6,  8- 11, 57- 60
****FILE 125
$$$$                       b             b             b         o       ob
$$$$ Equivalence {P } to {P }, {P } to {P }, {Y } to {Y }, and {u } to {u  }
$$$$               l       l     s       s     s       s         o       o
$$$$ if a scale factor is not specified on a DSFACT card.
$$$$
EQUIV    PL,PBL/DSCOSET/PS,PBS/DSCOSET/YS,YBS/DSCOSET/UOOV,UBOOV/   
         DSCOSET $  
****CARD   1-  6,  8- 11, 57- 60
****FILE 125
$$$$
PARAM    //*MPY*/NDSKIP/0/0 $   
****CARD   1-  6,  8- 11, 21, 57- 60
****FILE 125,126
$$$$ DSMG2 adds partitions of stiffness matrix to similar partitions of 
$$$$ differential stiffness matrix
$$$$
$$$$     b                   d
$$$$   [K  ] = [K  ] + beta[K  ]
$$$$     ll      aa          aa
$$$$
$$$$     b                   d
$$$$   [K  ] = [K  ] + beta[K  ]
$$$$     fs      fs          fs
$$$$
$$$$     b                   d
$$$$   [K  ] = [K  ] + beta[K  ]
$$$$     ss      ss          ss
$$$$
$$$$ and multiplies partitions of load vectors and displacement vectors by the 
$$$$ value of the differential stiffness scale factor (beta)
$$$$
$$$$     b                                b
$$$$   {P } = beta{P }                  {P } = beta{P }
$$$$     l          l                     s          s
$$$$
$$$$     b                                bo          o
$$$$   {Y } = beta{Y }                  {u  } = beta{u }
$$$$     s          s                     o           o
$$$$
DSMG2    MPT,KAA,KDAA,KFS,KDFS,KSS,KDSS,PL,PS,YS,UOOV/KBLL,KBFS,KBSS,   
         PBL,PBS,YBS,UBOOV/S,N,NDSKIP/S,N,REPEATD/DSCOSET $ 
****CARD   1-  6,  8- 11, 21, 57- 60
****FILE 125,126
$$$$ RBMG2 decomposes the combined differential stiffness matrix and elastic 
$$$$ stiffness matrix
$$$$ 
$$$$     b       b    b
$$$$   [K  ] = [L  ][U  ]
$$$$     ll      ll   ll
$$$$ 
RBMG2    KBLL/LBLL/S,N,POWER/S,N,DET $  
****CARD   1-  6,  8- 11, 21, 57- 60
****FILE 127
$$$$ PRTPARM prints the scaled value of the determinant of the combined 
$$$$ differential stiffness matrix and elastic stiffness matrix.
PRTPARM  //0/*DET* $
****CARD   1-  6,  8- 11, 21, 57- 60
$$$$ PRTPARM prints the scale factor (power of ten) of the determinant of the 
$$$$ combined differential stiffness matrix and the elastic stiffness matrix.
PRTPARM  //0/*POWER* $  
****CARD   1-  6,  8- 11, 21, 57- 60
$$$$ SSG3 solves for displacements of independent coordinates for the value of 
$$$$ the differential stiffness scale factor (beta)
$$$$ 
$$$$     b      b  -1  b
$$$$   {u } = [K  ]  {P }
$$$$     l      ll     l
$$$$ 
$$$$ and calculates residual vector (RULV) and residual vector error ratio for 
$$$$ current differential stiffness load vector
$$$$                
$$$$          b      b      b    b
$$$$   {deltaP } = {P } - [K  ]{u }
$$$$          l      l      ll   l
$$$$ 
$$$$                b T       b
$$$$              {u } {deltaP }
$$$$          b     l         l
$$$$   epsilon  = --------------
$$$$          l       b T  b
$$$$                {P } {u }
$$$$                  l    l
$$$$ 
SSG3     LBLL,KBLL,PBL,,,/UBLV,,RUBLV,/-1/V,Y,IRES/NDSKIP/  
         S,N,EPSI $ 
****CARD   1-  6,  8- 11, 21, 57- 60
****FILE 128
$$$$ Go to label LBL9D if the residual vector for current differential 
$$$$ stiffness load factor if not to be printed.
COND     LBL9D,IRES $   
****CARD   1-  6,  8- 11, 21, 57- 60
$$$$ MATGPR prints the residual vector for current differential stiffness load 
$$$$ factor.
MATGPR   GPL,USET,SIL,RUBLV//*L* $  
****CARD   1-  6,  8- 11, 21, 57- 60
$$$$
LABEL    LBL9D $
****CARD   1-  6,  8- 11, 21, 57- 60
$$$$ SDR1 recovers dependent components of displacements
$$$$ 
$$$$                                      b
$$$$                                     u
$$$$    b          b      ob              l        b
$$$$  {u } = [G ]{u } + {u  }           {----} = {u }
$$$$    o      o   l      o               b        f
$$$$                                     u
$$$$                                      o
$$$$ 
$$$$     b
$$$$    u       
$$$$     f       b                        b          b
$$$$   {---} = {u }                     {u } = [G ]{u }
$$$$     b       n                        m      m   n
$$$$    Y
$$$$     s
$$$$
$$$$     b
$$$$    u                 
$$$$     n      b        
$$$$   {--} = {u }
$$$$     b      g 
$$$$    u                  
$$$$     m
$$$$
$$$$ and recovers single-point forces of constraint for the current 
$$$$ differential stiffness scale factor
$$$$
$$$$     b       b      b    b      b    b
$$$$   {q } = -{p } + [K  ]{u } + [K  ]{Y }
$$$$     s       s      sf   f      ff   s
$$$$
SDR1     USET,,UBLV,UBOOV,YBS,GO,GM,PBS,KBFS,KBSS,/UBGV,,QBG/NDSKIP/
         *DS1* $
****CARD   1-  6,  8- 11, 21, 57- 60
****FILE 129
$$$$ SDR2 calculates element forces (OEFB1) and stresses (OESB1) and prepares 
$$$$ displacement vectors (OUBGV1) and single-point forces of constraint 
$$$$ (OQBG1) for output and translation components of the displacement vector 
$$$$ (PUBGV1) for the differential stiffness solution.
SDR2     CASECC,CSTM,MPT,DIT,EQEXIN,SIL,GPTT,EDT,BGPDP,,QBG,UBGV,EST,   
         ,,PCOMPS/,OQBG1,OUBGV1,OESB1,OEFB1,PUBGV1,OESB1L,OEFB1L/   
         *DS1*////COMPS $   
****CARD   1-  6,  8- 11, 21, 57- 60
****FILE 130
$$$$ OFP formats the tables prepared by SDR2 and places them on the system 
$$$$ output file for printing.
OFP      OQBG1,OUBGV1,OESB1,OEFB1,,//S,N,CARDNO $   
****CARD   1-  6,  8- 11, 21, 57- 60
****FILE 130
$$$$
OFP      OEFB1L,OESB1L,,,,//S,N,CARDNO $
****CARD   1-  6,  8- 11, 21, 57- 60
****FILE 130
$$$$ DPD extracts Eigenvalue Extraction Data from Dynamics data block.
DPD      DYNAMICS,GPL,SIL,USET/GPLD,SILD,USETD,,,,,,,EED,EQDYN/ 
         LUSET/LUSETD/NOTFL/NODLT/NOPSDL/NOFRL/ 
         NONLFT/NOTRL/S,N,NOEED//NOUE $ 
****CARD   1-  6,  8- 11, 21, 57- 60
****FILE 117
$$$$ Go to label ERROR3 and print Error Message No. 3 if there is no 
$$$$ Eigenvalue Extraction Data.
COND     ERROR3,NOEED $ 
****CARD   1-  6,  8- 11, 21, 57- 60
****FILE 117
****RFMT 187-189,191-198,200,201-204,207-209
$$$$
PARAM    //*MPY*/NEIGV/1/-1 $   
****CARD   1-  6,  8- 11, 21, 57- 62
****FILE 118
$$$$ READ extracts real eigenvalues and eigenvectors from the equation
$$$$ 
$$$$     b
$$$$   [K   - lambda M  ]{u } = 0
$$$$     ll           aa   a
$$$$ 
$$$$ calculates rigid body modes by finding a square matrix [phi  ] such that
$$$$                                                            ro
$$$$              T
$$$$   [m ] = [phi  ][m ][phi  ]
$$$$     o        ro   r     ro
$$$$ 
$$$$ is diagonal and normalized, computes rigid body eigenvectors
$$$$ 
$$$$             +       +
$$$$             |Dphi   |
$$$$             |    ro |
$$$$   [phi  ] = |-------|
$$$$       ao    |phi    |
$$$$             |   ro  |
$$$$             +       +
$$$$ 
$$$$ calculates modal mass matrix
$$$$ 
$$$$             T
$$$$   [m] = [phi ][M  ][phi ]
$$$$             a   aa     a
$$$$ 
$$$$ and normalizes eigenvectors according to one of the following user 
$$$$ requests:
$$$$   1. Unit value of a selected component.
$$$$   2. Unit value of the largest component.
$$$$   3. Unit value of the generalized mass.
READ     KBLL,MAA,,,EED,USET,CASECC/LAMA,PHIA,MI,OEIGS/*MODES*/ 
         S,N,NEIGV/3 $  
****CARD   1-  6,  8- 11, 21, 57- 62
****FILE 118
$$$$ OFP formats the summary of eigenvalue extraction information (OEIGS) 
$$$$ prepared by READ and places it on the system output file for printing.
OFP      OEIGS,,,,,//S,N,CARDNO $   
****CARD   1-  6,  8- 11, 21, 57- 62
****FILE 118
$$$$ Go to label ERROR4 and print Error Message No. 4 if no eigenvalues were 
$$$$ found.
COND     ERROR4,NEIGV $ 
****CARD   1-  6,  8- 11, 21, 57- 62
****FILE 118
$$$$ OFP formats the eigenvalues (LAMA) prepared by READ and places them on 
$$$$ the system output file for printing.
OFP      LAMA,,,,,//S,N,CARDNO $
****CARD   1-  6,  8- 11, 21, 57- 62
****FILE 118
$$$$ SDR1 recovers dependent components of eigenvectors
$$$$ 
$$$$                                   phi
$$$$                                      a
$$$$  {phi } = [G ]{phi }             {----} = {phi }
$$$$      o      o     a               phi         f
$$$$                                      o
$$$$
$$$$    phi       
$$$$       f
$$$$   {----} = {phi }                  {phi } = [G ]{phi }
$$$$    phi         n                       m      m     n
$$$$       s        
$$$$
$$$$    phi          
$$$$       n         
$$$$   {----} = {phi }
$$$$    phi         g 
$$$$       m         
$$$$                                                            T
$$$$ and recovers single-point forces of constraint {q } = [K  ] {phi }.
$$$$                                                  s      fs      f
$$$$
SDR1     USET,,PHIA,,,GO,GM,,KDFS,,/PHIG,,BQG/1/*REIG* $
****CARD   1-  6,  8- 11, 21, 57- 62
****FILE 119
****RFMT 187-189,191-198,200,201-204,207-209
$$$$ CASE copies the record corresponding to the third subcase from CASECC 
$$$$ into CASEXX.
CASE     CASECC,/CASEXX/*TRANRESP*/KEPEAT=3/LOOP $  
****CARD   1-  6,  8- 11, 13, 14, 16, 18, 19, 21, 24, 57- 62
****FILE 120
$$$$ SDR2 calculates element forces (OBEF1) and stresses (OBES1) and prepares 
$$$$ eigenvectors (OPHIG) and single-point forces of constraint (OBQG1) for 
$$$$ output and translation components of the eigenvectors (PPHIG) for the 
$$$$ normal mode solution.
SDR2     CASEXX,CSTM,MPT,DIT,EQEXIN,SIL,,,BGPDP,LAMA,BQG,PHIG,EST,,,
         PCOMPS/,OBQG1,OPHIG,OBES1,OBEF1,PPHIG,OBES1L,OBEF1L/   
         *REIG*////COMPS $  
****CARD  18, 19
****FILE 120
$$$$ OFP formats the tables prepared by SDR2 and places them on the system 
$$$$ output file for printing.
OFP      OPHIG,OBQG1,OBEF1,OBES1,,//S,N,CARDNO $
****CARD  19
****FILE 120
$$$$
OFP      OBEF1L,OBES1L,,,,//S,N,CARDNO $
****CARD  19
****FILE 120
$$$$ Go to label P3 if no real eigenvalue solution deformed structure plots 
$$$$ are requested.
COND     P3,JUMPPLOT $  
****SBST   7
****CARD  18
****FILE 134
$$$$ PLOT generates all requested real eigenvalue solution deformed structure 
$$$$ and contour plots.
PLOT     PLTPAR,GPSETS,ELSETS,CASECC,BGPDT,EQEXIN,SIP,,PPHIG,GPECT, 
         OBES1,OBES1L,/PLOTX3/NSIL/LUSEP/JUMPPLOT/PLTFLG/   
         S,N,PFILE $
****SBST   7
****CARD  18
****FILE 134
$$$$ PRTMSG prints plotter data, engineering data, and contour data for each 
$$$$ deformed plot generated.
PRTMSG   PLOTX3// $ 
****SBST   7
****CARD  18
****FILE 134
$$$$
LABEL    P3 $   
****SBST   7
****CARD  18
****FILE 134
$$$$ Go to label FINIS and make normal exit.
JUMP     FINIS $
****CARD   1-  6,  8- 11, 13- 16, 18, 19, 24, 57- 62
****RFMT 187-189,191-198,200,201-204,207-209
$$$$
LABEL    ERROR1 $   
****CARD   1,  2,  4-  6,  8, 16
****FILE  97
****RFMT 187-189,191-198,200,201-204,207-209
$$$$ Print Error Message No. 1 and terminate execution.
PRTPARM  //-1/*NMDS* $  
****CARD   1,  2,  4-  6,  8, 16
****FILE  97
****RFMT 187-189,191-198,200,201-204,207-209
$$$$
LABEL    ERROR2 $   
****CARD   1, 11
****RFMT 187-189,191-198,200,201-204,207-209
$$$$ Print Error Message No. 2 and terminate execution.
PRTPARM  //-2/*NMDS* $  
****CARD   1, 11
****RFMT 187-189,191-198,200,201-204,207-209
$$$$
LABEL    ERROR3 $   
****CARD   1-  6,  8- 11, 21, 57- 60
****FILE 117
****RFMT 187-189,191-198,200,201-204,207-209
$$$$ Print Error Message No. 3 and terminate execution.
PRTPARM  //-3/*NMDS* $  
****CARD   1-  6,  8- 11, 21, 57- 60
****FILE 117
****RFMT 187-189,191-198,200,201-204,207-209
$$$$
LABEL    ERROR4 $   
****CARD   1-  6,  8- 11, 21, 57- 62
****FILE 118
****RFMT 187-189,191-198,200,201-204,207-209
$$$$ Print Error Message No. 4 and terminate execution.
PRTPARM  //-4/*NMDS* $  
****CARD   1-  6,  8- 11, 21, 57- 62
****FILE 118
****RFMT 187-189,191-198,200,201-204,207-209
$$$$
LABEL    ERROR5 $   
****SBST   8
****CARD   1-  3,  5,  8, 24, 57
****FILE 133
****RFMT 187-189,191-198,200,201-204,207-209
$$$$ Print Error Message No. 5 and terminate execution.
PRTPARM  //-5/*NMDS* $  
****SBST   8
****CARD   1-  3,  5,  8, 24, 57
****FILE 133
****RFMT 187-189,191-198,200,201-204,207-209
$$$$
LABEL    ERROR6 $   
****CARD   1,  9- 11, 22, 23, 59
****FILE 111
****RFMT 187-189,191-198,200,201-204,207-209
$$$$ Print Error Message No. 6 and terminate execution.
PRTPARM  //-6/*NMDS* $  
****CARD   1,  9- 11, 22, 23, 59
****FILE 111
****RFMT 187-189,191-198,200,201-204,207-209
$$$$
LABEL    FINIS $
****CARD   1-  6,  8- 11, 13- 16, 18, 19, 24, 57- 62
****RFMT 187-189,191-198,200,201-204,207-209
$$$$
PURGE    DUMMY/MINUS1 $ 
****CARD   1-  6,  8- 11, 13- 16, 18, 19, 24, 57- 62
****RFMT 187-189,191-198,200,201-204,207-209
$$$$
END      $  
****CARD   1-  6,  8- 11, 13- 16, 18, 19, 24, 57- 62
****RFMT 187-189,191-198,200,201-204,207-209
$$$$
$*CARD BITS 
  1    AXIC     AXIF     CELAS1   CELAS2   CELAS3   CELAS4   CMASS1 
  1    CMASS2   
  1    CMASS3   CMASS4   CORD1C   CORD1R   CORD1S   CORD2C   CORD2R 
  1    CORD2S   
  1    FREEPT   GRDSET   GRID     GRIDB    POINTAX  PRESPT   RINGAX 
  1    RINGFL   SECTAX   SEQGP    SPOINT
  2    ADUM1    ADUM2    ADUM3    ADUM4    ADUM5    ADUM6    ADUM7  
  2    ADUM8
  2    ADUM9    BAROR    CBAR     CCONEAX  CDUM1    CDUM2    CDUM3  
  2    CDUM4
  2    CDUM5    CDUM6    CDUM7    CDUM8    CDUM9    CELBOW   CFLUID2
  2    CFLUID3  
  2    CFLUID4  CHEXA1   CHEXA2   CIHEX1   CIHEX2   CIHEX3   CIS2D8 
  2    CONROD   
  2    CQDMEM   CQDMEM1  CQDMEM2  CQDPLT   CQUAD1   CQUAD2   CROD   
  2    CSHEAR   CTETRA   CTORDRG  CTRAPAX  CTRAPRG  CTRBSC   CTRIA1 
  2    CTRIA2   CQUAD4   CTRIA3
  2    CTRIAAX  CTRIARG  CTRIM6   CTRMEM   CTRPLT   CTRPLT1  CTRSHL 
  2    CTUBE    CTWIST   CWEDGE 
  3    PBAR     PCONEAX  PDUM1    PDUM2    PDUM3    PDUM4    PDUM5  
  3    PDUM6
  3    PDUM7    PDUM8    PDUM9    PELBOW   PIHEX    PIS2D8   PQDMEM 
  3    PQDMEM1  PQDMEM2  PQDPLT   PQUAD1   PQUAD2   PROD     PSHEAR 
  3    PTORDRG  PSHELL   PCOMP    PCOMP1   PCOMP2
  3    PTRAPAX  PTRBSC   PTRIA1   PTRIA2   PTRIAAX  PTRIM6   PTRMEM 
  3    PTRPLT   PTRPLT1  PTRSHL   PTUBE    PTWIST   
  4    GENEL
  5    CONM1    CONM2    PMASS    FSLIST
  6    PELAS
  8    MAT1     MAT2     MAT3     MATT1    MATT2    MATT3    MAT8
  8    TABLEM1  TABLEM2  TABLEM3  TABLEM4  TEMPMT$  TEMPMX$  
  9    AXISYM$  CRIGD1   CRIGD2   CRIGD3   CRIGDR   CRROD    CRBAR  
  9    CRTRPLT  CRBE1    CRBE2    CRBE3    CRSPLINE MPC      MPCADD 
  9    MPCAX    MPC$
 10    SPC      SPC1     SPCADD   SPCAX    SPC$ 
 11    ASET     ASET1    OMIT     OMIT1    OMITAX   SUPAX    SUPORT 
 13    TEMP     TEMPAX   TEMPD    TEMPP1   TEMPP2   TEMPP3   TEMPRB 
 14    WTMASS   
 15    GRDPNT   
 16    PLOTEL   
 17    IRES 
 18    PLOT$
 19    POUT$
 21    DSFACT   DSCO$   
 22    ASETOUT  
 23    AUTOSPC  
 24    COUPMASS CPBAR    CPQDPLT  CPQUAD1  CPQUAD2  CPROD    CPTRBSC
 24    CPTRIA1  CPTRIA2  CPTRPLT  CPTUBE
 57    GRAV     RFORCE  
 58    TEMPLD$  
 59    DEFORM   DEFORM$  LOAD$    SPCD  
 60    FORCE    FORCE1   FORCE2   FORCEAX  LOAD     MOMAX    MOMENT 
 60    MOMENT1  PLOAD4
 60    MOMENT2  PLOAD    PLOAD1   PLOAD2   PLOAD3   PRESAX   SLOAD  
 61    EIGR 
 62    METHOD$  
$$$$
$*FILE BITS 
 94    BGPDT    CSTM     EQEXIN   GPDT     GPL      SIL 
 95    ECT  
 96    GPTT     SLT 
 97    EST      GEI      GPECT    MPTX     PCOMPS   EPTX
 98    KGGX 
 99    MGG  
100    KGG  
101    ASET     RG       USET     YS       OGPST
102    GPST 
103    GM   
104    KNN      MNN 
105    KFF      KFS      KSS      MFF   
106    GO       KAA      KOO      LOO   
107    LLL  
108    PG   
109    PL       PO       PS 
110    RULV     RUOV     ULV      UOOV  
111    PGG      QG       UGV
112    OEF1     OES1     OPG1     OQG1     OUGV1    PUGV1   
112    OEF1L    OES1L    OESF1
113    KDGG 
114    KDNN 
115    KDFF     KDFS     KDSS   
116    KDAA 
117    EED      EQDYN    GPLD     SILD     USETD
118    LAMA     MI       OEIGS    PHIA  
119    BQG      PHIG
120    OBEF1    OBES1    OBQG1    OPHIG    PPHIG
120    OBEF1L   OBES1L  
121    ELSETS   GPSETS   PLTPAR   PLTSETX   
122    MAA  
123    KDICT    KELM     MDICT    MELM  
125    PBL      PBS      UBOOV    YBS   
126    KBLL     KBFS     KBSS   
127    LBLL 
128    UBLV     RUBLV   
129    UBGV     QBG 
130    OQBG1    OUBGV1   OESB1    OEFB1    PUBGV1   
130    OEFB1L   OESB1L  
131    PLOTX1   
132    OGPWG
133    PLOTX2   
134    PLOTX3   
135    BGPDP    SIP 
$*  
=PAGE=
DISP14

APR.93
$$$$$$$$
BEGIN    DISP 14 - STATIC ANALYSIS WITH CYCLIC SYMMETRY - APR. 1993 $   
****CARD   1- 28, 59- 62
****RFMT 187-199,201-204,207-209
$$$$
PRECHK   ALL $  
****CARD   1- 28, 59- 62
****RFMT 187-199,201-204,207-209
$$$$
FILE     KKK=SAVE/PK=SAVE $ 
****CARD   1- 28, 59- 62
****RFMT 187-199,201-204,207-209
$$$$
FILE     UXV=APPEND $   
****CARD   1- 28, 59- 62
****RFMT 187-199,201-204,207-209
$$$$
PARAM    //*MPY*/CARDNO/0/0 $   
****CARD   1-  3,  5,  6,  8- 10, 14, 15, 18, 19, 21, 24
****FILE 101,114,122,123
$$$$
PARAM    //*NOP*/V,Y,CYCIO=1 $  
****CARD   1-  6,  8- 14, 59- 62
$$$$ GP1 generates coordinate system transformation matrices, tables of grid 
$$$$ point locations, and tables relating the internal and external grid point 
$$$$ numbers.
GP1      GEOM1,GEOM2,/GPL,EQEXIN,GPDT,CSTM,BGPDT,SIL/S,N,LUSET/ 
         NOGPDT/MINUS1=-1 $ 
****CARD   1
****FILE  94
$$$$ PLTTRAN modifies special scalar grid points in the BGPDT and SIL tables.
PLTTRAN  BGPDT,SIL/BGPDP,SIP/LUSET/S,N,LUSEP $  
****CARD   1
****FILE 125
$$$$ GP2 generates Element Connection Table with internal indices.
GP2      GEOM2,EQEXIN/ECT $ 
****CARD   1,  2,  4,  5, 16
****FILE  95
$$$$
PARAML   PCDB//*PRES*////JUMPPLOT $ 
****SBST   7
****CARD  18
****FILE 115,121
$$$$
PURGE    PLTSETX,PLTPAR,GPSETS,ELSETS/JUMPPLOT $
****SBST   7
****CARD  18
****FILE 115
$$$$ Go to label P1 if there are no structure plot requests.
COND     P1,JUMPPLOT $  
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 115,121
$$$$ PLTSET transforms user input into a form used to drive the structure 
$$$$ plotter.
PLTSET   PCDB,EQEXIN,ECT,EPT/PLTSETX,PLTPAR,GPSETS,ELSETS/S,N,NSIL/ 
         S,N,JUMPPLOT $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 115
$$$$ PRTMSG prints error messages associated with the structure plotter.
PRTMSG   PLTSETX// $
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 115
$$$$
PARAM    //*MPY*/PLTFLG/1/1 $   
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 121
$$$$
PARAM    //*MPY*/PFILE/0/0 $
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 121
$$$$ Go to label P1 if no undeformed structure plots are requested.
COND     P1,JUMPPLOT $  
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 121
$$$$ PLOT generates all requested undeformed structure plots.
PLOT     PLTPAR,GPSETS,ELSETS,CASECC,BGPDT,EQEXIN,SIL,,ECT,,,,/PLOTX1/  
         NSIL/LUSET/S,N,JUMPPLOT/S,N,PLTFLG/S,N,PFILE  $
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 121
$$$$ PRTMSG prints plotter data and engineering data for each undeformed plot 
$$$$ generated.
PRTMSG   PLOTX1// $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 121
$$$$
LABEL    P1 $   
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 115,121
$$$$ GP3 generates Static Loads Table and Grid Point Temperature Table.
GP3      GEOM3,EQEXIN,GEOM2/SLT,GPTT/S,N,NOGRAV $   
****CARD   1,  2, 13, 60, 61
****FILE  96
$$$$
PARAM    //*AND*/NOMGG/NOGRAV/V,Y,GRDPNT=-1 $   
****CARD   1,  2, 13, 15, 60, 61
****FILE 116
****RFMT 187-199,201-204,207-209
$$$$ TA1 generates element tables for use in matrix assembly and stress 
$$$$ recovery.
TA1      ECT,EPT,BGPDT,SIL,GPTT,CSTM,MPT,EQEXIN/EST,GEI,GPECT,,,MPTX,  
         PCOMPS,EPTX/LUSET/S,N,NOSIMP/1/S,N,NOGENL/GENEL/S,N,COMPS $
****CARD   1-  6, 13, 16
****FILE  97
$$$$
EQUIV    MPTX,MPT/COMPS/EPTX,EPT/COMPS $
****CARD   1- 28, 59- 62
****FILE  97
$$$$
PARAM    //*AND*/NOELMT/NOGENL/NOSIMP $ 
****CARD   1,  2,  4-  6, 16
****FILE  97
****RFMT 187-199,201-204,207-209
$$$$ Go to label ERROR4 and print Error Message No. 4 if no elements have been 
$$$$ defined. 
COND     ERROR4,NOELMT $
****CARD   1,  2,  4-  6, 16
****FILE  97
****RFMT 187-199,201-204,207-209
$$$$ Go to label LBL1 if there are no structural elements.
COND     LBL1,NOSIMP $  
****CARD   1-  3,  5,  6,  8, 14, 15, 24, 61
****FILE  98, 99,116,122
$$$$
PARAM    //*ADD*/NOKGGX/1/0 $   
****CARD   1-  3,  6,  8
****FILE 116
$$$$ EMG generates structural element stiffness and mass matrix tables and 
$$$$ dictionaries for later assembly by the EMA module. 
EMG      EST,CSTM,MPT,DIT,GEOM2,/KELM,KDICT,MELM,MDICT,,,/S,N,NOKGGX/   
         S,N,NOMGG////C,Y,COUPMASS/C,Y,CPBAR/   
         C,Y,CPROD/C,Y,CPQUAD1/C,Y,CPQUAD2/C,Y,CPTRIA1/C,Y,CPTRIA2/ 
         C,Y,CPTUBE/C,Y,CPQDPLT/C,Y,CPTRPLT/C,Y,CPTRBSC/
         C,Y,VOLUME/C,Y,SURFACE $   
****CARD   1-  3,  5,  6,  8, 14, 15, 24, 61
****FILE 116
$$$$
PURGE    KGGX/NOKGGX/MGG/NOMGG $
****CARD   1-  3,  6,  8
****FILE  98
$$$$ Go to label JMPKGG if no stiffness matrix is to be assembled.
COND     JMPKGG,NOKGGX $
****CARD   1-  3,  6,  8
****FILE  98
$$$$                                  x
$$$$ EMA assembles stiffness matrix [K  ] and Grid Point Singularity Table.
$$$$                                  gg
$$$$
EMA      GPECT,KDICT,KELM/KGGX $
****CARD   1-  3,  6,  8
****FILE  98
$$$$
PURGE    KDICT,KELM/MINUS1 $
****CARD   1-  3,  6,  8
****FILE 116
$$$$
LABEL    JMPKGG $   
****CARD   1-  3,  6,  8
****FILE  98
$$$$ Go to label JMPMGG if no mass matrix is to be assembled.
COND     JMPMGG,NOMGG $ 
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE  99
$$$$ EMA assembles mass matrix [M  ].
$$$$                             gg
$$$$
EMA      GPECT,MDICT,MELM/MGG/-1/C,Y,WTMASS=1.0 $   
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE  99
$$$$
PURGE    MDICT,MELM/MINUS1 $
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE 116
$$$$
LABEL    JMPMGG $   
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE  99
$$$$ Go to label LBL1 if no weight and balance information is requested.
COND     LBL1,GRDPNT $  
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 122
$$$$ Go to label ERROR2 and print Error Message No. 2 if no weight and balance 
$$$$ information is requested.
COND     ERROR2,NOMGG $ 
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 116
$$$$ GPWG generates weight and balance information.
GPWG     BGPDP,CSTM,EQEXIN,MGG/OGPWG/V,Y,GRDPNT/C,Y,WTMASS $
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 122
$$$$ OFP formats the weight and balance information prepared by GPWG and 
$$$$ places it on the system output file for printing.
OFP      OGPWG,,,,,//S,N,CARDNO $   
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 122
$$$$
LABEL    LBL1 $ 
****CARD   1-  3,  5,  6,  8, 14, 15, 24, 61
****FILE  98, 99,116,122
$$$$               x
$$$$ Equivalence [K  ] to [K  ] if no general elements exist.
$$$$               gg       gg
$$$$
EQUIV    KGGX,KGG/NOGENL $  
****CARD   1-  4,  6,  8
****FILE 100
$$$$ Go to label LBL11A if no general elements exist.
COND     LBL11A,NOGENL $
****CARD   1-  4,  6,  8
****FILE 100
$$$$                                 x
$$$$ SMA3 adds general elements to [K  ] to obtain stiffness matrix [K  ].
$$$$                                 gg                               gg
$$$$
SMA3     GEI,KGGX/KGG/LUSET/NOGENL/NOSIMP $ 
****CARD   1-  4,  6,  8
****FILE 100
$$$$
LABEL    LBL11A $   
****CARD   1-  4,  6,  8
****FILE 100
$$$$
GPSTGEN  KGG,SIL/GPST $ 
****CARD   1-  4,  6,  8
****FILE 102
$$$$
PARAM    //*MPY*/NSKIP/0/0 $
****CARD   1,  9- 11, 59
****FILE 101
$$$$ GP4 generates flags defining members of various displacement sets (USET), 
$$$$ forms multipoint constraint equations [R ] {u } = 0, and forms enforced
$$$$                                         g    g
$$$$ displacement vector {Y }.
$$$$                       s
$$$$
GP4      CASECC,GEOM4,EQEXIN,GPDT,BGPDT,CSTM,GPST/RG,YS,USET,   
         ASET,OGPST/LUSET/S,N,MPCF1/S,N,MPCF2/S,N,SINGLE/S,N,OMIT/  
         S,N,REACT/S,N,NSKIP/S,N,REPEAT/S,N,NOSET/S,N,NOL/S,N,NOA/  
         C,Y,ASETOUT/C,Y,AUTOSPC $  
****CARD   1-  4,  6,  8- 11, 20, 21, 59
****FILE 101
$$$$ OFP formats the table of potential grid point singularities prepared by 
$$$$ GPSP and places it on the system output file for printing.
OFP      OGPST,,,,,//S,N,CARDNO $   
****CARD   1-  4,  6,  8- 10, 21
****FILE 101
$$$$ Go to label ERROR3 and print Error Message No. 3 if no independent 
$$$$ degrees of freedom are defined.
COND     ERROR3,NOL $   
****CARD   1,  9- 11, 59
****FILE 101
****RFMT 187-199,201-204,207-209
$$$$
PARAM    //*NOT*/REACDATA/REACT $   
****CARD   1, 11, 59
****FILE 101
$$$$ Go to label ERROR6 and print Error Message No. 6 if free-body supports 
$$$$ are present.
COND     ERROR6,REACDATA $  
****CARD   1, 11, 59
****FILE 101
$$$$
PURGE    GM/MPCF1/GO,KOO,LOO,PO,UOOV,RUOV/OMIT/PS,KFS,KSS,QG/SINGLE $   
****CARD   1,  9- 11, 59
****FILE 103,105,106,111-113
$$$$ GPCYC prepares segment boundary table (CYCD).
GPCYC    GEOM4,EQEXIN,USET/CYCD/V,Y,CTYPE/S,N,NOGO $
****CARD   1-  4,  6,  8- 12, 22, 59
****FILE 107
$$$$ Go to label ERROR5 and print Error Message No. 5 if CYJOIN data is 
$$$$ inconsistent.
COND     ERROR5,NOGO $  
****CARD   1-  4,  6,  8- 12, 22, 59
****FILE 107
$$$$ Equivalence [K  ] to [K  ] if no multipoint constraints exist.
$$$$               gg       nn
$$$$
EQUIV    KGG,KNN/MPCF1 $
****CARD   1-  4,  6,  8,  9
****FILE 104
$$$$ Go to label LBL2 if no multipoint constraints exist.
COND     LBL2,MPCF1 $   
****CARD   1-  4,  6,  8,  9
****FILE 103,104
$$$$ MCE1 partitions multipoint constraint equation [R ] = [R |R ] and solves
$$$$                                                  g      m  n
$$$$                                                             -1
$$$$ for multipoint constraint transformation matrix [G ] = -[R ]  [R ].
$$$$                                                   m       m     n
$$$$
MCE1     USET,RG/GM $   
****CARD   1,  9
****FILE 103
$$$$ MCE2 partitions stiffness matrix
$$$$ 
$$$$           +_      +
$$$$           |K  |K  |
$$$$           | nn| nm|
$$$$   [K  ] = |---+---|
$$$$     gg    |K  |K  |
$$$$           | mn| mm|
$$$$           +       +
$$$$ 
$$$$ and performs matrix reduction
$$$$ 
$$$$            _        T           T           T
$$$$   [K  ] = [K  ] + [G ][K  ] + [K  ][G ] + [G ][K  ][G ]
$$$$     nn      nm      m   mn      mn   m      m   mm   m
$$$$
MCE2     USET,GM,KGG,,,/KNN,,, $
****CARD   1-  4,  6,  8,  9
****FILE 104
$$$$
LABEL    LBL2 $ 
****CARD   1-  4,  6,  8,  9
****FILE 103,104
$$$$ Equivalence [K  ] to [K  ] if no single-point constraints exist.
$$$$               nn       ff
$$$$
EQUIV    KNN,KFF/SINGLE $   
****CARD   1-  4,  6,  8- 10
****FILE 105
$$$$ Go to label LBL3 if no single-point constraints exist.
COND     LBL3,SINGLE $  
****CARD   1-  4,  6,  8- 10
****FILE 105
$$$$ SCE1 partitions out single-point constraints
$$$$ 
$$$$           +       +
$$$$           |K  |K  |
$$$$           | ff| fs|
$$$$   [K  ] = |---+---|
$$$$     nn    |K  |K  |
$$$$           | sf| ss|
$$$$           +       +
$$$$
SCE1     USET,KNN,,,/KFF,KFS,KSS,,, $   
****CARD   1-  4,  6,  8- 10
****FILE 105
$$$$
LABEL    LBL3 $ 
****CARD   1-  4,  6,  8- 10
****FILE 105
$$$$ Equivalence [K  ] to [K  ] if no omitted coordinates exist.
$$$$               ff       aa
$$$$
EQUIV    KFF,KAA/OMIT $ 
****CARD   1-  4,  6,  8- 11
****FILE 106
$$$$ Go to label LBL5 if no omitted coordinates exist.
COND     LBL5,OMIT $
****CARD   1-  4,  6,  8- 11
****FILE 106
$$$$ SMP1 partitions constrained stiffness matrix
$$$$
$$$$           +_      +
$$$$           |K  |K  |
$$$$           | aa| ao|
$$$$   [K  ] = |---+---|
$$$$     ff    |K  |K  |
$$$$           | oa| oo|
$$$$           +       +
$$$$           
$$$$                                               -1
$$$$ solves for transformation matrix [G ] = -[K  ]  [K  ]
$$$$                                    o       oo     oa
$$$$ 
$$$$                                        _        T
$$$$ and performs matrix reduction [K  ] = [K  ] + [K  ][G ].
$$$$                                 aa      aa      oa   o
$$$$
SMP1     USET,KFF,,,/GO,KAA,KOO,LOO,,,,, $  
****CARD   1-  4,  6,  8- 11
****FILE 106
$$$$
LABEL    LBL5 $ 
****CARD   1-  4,  6,  8- 11
****FILE 106
$$$$ SSG1 generates static load vectors {P }.
$$$$                                      g
$$$$
SSG1     SLT,BGPDT,CSTM,SIL,EST,MPT,GPTT,EDT,MGG,CASECC,DIT,PCOMPS/ 
         PG,,,,/LUSET/NSKIP/COMPS $ 
****CARD   1-  3,  5,  6,  8, 59- 62
****FILE 110
$$$$ Equivalence {P } to {P } if no constraints are applied.
$$$$               g       l
$$$$
EQUIV    PG,PL/NOSET $  
****CARD   1-  3,  5,  6,  8- 11, 59- 62
****FILE 111
$$$$ Go to label LBL9 if no constraints are applied.
COND     LBL9,NOSET $   
****CARD   1-  3,  5,  6,  8- 11, 17, 59- 62
****FILE 111,112
$$$$ SSG2 applies constraints to static load vectors
$$$$ 
$$$$           _
$$$$           P
$$$$            n            _       T
$$$$   {P } = {--} , {P } = {P } + [G ]{P }
$$$$     g     P       n      n      m   m
$$$$            m
$$$$ 
$$$$           _
$$$$           P
$$$$            f            _      
$$$$   {P } = {--} , {P } = {P } - [K  ]{Y }
$$$$     n     P       f      f      fs   s
$$$$            s
$$$$ 
$$$$           _
$$$$           P
$$$$            a            _       T
$$$$   {P } = {--} , {P } = {P } + [G ]{P }
$$$$     f     P       l      a      o   o
$$$$            o
$$$$
$$$$           P  
$$$$            l 
$$$$   {P } = {--}
$$$$     a     P  
$$$$            r 
$$$$ 
$$$$                                                                T
$$$$ and calculates determinate forces of reaction {q } = -{P } - [D ]{P }.
$$$$                                                 r       r          l
$$$$
SSG2     USET,GM,YS,KFS,GO,,PG/,PO,PS,PL $  
****CARD   1-  3,  5,  6,  8- 11, 59- 62
****FILE 111
$$$$ Go to label LBL9 if no omitted coordinates exist.
COND     LBL9,OMIT $
****CARD   1-  3,  5,  6,  8- 11, 59- 62
****FILE 112
$$$$ SSG3 solves for displacements of omitted coordinates (these are not 
$$$$ transformed)
$$$$ 
$$$$     o         -1  
$$$$   {u } = [K  ]  {P }
$$$$     o      oo     o
$$$$ 
$$$$ and calculates residual vector (RUOV) and residual vector error ratio for
$$$$ omitted coordinates
$$$$ 
$$$$                             o
$$$$   {deltaP } = {P } - [K  ]{u }
$$$$          o      o      oo   o
$$$$ 
$$$$                T        
$$$$              {u }{deltaP }
$$$$                o        o
$$$$   epsilon  = -------------
$$$$          o       T   o
$$$$                {P }{u }
$$$$                  o   o
$$$$
SSG3     LOO,KOO,PO,,,/UOOV,,RUOV,/-1/V,Y,IRES=-1 $ 
****CARD   1-  6,  8- 11, 17, 59- 62
****FILE 112
****RFMT 188
$$$$ Go to label LBL9 if residual vectors are not to be printed.
COND     LBL9,IRES $
****CARD   1-  6,  8- 11, 17, 59- 62
****RFMT 187-199,201-204,207-209
$$$$ MATGPR prints the residual vector for omitted coordinates (RUOV).
MATGPR   GPL,USET,SIL,RUOV//*O* $   
****CARD   1-  6,  8- 11, 17, 59- 62
****RFMT 187-199,201-204,207-209
$$$$
LABEL    LBL9 $ 
****CARD   1-  6,  8- 11, 17, 59- 62
****FILE 111,112
****RFMT 187-199,201-204,207-209
$$$$ Equivalence {P } to {P } if symmetric components of loads have been 
$$$$               l       x
$$$$ input.
EQUIV    PL,PX/CYCIO $  
****CARD   1-  6,  8- 11, 59- 62
****FILE 108
$$$$ Go to label LBL10 if symmetric components of loads have been input.
COND     LBL10,CYCIO $  
****CARD   1-  6,  8- 12, 22, 23, 59- 62
****FILE 108
$$$$ CYCT1 transforms loads on analysis points to symmetric components by the 
$$$$ equation
$$$$
$$$$   {P } = [G]{P }
$$$$     x         l
$$$$
CYCT1    PL/PX,GCYCF/V,Y,CTYPE/*FORE*/V,Y,NSEGS=-1/S,Y,KMAX=-1/V,Y, 
         NLOAD=1/S,N,NOGO $ 
****CARD   1-  6,  8- 12, 22, 23, 59- 62
****FILE 108
$$$$
LABEL    LBL10 $
****CARD   1-  6,  8- 12, 22, 23, 59- 62
****FILE 108
$$$$ Go to label ERROR5 and print Error Message No. 5 if a CYCT1 error was 
$$$$ found.
COND     ERROR5,NOGO $  
****CARD   1-  6,  8- 12, 59- 62
****FILE 108
$$$$
PARAM    //*ADD*/KINDEX/0/0 $   
****CARD   1-  6,  8- 12, 27, 28, 59- 62
****FILE 109
$$$$ Beginning of loop for cyclic index (KINDEX) values.
LABEL    LBL11 $
****CARD   1-  6,  8- 12, 27, 28, 59- 62
$$$$ CYCT2 transforms matrices and loads from symmetric components to solution 
$$$$ set by the equations
$$$$
$$$$             T               T
$$$$   [K  ] = [G ][K  ][G ] + [G ][K  ][G ]
$$$$     kk      1   aa   s      2   aa   2
$$$$
$$$$ where G  = G  (cosine) and G  = G  (sine) for rotational symmetry,
$$$$        1    c               2    s
$$$$
$$$$ and G  = G  (Symmetric) and G  = G  (Antisymmetric) for dihedral 
$$$$      1    S                  2    A
$$$$ 
$$$$ symmetry,
$$$$
$$$$            T          T
$$$$   {P } = [G ]{P } + [G ]{P } for rotational symmetry,
$$$$     k      c   c      s   s
$$$$
$$$$     1      T           T
$$$$   {P } = [G ]{P  } + [G ]{P  }, and
$$$$     k      S   cS      A   sA
$$$$
$$$$     2      T           T
$$$$   {P } = [G ]{P  } + [G ]{P  } for dihedral symmetry.
$$$$     k      A   cA      S   sS
$$$$
CYCT2    CYCD,KAA,,PX,,/KKK,,PK,,/*FORE*/V,Y,NSEGS/KINDEX/V,Y,  
         CYCSEQ=-1/V,Y,NLOAD/S,N,NOGO $ 
****CARD   1-  6,  8- 12, 23, 25, 27, 28, 59- 62
****FILE 109
$$$$ Go to label ERROR5 and print Error Message No. 5 if a CYCT2 error was 
$$$$ found.
COND     ERROR5,NOGO $  
****CARD   1-  6,  8- 12, 27, 28, 59- 62
****FILE 109
$$$$ RBMG2 decomposes constrained stiffness matrix for solution set
$$$$ 
$$$$   [K  ] = [L  ][U  ]
$$$$     kk      kk   kk
$$$$
RBMG2    KKK/LKK $  
****CARD   1-  6,  8- 12, 27, 28, 59- 62
****FILE 117
$$$$ SSG3 solves for displacements of solution set coordinates
$$$$ 
$$$$               -1  
$$$$   {u } = [K  ]  {P }
$$$$     k      kk     k
$$$$ 
$$$$ and calculates residual vector (RUKV) and residual vector error ratio for 
$$$$ solution set coordinates
$$$$                
$$$$   {deltaP } = {P } - [K  ]{u }
$$$$          k      k      kk   k
$$$$ 
$$$$                T        
$$$$              {u }{deltaP }
$$$$                k        k
$$$$   epsilon  = -------------
$$$$          k       T 
$$$$                {P }{u }
$$$$                  k   k
$$$$ 
SSG3     LKK,KKK,PK,,,/UKV,,RUKV,/-1/V,Y,IRES $ 
****CARD   1-  6,  8- 12, 17, 27, 28, 59- 62
****FILE 118
$$$$ CYCT2 finds symmetric components of displacement from solution set data 
$$$$ and appends to output for each KINDEX.
CYCT2    CYCD,,,UKV,RUKV,/,,UXV,RUXV,/*BACK*/V,Y,NSEGS/KINDEX/  
         V,Y,CYCSEQ/V,Y,NLOAD/S,N,NOGO $
****CARD   1-  6,  8- 12, 23, 25, 27, 28, 59- 62
****FILE 119
$$$$ Go to label ERROR5 and print Error Message No. 5 if a CYCT2 error was 
$$$$ found.
COND     ERROR5,NOGO $  
****CARD   1-  6,  8- 12, 27, 28, 59- 62
****FILE 119
$$$$ Go to label LBL14 if residual vectors are not to be printed.
COND     LBL14,IRES $   
****CARD   1-  6,  8- 12, 17, 27, 28, 59- 62
$$$$ MATGPR prints the residual vector for solution set coordinates (RUXV).
MATGPR   GPL,USET,SIL,RUXV//*A* $   
****CARD   1-  6,  8- 12, 17, 27, 28, 59- 62
$$$$
LABEL    LBL14 $
****CARD   1-  6,  8- 12, 17, 27, 28, 59- 62
$$$$
PARAM    //*ADD*/KINDEX/KINDEX/1 $  
****CARD   1-  6,  8- 12, 27, 28, 59- 62
$$$$
PARAM    //*SUB*/DONE/V,Y,KMAX/KINDEX $ 
****CARD   1-  6,  8- 12, 23, 27, 28, 59- 62
$$$$ Go to label LBL15 if all cyclic index (KINDEX) values are complete.
COND     LBL15,DONE $   
****CARD   1-  6,  8- 12, 27, 28, 59- 62
$$$$ Go to label LBL11 if additional cyclic index values are needed.
REPT     LBL11,360 $
****CARD   1-  6,  8- 12, 27, 28, 59- 62
$$$$ Go to label ERROR1 and print Error Message No. 1 if number of loops 
$$$$ exceeds 360.
JUMP     ERROR1 $   
****CARD   1-  6,  8- 12, 27, 28, 59- 62
$$$$
LABEL    LBL15 $
****CARD   1-  6,  8- 12, 27, 28, 59- 62
$$$$ Equivalence {u } to {u } if output of symmetric components was requested.
$$$$               x       l
$$$$
EQUIV    UXV,ULV/CYCIO $
****CARD   1-  6,  8- 12, 59- 62
****FILE 120
$$$$ Go to label LBL16 if output of symmetric components was requested.
COND     LBL16,CYCIO $  
****CARD   1-  6,  8- 12, 22, 23, 59- 62
****FILE 120
$$$$ CYCT1 transforms displacements from symmetric components to physical 
$$$$ components.
CYCT1    UXV/ULV,GCYCB/V,Y,CTYPE/*BACK*/V,Y,NSEGS/V,Y,KMAX/V,Y,NLOAD/   
         S,N,NOGO $ 
****CARD   1-  6,  8- 12, 22, 23, 59- 62
****FILE 120
$$$$ Go to label ERROR5 and print Error Message No. 5 if a CYCT1 error was 
$$$$ found.
COND     ERROR5,NOGO $  
****CARD   1-  6,  8- 12, 22, 23, 59- 62
****FILE 120
$$$$
LABEL    LBL16 $
****CARD   1-  6,  8- 12, 22, 23, 59- 62
****FILE 120
$$$$ SDR1 recovers dependent displacements
$$$$ 
$$$$                       o
$$$$   {u } = [G ]{u ] + {u }  ,
$$$$     o      o   a      o
$$$$ 
$$$$    u                u            
$$$$     a                f           
$$$$   {--} = {u }  ,   {--} = {u }  ,
$$$$    u       f        Y       n    
$$$$     o                s           
$$$$                                  
$$$$                        u         
$$$$                         n        
$$$$  {u } = [G ]{u ]  ,   {--} = {u }
$$$$    m      m   n        u       g 
$$$$                         m        
$$$$ 
$$$$ and recovers single-point forces of constraint
$$$$ 
$$$$                    T
$$$$   {q } = -{P } + [K  ]{u } +[K  ]{Y }
$$$$     s       s      fs   f     ss   s
$$$$
SDR1     USET,PG,ULV,UOOV,YS,GO,GM,PS,KFS,KSS,/UGV,PGG,QG/NSKIP/
         *STATICS* $
****CARD   1-  6,  8- 12, 59- 62
****FILE 113
$$$$ Go to label NOMPCF if no multipoint constraint force balance is 
$$$$ requested.
COND     NOMPCF,GRDEQ $ 
****CARD   7
****FILE 123
$$$$ EQMCK calculates the force and moment equilibrium check and prepares the 
$$$$ multipoint constraint force balance (OQM1) for output.
EQMCK    CASECC,EQEXIN,GPL,BGPDT,SIL,USET,KGG,GM,UGV,PGG,QG,CSTM/   
         OQM1/V,Y,OPT=0/V,Y,GRDEQ/NSKIP $   
****CARD   7
****FILE 123
$$$$ OFP formats the table prepared by EQMCK and places it on the system 
$$$$ output file for printing.
OFP      OQM1,,,,,//S,N,CARDNO $
****CARD   7
****FILE 123
$$$$
LABEL    NOMPCF $   
****CARD   7
****FILE 123
$$$$ SDR2 calculates element forces (OEF1) and stresses (OES1)
$$$$ and prepares load vectors (OPG1), displacement vectors (OUGV1), 
$$$$ and single-point forces of constraint (OQG1) for output and translation 
$$$$ components of the displacement vector (PUGV1).
SDR2     CASECC,CSTM,MPT,DIT,EQEXIN,SIL,GPTT,EDT,BGPDP,,QG,UGV,EST,,PGG,
         PCOMPS/OPG1,OQG1,OUGV1,OES1,OEF1,PUGV1,OES1L,OEF1L/ 
         *STATICS*////COMPS $   
****CARD  18, 19
****FILE 114
$$$$ OFP formats the tables prepared by SDR2 and places them on the system 
$$$$ output file for printing.
OFP      OUGV1,OPG1,OQG1,OEF1,OES1,//S,N,CARDNO $   
****CARD  19
****FILE 114
$$$$
OFP      OEF1L,OES1L,,,,//S,N,CARDNO $   
****CARD  19
****FILE 114
$$$$ SCAN examines the element stresses and forces calculated by SDR2 and 
$$$$ generates scanned output that meets the specifications set by the user.
SCAN     CASECC,OES1,OEF1/OESF1/*RF* $  
****CARD 19 
****FILE 114
$$$$ OFP formats the scanned output table prepared by SCAN and places it on 
$$$$ the system output file for printing.
OFP      OESF1,,,,,//S,N,CARDNO $   
****CARD  19
****FILE 114
$$$$ Go to label P2 if no deformed structure plots are requested.
COND     P2,JUMPPLOT $  
****SBST   7
****CARD  18
****FILE 124
$$$$ PLOT generates all requested deformed structure and contour plots.
PLOT     PLTPAR,GPSETS,ELSETS,CASECC,BGPDT,EQEXIN,SIP,PUGV1,,GPECT,OES1,
         OES1L,/PLOTX2/NSIL/LUSEP/JUMPPLOT/PLTFLG/S,N,PFILE $   
****SBST   7
****CARD  18
****FILE 124
$$$$ PRTMSG prints plotter data, engineering data, and contour data for each 
$$$$ deformed plot generated.
PRTMSG   PLOTX2// $ 
****SBST   7
****CARD  18
****FILE 124
$$$$
LABEL    P2 $   
****SBST   7
****CARD  18
****FILE 124
$$$$ Go to label FINIS and make normal exit.
JUMP     FINIS $
****CARD   1- 11, 13- 28, 59- 62
****RFMT 187-199,201-204,207-209
$$$$
LABEL    ERROR1 $   
****SBST   1,  3
****CARD   1-  6,  8- 12, 27, 28, 59- 62
****RFMT 187-199,201-204,207-209
$$$$ Print Error Message No. 1 and terminate execution.
PRTPARM  //-1/*CYCSTATICS* $
****SBST   1,  3
****CARD   1-  6,  8- 12, 27, 28, 59- 62
****RFMT 187-199,201-204,207-209
$$$$
LABEL    ERROR2 $   
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 116
****RFMT 187-199,201-204,207-209
$$$$ Print Error Message No. 2 and terminate execution.
PRTPARM  //-2/*CYCSTATICS* $
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 116
****RFMT 187-199,201-204,207-209
$$$$
LABEL    ERROR3 $   
****CARD   1,  9- 11, 59
****FILE 101
****RFMT 187-199,201-204,207-209
$$$$ Print Error Message No. 3 and terminate execution.
PRTPARM  //-3/*CYCSTATICS* $
****CARD   1,  9- 11, 59
****FILE 101
****RFMT 187-199,201-204,207-209
$$$$
LABEL    ERROR4 $   
****CARD   1,  2,  4-  6, 16
****FILE  97
****RFMT 187-199,201-204,207-209
$$$$ Print Error Message No. 4 and terminate execution.
PRTPARM  //-4/*CYCSTATICS* $
****CARD   1,  2,  4-  6, 16
****FILE  97
****RFMT 187-199,201-204,207-209
$$$$
LABEL    ERROR5 $   
****CARD   1-  6,  8- 12, 22, 23, 27, 28, 59- 62
****FILE 108,109,117,119
****RFMT 187-199,201-204,207-209
$$$$ Print Error Message No. 5 and terminate execution.
PRTPARM  //-5/*CYCSTATICS* $
****CARD   1-  6,  8- 12, 22, 23, 27, 28, 59- 62
****FILE 108,109,117,119
****RFMT 187-199,201-204,207-209
$$$$
LABEL    ERROR6 $   
****CARD   1,  9- 11, 59
****FILE 101
****RFMT 187-199,201-204,207-209
$$$$ Print Error Message No. 6 and terminate execution.
PRTPARM  //-6/*CYCSTATICS* $
****CARD   1,  9- 11, 59
****FILE 101
****RFMT 187-199,201-204,207-209
$$$$
LABEL    FINIS $
****CARD   1- 11, 13- 28, 59- 62
****RFMT 187-199,201-204,207-209
$$$$
PURGE    DUMMY/MINUS1 $ 
****CARD   1- 11, 13- 28, 59- 62
****RFMT 187-199,201-204,207-209
$$$$
END      $  
****CARD   1- 11, 13- 28, 59- 62
****RFMT 187-199,201-204,207-209
$$$$
$*CARD BITS 
  1    AXIC     AXIF     CELAS1   CELAS2   CELAS3   CELAS4   CMASS1 
  1    CMASS2   
  1    CMASS3   CMASS4   CORD1C   CORD1R   CORD1S   CORD2C   CORD2R 
  1    CORD2S   
  1    GRDSET   GRID     GRIDB    POINTAX  RINGAX   RINGFL   SECTAX 
  1    SEGGP    SPOINT  
  2    ADUM1    ADUM2    ADUM3    ADUM4    ADUM5    ADUM6    ADUM7  
  2    ADUM8
  2    ADUM9    BAROR    CBAR     CCONEAX  CDUM1    CDUM2    CDUM3  
  2    CDUM4
  2    CDUM5    CDUM6    CDUM7    CDUM8    CDUM9    CELBOW   CHBDY  
  2    CHEXA1   
  2    CHEXA2   CIHEX1   CIHEX2   CIHEX3   CIS2D8   CONROD   CQDMEM 
  2    CQDMEM1  CQDMEM2  CQDPLT   CQUAD1   CQUAD2   CROD     CSHEAR 
  2    CTETRA   CQUAD4   CTRIA3
  2    CTORDRG  CTRAPAX  CTRAPRG  CTRBSC   CTRIA1   CTRIA2   CTRIAAX
  2    CTRIARG  
  2    CTRIM6   CTRMEM   CTRPLT   CTRPLT1  CTRSHL   CTUBE    CTWIST 
  2    CWEDGE   
  3    PBAR     PCONEAX  PDUM1    PDUM2    PDUM3    PDUM4    PDUM5  
  3    PDUM6
  3    PDUM7    PDUM8    PDUM9    PELBOW   PHBDY    PIHEX    PIS2D8 
  3    PQDMEM   PQDMEM1  PQDMEM2  PQDPLT   PQUAD1   PQUAD2   PROD   
  3    PSHEAR   PSHELL   PCOMP    PCOMP1   PCOMP2
  3    PTORDRG  PTRAPAX  PTRBSC   PTRIA1   PTRIA2   PTRIAAX  PTRIM6 
  3    PTRMEM   PTRPLT   PTRPLT1  PTRSHL   PTUBE    PTWIST  
  4    GENEL
  5    CONM1    CONM2    PMASS  
  6    PELAS
  7    AOUT$
  8    MAT1     MAT2     MAT3     MAT4     MAT5     MATT1    MATT2  
  8    MATT3    MATT4    MATT5    MAT8     TABLEM1  TABLEM2  TABLEM3
  8    TABLEM4  TEMPMT$  TEMPMX$  
  9    AXISYM$  CRIGD1   CRIGD2   CRIGD3   CRIGDR   CRROD    CRBAR  
  9    CRTRPLT  CRBE1    CRBE2    CRBE3    CRSPLINE MPC      MPCADD 
  9    MPCAX    MPC$
 10    SPC      SPC1     SPCADD   SPCAX    SPC$ 
 11    ASET     ASET1    OMIT     OMIT1    OMITAX   SUPAX    SUPORT 
 12    CYJOIN   
 13    TEMP     TEMPAX   TEMPD    TEMPP1   TEMPP2   TEMPP3   TEMPRB 
 14    WTMASS   
 15    GRDPNT   
 16    PLOTEL   
 17    IRES 
 18    PLOT$
 19    POUT$
 20    ASETOUT  
 21    AUTOSPC  
 22    CTYPE
 23    NSEGS    KMAX     NLOAD  
 24    COUPMASS CPBAR    CPQDPLT  CPQUAD1  CPQUAD2  CPROD    CPTRBSC
 24    CPTRIA1  CPTRIA2  CPTRPLT  CPTUBE
 25    CYCSEQ   
 26    OPT      GRDEQ   
 27    LOOP$
 28    LOOP1$   
 59    DEFORM   DEFORM$  LOAD$    RFORCE$  SPCD 
 60    FORCE    FORCE1   FORCE2   FORCEAX  LOAD     MOMAX    MOMENT 
 60    MOMENT1  PLOAD4
 60    MOMENT2  PLOAD    PLOAD1   PLOAD2   PLOAD3   PRESAX   QBDY1  
 60    QBDY2    QHBDY    QVECT    QVOL     SLOAD
 61    GRAV     RFORCE  
 62    TEMPLD$  
$$$$
$*FILE BITS 
 94    BGPDT    CSTM     EQEXIN   GPDT     GPL      SIL 
 95    ECT  
 96    GPTT     SLT 
 97    GPECT    EST      GEI      MPTX     PCOMPS   EPTX
 98    KGGX 
 99    MGG  
100    KGG  
101    ASET     RG       USET     YS       OGPST
102    GPST 
103    GM   
104    KNN  
105    KFF      KFS      KSS
106    GO       KAA      KOO      LOO   
107    CYCD 
108    GCYCF    PX  
109    KKK      PK  
110    PG   
111    PL       PO       PS       QR
112    RUOV     UOOV
113    PGG      QG       UGV
114    OEF1     OES1     OPG1     OQG1     OUGV1    PUGV1   
114    OEF1L    OES1L    OESF1
115    ELSETS   GPSETS   PLTPAR   PLTSETX   
116    KDICT    KELM     MDICT    MELM  
117    LKK  
118    RUKV     UKV 
119    RUXV     UXV 
120    ULV      GCYCB   
121    PLOTX1   
122    OGPWG
123    OQM1 
124    PLOTX2   
125    BGPDP    SIP 
$*  
=PAGE=
DISP15

APR.93
$$$$$$$$
BEGIN    DISP 15 NORMAL MODES ANALYSIS WITH CYCLIC SYMMETRY - APR 1993 $
****CARD   1- 15, 18- 24, 61, 62
****RFMT 187-200,202-204,207-209
$$$$
PRECHK   ALL $  
****CARD   1- 15, 18- 24, 61, 62
****RFMT 187-200,202-204,207-209
$$$$
PARAM    //*MPY*/CARDNO/0/0 $   
****CARD   1- 12, 14, 15, 18, 19, 21, 24, 61, 62
****FILE 101,109,114,120,121
$$$$ GP1 generates coordinate system transformation matrices, tables of grid 
$$$$ point locations, and tables relating the internal and external grid point 
$$$$ numbers.
GP1      GEOM1,GEOM2,/GPL,EQEXIN,GPDT,CSTM,BGPDT,SIL/S,N,LUSET/ 
         NOGPDT/MINUS1=-1 $ 
****CARD   1
****FILE  94
$$$$ PLTTRAN modifies special scalar grid points in the BGPDT and SIL tables.
PLTTRAN  BGPDT,SIL/BGPDP,SIP/LUSET/S,N,LUSEP $  
****CARD   1
****FILE 115
$$$$ GP2 generates Element Connection Table with internal indices.
GP2      GEOM2,EQEXIN/ECT $ 
****CARD   1,  2,  4,  5, 16
****FILE  95
$$$$
PARAML   PCDB//*PRES*////JUMPPLOT $ 
****SBST   7
****CARD  18
****FILE 116,119
$$$$
PURGE    PLTSETX,PLTPAR,GPSETS,ELSETS/JUMPPLOT $
****SBST   7
****CARD  18
****FILE 116
$$$$ Go to label P1 if there are no structure plot requests.
COND     P1,JUMPPLOT $  
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 116,119
$$$$ PLTSET transforms user input into a form used to drive the structure 
$$$$ plotter.
PLTSET   PCDB,EQEXIN,ECT,EPT/PLTSETX,PLTPAR,GPSETS,ELSETS/S,N,NSIL/ 
         S,N,JUMPPLOT $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 116
$$$$ PRTMSG prints error messages associated with the structure plotter.
PRTMSG   PLTSETX// $
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 116
$$$$
PARAM    //*MPY*/PLTFLG/1/1 $   
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 119
$$$$
PARAM    //*MPY*/PFILE/0/0 $
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 119
$$$$ Go to label P1 if no undeformed structure plots are requested.
COND     P1,JUMPPLOT $  
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 119
$$$$ PLOT generates all requested undeformed structure plots.
PLOT     PLTPAR,GPSETS,ELSETS,CASECC,BGPDT,EQEXIN,SIL,,ECT,,,,/PLOTX1/  
         NSIL/LUSET/S,N,JUMPPLOT/S,N,PLTFLG/S,N,PFILE  $
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 119
$$$$ PRTMSG prints plotter data and engineering data for each undeformed plot 
$$$$ generated.
PRTMSG   PLOTX1//$  
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 119
$$$$
LABEL    P1 $   
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 116,119
$$$$ GP3 generates Static Loads Table and Grid Point Temperature Table.
GP3      GEOM3,EQEXIN,GEOM2/,GPTT/NOGRAV $  
****CARD   1,  2, 13
****FILE  96
$$$$ TA1 generates element tables for use in matrix assembly and stress 
$$$$ recovery.
TA1      ECT,EPT,BGPDT,SIL,GPTT,CSTM,MPT,EQEXIN/EST,GEI,GPECT,,,MPTX,
         PCOMPS,EPTX/LUSET/S,N,NOSIMP/1/S,N,NOGENL/GENEL/S,N,COMPS $   
****CARD   1-  6, 13, 16
****FILE  97
$$$$
EQUIV    MPTX,MPT/COMPS/EPTX,EPT/COMPS $
****CARD   1- 15, 18- 24, 61, 62
****FILE  97
$$$$ Go to label ERROR6 and print Error Message No. 6 if no structural 
$$$$ elements have been defined.
COND     ERROR6,NOSIMP $
****CARD   1,  2,  4-  6,  8, 16, 24
****FILE  97
$$$$
PARAM    //*ADD*/NOKGGX/1/0 $   
****CARD   1-  3,  6,  8
****FILE 118
$$$$
PARAM    //*ADD*/NOMGG/1/0 $
****CARD   1-  3,  5,  8
****FILE 118
****RFMT 187,190-192
$$$$ EMG generates structural element stiffness and mass matrix tables and 
$$$$ dictionaries for later assembly by the EMA module. 
EMG      EST,CSTM,MPT,DIT,GEOM2,/KELM,KDICT,MELM,MDICT,,,/S,N,NOKGGX/   
         S,N,NOMGG////C,Y,COUPMASS/C,Y,CPBAR/C,Y,CPROD/ 
         C,Y,CPQUAD1/C,Y,CPQUAD2/C,Y,CPTRIA1/C,Y,CPTRIA2/C,Y,CPTUBE/
         C,Y,CPQDPLT/C,Y,CPTRPLT/C,Y,CPTRBSC/   
         C,Y,VOLUME/C,Y,SURFACE $   
****CARD   1-  3,  5,  6,  8, 24
****FILE 118
****RFMT 187,190-192
$$$$
PURGE    KGGX/NOKGGX $  
****CARD   1-  3,  6,  8
****FILE  98
$$$$ Go to label JMPKGG if no stiffness matrix is to be assembled.
COND     JMPKGG,NOKGGX $
****CARD   1-  3,  6,  8
****FILE  98
$$$$                                  x
$$$$ EMA assembles stiffness matrix [K  ] and Grid Point Singularity Table.
$$$$                                  gg
$$$$
EMA      GPECT,KDICT,KELM/KGGX $
****CARD   1-  3,  6,  8
****FILE  98
$$$$
PURGE    KDICT,KELM/MINUS1 $
****CARD   1-  3,  6,  8
****FILE 118
$$$$
LABEL    JMPKGG $   
****CARD   1-  3,  6,  8
****FILE  98
$$$$ Go to label ERROR1 and print Error Message No. 1 if no mass matrix is to 
$$$$ be assembled.
COND     ERROR1,NOMGG $ 
****CARD   1-  3,  5,  8, 24
****FILE 118
****RFMT 187-200,202-204,207-209
$$$$ EMA assembles mass matrix [M  ].
$$$$                             gg
$$$$
EMA      GPECT,MDICT,MELM/MGG/-1/C,Y,WTMASS=1.0 $   
****CARD   1-  3,  5,  8, 24
****FILE  99
****RFMT 187,190-192
$$$$
PURGE    MDICT,MELM/MINUS1 $
****CARD   1-  3,  5,  8, 24
****FILE 118
$$$$ Go to label LGPWG if no weight and balance information is requested.
COND     LGPWG,GRDPNT $ 
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 120
$$$$ GPWG generates weight and balance information.
GPWG     BGPDP,CSTM,EQEXIN,MGG/OGPWG/V,Y,GRDPNT=-1/C,Y,WTMASS $ 
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 120
$$$$ OFP formats the weight and balance information prepared by GPWG and 
$$$$ places it on the system output file for printing.
OFP      OGPWG,,,,,//S,N,CARDNO $   
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 120
$$$$
LABEL    LGPWG $
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 120
$$$$               x
$$$$ Equivalence [K  ] to [K  ] if no general elements exist.
$$$$               gg       gg
$$$$
EQUIV    KGGX,KGG/NOGENL $  
****CARD   1-  4,  6,  8
****FILE 100
$$$$ Go to label LBL11 if no general elements exist.
COND     LBL11,NOGENL $ 
****CARD   1-  4,  6,  8
****FILE 100
$$$$                                 x
$$$$ SMA3 adds general elements to [K  ] to obtain stiffness matrix [K  ].
$$$$                                 gg                               gg
$$$$
SMA3     GEI,KGGX/KGG/LUSET/NOGENL/NOSIMP $ 
****CARD   1-  4,  6,  8
****FILE 100
$$$$
LABEL    LBL11 $
****CARD   1-  4,  6,  8
****FILE 100
$$$$
GPSTGEN  KGG,SIL/GPST $ 
****CARD   1-  4,  6,  8
****FILE 102
$$$$
PARAM    //*MPY*/NSKIP/0/0 $
****CARD   1,  9- 11
****FILE 101
$$$$ GP4 generates flags defining members of various displacement sets (USET), 
$$$$ forms multipoint constraint equations [R ] {u } = 0, and forms enforced
$$$$                                         g    g
$$$$ displacement vector {Y }.
$$$$                       s
$$$$
GP4      CASECC,GEOM4,EQEXIN,GPDT,BGPDT,CSTM,GPST/RG,,USET, 
         ASET,OGPST/LUSET/S,N,MPCF1/S,N,MPCF2/S,N,SINGLE/S,N,OMIT/  
         S,N,REACT/S,N,NSKIP/S,N,REPEAT/S,N,NOSET/S,N,NOL/S,N,NOA/  
         C,Y,ASETOUT/C,Y,AUTOSPC $  
****CARD   1-  4,  6,  8- 11, 20, 21
****FILE 101
$$$$ OFP formats the table of potential grid point singularities prepared by 
$$$$ GPSP and places it on the system output file for printing.
OFP      OGPST,,,,,//S,N,CARDNO $   
****CARD   1-  4,  6,  8- 10, 21
****FILE 101
$$$$ Go to label ERROR3 and print Error Message No. 3 if no independent 
$$$$ degrees of freedom are defined.
COND     ERROR3,NOL $   
****CARD   1,  9- 11, 20, 21
****FILE 101
$$$$
PARAM    //*NOT*/REACDATA/REACT $   
****CARD   1, 11
****FILE 101
$$$$ Go to label ERROR4 and print Error Message No. 4 if free-body supports 
$$$$ are present.
COND     ERROR4,REACDATA $  
****CARD   1, 11
****FILE 101
$$$$
PURGE    GM/MPCF1/GO/OMIT/KFS,QG/SINGLE $   
****CARD   1,  9- 11
****FILE 103,105,106,113
$$$$ GPCYC prepares segment boundary table (CYCD).
GPCYC    GEOM4,EQEXIN,USET/CYCD/V,Y,CTYPE/S,N,NOGO $
****CARD   1,  9- 12, 22
****FILE 107
$$$$ Go to label ERROR5 and print Error Message No. 5 if CYJOIN data is 
$$$$ inconsistent.
COND     ERROR5,NOGO $  
****CARD   1,  9- 12, 22
****FILE 107
$$$$ Equivalence [K  ] to [K  ] and [M  ] to [M  ]
$$$$               gg       nn        gg       nn
$$$$ if no multipoint constraints exist.
EQUIV    KGG,KNN/MPCF1/MGG,MNN/MPCF1 $  
****CARD   1-  6,  8,  9, 14, 24
****FILE 104
$$$$ Go to label LBL2 if no multipoint constraints exist.
COND     LBL2,MPCF1 $   
****CARD   1-  6,  8,  9, 14, 24
****FILE 103,104
$$$$ MCE1 partitions multipoint constraint equation [R ] = [R |R ] and solves
$$$$                                                  g      m  n
$$$$                                                             -1
$$$$ for multipoint constraint transformation matrix [G ] = -[R ]  [R ].
$$$$                                                   m       m     n
$$$$
MCE1     USET,RG/GM $   
****CARD   1,  9
****FILE 103
$$$$ MCE2 partitions stiffness matrix
$$$$ 
$$$$           +_      +
$$$$           |K  |K  |
$$$$           | nn| nm|
$$$$   [K  ] = |---+---|
$$$$     gg    |K  |K  |
$$$$           | mn| mm|
$$$$           +       +
$$$$ 
$$$$ and performs matrix reduction
$$$$ 
$$$$            _        T           T           T
$$$$   [K  ] = [K  ] + [G ][K  ] + [K  ][G ] + [G ][K  ][G ]
$$$$     nn      nm      m   mn      mn   m      m   mm   m
$$$$
MCE2     USET,GM,KGG,MGG,,/KNN,MNN,, $  
****CARD   1-  6,  8,  9, 14, 24
****FILE 104
$$$$
LABEL    LBL2 $ 
****CARD   1-  6,  8,  9, 14, 24
****FILE 103,104
$$$$ Equivalence [K  ] to [K  ] and [M  ] to [M  ]
$$$$               nn       ff        nn       ff
$$$$ if no single-point constraints exist.
EQUIV    KNN,KFF/SINGLE/MNN,MFF/SINGLE $
****CARD   1-  6,  8- 10, 14, 24
****FILE 105
$$$$ Go to label LBL3 if no single-point constraints exist.
COND     LBL3,SINGLE $  
****CARD   1-  6,  8- 10, 14, 24
****FILE 105
$$$$ SCE1 partitions out single-point constraints
$$$$ 
$$$$           +       +                        +       +
$$$$           |K  |K  |                        |M  |M  |
$$$$           | ff| fs|                        | ff| fs|
$$$$   [K  ] = |---+---|                [M  ] = |---+---|
$$$$     nn    |K  |K  |                  nn    |M  |M  |
$$$$           | sf| ss|                        | sf| ss|
$$$$           +       +                        +       +
$$$$
SCE1     USET,KNN,MNN,,/KFF,KFS,,MFF,, $
****CARD   1-  6,  8- 10, 14, 24
****FILE 105
$$$$
LABEL    LBL3  $
****CARD   1-  6,  8- 10, 14, 24
****FILE 105
$$$$ Equivalence [K  ] to [K  ] if no omitted coordinates exist.
$$$$               ff       aa
$$$$
EQUIV    KFF,KAA/OMIT $ 
****CARD   1-  4,  6,  8- 11
****FILE 106
$$$$ Equivalence [M  ] to [M  ] if no omitted coordinates exist.
$$$$               ff       aa
$$$$
EQUIV    MFF,MAA/OMIT $ 
****CARD   1-  6,  8- 11, 14, 24
****FILE 117
$$$$ Go to label LBL5 if no omitted coordinates exist.
COND     LBL5,OMIT $
****CARD   1-  6,  8- 11, 14, 24
****FILE 106,117
$$$$ SMP1 partitions constrained stiffness matrix
$$$$
$$$$           +_      +
$$$$           |K  |K  |
$$$$           | aa| ao|
$$$$   [K  ] = |---+---|
$$$$     ff    |K  |K  |
$$$$           | oa| oo|
$$$$           +       +
$$$$           
$$$$                                               -1
$$$$ solves for transformation matrix [G ] = -[K  ]  [K  ]
$$$$                                    o       oo     oa
$$$$ 
$$$$                                        _        T
$$$$ and performs matrix reduction [K  ] = [K  ] + [K  ][G ].
$$$$                                 aa      aa      oa   o
$$$$
SMP1     USET,KFF,,,/GO,KAA,KOO,LOO,,,,, $  
****CARD   1-  4,  6,  8- 11
****FILE 106
$$$$ SMP2 partitions constrained mass matrix
$$$$
$$$$           +_      +
$$$$           |M  |M  |
$$$$           | aa| ao|
$$$$   [M  ] = |---+---|
$$$$     ff    |M  |M  |
$$$$           | oa| oo|
$$$$           +       +
$$$$                                                         
$$$$ and performs matrix reduction                           
$$$$                                                         
$$$$            _        T           T           T
$$$$   [M  ] = [M  ] + [M  ][G ] + [G ][M  ] + [G ][M  ][G ]
$$$$     aa      aa      oa   o      o   oa      o   oo   o
$$$$
SMP2     USET,GO,MFF/MAA $  
****CARD   1-  6,  8- 11, 14, 24
****FILE 117
$$$$
LABEL    LBL5 $ 
****CARD   1-  6,  8- 11, 14, 24
****FILE 106,117
$$$$ DPD extracts Eigenvalue Extraction Data from Dynamics data block.
DPD      DYNAMICS,GPL,SIL,USET/GPLD,SILD,USETD,,,,,,,EED,EQDYN/ 
         LUSET/LUSETD/NOTFL/NODLT/NOPSDL/NOFRL/ 
         NONLFT/NOTRL/S,N,NOEED//NOUE $ 
****CARD   1,  9- 11, 61
****FILE 111
$$$$ Go to label ERROR2 and print Error Message No. 2 if there is no 
$$$$ Eigenvalue Extraction Data.
COND     ERROR2,NOEED $ 
****CARD   1,  9- 11, 61
****FILE 111
****RFMT 187-200,202-204,207-209
$$$$ CYCT2 transforms matrices from symmetric components to solution 
$$$$ set by the equations
$$$$
$$$$             T               T
$$$$   [K  ] = [G ][K  ][G ] + [G ][K  ][G ]
$$$$     kk      1   aa   1      2   aa   2
$$$$
$$$$             T               T
$$$$   [M  ] = [G ][M  ][G ] + [G ][M  ][G ]
$$$$     kk      1   aa   1      2   aa   2
$$$$
$$$$ where G  = G  (cosine) and G  = G  (sine) for rotational symmetry,
$$$$        1    c               2    s
$$$$
$$$$ and G  = G  (Symmetric) and G  = G  (Antisymmetric) for dihedral 
$$$$      1    S                  2    A
$$$$ 
$$$$ symmetry.
CYCT2    CYCD,KAA,MAA,,,/KKK,MKK,,,/*FORE*/V,Y,NSEGS=-1/V,Y,KINDEX=-1/  
         V,Y,CYCSEQ=-1/1/S,N,NOGO $ 
****CARD   1-  6,  8- 12, 23, 61
****FILE 108
$$$$ Go to label ERROR5 and print Error Message No. 5 if a CYCT2 error was 
$$$$ found.
COND     ERROR5,NOGO $  
****CARD   1-  6,  8- 12, 23, 61
****FILE 108
$$$$ READ extracts real eigenvalues and eigenvectors from the equation
$$$$ 
$$$$   [K   - lambda M  ]{u } = 0
$$$$     kk           kk   k
$$$$ 
$$$$ calculates modal mass matrix
$$$$ 
$$$$             T
$$$$   [m] = [phi ][M  ][phi ]
$$$$             a   kk     k
$$$$ 
$$$$ and normalizes eigenvectors according to one of the following user 
$$$$ requests:
$$$$   1. Unit value of a selected component.
$$$$   2. Unit value of the largest component.
$$$$   3. Unit value of the generalized mass.
READ     KKK,MKK,,,EED,,CASECC/LAMK,PHIK,MI,OEIGS/*MODES*/S,N,NEIGV $   
****CARD   1-  6,  8- 12, 14, 24, 61, 62
****FILE 109
$$$$ OFP formats the summary of eigenvalue extraction information (OEIGS) 
$$$$ prepared by READ and places it on the system output file for printing.
OFP      OEIGS,,,,,//S,N,CARDNO $   
****CARD   1-  6,  8- 12, 14, 24, 61, 62
****FILE 109
$$$$ Go to label FINIS and make normal exit if no eigenvalues were found.
COND     FINIS,NEIGV $  
****CARD   1- 12, 14, 18, 19, 23, 24, 61, 62
****FILE 112-114,121,122
$$$$ OFP formats the eigenvalues (LAMK) prepared by READ and places them on 
$$$$ the system output file for printing.
OFP      LAMK,,,,,//S,N,CARDNO $
****CARD   1-  6,  8- 12, 14, 24, 61, 62
****FILE 109
$$$$ CYCT2 finds symmetric components of eigenvectors from solution set 
$$$$ eigenvectors.
CYCT2    CYCD,,,,PHIK,LAMK/,,,PHIA,LAMA/*BACK*/V,Y,NSEGS/V,Y,KINDEX/
         V,Y,CYCSEQ/1/S,N,NOGO $
****CARD   1-  6,  8- 12, 14, 23, 24, 61, 62
****FILE 112
$$$$ Go to label ERROR5 and print Error Message No. 5 if a CYCT2 error was 
$$$$ found.
COND     ERROR5,NOGO $  
****CARD   1-  6,  8- 12, 14, 23, 24, 61, 62
****FILE 112
$$$$ SDR1 recovers dependent components of eigenvectors
$$$$ 
$$$$                                   phi
$$$$                                      a
$$$$  {phi } = [G ]{phi }             {----} = {phi }
$$$$      o      o     a               phi         f
$$$$                                      o
$$$$
$$$$    phi       
$$$$       f
$$$$   {----} = {phi }                  {phi } = [G ]{phi }
$$$$    phi         n                       m      m     n
$$$$       s        
$$$$
$$$$    phi          
$$$$       n         
$$$$   {----} = {phi }
$$$$    phi         g 
$$$$       m         
$$$$                                                            T
$$$$ and recovers single-point forces of constraint {q } = [K  ] {phi }.
$$$$                                                  s      fs      f
$$$$
SDR1     USET,,PHIA,,,GO,GM,,KFS,,/PHIG,,QG/1/*REIG* $  
****CARD   1-  6,  8- 12, 14, 24, 61, 62
****FILE 113
****RFMT 187-200,202-204,207-209
$$$$ Go to label NOMPCF if no multipoint constraint force balance is 
$$$$ requested.
COND     NOMPCF,GRDEQ $ 
****CARD   7
****FILE 121
$$$$ EQMCK calculates the force and moment equilibrium check and prepares the 
$$$$ multipoint constraint force balance (OQM1) for output.
EQMCK    CASECC,EQEXIN,GPL,BGPDT,SIL,USET,KGG,GM,PHIG,LAMA,QG,CSTM/ 
         OQM1/V,Y,OPT=0/V,Y,GRDEQ/-1 $  
****CARD   7
****FILE 121
$$$$ OFP formats the table prepared by EQMCK and places it on the system 
$$$$ output file for printing.
OFP      OQM1,,,,,//S,N,CARDNO $
****CARD   7
****FILE 121
$$$$
LABEL    NOMPCF $   
****CARD   7
****FILE 121
$$$$ SDR2 calculates element forces (OEF1) and stresses (OES1) and prepares 
$$$$ eigenvectors (OPHIG) and single-point forces of constraint (OQG1) for 
$$$$ output and translation components of the eigenvectors (PPHIG). 
SDR2     CASECC,CSTM,MPT,DIT,EQEXIN,SIL,,,BGPDP,LAMA,QG,PHIG,EST,,, 
         PCOMPS/,OQG1,OPHIG,OES1,OEF1,PPHIG,OES1L,OEF1L/ 
         *REIG*////COMPS $  
****CARD  18, 19
****FILE 114
$$$$ OFP formats the tables prepared by SDR2 and places them on the system 
$$$$ output file for printing.
OFP      OPHIG,OQG1,OEF1,OES1,,//S,N,CARDNO $   
****CARD  19
****FILE 114
$$$$
OFP      OEF1L,OES1L,,,,//S,N,CARDNO $   
****CARD  19
****FILE 114
$$$$ SCAN examines the element stresses and forces calculated by SDR2 and 
$$$$ generates scanned output that meets the specifications set by the user.
SCAN     CASECC,OES1,OEF1/OESF1/*RF* $  
****CARD 19 
****FILE 114
$$$$ OFP formats the scanned output table prepared by SCAN and places it on 
$$$$ the system output file for printing.
OFP      OESF1,,,,,//S,N,CARDNO $   
****CARD  19
****FILE 114
$$$$ Go to label P2 if no deformed structure plots are requested.
COND     P2,JUMPPLOT $  
****SBST   7
****CARD  18
****FILE 122
$$$$ PLOT generates all requested deformed structure and contour plots.
PLOT     PLTPAR,GPSETS,ELSETS,CASECC,BGPDT,EQEXIN,SIP,,PPHIG,GPECT,OES1,
         OES1L,/PLOTX2/NSIL/LUSEP/JUMPPLOT/PLTFLG/S,N,PFILE $   
****SBST   7
****CARD  18
****FILE 122
$$$$ PRTMSG prints plotter data, engineering data, and contour data for each 
$$$$ deformed plot generated.
PRTMSG   PLOTX2// $ 
****SBST   7
****CARD  18
****FILE 122
$$$$
LABEL    P2 $   
****SBST   7
****CARD  18
****FILE 122
$$$$ Go to label FINIS and make normal exit.
JUMP     FINIS $
****CARD   1- 14, 18- 24, 61, 62
****RFMT 187-200,202-204,207-209
$$$$
LABEL    ERROR1 $   
****CARD   1-  3,  5,  8, 24
****FILE 118
****RFMT 187-200,202-204,207-209
$$$$ Print Error Message No. 1 and terminate execution.
PRTPARM  //-1/*CYCMODES* $  
****CARD   1-  3,  5,  8, 24
****FILE 118
****RFMT 187-200,202-204,207-209
$$$$
LABEL    ERROR2 $   
****CARD   1,  9- 11, 61
****FILE 111
****RFMT 187-200,202-204,207-209
$$$$ Print Error Message No. 2 and terminate execution.
PRTPARM  //-2/*CYCMODES* $  
****CARD   1,  9- 11, 61
****FILE 111
****RFMT 187-200,202-204,207-209
$$$$
LABEL    ERROR3 $   
****CARD   1,  9- 11, 20, 21
****FILE 101
****RFMT 187-200,202-204,207-209
$$$$ Print Error Message No. 3 and terminate execution.
PRTPARM  //-3/*CYCMODES* $  
****CARD   1,  9- 11, 20, 21
****FILE 101
****RFMT 187-200,202-204,207-209
$$$$
LABEL    ERROR4 $   
****CARD   1,  9- 11
****FILE 101
****RFMT 187-200,202-204,207-209
$$$$ Print Error Message No. 4 and terminate execution.
PRTPARM  //-4/*CYCMODES* $  
****CARD   1,  9- 11
****FILE 101
****RFMT 187-200,202-204,207-209
$$$$
LABEL    ERROR5 $   
****CARD   1-  6,  8- 12, 14, 22- 24, 61, 62
****FILE 107,108,112
****RFMT 187-200,202-204,207-209
$$$$ Print Error Message No. 5 and terminate execution.
PRTPARM  //-5/*CYCMODES* $  
****CARD   1-  6,  8- 12, 14, 22- 24, 61, 62
****FILE 107,108,112
****RFMT 187-200,202-204,207-209
$$$$
LABEL    ERROR6 $   
****CARD   1,  2,  4-  6,  8, 16, 24
****FILE  97
****RFMT 187-200,202-204,207-209
$$$$ Print Error Message No. 6 and terminate execution.
PRTPARM  //-6/*CYCMODES* $  
****CARD   1,  2,  4-  6,  8, 16, 24
****FILE  97
****RFMT 187-200,202-204,207-209
$$$$
LABEL    FINIS $
****CARD   1- 14, 16, 18- 24, 61, 62
****RFMT 187-200,202-204,207-209
$$$$
PURGE    DUMMY/MINUS1 $ 
****CARD   1- 14, 16, 18- 24, 61, 62
****RFMT 187-200,202-204,207-209
$$$$
END      $  
****CARD   1- 14, 16, 18- 24, 61, 62
****RFMT 187-200,202-204,207-209
$$$$
$*CARD BITS 
  1    AXIC     AXIF     AXSLOT   CELAS1   CELAS2   CELAS3   CELAS4 
  1    CMASS1   
  1    CMASS2   CMASS3   CMASS4   CORD1C   CORD1R   CORD1S   CORD2C 
  1    CORD2R   
  1    CORD2S   FREEPT   GRDSET   GRID     GRIDB    GRIDF    GRIDS  
  1    POINTAX  
  1    PRESPT   RINGAX   RINGFL   SECTAX   SEQGP    SLBDY    SPOINT 
  2    ADUM1    ADUM2    ADUM3    ADUM4    ADUM5    ADUM6    ADUM7  
  2    ADUM8
  2    ADUM9    BAROR    CAXIF2   CAXIF3   CAXIF4   CBAR     CCONEAX
  2    CDUM1
  2    CDUM2    CDUM3    CDUM4    CDUM5    CDUM6    CDUM7    CDUM8  
  2    CDUM9
  2    CELBOW   CFLUID2  CFLUID3  CFLUID4  CHEXA1   CHEXA2   CIHEX1 
  2    CIHEX2   CIHEX3   CIS2D8   CONROD   CQDMEM   CQDMEM1  CQDMEM2
  2    CQDPLT   CQUAD4   CTRIA3
  2    CQUAD1   CQUAD2   CROD     CSHEAR   CSLOT3   CSLOT4   CTETRA 
  2    CTORDRG  
  2    CTRAPAX  CTRAPRG  CTRBSC   CTRIA1   CTRIA2   CTRIAAX  CTRIARG
  2    CTRIM6   
  2    CTRMEM   CTRPLT   CTRPLT1  CTRSHL   CTUBE    CTWIST   CWEDGE 
  3    PBAR     PCONEAX  PDUM1    PDUM2    PDUM3    PDUM4    PDUM5  
  3    PDUM6
  3    PDUM7    PDUM8    PDUM9    PELBOW   PIHEX    PIS2D8   PQDMEM 
  3    PQDMEM1  PQDMEM2  PQDPLT   PQUAD1   PQUAD2   PROD     PSHEAR 
  3    PTORDRG  PSHELL   PCOMP    PCOMP1   PCOMP2
  3    PTRAPAX  PTRBSC   PTRIA1   PTRIA2   PTRIAAX  PTRIM6   PTRMEM 
  3    PTRPLT   PTRPLT1  PTRSHL   PTUBE    PTWIST   
  4    GENEL
  5    CONM1    CONM2    FSLIST   PMASS 
  6    PELAS
  7    AOUT$
  8    MAT1     MAT2     MAT3     MATT1    MATT2    MATT3    MAT8
  8    TABLEM1  TABLEM2  TABLEM3  TABLEM4  TEMPMT$  TEMPMX$  
  9    AXISYM$  CRIGD1   CRIGD2   CRIGD3   CRIGDR   CRROD    CRBAR  
  9    CRTRPLT  CRBE1    CRBE2    CRBE3    CRSPLINE MPC      MPCADD 
  9    MPCAX    MPC$
 10    SPC      SPC1     SPCADD   SPCAX    SPC$ 
 11    ASET     ASET1    OMIT     OMIT1    OMITAX   SUPAX    SUPORT 
 12    CYJOIN   
 13    TEMP     TEMPAX   TEMPD    TEMPP1   TEMPP2   TEMPP3   TEMPRB 
 14    WTMASS   
 15    GRDPNT   
 16    PLOTEL   
 18    PLOT$
 19    POUT$
 20    ASETOUT  
 21    AUTOSPC  
 22    CTYPE
 23    NSEGS    KINDEX   CYCSEQ 
 24    COUPMASS CPBAR    CPQDPLT  CPQUAD1  CPQUAD2  CPROD    CPTRBSC
 24    CPTRIA1  CPTRIA2  CPTRPLT  CPTUBE
 25    OPT      GRDEQ   
 61    EIGR 
 62    METHOD$  
$$$$
$*FILE BITS 
 94    BGPDT    CSTM     EQEXIN   GPDT     GPL      SIL 
 95    ECT  
 96    GPTT 
 97    EST      GEI      GPECT    MPTX     PCOMPS   EPTX
 98    KGGX 
 99    MGG  
100    KGG  
101    ASET     RG       USET     OGPST 
102    GPST 
103    GM   
104    KNN      MNN 
105    KFF      KFS      MFF
106    GO       KOO      LOO      KAA   
107    CYCD 
108    KKK      MKK 
109    LAMK     PHIK     MI       OEIGS 
111    EED      EQDYN    GPLD     SILD     USETD
112    LAMA     PHIA
113    PHIG     QG  
114    OEF1     OES1     OPHIG    OQG1     PPHIG
114    OEF1L    OES1L    OESF1
115    BGPDP    SIP 
116    ELSETS   GPSETS   PLTPAR   PLTSETX   
117    MAA  
118    KDICT    KELM     MDICT    MELM  
119    PLOTX1   
120    OGPWG
121    OQM1 
122    PLOTX2   
$*  
=PAGE=
DISP16

APR.93
$$$$$$$$
BEGIN    DISP 16 STATIC AEROTHERMOELASTIC DESIGN/ANALYSIS - APR. 1993 $ 
****CARD   1- 19, 22- 24, 59- 62
****RFMT 187-189,191-204,207-209
$$$$
PRECHK   ALL $  
****CARD   1- 19, 22- 24, 59- 62
****RFMT 187-189,191-204,207-209
$$$$
PARAM    //*MPY*/CARDNO/0/0 $   
****CARD   1- 11, 14, 15, 19, 22, 23, 24, 26, 59- 62
$$$$ GP1 generates coordinate system transformation matrices, tables of grid 
$$$$ point locations, and tables relating the internal and external grid point 
$$$$ numbers.
GP1      GEOM1,GEOM2,/GPL,EQEXIN,GPDT,CSTM,BGPDT,SIL/S,N,LUSET/S,N, 
         NOGPDT/MINUS1=-1 $ 
****CARD   1
****FILE  94
$$$$ Go to label ERROR3 and print Error Message No. 3 if there is no Grid 
$$$$ Point Definition Table.
COND     ERROR3,NOGPDT $
****CARD   1
****FILE  94
$$$$ PLTTRAN modifies special scalar grid points in the BGPDT and SIL tables.
PLTTRAN  BGPDT,SIL/BGPDP,SIP/LUSET/S,N,LUSEP $  
****CARD   1
****FILE 157
$$$$ GP2 generates Element Connection Table with internal indices.
GP2      GEOM2,EQEXIN/ECT $ 
****CARD   1,  2,  4,  5, 16
****FILE  95
$$$$
PARAML   PCDB//*PRES*////JUMPPLOT $ 
****SBST   7
****CARD  18
****FILE 122
$$$$ PARAMR sets CSIGN=(SIGN, 0.0), where SIGN = +1.0 for analysis type run 
$$$$ and SIGN = -1.0 for design type run.
PARAMR   //*COMPLEX*//V,Y,SIGN/0.0/CSIGN $  
****CARD  26
****FILE 117
$$$$
PURGE    PLTSETX,PLTPAR,GPSETS,ELSETS/JUMPPLOT $
****SBST   7
****CARD  18
****FILE 122
$$$$ Go to label P1 if there are no structure plot requests.
COND     P1,JUMPPLOT $  
****SBST   7
****CARD  18
****FILE 122
$$$$ PLTSET transforms user input into a form used to drive the structure 
$$$$ plotter.
PLTSET   PCDB,EQEXIN,ECT,EPT/PLTSETX,PLTPAR,GPSETS,ELSETS/S,N,NSIL/S,N, 
         JUMPPLOT $ 
****SBST   7
****CARD  18
****FILE 122
$$$$ PRTMSG prints error messages associated with the structure plotter.
PRTMSG   PLTSETX// $
****SBST   7
****CARD  18
$$$$
PARAM    //*MPY*/PLTFLG/1/1 $   
****SBST   7
****CARD  18
****FILE 122
$$$$
PARAM    //*MPY*/PFILE/0/0 $
****SBST   7
****CARD  18
****FILE 122
$$$$ Go to label P1 if no undeformed structure plots are requested.
COND     P1,JUMPPLOT $  
****SBST   7
****CARD  18
****FILE 135
$$$$ PLOT generates all requested undeformed structure plots.
PLOT     PLTPAR,GPSETS,ELSETS,CASECC,BGPDT,EQEXIN,SIL,,,,,,/PLOTX1/ 
         NSIL/LUSET/S,N,JUMPPLOT/S,N,PLTFLG/S,N,PFILE $ 
****SBST   7
****CARD  18
****FILE 135
$$$$ PRTMSG prints plotter data and engineering data for each undeformed plot 
$$$$ generated.
PRTMSG   PLOTX1// $ 
****SBST   7
****CARD  18
$$$$
LABEL    P1 $   
****SBST   7
****CARD  18
****FILE 122,135
$$$$ GP3 generates Static Loads Table and Grid Point Temperature Table.
GP3      GEOM3,EQEXIN,GEOM2/SLT,GPTT/S,N,NOGRAV $   
****CARD   1,  2, 13, 60, 61
****FILE  96
$$$$
PARAM    //*AND*/NOMGG/NOGRAV/V,Y,GRDPNT=-1 $   
****CARD   1,  2, 13, 15, 60, 61
****FILE  96, 99
****RFMT 187-189,191-204,207-209
$$$$ TA1 generates element tables for use in matrix assembly and stress 
$$$$ recovery.
TA1      ECT,EPT,BGPDT,SIL,GPTT,CSTM,MPT,EQEXIN/EST,GEI,GPECT,,,MPTX,
         PCOMPS,EPTX/LUSET/S,N,NOSIMP/1/S,N,NOGENL/S,N,GENEL/S,N,COMPS $   
****CARD   1-  7, 13
****FILE  97
$$$$
EQUIV    MPTX,MPT/COMPS/EPTX,EPT/COMPS $
****CARD   1-  7, 13
****FILE  97
$$$$ Go to label ERROR1 and print Error Message No. 1 if no structural 
$$$$ elements have been defined.
COND     ERROR1,NOSIMP $
****CARD   1-  8, 13
****FILE  97, 99
****RFMT 187-189,191-204,207-209
$$$$
PARAM    //*ADD*/NOKGGX/1/0 $   
****CARD   1-  4,  6
****FILE  98
$$$$ EMG generates structural element stiffness and mass matrix tables and 
$$$$ dictionaries for later assembly by the EMA module. 
EMG      EST,CSTM,MPT,DIT,GEOM2,/KELM,KDICT,MELM,MDICT,,,/S,N,NOKGGX/   
         S,N,NOMGG////C,Y,COUPMASS/C,Y,CPBAR/C,Y,CPROD/C,Y, 
         CPQUAD1/C,Y,CPQUAD2/C,Y,CPTRIA1/C,Y,CPTRIA2/C,Y,CPTUBE/C,Y,
         CPQDPLT/C,Y,CPTRPLT/C,Y,CPTRBSC/   
         C,Y,VOLUME/C,Y,SURFACE $   
****CARD   1-  8
****FILE 123
$$$$ Go to label JMPKGG if no stiffness matrix is to be assembled.
COND     JMPKGG,NOKGGX $
****CARD   1-  4,  6,  8
****FILE  98
$$$$                                  x
$$$$ EMA assembles stiffness matrix [K  ] and Grid Point Singularity Table.
$$$$                                  gg
$$$$
EMA      GPECT,KDICT,KELM/KGGX $
****CARD   1-  4,  6,  8
****FILE  98
$$$$
LABEL    JMPKGG $   
****CARD   1-  4,  6,  8
****FILE  98
$$$$ Go to label JMPMGG if no mass matrix is to be assembled.
COND     JMPMGG,NOMGG $ 
****CARD   1-  5,  7,  8, 14, 24
****FILE  99
$$$$ EMA assembles mass matrix [M  ].
$$$$                             gg
$$$$
EMA      GPECT,MDICT,MELM/MGG/-1/C,Y,WTMASS=1.0 $   
****CARD   1-  5,  7,  8, 14, 24
****FILE  99
$$$$
PURGE    MDICT,MELM/MINUS1 $
****CARD   1-  5,  7,  8, 14, 24
****FILE 123
$$$$
LABEL    JMPMGG $   
****CARD   1-  5,  7,  8, 14, 24
****FILE  99
$$$$ Go to label LBL1 if no weight and balance information is requested.
COND     LBL1,GRDPNT $  
****SBST   8
****CARD   1-  3,  5,  7,  8, 14, 15, 24
****FILE 136
$$$$ Go to label ERROR4 and print Error Message No. 4 if no mass matrix 
$$$$ exists.
COND     ERROR4,NOMGG $ 
****SBST   8
****CARD   1-  3,  5,  7,  8, 14, 15, 24
****FILE 136
$$$$ GPWG generates weight and balance information.
GPWG     BGPDP,CSTM,EQEXIN,MGG/OGPWG/V,Y,GRDPNT/C,Y,WTMASS $
****SBST   8
****CARD   1-  3,  5,  7,  8, 14, 15, 24
****FILE 136
$$$$ OFP formats the weight and balance information prepared by GPWG and 
$$$$ places it on the system output file for printing.
OFP      OGPWG,,,,,//S,N,CARDNO $   
****SBST   8
****CARD   1-  3,  5,  7,  8, 14, 15, 24
$$$$
LABEL    LBL1 $ 
****SBST   8
****CARD   1-  3,  5,  7,  8, 14, 15, 24
****FILE 136
****FILE  97, 99
$$$$               x
$$$$ Equivalence [K  ] to [K  ] if no general elements exist.
$$$$               gg       gg
$$$$
EQUIV    KGGX,KGG/NOGENL $  
****CARD   1-  4,  6,  8
****FILE 100
$$$$ Go to label LBL11 if no general elements exist.
COND     LBL11,NOGENL $ 
****CARD   1-  4,  6,  8
****FILE 100
$$$$                                 x
$$$$ SMA3 adds general elements to [K  ] to obtain stiffness matrix [K  ].
$$$$                                 gg                               gg
$$$$
SMA3     GEI,KGGX/KGG/LUSET/NOGENL/NOSIMP $ 
****CARD   1-  4,  6,  8
****FILE 100
$$$$
LABEL    LBL11 $
****CARD   1-  4,  6,  8
****FILE 100
$$$$
GPSTGEN  KGG,SIL/GPST $ 
****CARD   1-  4,  6,  8
****FILE 102
$$$$
PARAM    //*MPY*/NSKIP/0/0 $
****CARD   1,  9- 11
****FILE 101
$$$$ GP4 generates flags defining members of various displacement sets (USET), 
$$$$ forms multipoint constraint equations [R ] {u } = 0, and forms enforced
$$$$                                         g    g
$$$$ displacement vector {Y }.
$$$$                       s
$$$$
GP4      CASECC,GEOM4,EQEXIN,GPDT,BGPDT,CSTM,GPST/RG,YS,USET,   
         ASET,OGPST/LUSET/S,N,MPCF1/S,N,MPCF2/S,N,SINGLE/S,N,OMIT/  
         S,N,REACT/S,N,NSKIP/S,N,REPEAT/S,N,NOSET/S,N,NOL/S,N,NOA/  
         C,Y,ASETOUT/C,Y,AUTOSPC $  
****CARD   1,  4,  6,  8- 11, 59
****FILE 101
$$$$ OFP formats the table of potential grid point singularities prepared by 
$$$$ GPSP and places it on the system output file for printing.
OFP      OGPST,,,,,//S,N,CARDNO $   
****CARD   1,  4,  6,  8- 11, 59
****FILE 101
$$$$ Go to label ERROR5 and print Error Message No. 5 if no independent 
$$$$ degrees of freedom are defined.
COND     ERROR5,NOL $   
****CARD   1,  9- 11, 59
****FILE 101
****RFMT 187-189,193-204,207-209
$$$$ Go to label LBL4D if no free-body supports are supplied.
COND     LBL4D,REACT $  
****CARD   1, 12
****RFMT 187-189,193-204,207-209
$$$$ Go to label ERROR2 and print Error Message No. 2.
JUMP     ERROR2 $   
****CARD   1, 12
****RFMT 187-189,193-204,207-209
$$$$
LABEL    LBL4D $
****CARD   1, 12
****RFMT 187-189,193-204,207-209
$$$$
PURGE    GM/MPCF1/GO,KOO,LOO,PO,UOOV,RUOV/OMIT/PS,KFS,KSS,QG/SINGLE/
         PBS,KBFS,KBSS,KDFS,KDSS/SINGLE $   
****CARD   1,  9- 11, 59
****FILE 103,105,106,109-111,115,139,140,147
$$$$ Equivalence [K  ] to [K  ] if no multipoint constraints exist.
$$$$               gg       nn
$$$$
EQUIV    KGG,KNN/MPCF1 $
****CARD   1-  4,  6,  8,  9
****FILE 104
$$$$ Go to label LBL2 if no multipoint constraints exist.
COND     LBL2,MPCF2 $   
****CARD   1-  4,  6,  8,  9
****FILE 103,104
$$$$ MCE1 partitions multipoint constraint equation [R ] = [R |R ] and solves
$$$$                                                  g      m  n
$$$$                                                             -1
$$$$ for multipoint constraint transformation matrix [G ] = -[R ]  [R ].
$$$$                                                   m       m     n
$$$$
MCE1     USET,RG/GM $   
****CARD   1,  9
****FILE 103
$$$$ MCE2 partitions stiffness matrix
$$$$ 
$$$$           +_      +
$$$$           |K  |K  |
$$$$           | nn| nm|
$$$$   [K  ] = |---+---|
$$$$     gg    |K  |K  |
$$$$           | mn| mm|
$$$$           +       +
$$$$ 
$$$$ and performs matrix reduction
$$$$ 
$$$$            _        T           T           T
$$$$   [K  ] = [K  ] + [G ][K  ] + [K  ][G ] + [G ][K  ][G ]
$$$$     nn      nn      m   mn      mn   m      m   mm   m
$$$$
MCE2     USET,GM,KGG,,,/KNN,,, $
****CARD   1-  4,  6,  8,  9
****FILE 104
$$$$
LABEL    LBL2 $ 
****CARD   1-  4,  6,  8,  9
****FILE 103,104
$$$$ Equivalence [K  ] to [K  ] if no single-point constraints exist.
$$$$               nn       ff
$$$$
EQUIV    KNN,KFF/SINGLE $   
****CARD   1-  4,  6,  8- 10
****FILE 105
$$$$ Go to label LBL3 if no single-point constraints exist.
COND     LBL3,SINGLE $  
****CARD   1-  4,  6,  8- 10
****FILE 105
$$$$ SCE1 partitions out single-point constraints
$$$$ 
$$$$           +       +
$$$$           |K  |K  |
$$$$           | ff| fs|
$$$$   [K  ] = |---+---|
$$$$     nn    |K  |K  |
$$$$           | sf| ss|
$$$$           +       +
$$$$ 
SCE1     USET,KNN,,,/KFF,KFS,KSS,,, $   
****CARD   1-  4,  6,  8- 10
****FILE 105
$$$$
LABEL    LBL3 $ 
****CARD   1-  4,  6,  8- 10
****FILE 105
$$$$ Equivalence [K  ] to [K  ] if no omitted coordinates exist.
$$$$               ff       aa
$$$$
EQUIV    KFF,KAA/OMIT $ 
****CARD   1-  4,  6,  8- 11
****FILE 106
$$$$ Go to label LBL5 if no omitted constraints exist.
COND     LBL5,OMIT $
****CARD   1-  4,  6,  8- 11
****FILE 106
$$$$ SMP1 partitions constrained stiffness matrix
$$$$
$$$$           +_      +
$$$$           |K  |K  |
$$$$           | aa| ao|
$$$$   [K  ] = |---+---|
$$$$     ff    |K  |K  |
$$$$           | oa| oo|
$$$$           +       +
$$$$           
$$$$                                               -1
$$$$ solves for transformation matrix [G ] = -[K  ]  [K  ]
$$$$                                    o       oo     oa
$$$$ 
$$$$                                        _        T
$$$$ and performs matrix reduction [K  ] = [K  ] + [K  ][G ].
$$$$                                 aa      aa      oa   o
$$$$
SMP1     USET,KFF,,,/GO,KAA,KOO,LOO,,,,, $  
****CARD   1-  4,  6,  8- 11
****FILE 106
$$$$
LABEL    LBL5 $ 
****CARD   1-  4,  6,  8- 11
****FILE 106
$$$$ RBMG2 decomposes constrained stiffness matrix [K  ] = [L  ][U  ].
$$$$                                                 aa      ll   ll
$$$$
RBMG2    KAA/LLL $  
****CARD   1-  4,  6,  8- 11
****FILE 107
$$$$                                                      NA
$$$$ SSG1 generates non-aerodynamic static load vectors {P  }.
$$$$                                                      g
$$$$
SSG1     SLT,BGPDT,CSTM,SIL,EST,MPT,GPTT,EDT,MGG,CASECC,DIT,PCOMPS/ 
         PGNA,,,,/LUSET/1/COMPS $   
****CARD   1-  3,  5-  8, 13, 59- 62
****FILE 132
$$$$
PARAM    //*AND*/ALOAD/V,Y,APRESS/V,Y,ATEMP $   
****CARD   1-  3,  5-  8, 13, 26, 59- 62
****FILE 124
$$$$ Go to label NOAL if no aerodynamic loads exist.
COND     NOAL,ALOAD $   
****CARD   1-  3,  5-  8, 13, 26, 59- 62
****FILE 124
$$$$ ALG generates aerodynamic load data.
ALG      CASECC,,EQEXIN,,ALGDB,,/CASECCA1,GEOM3A1/S,Y,APRESS/S,Y,   
         ATEMP/-1/-1/V,Y,IPRTCI/S,N,IFAIL $ 
****CARD   1-  3,  5-  8, 13, 26, 27, 59- 62
****FILE 124
$$$$
COND     FINIS,IFAIL $  
****CARD   1-  3,  5-  8, 13, 26, 59- 62
$$$$
PARAM    //*AND*/ALOAD/V,Y,APRESS/V,Y,ATEMP $   
****CARD   1-  3,  5-  8, 13, 26, 59- 62
****FILE 125
$$$$
COND     NOAL,ALOAD $   
****CARD   1-  3,  5-  8, 13, 26, 59- 62
****FILE 125
$$$$
GP3      GEOM3A1,EQEXIN,GEOM2/SLTA1,GPTTA1/NOGRAV $ 
****CARD   1-  3,  5-  8, 13, 26, 59- 62
****FILE 125
$$$$                                          A
$$$$ SSG1 generates aerodynamic load vector {P }.
$$$$                                          g
$$$$ 
SSG1     SLTA1,BGPDT,CSTM,SIL,EST,MPT,GPTTA1,EDT,MGG,CASECCA1,DIT,  
         PCOMPS/PGA1,,,,/LUSET/1/COMPS $
****CARD   1-  3,  5-  8, 13, 26, 59- 62
****FILE 126
$$$$       NA        A
$$$$ Add {P  } and {P } to form total load vector {P }.
$$$$       g         g                              g
$$$$
ADD      PGNA,PGA1/PG/(1.0,0.0)/(1.0,0.0) $ 
****CARD   1-  3,  5-  8, 13, 26, 59- 62
****FILE 108
$$$$
LABEL    NOAL $ 
****CARD   1-  3,  5-  8, 13, 26, 59- 62
****FILE 108
$$$$                       NA
$$$$ Equivalence {P } to {P  } if no aerodynamic loads exist.
$$$$               g       g
$$$$
EQUIV    PGNA,PG/ALOAD $
****CARD   1-  3,  5-  8, 13, 26, 59- 62
****FILE 108
$$$$ Equivalence {P } to {P } if no constraints are applied.
$$$$               g       l
$$$$
EQUIV    PG,PL/NOSET $  
****CARD   1-  3,  5- 11, 13, 26, 59- 62
****FILE 109
$$$$ Go to label LBL10 if no constraints are applied.
COND     LBL10,NOSET $  
****CARD   1-  3,  5- 11, 13, 26, 59- 62
****FILE 109
$$$$ SSG2 applies constraints to static load vectors
$$$$ 
$$$$           _
$$$$           P
$$$$            n            _       T
$$$$   {P } = {--} , {P } = {P } + [G ]{P }
$$$$     g     P       n      n      m   m
$$$$            m
$$$$ 
$$$$           _
$$$$           P
$$$$            f            _      
$$$$   {P } = {--} , {P } = {P } - [K  ]{Y }
$$$$     n     P       f      f      fs   s
$$$$            s
$$$$ 
$$$$            
$$$$           P
$$$$            a                    T
$$$$   {P } = {--} , {P } = {P } + [G ]{P }
$$$$     f     P       l      a      o   o
$$$$            o
$$$$
SSG2     USET,GM,YS,KFS,GO,,PG/,PO,PS,PL $  
****CARD   1-  3,  5- 11, 13, 26, 59- 62
****FILE 109
$$$$
LABEL    LBL10 $
****CARD   1-  3,  5- 11, 13, 26, 59- 62
****FILE 109
$$$$ SSG3 solves for displacements of independent coordinates
$$$$ 
$$$$               -1  
$$$$   {u } = [K  ]  {P }
$$$$     l      aa     l
$$$$ 
$$$$ solves for displacements of omitted coordinates
$$$$ 
$$$$     o         -1  
$$$$   {u } = [K  ]  {P }
$$$$     o      oo     o
$$$$ 
$$$$ calculates residual vector (RULV) and residual vector error ratio for 
$$$$ independent coordinates
$$$$                
$$$$   {deltaP } = {P } - [K  ]{u }
$$$$          l      l      aa   l
$$$$ 
$$$$                T        
$$$$              {u }{deltaP }
$$$$                l        l
$$$$   epsilon  = -------------
$$$$          l       T 
$$$$                {P }{u }
$$$$                  l   l
$$$$ 
$$$$ and calculates residual vector (RUOV) and residual vector error ratio for
$$$$ omitted coordinates
$$$$ 
$$$$                             o
$$$$   {deltaP } = {P } - [K  ]{u }
$$$$          o      o      oo   o
$$$$ 
$$$$                T        
$$$$              {u }{deltaP }
$$$$                o        o
$$$$   epsilon  = -------------
$$$$          o       T   o
$$$$                {P }{u }
$$$$                  o   o
$$$$
SSG3     LLL,KAA,PL,LOO,KOO,PO/ULV,UOOV,RULV,RUOV/OMIT/V,Y,IRES=-1/ 
         1/S,N,EPSI $   
****CARD   1- 11, 13, 26, 59- 62
****FILE 110
****RFMT 188
$$$$ Go to label LBL9 if residual vectors are not to be printed.
COND     LBL9,IRES $
****CARD   1- 11, 13, 17, 26, 59- 62
****RFMT 188,189,192-204,207-209
$$$$ MATGPR prints the residual vector for independent coordinates (RULV).
MATGPR   GPL,USET,SIL,RULV//*L* $   
****CARD   1- 11, 13, 17, 26, 59- 62
****RFMT 188,189,192-204,207-209
$$$$ MATGPR prints the residual vector for omitted coordinates (RUOV).
MATGPR   GPL,USET,SIL,RUOV//*O* $   
****CARD   1- 11, 13, 17, 26, 59- 62
****RFMT 188,189,192-204,207-209
$$$$
LABEL    LBL9 $ 
****CARD   1- 11, 13, 17, 26, 59- 62
****RFMT 188,189,192-204,207-209
$$$$ SDR1 recovers dependent displacements
$$$$ 
$$$$                       o
$$$$   {u } = [G ]{u ] + {u }  ,
$$$$     o      o   l      o
$$$$ 
$$$$    u                u            
$$$$     a                f           
$$$$   {--} = {u }  ,   {--} = {u }  ,
$$$$    u       f        Y       n    
$$$$     o                s           
$$$$                                  
$$$$                        u         
$$$$                         n        
$$$$  {u } = [G ]{u ]  ,   {--} = {u }
$$$$    m      m   n        u       g 
$$$$                         m        
$$$$ 
$$$$ and recovers single-point forces of constraint
$$$$ 
$$$$                    T
$$$$   {q } = -{P } + [K  ]{u } +[K  ]{Y }
$$$$     s       s      fs   f     ss   s
$$$$
SDR1     USET,,ULV,UOOV,YS,GO,GM,PS,KFS,KSS,/UGV,PG1,QG/1/*DS0* $   
****CARD   1- 11, 13, 26, 59- 62
****FILE 111
$$$$ SDR2 calculates element forces (OEF1) and stresses (OES1)
$$$$ and prepares load vectors (OPG1), displacement vectors (OUGV1), 
$$$$ and single-point forces of constraint (OQG1) for output and translation 
$$$$ components of the displacement vector (PUGV1) for the static solution.
SDR2     CASECC,CSTM,MPT,DIT,EQEXIN,SIL,GPTT,EDT,BGPDP,,QG,UGV,EST,,PG, 
         PCOMPS/OPG1,OQG1,OUGV1,OES1,OEF1,PUGV1,OES1L,OEF1L/ 
         *DS0*////COMPS $   
****CARD  19
****FILE 112
$$$$ OFP formats the tables prepared by SDR2 and places them on the system 
$$$$ output file for printing.
OFP      OUGV1,OPG1,OQG1,OEF1,OES1,//S,N,CARDNO $   
****CARD  19
****FILE 112
$$$$ 
OFP      OEF1L,OES1L,,,,//S,N,CARDNO $   
****CARD  19
****FILE 112
$$$$ Go to label P2 if no deformed static solution structure plots are 
$$$$ requesed.
COND     P2,JUMPPLOT $  
****SBST   7
****CARD  18
****FILE 137
$$$$ PLOT generates all requested static solution deformed structure and 
$$$$ contour plots.
PLOT     PLTPAR,GPSETS,ELSETS,CASECC,BGPDT,EQEXIN,SIP,PUGV1,,GPECT,OES1,
         OES1L,/PLOTX2/NSIL/LUSET/JUMPPLOT/PLTFLG/S,N,PFILE $   
****SBST   7
****CARD  18
****FILE 137
$$$$ PRTMSG prints plotter data, engineering data, and contour data for each 
$$$$ static solution deformed plot generated.
PRTMSG   PLOTX2// $ 
****SBST   7
****CARD  18
$$$$
LABEL    P2 $   
****SBST   7
****CARD  18
****FILE 137
$$$$ TA1 generates element tables for use in differential stiffness matrix 
$$$$ assembly.
TA1      ECT,EPT,BGPDT,SIL,GPTT,CSTM,,/X1,X2,X3,ECPT,GPCT,,,/LUSET/  
         NOSIMP/0/NOGENL/GENEL $
****CARD   1- 11, 26, 59- 62
****FILE 138
$$$$                                                 d
$$$$ DSMG1 generates differential stiffness matrix [K  ].
$$$$                                                 gg
$$$$
DSMG1    CASECC,GPTT,SIL,EDT,UGV,CSTM,MPT,ECPT,GPCT,DIT/KDGG/   
         DSCOSET$   
****CARD   1- 11, 26, 59- 62
****FILE 113
$$$$ Go to label NOAL0 if no aerodynamic loads exist.
COND     NOAL0,ALOAD $  
****CARD   1-  3,  5-  8, 13, 26, 59- 62
****FILE 108
$$$$               NA
$$$$ Equivalence {P  } to {P } to remove aerodynamic loads from total load
$$$$               g        g
$$$$ vector before entering the differential stiffness loop. New aerodynamic 
$$$$ loads will be generated in the loop.
EQUIV    PGNA,PG $  
****CARD   1-  3,  5-  8, 13, 26, 59- 62
****FILE 108
$$$$
LABEL    NOAL0 $
****CARD   1-  3,  5-  8, 13, 26, 59- 62
****FILE 108
$$$$
PARAM    //*ADD*/SHIFT/-1/0 $   
****CARD   1- 11, 26, 59- 62
$$$$
PARAM    //*ADD*/COUNT/ALWAYS=-1/NEVER=1 $  
****CARD   1- 11, 26, 59- 62
$$$$
PARAMR   //*ADD*/DSEPSI/0.0/0.0 $   
****CARD   1- 11, 26, 59- 62
$$$$
PARAML   YS//*NULL*////NOYS $   
****CARD   1- 11, 26, 59- 62
$$$$ Beginning of outer (stiffness adjustment) loop for differential stiffness 
$$$$ iteration.
LABEL    OUTLPTOP $ 
****CARD   1- 11, 26, 59- 62
$$$$ Equivalence {P } to {P  } if no enforced displacements are specified.
$$$$               g       g1
$$$$
EQUIV    PG,PG1/NOYS $  
****CARD   1- 11, 26, 59- 62
****FILE 111
$$$$
PARAM    //*KLOCK*/TO $ 
****CARD   1- 11, 26, 59- 62
****FILE 114
$$$$               d        d
$$$$ Equivalence [K  ] to [K  ] if no multipoint constraints exist.
$$$$               gg       nn
$$$$
EQUIV    KDGG,KDNN/MPCF2 $  
****CARD   1- 11, 26, 59- 62
****FILE 114
$$$$ Go to label LBL2D if no multipoint constraints exist.
COND     LBL2D,MPCF2 $  
****CARD   1- 11, 26, 59- 62
****FILE 114
$$$$ MCE2 partitions differential stiffness matrix
$$$$ 
$$$$           +_d   d +
$$$$           |K  |K  |
$$$$     d     | nn| nm|
$$$$   [K  ] = |---+---|
$$$$     gg    | d | d |
$$$$           |K  |K  |
$$$$           + mn| mm+
$$$$ 
$$$$ and performs matrix reduction
$$$$ 
$$$$     d      _d       T   d       d           T   d
$$$$   [K  ] = [K  ] + [G ][K  ] + [K  ][G ] + [G ][K  ][G ]
$$$$     nn      nm      m   mn      mn   m      m   mm   m
$$$$
MCE2     USET,GM,KDGG,,,/KDNN,,, $  
****CARD   1- 11, 26, 59- 62
****FILE 114
$$$$
LABEL    LBL2D $
****CARD   1- 11, 26, 59- 62
****FILE 114
$$$$               d        d
$$$$ Equivalence [K  ] to [K  ] if no single-point constraints exist.
$$$$               nn       ff
$$$$
EQUIV    KDNN,KDFF/SINGLE $ 
****CARD   1- 11, 26, 59- 62
****FILE 115
$$$$ Go to label LBL3D if no single-point constraints exist.
COND     LBL3D,SINGLE $ 
****CARD   1- 11, 26, 59- 62
****FILE 115
$$$$ SCE1 partitions out single-point constraints
$$$$ 
$$$$           + d   d +
$$$$           |K  |K  |
$$$$     d     | ff| fs|
$$$$   [K  ] = |---+---|
$$$$     nn    | d | d |
$$$$           |K  |K  |
$$$$           + sf| ss+
$$$$
SCE1     USET,KDNN,,,/KDFF,KDFS,KDSS,,, $   
****CARD   1- 11, 26, 59- 62
****FILE 115
$$$$
LABEL    LBL3D $
****CARD   1- 11, 26, 59- 62
****FILE 115
$$$$               d        d
$$$$ Equivalence [K  ] to [K  ] if no omitted coordinates exist.
$$$$               ff       aa
$$$$
EQUIV    KDFF,KDAA/OMIT $   
****CARD   1- 11, 26, 59- 62
****FILE 116
$$$$ Go to label LBL5D if no omitted coordinates exist.
COND     LBL5D,OMIT $   
****CARD   1- 11, 26, 59- 62
****FILE 116
$$$$ SMP2 partitions constrained differential stiffness matrix
$$$$
$$$$           +       +
$$$$           |_d   d |
$$$$           |K  |K  |
$$$$     d     | aa| ao|
$$$$   [K  ] = |---+---|
$$$$     ff    | d | d |
$$$$           |K  |K  |
$$$$           | oa| oo|
$$$$           +       +
$$$$ 
$$$$ and performs matrix reduction
$$$$
$$$$     d      _d       d             T  d         T  d
$$$$   [K  ] = [K  ] + [K  ][G ] + [G ] [K  ] + [G ] [K  ][G ]
$$$$     aa      aa      oa   o      o    oa      o    oo   o
$$$$
SMP2     USET,GO,KDFF/KDAA $
****CARD   1- 11, 26, 59- 62
****FILE 116
$$$$
LABEL    LBL5D $
****CARD   1- 11, 26, 59- 62
****FILE 116
$$$$                 d             b
$$$$ ADD [K  ] and [K  ] to form [K  ].
$$$$       aa        aa            ll
$$$$
ADD      KAA,KDAA/KBLL/(1.0,0.0)/CSIGN $
****CARD   1- 11, 26, 59- 62
****FILE 117
$$$$                 phi            beta
$$$$ ADD [K  ] and [K   ] to form [K    ].
$$$$       fs        fs             fs
$$$$
ADD      KFS,KDFS/KBFS/(1.0,0.0)/CSIGN $
****CARD   1- 11, 26, 59- 62
****FILE 139
$$$$                 d             b
$$$$ ADD [K  ] and [K  ] to form [K  ].
$$$$       ss        ss            ss
$$$$
ADD      KSS,KDSS/KBSS/(1.0,0.0)/CSIGN $
****CARD   1- 11, 26, 59- 62
****FILE 140
$$$$ Go to label PGOK if no enforced displacements are specified.
COND     PGOK,NOYS $
****CARD   1- 11, 26, 59- 62
****FILE 111,141-145
$$$$                    b
$$$$ MPYAD multiplies [K  ] and {Y } to form {P  }.
$$$$                    ss        s            ss
$$$$
MPYAD    KBSS,YS,/PSS/0 $   
****CARD   1- 11, 26, 59- 62
****FILE 141
$$$$                    b
$$$$ MPYAD multiplies [K  ] and {Y } to form {P  }.
$$$$                    fs        s            fs
$$$$
MPYAD    KBFS,YS,/PFS/0 $   
****CARD   1- 11, 26, 59- 62
****FILE 142
$$$$ UMERGE combines {P  } and {P  } to form {P }.
$$$$                   fs        ss            n
$$$$
UMERGE   USET,PFS,PSS/PN/*N*/*F*/*S* $  
****CARD   1- 11, 26, 59- 62
****FILE 143
$$$$                       x
$$$$ Equivalence {P } to {P } if no multipoint constraints exist.
$$$$               n       g
$$$$
EQUIV    PN,PGX/MPCF2 $ 
****CARD   1- 11, 26, 59- 62
****FILE 144
$$$$ Go to label LBL6D if no multipoint constraints exist.
COND     LBL6D,MPCF2 $  
****CARD   1- 11, 26, 59- 62
****FILE 144
$$$$                               x
$$$$ UMERGE expands {P } to form {P }.
$$$$                  n            g
$$$$
UMERGE   USET,PN,/PGX/*G*/*N*/*M* $ 
****CARD   1- 11, 26, 59- 62
****FILE 144
$$$$
LABEL    LBL6D $
****CARD   1- 11, 26, 59- 62
****FILE 144
$$$$        x
$$$$ ADD -{P } and {P } to form {P  }.
$$$$        g        g            gg
$$$$
ADD      PGX,PG/PGG/(-1.0,0.0)/(1.0,0.0) $  
****CARD   1- 11, 26, 59- 62
****FILE 145
$$$$ Equivalence {P  } to {P  }.
$$$$               gg       g1
$$$$
EQUIV    PGG,PG1/ALWAYS $   
****CARD   1- 11, 26, 59- 62
****FILE 111
$$$$
LABEL    PGOK $ 
****CARD   1- 11, 26, 59- 62
****FILE 111,141-145
$$$$ ADD {P  } and nothing to create {P  }.
$$$$       g1                          go
$$$$
ADD      PG1,/PG0/(1.0,0.0) $   
****CARD   1- 11, 26, 59- 62
****FILE 146
$$$$                A
$$$$ Copy {u } to {u } to initialize aerodynamic displacements.
$$$$        g       g
$$$$
COPY     UGV/AUGV $ 
****CARD   1- 11, 26, 59- 62
****FILE 133
$$$$ RBMG2 decomposes the combined differential stiffness matrix and elastic 
$$$$ stiffness matrix
$$$$                                                                  
$$$$     b       b    b                                               
$$$$   [K  ] = [L  ][U  ]
$$$$     ll      ll   ll  
$$$$ 
RBMG2    KBLL/LBLL/S,N,POWER/S,N,DET $  
****CARD   1- 11, 22, 23, 26, 59- 62
****FILE 118
$$$$ PRTPARM prints the scaled value of the determinant of the combined 
$$$$ differential stiffness matrix and elastic stiffness matrix.
PRTPARM  //0/*DET* $
****CARD   1- 11, 22, 23, 26, 59- 62
****FILE 118
$$$$ PRTPARM prints the scale factor (power of ten) of the determinant of the 
$$$$ combined differential stiffness matrix and elastic stiffness matrix.
PRTPARM  //0/*POWER* $  
****CARD   1- 11, 22, 23, 26, 59- 62
****FILE 118
$$$$ Beginning of inner (load correction) loop for differential stiffness 
$$$$ iteration.
LABEL    INLPTOP $  
****CARD   1- 11, 22, 23, 26, 59- 62
$$$$
PARAM    //*KLOCK*/TI $ 
****CARD   1- 11, 22, 23, 26, 59- 62
$$$$ Go to label NOAL1 if no aerodynamic loads exist.
COND     NOAL1,ALOAD $  
****CARD   1- 11, 22, 23, 26, 59- 62
****FILE 127-130
$$$$ ALG generates aerodynamic load data.
ALG      CASECC,EDT,EQEXIN,AUGV,ALGDB,CSTM,BGPDT/CASECCA,GEOM3A/S,Y,
         APRESS/S,Y,ATEMP/-1/-1/V,Y,IPRTCL/S,N,IFAIL/V,Y,SIGN/V,
         Y,ZORIGN/V,Y,FXCOOR/V,Y,FYCOOR/V,Y,FZCOOR $
****CARD   1- 11, 22, 23, 26, 27, 59- 62
****FILE 127
$$$$ Go to label DONE if ALG fails to converge while generating aerodynamic 
$$$$ load data.
COND     DONE,IFAIL $   
****CARD   1- 11, 22, 23, 26, 59- 62
$$$$
PARAM    //*MPY*/V,Y,IPRTCL/0 $ 
****CARD   1- 11, 22, 23, 26, 59- 62
$$$$
PARAM    //*AND*/ALOAD/V,Y,APRESS/V,Y,ATEMP $   
****CARD   1- 11, 22, 23, 26, 59- 62
$$$$
COND     NOAL1,ALOAD $  
****CARD   1- 11, 22, 23, 26, 59- 62
****FILE 128-130
$$$$
GP3      GEOM3A,EQEXIN,GEOM2/SLTA,GPTTA/NOASL/NOGRAV/NOATL $
****CARD   1- 11, 22, 23, 26, 59- 62
****FILE 128
$$$$                                          A
$$$$ SSG1 generates aerodynamic load vector {P }.
$$$$                                          g
$$$$
SSG1     SLTA,BGPDT,CSTM,SIL,EST,MPT,GPTTA,EDT,MGG,CASECCA,DIT,PCOMPS/  
         PGA,,,,/LUSET/1/COMPS $ $  
****CARD   1- 11, 22, 23, 26, 59- 62
****FILE 129
$$$$                A
$$$$ ADD {P  } to {P } to form total load vector {P  }.
$$$$       g1       g                              g2
$$$$
ADD      PG1,PGA/PG2/(1.0,0.0)/(1.0,0.0) $  
****CARD   1- 11, 22, 23, 26, 59- 62
****FILE 130
$$$$
LABEL    NOAL1 $
****CARD   1- 11, 22, 23, 26, 59- 62
****FILE 127-130
$$$$
EQUIV    PG1,PG2/ALOAD $
****CARD   1- 11, 22, 23, 26, 59- 62
****FILE 130
$$$$ SSG2 applies constraints to static load vectors
$$$$ 
$$$$            _b
$$$$            P
$$$$             n      b     _b      T   b
$$$$   {P  } = {--} , {P } = {P } + [G ]{P }
$$$$     g1      b      n      n      m   m
$$$$            P 
$$$$             m
$$$$ 
$$$$           _b
$$$$           P
$$$$     b      f            _b      d
$$$$   {P } = {--} , {P } = {P } - [K  ]{Y }
$$$$     n      b      f      f      fs   s
$$$$           P 
$$$$            s
$$$$ 
$$$$           _b
$$$$           P
$$$$     b      a             b      T   b
$$$$   {P } = {--} , {P } = {P } + [G ]{P }
$$$$     f      b      l      a      o   o
$$$$           P 
$$$$            o
$$$$
SSG2     USET,GM,YS,KDFS,GO,,PG2/,PBO,PBS,PBL $ 
****CARD   1- 11, 22, 23, 26, 59- 62
****FILE 147
$$$$ SSG3 solves for displacements of independent coordinates for current 
$$$$ differential stiffness load vector
$$$$ 
$$$$     b      b  -1  b
$$$$   {u } = [K  ]  {P }
$$$$     l      ll     l
$$$$ 
$$$$ and calculates residual vector (RBULV) and residual vector error ratio for 
$$$$ differential stiffness load vector
$$$$ 
$$$$          b      b      b    b
$$$$   {deltaP } = {P } - [K  ]{u }
$$$$          l      l      ll   l
$$$$ 
$$$$                b T       b
$$$$              {u } {deltaP }
$$$$          b     l         l
$$$$   epsilon  = --------------
$$$$          l       b T  b
$$$$                {P } {u }
$$$$                  l    l
$$$$
SSG3     LBLL,KBLL,PBL,,,/UBLV,,RUBLV,/-1/V,Y,IRES/NDSKIP/S,N,  
         EPSI $ 
****CARD   1- 11, 22, 23, 26, 59- 62
****FILE 119
$$$$ Go to label LBL9D if the residual vector for current differential 
$$$$ stiffness solution is not to be printed.
COND     LBL9D,IRES $   
****CARD   1- 11, 17, 22, 23, 26, 59- 62
$$$$ MATGPR prints the residual vector for current differential stiffness 
$$$$ solution.
MATGPR   GPL,USET,SIL,RUBLV//*L* $  
****CARD   1- 11, 17, 22, 23, 26, 59- 62
$$$$
LABEL    LBL9D $
****CARD   1- 11, 17, 22, 23, 26, 59- 62
****FILE 130
$$$$ SDR1 recovers dependent displacements for the current differential 
$$$$ stiffness solution
$$$$ 
$$$$                                      b
$$$$                                     u
$$$$    b          b      ob              l
$$$$  {u } = [G ]{u } + {u  }           {----} = {u }
$$$$    o      o   l      o               b        f
$$$$                                     u
$$$$                                      o
$$$$ 
$$$$     b
$$$$    u       
$$$$     f       b                        b          b
$$$$   {---} = {u }                     {u } = [G ]{u }
$$$$     b       n                        m      m   n
$$$$    Y
$$$$     s
$$$$
$$$$     b
$$$$    u                 
$$$$     n      b        
$$$$   {--} = {u }
$$$$     b      g 
$$$$    u                  
$$$$     m
$$$$
$$$$ and recovers single-point forces of constraint for the current 
$$$$ differential stiffness solution
$$$$
$$$$     b       b      b    b      b    b
$$$$   {q } = -{P } + [K  ]{u } + [K  ]{Y }
$$$$     s       s      sf   f      ff   s
$$$$ 
SDR1     USET,,UBLV,,YS,GO,GM,PBS,KBFS,KBSS,/UBGV,,QBG/1/*DS1* $
****CARD   1- 11, 22, 23, 26, 59- 62
****FILE 148
****RFMT 187-189,191-204,207-209
$$$$ Go to label NOAL2 if no aerodynamic loads exist.
COND     NOAL2,ALOAD $  
****CARD   1- 11, 22, 23, 26, 59- 62
****FILE 133
$$$$               B        A
$$$$ Equivalence {u } and {u }.
$$$$               g        g
$$$$
$$$$
EQUIV    UBGV,AUGV $
****CARD   1- 11, 22, 23, 26, 59- 62
****FILE 133
$$$$
LABEL    NOAL2 $
****CARD   1- 11, 22, 23, 26, 59- 62
****FILE 133
$$$$        b                     d
$$$$ ADD -{U } and {U } to form {U }.
$$$$        g        g            g
$$$$
ADD      UBGV,UGV/DUGV/(-1.0,0.0)/(1.0,0.0) $   
****CARD   1- 11, 22, 23, 26, 59- 62
****FILE 149
$$$$                                                     d
$$$$ DSMG1 generates differential stiffness matrix [delta  ].
$$$$                                                     gg
$$$$
DSMG1    CASECC,GPTT,SIL,EDT,DUGV,CSTM,MPT,ECPT,GPCT,DIT/DKDGG/V,N, 
         DSCOSET $  
****CARD   1- 11, 22, 23, 26, 59- 62
****FILE 150
$$$$ MPYAD forms the load vector for inner loop iteration
$$$$
$$$$                    d     b
$$$$   {P   } = [delta K  ] {U } + {P  }.
$$$$     g              gg    g      go
$$$$      I1
$$$$ 
MPYAD    DKDGG,UBGV,PG0/PGI1/0 $
****CARD   1- 11, 22, 23, 26, 59- 62
****FILE 151
$$$$                  A
$$$$ ADD {P   } and {P } to form {P   }.
$$$$       g          g            g
$$$$        I1                      I2
$$$$ 
ADD      PGI1,PGA/PGI2/(1.0,0.0)/(1.0,0.0) $
****CARD   1- 11, 22, 23, 26, 59- 62
****FILE 134
$$$$ DSCHK performs differential stiffness convergence checks.
DSCHK    PG2,PGI2,UBGV//C,Y,EPSIO=1.E-5/S,N,DSEPSI/C,Y,NT=10/   
         TO/TI/S,N,DONE/S,N,SHIFT/S,N,COUNT/C,Y,BETAD=4 $   
****CARD   1- 11, 22, 23, 26, 59- 62
$$$$ Go to label DONE if differential stiffness iteration is complete.
COND     DONE,DONE $
****CARD   1- 11, 22, 23, 26, 59- 62
$$$$ Go to label SHIFT if additional differential stiffness matrix changes are 
$$$$ necessary for further iteration.
COND     SHIFT,SHIFT $  
****CARD   1- 11, 22, 23, 26, 59- 62
$$$$ Break the previous equivalence of {P } and {P  }.
$$$$                                     g        g1
$$$$
EQUIV    PG,PG1/NEVER $ 
****CARD   1- 11, 22, 23, 26, 59- 62
****FILE 111
$$$$ Equivalence {P   } to {P  }.
$$$$               g         g1
$$$$                I1
$$$$
EQUIV    PGI1,PG1/ALWAYS $  
****CARD   1- 11, 22, 23, 26, 59- 62
****FILE 111
$$$$ Break the previous equivalence of {P  } to {P   }.
$$$$                                     g1       g
$$$$                                               I1
$$$$
EQUIV    PG1,PGI1/NEVER $   
****CARD   1- 11, 22, 23, 26, 59- 62
****FILE 151
$$$$ Go to label INLPTOP for an additional inner loop differential stiffness 
$$$$ iteration.
REPT     INLPTOP,1000 $ 
****CARD   1- 11, 22, 23, 26, 59- 62
$$$$ TABPT table prints vectors {P   }, {P  }, and {P }.
$$$$                              g       g1         g
$$$$                               I1
$$$$
TABPT    PGI1,PG1,PG,,// $  
****CARD   1- 11, 22, 23, 26, 59- 62
$$$$
LABEL    SHIFT $
****CARD   1- 11, 22, 23, 26, 59- 62
$$$$              d         d             d
$$$$ ADD -[delta K  ] and [K  ] to form [K   ].
$$$$              gg        gg            gg1
$$$$ 
ADD      DKDGG,KDGG/KDGG1/(-1.0,0.0)/(1.0,0.0) $
****CARD   1- 11, 22, 23, 26, 59- 62
****FILE 152
$$$$               b                d         d
$$$$ Equivalence {U } to {U } and [K   ] to [K  ].
$$$$               g       g        gg1       gg
$$$$
EQUIV    UBGV,UGV/ALWAYS/KDGG1,KDGG/ALWAYS $
****CARD   1- 11, 22, 23, 26, 59- 62
****FILE 111,113
$$$$                                     d        d                  b
$$$$ Break the previous equivalence of [K  ] to [K   ] and {U } to {U }.
$$$$                                     gg       gg1        g       g
$$$$
EQUIV    KDGG,KDGG1/NEVER/UGV,UBGV/NEVER $  
****CARD   1- 11, 22, 23, 26, 59- 62
****FILE 120,152
$$$$ Go to label OUTLPTOP for an additional outer loop differential stiffness 
$$$$ iteration.
REPT     OUTLPTOP,1000 $
****CARD   1- 11, 22, 23, 26, 59- 62
$$$$                      d       d
$$$$ TABPT table prints [K   ], [K  ], and {u }.
$$$$                      gg1     gg         g
$$$$
TABPT    KDGG1,KDGG,UGV,,// $   
****CARD   1- 11, 22, 23, 26, 59- 62
$$$$
LABEL    DONE $ 
****CARD   1- 11, 22, 23, 26, 59- 62
$$$$
PARAM    //*NOP*/V,Y,KTOUT=-1 $ 
****CARD   1- 11, 22, 23, 26, 28, 59- 62
$$$$ Go to label JMPKTOUT if the total stiffness matrix [KTOTAL] is not to be 
$$$$ saved on an external file.
COND     JMPKTOUT,KTOUT $   
****CARD   1- 11, 22, 23, 26, 28, 59- 62
****FILE 153
$$$$                 d
$$$$ ADD [K  ] and [K  ] to form [KTOTAL].
$$$$       gg        gg
$$$$
ADD      KGG,KDGG/KTOTAL/(1.0,0.0)/CSIGN $  
****CARD   1- 11, 22, 23, 26, 28, 59- 62
****FILE 153
$$$$ OUTPUT1 outputs [KTOTAL] to an external file.
OUTPUT1  KTOTAL,,,,//C,Y,LOCATION=-1/C,Y,INPTUNIT=0 $   
****CARD   1- 11, 22, 23, 26, 28, 59- 62
$$$$ OUTPUT1 prints the names of the data blocks on the external file.
OUTPUT1, ,,,,//-3/0 $   
****CARD   1- 11, 22, 23, 26, 28, 59- 62
$$$$
LABEL    JMPKTOUT $ 
****CARD   1- 11, 22, 23, 26, 28, 59- 62
****FILE 153
$$$$ ALG generates final aerodynamic results and generates GRID and STREAML2 
$$$$ bulk data cards on the system punch file, if requested.
ALG      CASECC,EDT,EQEXIN,UBGV,ALGDB,CSTM,BGPDT/CASECCB,GEOM3B/
         -1/-1/V,Y,STREAML/V,Y,PGEOM/V,Y,IPRTCF/S,N,IFAIL/V,Y,SIGN/ 
         V,Y,ZORIGN/V,Y,FXCOOR/V,Y,FYCOOR/V,Y,FZCOOR $  
****CARD   1- 11, 22, 23, 26, 27, 59- 62
****FILE 131
$$$$ SDR2 calculates element forces (OEFB1) and stresses (OESB1) and prepares 
$$$$ displacement vectors (OUBGV1), and single-point forces of constraint 
$$$$ (OQBG1) for output and translation components of the displacement vector 
$$$$ (PUBGV1) for the differential stiffness  solution.
SDR2     CASECC,CSTM,MPT,DIT,EQEXIN,SIL,GPTT,EDT,BGPDP,,QBG,UBGV,EST,,, 
         PCOMPS/,OQBG1,OUBGV1,OESB1,OEFB1,PUBGV1,OESB1L,OEFB1L/ 
         *DS1*////COMPS $   
****CARD  18, 19
****FILE 121
$$$$ OFP formats the tables prepared by SDR2 and places them on the system 
$$$$ output file for printing.
OFP      OUBGV1,OQBG1,OEFB1,OESB1,,//S,N,CARDNO $   
****CARD  19
****FILE 121
$$$$
OFP      OEFB1L,OESB1L,,,,//S,N,CARDNO $
****CARD  19
****FILE 121
$$$$ SDR1 recovers dependent displacements after differential stiffness loop 
$$$$ for grid point force balance.
SDR1     USET,PG2,UBLV,,YS,GO,GM,PBS,KBFS,KBSS,/AUBGV,APGG,AQBG/
         1/*DS1* $  
****CARD   1- 11, 22, 23, 26, 59- 62
****FILE 154
$$$$ GPFDR calculates for requested sets the grid point force balance and 
$$$$ element strain energy for output.
GPFDR    CASECC,AUBGV,KELM,KDICT,ECT,EQEXIN,GPECT,APGG,AQBG/ONRGY1, 
         OGPFB1/*STATICS* $ 
****CARD   1- 11, 22, 23, 26, 59- 62
****FILE 155
$$$$
PURGE    KDICT,KELM/MINUS1 $
****CARD   1- 11, 22, 23, 26, 59- 62
****FILE 123
$$$$ OFP formats the tables prepared by GPFDR and places them on the system 
$$$$ output file for printing.
OFP      ONRGY1,OGPFB1,,,,//S,N,CARDNO $
****CARD   1- 11, 22, 23, 26, 59- 62
$$$$ Go to label P3 if no differential stiffness solution deformed plots are 
$$$$ requested.
COND     P3,JUMPPLOT $  
****SBST   7
****CARD  18
****FILE 156
$$$$ PLOT generates all requested differential stiffness solution deformed 
$$$$ structure and contour plots.
PLOT     PLTPAR,GPSETS,ELSETS,CASECC,BGPDT,EQEXIN,SIP,PUBGV1,,GPECT,
         OESB1,OESB1L,ONRGY1/PLOTX3/NSIL/LUSET/JUMPPLOT/PLTFLG/ 
         S,N,PFILE $
****SBST   7
****CARD  18
****FILE 156
$$$$ PRTMSG prints plotter data, engineering data, and contour data for each 
$$$$ differential stiffness solution deformed plot generated.
PRTMSG   PLOTX3// $ 
****SBST   7
****CARD  18
$$$$
LABEL    P3 $   
****SBST   7
****CARD  18
****FILE 156
$$$$ Go to label FINIS and make normal exit.
JUMP     FINIS $
****CARD   1- 19, 22- 24, 59- 62
****RFMT 187-189,191-204,207-209
$$$$
LABEL    ERROR1 $   
****CARD   1- 19, 22- 24, 59- 62
****RFMT 187-189,191-204,207-209
$$$$ Print Error Message No. 1 and terminate execution.
PRTPARM  //-1/*ASTA* $  
****CARD   1- 19, 22- 24, 59- 62
****RFMT 187-189,191-204,207-209
$$$$
LABEL    ERROR2 $   
****CARD   1- 19, 22- 24, 59- 62
****RFMT 187-189,191-204,207-209
$$$$ Print Error Message No. 2 and terminate execution.
PRTPARM  //-2/*ASTA* $  
****CARD   1- 19, 22- 24, 59- 62
****RFMT 187-189,191-204,207-209
$$$$
LABEL    ERROR3 $   
****CARD   1- 19, 22- 24, 59- 62
****RFMT 187-189,191-204,207-209
$$$$ Print Error Message No. 3 and terminate execution.
PRTPARM  //-3/*ASTA* $  
****CARD   1- 19, 22- 24, 59- 62
****RFMT 187-189,191-204,207-209
$$$$
LABEL    ERROR4 $   
****SBST   8
****CARD   1- 19, 22- 24, 59- 62
****RFMT 187-189,191-204,207-209
$$$$ Print Error Message No. 4 and terminate execution.
PRTPARM  //-4/*ASTA* $  
****SBST   8
****CARD   1- 19, 22- 24, 59- 62
****RFMT 187-189,191-204,207-209
$$$$
LABEL    ERROR5 $   
****CARD   1- 19, 22- 24, 59- 62
****RFMT 187-189,191-204,207-209
$$$$ Print Error Message No. 5 and terminate execution.
PRTPARM  //-5/*ASTA* $  
****CARD   1- 19, 22- 24, 59- 62
****RFMT 187-189,191-204,207-209
$$$$
LABEL    FINIS $
****CARD   1- 19, 22- 24, 59- 62
****RFMT 187-189,191-204,207-209
$$$$
PURGE    DUMMY/MINUS1 $ 
****CARD   1- 19, 22- 24, 59- 62
****RFMT 187-189,191-204,207-209
$$$$
END      $  
****CARD   1- 19, 22- 24, 59- 62
****RFMT 187-189,191-204,207-209
$$$$
$*CARD BITS 
  1    AXIC     AXIF     CELAS1   CELAS2   CELAS3   CELAS4   CMASS1 
  1    CMASS2   
  1    CMASS3   CMASS4   CORD1C   CORD1R   CORD1S   CORD2C   CORD2R 
  1    CORD2S   
  1    FREEPT   GRDSET   GRID     GRIDB    POINTAX  PRESPT   RINGAX 
  1    RINGFL   
  1    SECTAX   SEQGP    SPOINT 
  2    ADUM1    ADUM2    ADUM3    ADUM4    ADUM5    ADUM6    ADUM7  
  2    ADUM8
  2    ADUM9    BAROR    CBAR     CCONEAX  CDUM1    CDUM2    CDUM3  
  2    CDUM4
  2    CDUM5    CDUM6    CDUM7    CDUM8    CDUM9    CFLUID2  CFLUID3
  2    CFLUID4  
  2    CHEXA1   CHEXA2   CIHEX1   CIHEX2   CIHEX3   CONROD   CQDMEM 
  2    CQDMEM1  
  2    CQDMEM2  CQDPLT   CQUAD1   CQUAD2   CROD     CSHEAR   CTETRA 
  2    CTORDRG  CTRAPAX  CTRAPRG  CTRBSC   CTRIA1   CTRIA2   CTRIAAX
  2    CTRIARG  CQUAD4   CTRIA3
  2    CTRIM6   CTRMEM   CTRPLT   CTRPLT1  CTRSHL   CTUBE    CTWIST 
  2    CWEDGE   
  3    PBAR     PCONEAX  PDUM1    PDUM2    PDUM3    PDUM4    PDUM5  
  3    PDUM6
  3    PDUM7    PDUM8    PDUM9    PIHEX    PQDMEM   PQDMEM1  PQDMEM2
  3    PQDPLT   PQUAD1   PQUAD2   PROD     PSHEAR   PTORDRG  PTRAPAX
  3    PTRBSC   PSHELL   PCOMP    PCOMP1   PCOMP2
  3    PTRIA1   PTRIA2   PTRIAAX  PTRIM6   PTRMEM   PTRPLT   PTRPLT1
  3    PTRSHL   PTUBE    PTWIST 
  4    GENEL
  5    CONM1    CONM2   
  6    PELAS
  7    PMASS
  8    MAT1     MAT2     MAT3     MATT1    MATT2    MATT3    MAT8
  8    TABLEM1  TABLEM2  TABLEM3  TABLEM4  TEMPMT$  TEMPMX$  
  9    AXISYM$  CRIGD1   CRIGD2   CRIGD3   CRIGDR   CRROD    CRBAR  
  9    CRTRPLT  CRBE1    CRBE2    CRBE3    CRSPLINE MPC      MPCADD 
  9    MPCAX    MPC$
 10    SPC      SPC1     SPCADD   SPCAX    SPC$ 
 11    ASET     ASET1    OMIT     OMIT1    OMITAX   
 12    SUPAX    SUPORT  
 13    TEMP     TEMPAX   TEMPD    TEMPP1   TEMPP2   TEMPP3   TEMPRB 
 14    WTMASS   
 15    GRDPNT   
 16    PLOTEL   
 17    IRES 
 18    PLOT$
 19    POUT$
 22    LOOP$
 23    LOOP1$   
 24    COUPMASS CPBAR    CPQDPLT  CPQUAD1  CPQUAD2  CPROD    CPTRBSC
 24    CPTRIA1  CPTRIA2  CPTRPLT  CPTUBE
 26    APRESS   
 26    ATEMP
 26    DTI  
 26    FXCOOR   FYCOOR   FZCOOR 
 26    PGEOM
 26    SIGN     STREAML  STREAML1   
 26    ZORIGN   
 27    KTOUT
 59    DEFORM   DEFORM$  LOAD$    SPCD     RFORCE$  
 60    FORCE    FORCE1   FORCE2   FORCEAX  LOAD     MOMAX    MOMENT 
 60    MOMENT1  PLOAD4
 60    MOMENT2  PLOAD    PLOAD1   PLOAD2   PLOAD3   PRESAX   SLOAD  
 61    GRAV     RFORCE  
 62    TEMPLD$  
$$$$
$*FILE BITS 
 94    BGPDT    CSTM     EQEXIN   GPDT     GPL      SIL 
 95    ECT  
 96    GPTT     SLT 
 97    EST      GEI      GPECT    MPTX     PCOMPS   EPTX
 98    KGGX 
 99    MGG  
100    KGG  
101    ASET     RG       USET     YS       OGPST
102    GPST 
103    GM   
104    KNN  
105    KFF      KFS      KSS
106    GO       KAA      KOO      LOO   
107    LLL  
108    PG   
109    PL       PO       PS 
110    RULV     RUOV     ULV      UOOV  
111    PG1      QG       UGV
112    OEF1     OES1     OPG1     OQG1     OUGV1    PUGV1   
112    OEF1L    OES1L
113    KDGG 
114    KDNN 
115    KDFF     KDFS     KDSS   
116    KDAA 
117    KBLL 
118    LBLL 
119    UBLV     RUBLV   
120    QBG      UBGV
121    OEFB1    OESB1    OQBG1    OUBGV1   PUBGV1   
121    OEFB1L   OESB1L  
122    ELSETS   GPSETS   PLTPAR   PLTSETX   
123    KDICT    KELM     MDICT    MELM  
124    CASECCA1 GEOM3A1 
125    SLTA1    GPTTA1  
126    PGA1 
127    CASECCA  GEOM3A  
128    SLTA     GPTTA   
129    PGA  
130    PG2  
131    CASECCB  GEOM3B  
132    PGNA 
133    AUGV 
134    PGI2 
135    PLOTX1   
136    OGPWG
137    PLOTX2   
138    ECPT     GPCT
139    KBFS 
140    KBSS 
141    PSS  
142    PFS  
143    PN   
144    PGX  
145    PGG  
146    PG0  
147    PBO      PBS      PBL
148    UBGV     QBG 
149    DUGV 
150    DKDGG
151    PGI1 
152    KDGG1
153    KTOTAL   
154    AUBGV    APGG     AQBG   
155    ONRGY1   OGPFB1  
156    PLOTX3   
157    BGPDP    SIP 
$*  
=PAGE=
DISP17

APR.93  
$
$ NOTE: The DMAP sequence for static analysis involves use of parameters 
$ INTERACT and SYS21. These parameters are of relevance only when the 
$ primary purpose of the user is to make interactive restart runs. (The two 
$ parameters are then specified via the PARAM card in the bulk data deck.) 
$ However, these two parameters are not required for normal non-interactive 
$ batch runs. Consequently, the rigid format DMAP listing shown here was 
$ generated by not specifying those parameters (via the PARAM bulk data 
$ card). As a result, the COMPOFF and COMPON instructions using those  
$ parameters assume a value of 0 for these parameters (see Volume I, 
$ Section 5.7). 
BEGIN    DISP 01 - STATIC ANALYSIS - APR. 1993 $   
*     CARD   1- 20, 22- 24, 28, 31, 59- 62
*     RFMT 188-204,207-209
$
FILE     OPTP2=SAVE/EST1=SAVE $ 
*     SBST   9
*     CARD   1- 20, 22- 24, 28, 31, 59- 62
*     RFMT 188-204,207-209
$
FILE     QG=APPEND/PGG=APPEND/UGV=APPEND/GM=SAVE/KNN=SAVE $ 
*     SBST   1,  3
*     CARD   1- 20, 22- 24, 28, 31, 59- 62
*     RFMT 188-204,207-209
$
SETVAL   //V,Y,INTERACT/0/V,Y,SYS21/0 $ 
*     CARD   1- 20, 22- 24, 28, 31, 59- 62
$
PARAM    //*MPY*/CARDNO/0/0 $   
*     CARD   1-  3,  5- 10, 14, 15, 18, 19, 22- 24, 28, 61
*     FILE 101,114,119,121-125,127
*     PHS1   I1   
$ COMPOFF causes the DMAP compiler to compile the next instruction as the 
$ parameter INTERACT is 0. (See NOTE above.)
COMPOFF  1,INTERACT $   
*     CARD   1- 20, 22- 24, 28, 31, 59- 62
$
PRECHK   ALL $  
*     CARD   1- 20, 22- 24, 28, 31, 59- 62
*     RFMT 188-204,207-209
$ COMPON causes the DMAP compiler to skip the compilation of the next 
$ instruction as the parameter INTERACT is 0. (See NOTE above.) 
COMPON   1,INTERACT $   
*     CARD   1- 20, 22- 24, 28, 31, 59- 62
$
PRECHK   BGPDT,EQEXIN,SIL,SIP,ECT,GPECT,
         OUGV1,OES1,OEF1,OPG1,OQG1,PUGV1,   
         OUGV2,OES2,OEF2,OPG2,OQG2,DUMMY,   
         OES1L,OEF1L,ONRGY1 $   
*     CARD   1- 20, 22- 24, 28, 31, 59- 62
$ COMPOFF causes the DMAP compiler to compile all of the following 
$ instructions through LABEL LBLINT02 as the parameter SYS21 is 0. (See 
$ NOTE above.)
COMPOFF  LBLINT02,SYS21 $    
*     CARD   1-20,22-24,28,31,59-62   
$ GP1 generates coordinate system transformation matrices, tables of grid 
$ point locations, and tables relating the internal and external grid point 
$ numbers.
GP1      GEOM1,GEOM2,/GPL,EQEXIN,GPDT,CSTM,BGPDT,SIL/S,N,LUSET/ 
         NOGPDT/ALWAYS=-1 $ 
*     CARD   1
*     FILE  94
*     PHS2   D5   
$ PLTTRAN modifies special scalar grid points in the BGPDT and SIL tables.
PLTTRAN  BGPDT,SIL/BGPDP,SIP/LUSET/S,N,LUSEP $  
*     CARD   1
*     FILE 129
$ GP2 generates Element Connection Table with internal indices.
GP2      GEOM2,EQEXIN/ECT $ 
*     CARD   1,  2,  4,  5, 16
*     FILE  95
$
PARAML   PCDB//*PRES*////JUMPPLOT $ 
*     SBST   7
*     CARD  18
*     FILE 115,120
*     PHS2   DB5  
$
PURGE    PLTSETX,PLTPAR,GPSETS,ELSETS/JUMPPLOT $
*     SBST   7
*     CARD  18
*     FILE 115
$ Go to label P1 if there are no structure plot requests.
COND     P1,JUMPPLOT $  
*     SBST   7
*     CARD   1,  2,  4,  5, 16, 18
*     FILE 115,120
$ PLTSET transforms user input into a form used to drive the structure 
$ plotter.
PLTSET   PCDB,EQEXIN,ECT,EPT/PLTSETX,PLTPAR,GPSETS,ELSETS/S,N,NSIL/ 
         S,N,JUMPPLOT $ 
*     SBST   7
*     CARD   1,  2,  4,  5, 16, 18
*     FILE 115
$ PRTMSG prints error messages associated with the structure plotter.
PRTMSG   PLTSETX// $
*     SBST   7
*     CARD   1,  2,  4,  5, 16, 18
*     FILE 115
$
PARAM    //*MPY*/PLTFLG/1/1 $   
*     SBST   7
*     CARD   1,  2,  4,  5, 16, 18
*     FILE 120
$
PARAM    //*MPY*/PFILE/0/0 $
*     SBST   7
*     CARD   1,  2,  4,  5, 16, 18
*     FILE 120
$ Go to label P1 if no undeformed structure plots are requested.
COND     P1,JUMPPLOT $  
*     SBST   7
*     CARD   1,  2,  4,  5, 16, 18
*     FILE 120
$ PLOT generates all requested undeformed structure plots.
PLOT     PLTPAR,GPSETS,ELSETS,CASECC,BGPDT,EQEXIN,SIL,,ECT,,,,/PLOTX1/
         NSIL/LUSET/S,N,JUMPPLOT/S,N,PLTFLG/S,N,PFILE  $
*     SBST   7
*     CARD   1,  2,  4,  5, 16, 18
*     FILE 120
$ PRTMSG prints plotter data and engineering data for each undeformed plot 
$ generated.
PRTMSG   PLOTX1// $ 
*     SBST   7
*     CARD   1,  2,  4,  5, 16, 18
*     FILE 120
$
LABEL    P1 $   
*     SBST   7
*     CARD   1,  2,  4,  5, 16, 18
*     FILE 115,120
*     PHS2   DE5  
$ GP3 generates Static Loads Table and Grid Point Temperature Table.
GP3      GEOM3,EQEXIN,GEOM2/SLT,GPTT/S,N,NOGRAV/NEVER=1 $   
*     CARD   1,  2, 13, 60, 61
*     FILE  96
$
PARAM    //*AND*/NOMGG/NOGRAV/V,Y,GRDPNT=-1 $   
*     CARD   1,  2, 15, 61
*     FILE  96, 99
*     RFMT 188-204,207-209
$ TA1 generates element tables for use in matrix assembly and stress 
$ recovery.
TA1      ECT,EPT,BGPDT,SIL,GPTT,CSTM,MPT,EQEXIN/EST,GEI,GPECT,,,MPTX,
         PCOMPS,EPTX/LUSET/S,N,NOSIMP/1/S,N,NOGENL/GENEL/S,N,COMPS $   
*     CARD   1-  6, 13, 16
*     FILE  97
$
EQUIV    MPTX,MPT/COMPS/EPTX,EPT/COMPS $
*     CARD   1- 20, 22- 24, 28, 31, 59- 62
*     FILE  97
$
PARAM    //*AND*/NOELMT/NOGENL/NOSIMP $ 
*     CARD   1,  2,  4-  6, 16
*     FILE  97
*     PHS2   DB5  
*     RFMT 188-204,207-209
$ Go to label ERROR4 and print Error Message No. 4 if no elements have been 
$ defined.
COND     ERROR4,NOELMT $
*     CARD   1,  2,  4-  6, 16
*     FILE  97
*     PHS2   DE5  
*     RFMT 188-204,207-209
$
PURGE    KGGX/NOSIMP $  
*     CARD   1,  2,  4-  6, 16
*     FILE  98
$ OPTPR1 performs phase one property optimization and initialization check.
OPTPR1   MPT,EPT,ECT,DIT,EST/OPTP1/S,N,PRINT/S,N,TSTART/S,N,COUNT $ 
*     SBST   9
*     CARD   1-  6,  8, 13
*     FILE 117
$ Beginning of loop for property optimization.
LABEL    LOOPTOP $  
*     SBST   9
*     CARD   1-  6
*     FILE 117
$ Go to label LBL1 if there are no structural elements.
COND     LBL1,NOSIMP $  
*     CARD   1-  3,  5,  6,  8, 13- 16, 24, 61
*     FILE  98, 99,116,121
$
PARAM    //*ADD*/NOKGGX/1/0 $   
*     CARD   1-  3,  6,  8
*     FILE  98
$
EQUIV    OPTP1,OPTP2/NEVER/EST,EST1/NEVER $ 
*     SBST   9
*     CARD   1-  6, 13, 16
*     FILE 118
*     PHS2   D5   
$ EMG generates structural element stiffness and mass matrix tables and 
$ dictionaries for later assembly by the EMA module.
EMG      EST,CSTM,MPT,DIT,GEOM2,/KELM,KDICT,MELM,MDICT,,,/S,N,NOKGGX/   
         S,N,NOMGG////C,Y,COUPMASS/C,Y,CPBAR/   
         C,Y,CPROD/C,Y,CPQUAD1/C,Y,CPQUAD2/C,Y,CPTRIA1/C,Y,CPTRIA2/ 
         C,Y,CPTUBE/C,Y,CPQDPLT/C,Y,CPTRPLT/C,Y,CPTRBSC/
         C,Y,VOLUME/C,Y,SURFACE $   
*     CARD   1-  3,  5,  6,  8, 13, 15, 24, 61
*     FILE 116
$ Go to label JMPKGG if no stiffness matrix is to be assembled.
COND     JMPKGG,NOKGGX $
*     CARD   1-  3,  6,  8
*     FILE  98
$                                  x
$ EMA assembles stiffness matrix [K  ] and Grid Point Singularity Table.
$                                  gg
$ 
EMA      GPECT,KDICT,KELM/KGGX $
*     CARD   1-  3,  6,  8
*     FILE  98
$
LABEL    JMPKGG $   
*     CARD   1-  3,  6,  8
*     FILE  98
$
PURGE    MGG/NOMGG $
*     CARD   1-  3,  5,  8, 14, 15, 24, 61
*     FILE  99
$ Go to label JMPMGG if no mass matrix is to be assembled.
COND     JMPMGG,NOMGG $ 
*     CARD   1-  3,  5,  8, 14, 15, 24, 61
*     FILE  99
$ EMA assembles mass matrix [M  ].
$                             gg
$ 
EMA      GPECT,MDICT,MELM/MGG/-1/C,Y,WTMASS=1.0 $   
*     CARD   1-  3,  5,  8, 14, 15, 24, 61
*     FILE  99
$
PURGE    MDICT,MELM/ALWAYS $
*     CARD   1-  3,  5,  8, 14, 15, 24, 61
*     FILE 116
$
LABEL    JMPMGG $   
*     CARD   1-  3,  5,  8, 14, 15, 24, 61
*     FILE  99
$ Go to label LBL1 if no weight and balance information is requested.
COND     LBL1,GRDPNT $  
*     SBST   8
*     CARD   1-  3,  5,  8, 14, 15, 24, 61
*     FILE 121
$ Go to label ERROR2 and print Error Message No. 2 if no mass matrix 
$ exists.
COND     ERROR2,NOMGG $ 
*     SBST   8
*     CARD   1-  3,  5,  8, 14, 15, 24, 61
*     FILE 121
$ GPWG generates weight and balance information.
GPWG     BGPDP,CSTM,EQEXIN,MGG/OGPWG/V,Y,GRDPNT/C,Y,WTMASS $
*     SBST   8
*     CARD   1-  3,  5,  8, 14, 15, 24, 61
*     FILE 121
$ OFP formats the weight and balance information prepared by GPWG and 
$ places it on the system output file for printing.
OFP      OGPWG,,,,,//S,N,CARDNO $   
*     SBST   8
*     CARD   1-  3,  5,  8, 14, 15, 24, 61
*     FILE 121
$
LABEL    LBL1 $ 
*     CARD   1-  3,  5,  8, 13- 16, 24, 61
*     FILE  98, 99,116,121
$               x
$ Equivalence [K  ] to [K  ] if no general elements exist.
$               gg       gg
$ 
EQUIV    KGGX,KGG/NOGENL $  
*     CARD   1-  4,  6,  8
*     FILE 100
*     PHS2   DB5  
$ Go to label LBL11A if no general elements exist.
COND     LBL11A,NOGENL $
*     CARD   1-  4,  6,  8
*     FILE 100
$                                 x
$ SMA3 adds general elements to [K  ] to obtain stiffness matrix [K  ].
$                                 gg                               gg
$ 
SMA3     GEI,KGGX/KGG/LUSET/NOGENL/NOSIMP $ 
*     CARD   1-  4,  6,  8
*     FILE 100
$
LABEL    LBL11A $   
*     CARD   1-  4,  6,  8
*     FILE 100
$
GPSTGEN  KGG,SIL/GPST $ 
*     CARD   1-  4,  6,  8
*     FILE 102
*     PHS2   DE5  
$
PARAM    //*MPY*/NSKIP/0/0 $
*     CARD   1,  9- 12, 22, 23, 31, 59
*     FILE 101
$ Beginning of loop for multiple constraint sets.
LABEL    LBL11 $
*     SBST   1,  3
*     CARD  22, 23
*     FILE 101
$ GP4 generates flags defining members of various displacement sets 
$ (USET), forms multipoint constraint equations [R ] {u } = 0, and forms 
$                                                 g    g
$ enforced displacement vector {Y }.
$                                s
$ 
GP4      CASECC,GEOM4,EQEXIN,GPDT,BGPDT,CSTM,GPST/RG,YS,USET,   
         ASET,OGPST/LUSET/S,N,MPCF1/S,N,MPCF2/S,N,SINGLE/S,N,OMIT/  
         S,N,REACT/S,N,NSKIP/S,N,REPEAT/S,N,NOSET/S,N,NOL/S,N,NOA/  
         C,Y,ASETOUT/C,Y,AUTOSPC $  
*     CARD   1-  4,  6,  8- 12, 20, 22, 23, 28, 31, 59
*     FILE 101
$
OFP      OGPST,,,,,//S,N,CARDNO $   
*     CARD   1-  4,  6,  8- 12, 22, 23, 28
*     FILE 101
$ Go to label ERROR3 and print Error Message No. 3 if no independent 
$ degrees of freedom are defined.
COND     ERROR3,NOL $   
*     CARD   1,  9- 12, 22, 23, 59
*     FILE 101
*     RFMT 188-204,207-209
*     PHS1   I1   
$
PARAM    //*AND*/NOSR/SINGLE/REACT $
*     CARD   1,  9- 12, 22, 23, 59
*     FILE 101
$
PURGE    KRR,KLR,QR,DM/REACT/GM/MPCF1/GO,KOO,LOO,PO,UOOV,RUOV/OMIT/PS,  
         KFS,KSS/SINGLE/QG/NOSR $   
*     CARD   1,  9- 12, 22, 23, 59
*     FILE 103,105-107,109,111,113
$ Equivalence [K  ] to [K  ] if no multipoint constraints exist.
$               gg       nn
EQUIV    KGG,KNN/MPCF1 $
*     CARD   1-  4,  6,  8,  9, 22, 23
*     FILE 104
$ Go to label LBL2 if the MPC set for the current pass is unchanged from 
$ that of the previous pass.
COND     LBL2,MPCF2 $   
*     CARD   1-  4,  6,  8,  9, 22, 23
*     FILE 103,104
$ MCE1 partitions multipoint constraint equations [R ] = [R |R ] and
$                                                   g      m  n
$ solves for multipoint constraint transformation matrix [G ] =
$       -1                                                 m
$ - [R ]   [R ].
$     m      n
$ 
MCE1     USET,RG/GM $   
*     CARD   1,  9, 22, 23
*     FILE 103
$ MCE2 partitions stiffness matrix
$ 
$           +_      +
$           |K  |K  |
$           | nn| nm|
$   [K  ] = |---+---|
$     gg    |K  |K  |
$           | mn| mm|
$           +       +
$           
$ and performs the matrix reduction
$ 
$            _        T           T           T
$   [K  ] = [K  ] + [G ][K  ] + [K  ][G ] + [G ][K  ][G ]
$     nn      nm      m   mn      mn   m      m   mm   m
$ 
MCE2     USET,GM,KGG,,,/KNN,,, $
*     CARD   1-  4,  6,  8,  9, 22, 23
*     FILE 104
$
LABEL    LBL2 $ 
*     CARD   1-  4,  6,  8,  9, 22, 23
*     FILE 103,104
$ Equivalence [K  ] to [K  ] if no single-point constraints exist.
$               nn       ff
$ 
EQUIV    KNN,KFF/SINGLE $   
*     CARD   1-  4,  6,  8- 10, 22, 23
*     FILE 105
$ Go to label LBL3 if no single-point constraints exist.
COND     LBL3,SINGLE $  
*     CARD   1-  4,  6,  8- 10, 22, 23
*     FILE 105
$ SCE1 partitions out single-point constraints.
$ 
$           +       +
$           |K  |K  |
$           | ff| fs|
$   [K  ] = |---+---|
$     nn    |K  |K  |
$           | sf| ss|
$           +       +
$ 
SCE1     USET,KNN,,,/KFF,KFS,KSS,,, $   
*     CARD   1-  4,  6,  8- 10, 22, 23
*     FILE 105
$
LABEL    LBL3 $ 
*     CARD   1-  4,  6,  8- 10, 22, 23
*     FILE 105
$ Equivalence [K  ] to [K  ] if no omitted coordinates exist.
$               ff       aa
$ 
EQUIV    KFF,KAA/OMIT $ 
*     CARD   1-  4,  6,  8- 11, 22, 23
*     FILE 106
$ Go to label LBL5 if no omitted coordinates exist.
COND     LBL5,OMIT $
*     CARD   1-  4,  6,  8- 11, 22, 23
*     FILE 106
$ SMP1 partitions constrained stiffness matrix
$
$           +_      +
$           |K  |K  |
$           | aa| ao|
$   [K  ] = |---+---|
$     ff    |K  |K  |
$           | oa| oo|
$           +       +
$           
$                                                -1
$ solves for transformation matrix [G ] = -[[K  ]  [K  ]
$                                    o        oo     oa
$ 
$                                        _        T
$ and performs matrix reduction [K  ] = [K  ] + [K  ]{G ].
$                                 aa      aa      oa   o
$ 
SMP1     USET,KFF,,,/GO,KAA,KOO,LOO,,,,, $  
*     CARD   1-  4,  6,  8- 11, 22, 23
*     FILE 106
$
LABEL    LBL5 $ 
*     CARD   1-  4,  6,  8- 11, 22, 23
*     FILE 106
$ Equivalence [K  ] to [K  ] if no free-body supports exist.
$               aa       ll
$ 
EQUIV    KAA,KLL/REACT $
*     CARD   1-  4,  6,  8- 12, 22, 23
*     FILE 107
*     PHS1   DB1  
*     PHS3   DB1  
$ Go to label LBL6 if no free-body supports exist.
COND     LBL6,REACT $   
*     CARD   1-  4,  6,  8- 12, 22, 23
*     FILE 107
$ RBMG1 partitions out free-body supports
$ 
$           +       +
$           |K  |K  |
$           | ll| lr|
$   [K  ] = |---+---|
$     aa    |K  |K  |
$           | rl| rr|
$           +       +
$ 
RBMG1    USET,KAA,/KLL,KLR,KRR,,, $ 
*     CARD   1-  4,  6,  8- 12, 22, 23
*     FILE 107
$
LABEL    LBL6 $ 
*     CARD   1-  4,  6,  8- 12, 22, 23
*     FILE 107
$ RBMG2 decomposes constrained stiffness matrix [K  ] = [L  ][U  ].
$                                                 ll      ll   ll
$ 
RBMG2    KLL/LLL $  
*     CARD   1-  4,  6,  8- 12, 22, 23
*     FILE 108
$ Go to label LBL7 if no free-body supports exist.
COND     LBL7,REACT $   
*     CARD   1-  4,  6,  8- 12, 22, 23
*     FILE 109
$ RBMG3 forms rigid body transformation matrix
$ 
$               -1
$   [D] = -[K  ]  [K  ]
$            ll     lr
$ 
$ calculates rigid body check matrix
$
$                   T
$   [X] = [K  ] + [K  ][D]
$           rr      lr
$ 
$ and calculates rigid body error ratio
$ 
$ 
$               ||X||
$   epsilon = ---------
$              ||K  ||
$                 rr
$ 
RBMG3    LLL,KLR,KRR/DM $   
*     CARD   1-  4,  6,  8- 12, 22, 23
*     FILE 109
$
LABEL    LBL7 $ 
*     CARD   1-  4,  6,  8- 12, 22, 23
*     FILE 109
*     PHS1   DE1  
*     PHS3   DE1  
$ SSG1 generates static load vectors {P }.
$                                      g
$ 
SSG1     SLT,BGPDT,CSTM,SIL,EST,MPT,GPTT,EDT,MGG,CASECC,DIT,PCOMPS/ 
         PG,,,,/LUSET/NSKIP/COMPS $ 
*     CARD   1-  3,  5,  6,  8, 13, 22, 23, 59- 62
*     FILE 110
$ Equivalence {P } to {P } if no constraints are applied.
$               g       l
$ 
EQUIV    PG,PL/NOSET $  
*     CARD   1-  3,  5,  6,  8- 13, 22, 23, 59- 62
*     FILE 111
*     PHS1   DB1  
$ Go to label LBL10 if no constraints are applied.
COND     LBL10,NOSET $  
*     CARD   1-  3,  5,  6,  8- 13, 22, 23, 59- 62
*     FILE 111
*     PHS3   DB7  
$ SSG2 applies constraints to static load vectors
$ 
$           _
$           P
$            n            _       T
$   {P } = {--} , {P } = {P } + [G ]{P }
$     g     P       n      n      m   m
$            m
$ 
$           _
$           P
$            f            _      
$   {P } = {--} , {P } = {P } - [K  ]{Y }
$     n     P       f      f      fs   s
$            s
$ 
$           _
$           P
$            a            _       T
$   {P } = {--} , {P } = {P } + [G ]{P }
$     f     P       a      a      o   o
$            o
$ 
$           P
$            l
$   {P } = {--}
$     a     P
$            r
$                                                                T
$ and calculates determinate forces of reaction {q } = -{P } - [D ]{P }.
$                                                 r       r          l
$ 
SSG2     USET,GM,YS,KFS,GO,DM,PG/QR,PO,PS,PL $  
*     CARD   1-  3,  5,  6,  8- 13, 22, 23, 59- 62
*     FILE 111
$
LABEL    LBL10 $
*     CARD   1-  3,  5,  6,  8- 13, 22, 23, 59- 62
*     FILE 111
$ SSG3 solves for displacements of independent coordinates
$ 
$               -1
$   {u } = [K  ]  {P }
$     l      ll     l
$ 
$ solves for displacements of omitted coordinates
$ 
$     o         -1
$   {u } = [K  ]  {P }
$     o      oo     o
$ 
$ calculates residual vector (RULV) and residual vector error ratio for 
$ independent coordinates
$ 
$   {deltaP } = {P } - [K  ]{u }
$          l      l      ll   l
$ 
$                T
$              {u }{deltaP }
$                l        l
$   epsilon  = -------------
$          l       T
$                {P }{u }
$                  l   l
$ 
$ and calculates residual vector (RUOV) and residual vector error ratio for
$ omitted coordinates
$                             o
$   {deltaP } = {P } - [K  ]{u }
$          o      o      oo   o
$ 
$                T
$              {u }{deltaP }
$                o        o
$   epsilon  = -------------
$          o       T   o
$                {P }{u }
$                  o   o
$ 
SSG3     LLL,KLL,PL,LOO,KOO,PO/ULV,UOOV,RULV,RUOV/OMIT/V,Y,IRES=-1/ 
         NSKIP/S,N,EPSI $   
*     CARD   1-  6,  8- 13, 17, 22, 23, 59- 62
*     FILE 112
*     RFMT 188
$ Go to label LBL9 if residual vectors are not to be printed.
COND     LBL9,IRES $
*     CARD   1-  6,  8- 13, 17, 22, 23, 59- 62
*     RFMT 188-204,207-209
$ MATGPR prints the residual vector for independent coordinates (RULV).
MATGPR   GPL,USET,SIL,RULV//*L* $   
*     CARD   1-  6,  8- 13, 17, 22, 23, 59- 62
*     RFMT 188-204,207-209
$ MATGPR prints the residual vector for omitted coordinates (RUOV).
MATGPR   GPL,USET,SIL,RUOV//*O* $   
*     CARD   1-  6,  8- 13, 17, 22, 23, 59- 62
*     RFMT 188-204,207-209
$
LABEL    LBL9 $ 
*     CARD   1-  6,  8- 13, 17, 22, 23, 59- 62
*     RFMT 188-204,207-209
*     PHS3   DE7  
$ SDR1 recovers dependent displacements
$ 
$    u
$     l                                  o
$   {--} = {u }  ,   {u } = [G ]{u ] + {u }  ,
$    u       a         o      o   a      o
$     r
$ 
$    u                u            
$     a                f           
$   {--} = {u }  ,   {--} = {u }  ,
$    u       f        Y       n    
$     o                s           
$                                  
$                        u         
$                         n        
$  {u } = [G ]{u ]  ,   {--} = {u }
$    m      m   n        u       g 
$                         m        
$ 
$ and recovers single-point forces of constraint
$ 
$                    T
$   {q } = -{P } + [K  ]{u } + [K  ]{Y }
$     s       s      fs   f      ss   s
$ 
SDR1     USET,PG,ULV,UOOV,YS,GO,GM,PS,KFS,KSS,QR/UGV,PGG,QG/NSKIP/  
         *STATICS* $
*     CARD   1-  6,  8- 13, 22, 23, 59- 62
*     FILE 113
*     RFMT 188-204,207-209
*     PHS3   I7     
$ Go to label LBL8 if all constraint sets have been processed.
COND     LBL8,REPEAT $  
*     SBST   1,  3
*     CARD  22, 23
*     RFMT 188-204,207-209
$ Go to label LBL11 if additional sets of constraints need to be processed.
REPT     LBL11,360 $
*     SBST   1,  3
*     CARD  22, 23
*     RFMT 188-204,207-209
$ Go to label ERROR1 and print Error Message No. 1 if the number of 
$ constraint sets exceeds 360.
JUMP     ERROR1 $   
*     SBST   1,  3
*     CARD  22, 23
*     RFMT 188-204,207-209
$
PARAM    //*NOT*/TEST/REPEAT $  
*     CARD  22, 23
*     RFMT 188-204,207-209
$ Go to label ERROR5 and print Error Message No. 5 if multiple boundary 
$ conditions are attempted with an improper subset.
COND     ERROR5,TEST $  
*     CARD  22, 23
*     RFMT 188-204,207-209
$
LABEL    LBL8 $ 
*     SBST   1,  3
*     CARD  22, 23
*     RFMT 188-204,207-209
$ GPFDR calculates the grid point force balance (OGPFB1) and element strain 
$ energy (ONRGY1) for requested sets.
GPFDR    CASECC,UGV,KELM,KDICT,ECT,EQEXIN,GPECT,PGG,QG/ONRGY1,OGPFB1/   
         *STATICS* $
*     CARD  18, 19
*     FILE 119
*     PHS2   DB5  
$
PURGE    KDICT,KELM/REPEAT $
*     CARD   1-  3,  6,  8, 18, 19
*     FILE 116
$ OFP formats the tables prepared by GPFDR and places them on the system 
$ output file for printing.
OFP      ONRGY1,OGPFB1,,,,//S,N,CARDNO $
*     CARD  18, 19
*     FILE 119
$ Go to label NOMPCF if no multipoint constraint force balance is 
$ requested.
COND     NOMPCF,GRDEQ $ 
*     CARD   7
*     FILE 127
$ EQMCK calculates the force and moment equilibrium check and prepares the 
$ multipoint constraint force balance (OQM1) for output.
EQMCK    CASECC,EQEXIN,GPL,BGPDT,SIL,USET,KGG,GM,UGV,PGG,QG,CSTM/   
         OQM1/V,Y,OPT=0/V,Y,GRDEQ/NSKIP $   
*     CARD   7
*     FILE 127
$ OFP formats the table prepared by EQMCK and places it on the system 
$ output file for printing.
OFP      OQM1,,,,,//S,N,CARDNO $
*     CARD   7
*     FILE 127
$
LABEL    NOMPCF $   
*     CARD   7
*     FILE 127
$ SDR2 calculates the element forces (OEF1) and stresses (OES1) and 
$ prepares load vectors (OPG1), displacement vectors (OUGV1), and single-
$ point forces of constraint (OQG1) for output and translation components 
$ of the displacement vectors (PUGV1).
SDR2     CASECC,CSTM,MPT,DIT,EQEXIN,SIL,GPTT,EDT,BGPDP,,QG,UGV,EST, 
         XYCDB,PGG,PCOMPS/OPG1,OQG1,OUGV1,OES1,OEF1,PUGV1,OES1L,
         OEF1L/*STATICS*/S,N,NOSORT2/-1/S,N,STRNFLG/COMPS $ 
*     CARD  18, 19
*     FILE 114
$ Go to label LBLSTRS if element stresses in material coordinate system and 
$ stresses at the connected grid points are not to be calculated.
COND     LBLSTRS,STRESS $   
*     CARD  18, 19
*     FILE 122
$ CURV calculates element stresses in material coordinate system (OES1M) 
$ and stresses at the connected grid points (OES1G).
CURV     OES1,MPT,CSTM,EST,SIL,GPL/OES1M,OES1G/V,Y,STRESS/  
         V,Y,NINTPTS $  
*     CARD  18, 19
*     FILE 122
$
LABEL    LBLSTRS $  
*     CARD  18, 19
*     FILE 122
$
PURGE    OES1M/STRESS $ 
*     CARD  18, 19
*     FILE 122
$ Go to label LBLSTRN if element strains/curvatures are not to be 
$ calculated.
COND     LBLSTRN,STRNFLG $  
*     CARD  18, 19
*     FILE 123,124
$ SDR2 calculates element strains/curvatures (OES1A).
SDR2     CASECC,CSTM,MPT,DIT,EQEXIN,SIL,GPTT,EDT,BGPDT,,,UGV,EST,,,/
         ,,,OES1A,,,,/*STATICS*//1 $
*     CARD  18, 19
*     FILE 123
$ Go to label LBLSTRN if element strains/curvatures in material coordinate 
$ system and strains/curvatures at the connected grid points are not to be 
$ calculated.
COND     LBLSTRN,STRAIN $   
*     CARD  18, 19
*     FILE 124
$ CURV calculates element strains/curvatures in material coordinate system 
$ (OES1AM) and strains/curvatures at the connected grid points (OES1AG).
CURV     OES1A,MPT,CSTM,EST,SIL,GPL/OES1AM,OES1AG/V,Y,STRAIN/   
         V,Y,NINTPTS $  
*     CARD  18, 19
*     FILE 124
$
LABEL    LBLSTRN $  
*     CARD  18, 19
*     FILE 123,124
$
PURGE    OES1A/STRNFLG $
*     CARD  18, 19
*     FILE 123,124
$ Go to label LBL17 if there are no requests for output sorted by grid 
$ point number or element number.
COND     LBL17,NOSORT2 $
*     CARD  18, 19, 29
*     FILE 125,126
$ SDR3 prepares requested output sorted by grid point number or element 
$ number.
SDR3     OUGV1,OPG1,OQG1,OEF1,OES1,/OUGV2,OPG2,OQG2,OEF2,OES2, $
*     CARD  18, 19
*     FILE 125
$
PARAM    //*SUB*/PRTSORT2/NOSORT2/1 $   
*     CARD  18, 19
*     FILE 125
$ Go to label LBLSORT1 if printed output sorted by grid point number or 
$ element number is not required.
COND     LBLSORT1,PRTSORT2 $
*     CARD  18, 19
*     FILE 125
$ OFP formats the tables prepared by SDR3 for output sorted by grid point 
$ number or element number and places them on the system output file for 
$ printing.
OFP      OUGV2,OPG2,OQG2,OEF2,OES2,//S,N,CARDNO $   
*     CARD  18, 19
*     FILE 125
$ SCAN examines the element stresses and forces calculated by SDR3 and 
$ generates scanned output that meets the specifications set by the user.
SCAN     CASECC,OES2,OEF2/OESF2/*RF* $  
*     CARD 19 
*     FILE 125
$ OFP formats the scanned output table prepared by SCAN and places it on 
$ the system output file for printing.
OFP      OESF2,,,,,//S,N,CARDNO $   
*     CARD  19
*     FILE 125
$ Go to label LBLXYPLT.
JUMP     LBLXYPLT $ 
*     CARD  18, 19
*     FILE 125
$
LABEL    LBLSORT1 $ 
*     CARD  18, 19
*     FILE 125
$ OFP formats the tables prepared by SDR2 for output sorted by subcase 
$ number and places them on the system output file for printing.
OFP      OUGV1,OPG1,OQG1,OEF1,OES1,//S,N,CARDNO $   
*     CARD  18, 19
*     FILE 114
$
OFP      OEF1L,OES1L,,,,//S,N,CARDNO $  
*     CARD  18, 19
*     FILE 114
$ SCAN examines the element stresses and forces calculated by SDR2 and 
$ generates scanned output that meets the specifications set by the user.
SCAN     CASECC,OES1,OEF1/OESF1/*RF* $  
*     CARD 19 
*     FILE 114
$ OFP formats the scanned output table prepared by SCAN and places it on 
$ the system output file for printing.
OFP      OESF1,,,,,//S,N,CARDNO $   
*     CARD  19
*     FILE 114
$
LABEL    LBLXYPLT $ 
*     CARD  18, 19
*     FILE 125
$ OFP formats the tables prepared by CURV and SDR2 for output sorted by 
$ subcase number and places them on the system output file for printing.
OFP      OES1M,OES1G,OES1A,OES1AM,OES1AG,//S,N,CARDNO $ 
*     CARD  18, 19
*     FILE 114
$ XYTRAN prepares the input for requested X-Y plots.
XYTRAN   XYCDB,OPG2,OQG2,OUGV2,OES2,OEF2/XYPLTT/*TRAN*/*PSET*/S,N,  
         PFILE/S,N,CARDNO $ 
*     SBST   7
*     CARD  29
*     FILE 126
$ XYPLOT prepares the requested X-Y plots of displacements, forces, 
$ stresses, loads, and single-point forces of constraint vs. subcase.
XYPLOT   XYPLTT// $ 
*     SBST   7
*     CARD  29
*     FILE 126
$ Go to label DPLOT.
JUMP     DPLOT $
*     SBST   7
*     CARD  29
*     FILE 126
$
LABEL    LBL17 $
*     CARD  18, 19, 29
*     FILE 125,126
$
PURGE    OUGV2/NOSORT2 $
*     CARD  18, 19
*     FILE 125,126
$ Go to label LBLOFP if there is no phase two property optimization.
COND     LBLOFP,COUNT $ 
*     SBST   9
*     CARD  18, 19
*     FILE 118
$ OPTPR2 performs phase two property optimization.
OPTPR2   OPTP1,OES1,EST/OPTP2,EST1/S,N,PRINT/TSTART/S,N,COUNT/S,N,  
         CARDNO $   
*     SBST   9
*     CARD  18, 19
*     FILE 118
$ Equivalence EST2 to EST and OPTP2 to OPTP1.
EQUIV    EST1,EST/ALWAYS/OPTP2,OPTP1/ALWAYS $   
*     SBST   9
*     CARD  18, 19
*     FILE  97,117
$ Go to label LOOPEND if no additional output is to be printed for this 
$ loop.
COND     LOOPEND,PRINT $
*     SBST   9
*     CARD  18, 19
*     FILE 118,128
$
LABEL    LBLOFP $   
*     SBST   9
*     CARD  18, 19
*     FILE 118
$ OFP formats the tables prepared by SDR2 for output sorted by subcase 
$ number and places them on the system output file for printing.
OFP      OUGV1,OPG1,OQG1,OEF1,OES1,//S,N,CARDNO $   
*     CARD  19
*     FILE 114
$
OFP      OEF1L,OES1L,,,,//S,N,CARDNO $  
*     CARD  19
*     FILE 114
$ SCAN examines the element stresses and forces calculated by SDR2 and 
$ generates scanned output that meets the specifications set by the user.
SCAN     CASECC,OES1,OEF1/OESF1X/*RF* $ 
*     CARD 19 
*     FILE 114
$ OFP formats the scanned output table prepared by SCAN and places it on 
$ the system output file for printing.
OFP      OESF1X,,,,,//S,N,CARDNO $  
*     CARD  19
*     FILE 114
$ OFP formats the tables prepared by CURV and SDR2 for output sorted by 
$ sucbcase number and places them on the system output file for printing.
OFP      OES1M,OES1G,OES1A,OES1AM,OES1AG,//S,N,CARDNO $ 
*     CARD  19
*     FILE 122-124
$
LABEL    DPLOT $
*     SBST   7
*     CARD  18, 29
*     FILE 126
$ Go to label P2 if no deformed structure plots are requested.
COND     P2,JUMPPLOT $  
*     SBST   7
*     CARD  18
*     FILE 128
$ PLOT generates all requested deformed structure and contour plots.
PLOT     PLTPAR,GPSETS,ELSETS,CASECC,BGPDT,EQEXIN,SIP,PUGV1,,GPECT,OES1,
         OES1L,ONRGY1/PLOTX2/NSIL/LUSEP/JUMPPLOT/PLTFLG/S,N,PFILE $ 
*     SBST   7
*     CARD  18, 29
*     FILE 128
$ PRTMSG prints plotter data, engineering data, and contour data for each 
$ deformed plot generated.
PRTMSG   PLOTX2// $ 
*     SBST   7
*     CARD  18, 29
*     FILE 128
$
LABEL    P2 $   
*     SBST   7
*     CARD  18
*     FILE 128
$
LABEL    LOOPEND $  
*     SBST   9
*     CARD  18, 22, 23
*     FILE 128
*     PHS1   DE1  
*     PHS2   DE5  
$ Go to label FINIS and make normal exit if property optimization is 
$ complete.
COND     FINIS,COUNT $  
*     SBST   9
*     CARD  18, 22, 23
$ Go to label LOOPTOP if additional loops for property optimization are 
$ needed.
REPT     LOOPTOP,360 $  
*     SBST   9
*     CARD  18, 22, 23
$ Go to label FINIS and make normal exit.
JUMP     FINIS $
*     CARD   1- 20, 22- 24, 28, 29, 31, 59- 62
*     RFMT 188-204,207-209
$
LABEL    ERROR1 $   
*     SBST   1,  3
*     CARD  22, 23
*     RFMT 188-204,207-209
$ Print Error Message No. 1 and terminate execution.
PRTPARM  //-1/*STATICS* $   
*     SBST   1,  3
*     CARD  22, 23
*     RFMT 188-204,207-209
$
LABEL    ERROR2 $   
*     SBST   8
*     CARD   1-  3,  5,  8, 14, 15, 24, 61
*     FILE 121
*     RFMT 188-204,207-209
$ Print Error Message No. 2 and terminate execution.
PRTPARM  //-2/*STATICS* $   
*     SBST   8
*     CARD   1-  3,  5,  8, 14, 15, 24, 61
*     FILE 121
*     RFMT 188-204,207-209
$ 
LABEL    ERROR3 $   
*     CARD   1,  9- 12, 22, 23, 59
*     FILE 101
*     RFMT 188-204,207-209
$ Print Error Message No. 3 and terminate execution.
PRTPARM  //-3/*STATICS* $   
*     CARD   1,  9- 12, 22, 23, 59
*     FILE 101
*     RFMT 188-204,207-209
$
LABEL    ERROR4 $   
*     CARD   1,  2,  4-  6, 16
*     FILE  97
*     RFMT 188-204,207-209
$ Print Error Message No. 4 and terminate execution.
PRTPARM  //-4/*STATICS* $   
*     CARD   1,  2,  4-  6, 16
*     FILE  97
*     RFMT 188-204,207-209
$
LABEL    ERROR5 $   
*     CARD  22, 23
*     RFMT 188-204,207-209
$ Print Error Message No. 5 and terminate execution.
PRTPARM  //-5/*STATICS* $   
*     CARD  22, 23
*     RFMT 188-204,207-209
$
LABEL    FINIS $
*     CARD   1- 24, 28, 29, 31, 59- 62
*     RFMT 188-204,207-209
$
PURGE    DUMMY/ALWAYS $ 
*     CARD   1- 24, 28, 29, 31, 59- 62
*     RFMT 188-204,207-209
$
LABEL    LBLINT02 $ 
*     CARD   1-20,22-24,28,31,59-62   
$ COMPON causes the DMAP compiler to skip the compilation of all of the 
$ following instructions through label LBLINT01 as the parameter SYS21 is 0 
$ (see NOTE at the beginning).
COMPON   LBLINT01,SYS21 $   
*     CARD   1- 20, 22- 24, 28, 31, 59- 62
$
PARAM    //*SYST*//86/1 $   
*     CARD   1-20,22-24,28,31,59-62   
$
SETVAL   //V,N,PFILE/0 $
*     CARD   1-20,22-24,28,31,59-62   
$
LABEL    AGAIN $
*     CARD   1- 20, 22- 24, 28, 31, 59- 62
$
PROMPT1  //S,N,PEXIT/S,N,PLOT1/S,N,PLOT2/S,N,XYPLOT/
         S,N,SCAN1/S,N,SCAN2 $  
*     CARD   1-20,22-24,28,31,59-62   
$
COND     LBLINT1,PEXIT $
*     CARD   1-20,22-24,28,31,59-62   
$
PARAM    //*OR*/V,N,PLOTZ/V,N,PLOT1/V,N,PLOT2 $ 
*     CARD   1-20,22-24,28,31,59-62   
$
PARAM    //*NOT*/V,N,NOPLOTZ/V,N,PLOTZ $
*     CARD   1-20,22-24,28,31,59-62   
$
COND     STEPPLOT,NOPLOTZ $ 
*     CARD   1- 20, 22- 24, 28, 31, 59- 62
$
PURGE    PLTSETR,PLTPARR,GPSETR,ELSETR/NOPLOTZ $
*     CARD   1-20,22-24,28,31,59-62   
$
PLTSET   PCDB,EQEXIN,ECT,/PLTSETR,PLTPARR,GPSETR,ELSETR/S,N,NSIL/
         S,N,JUMPPLOT $ 
*     CARD   1- 20, 22- 24, 28, 31, 59- 62
$
PRTMSG   PLTSETR $  
*     CARD   1-20,22-24,28,31,59-62   
$
COND     LBLINT2,PLOT2 $
*     CARD   1-20,22-24,28,31,59-62   
$
SETVAL   //S,N,PLTFG1/1 $   
*     CARD   1-20,22-24,28,31,59-62   
$
PLOT     PLTPARR,GPSETR,ELSETR,CASECC,BGPDT,EQEXIN,SIL,,ECT,,,,/
         PLOTX3/NSIL/LUSET/JUMPPLOT/PLTFG1/S,N,PFILE $  
*     CARD   1-20,22-24,28,31,59-62   
$
PRTMSG   PLOTX3 $   
*     CARD   1-20,22-24,28,31,59-62   
$
SITEPLOT   $
*     CARD   1-20,22-24,28,31,59-62   
$
PURGE    PLTSETR,PLTPARR,GPSETR,ELSETR $
*     CARD   1-20,22-24,28,31,59-62   
$
JUMP     LBLINTEX $ 
*     CARD   1-20,22-24,28,31,59-62   
$
LABEL    LBLINT2 $  
*     CARD   1-20,22-24,28,31,59-62   
$
SETVAL   //S,N,PLTFG2/-1 $  
*     CARD   1-20,22-24,28,31,59-62   
$
PLOT     PLTPARR,GPSETR,ELSETR,CASECC,BGPDT,EQEXIN,SIP,PUGV1,,GPECT,
         OES1,OES1L,ONRGY1/PLOTX4/NSIL/LUSEP/JUMPPLOT/PLTFG2/S,N,PFILE $
*     CARD   1-20,22-24,28,31,59-62   
$
PRTMSG   PLOTX4// $ 
*     CARD   1-20,22-24,28,31,59-62   
$
SITEPLOT  $ 
*     CARD   1-20,22-24,28,31,59-62   
$
PURGE    PLTSETR,PLTPARR,GPSETR,ELSETR $
*     CARD   1-20,22-24,28,31,59-62   
$
JUMP     LBLINTEX $ 
*     CARD   1-20,22-24,28,31,59-62   
$
LABEL    STEPPLOT $ 
*     CARD   1- 20, 22- 24, 28, 31, 59- 62
$
PARAM    //*OR*/V,N,SCANZ/V,N,SCAN1/V,N,SCAN2 $ 
*     CARD   1-20,22-24,28,31,59-62   
$
PARAM    //*NOT*/V,N,NOSCANZ/V,N,SCANZ $
*     CARD   1-20,22-24,28,31,59-62   
$
COND     STEPSCAN,NOSCANZ $ 
*     CARD   1-20,22-24,28,31,59-62   
$
PURGE    OESF1I,OESF2I/NOSCANZ $
*     CARD   1-20,22-24,28,31,59-62   
$
COND     LBLINT3,SCAN2 $
*     CARD   1-20,22-24,28,31,59-62   
$
SCAN     CASECC,OES1,OEF1/OESF1I/*OL1* $
*     CARD   1-20,22-24,28,31,59-62   
$
OFP      OESF1I,,,,,//S,N,CARDNO $  
*     CARD   1-20,22-24,28,31,59-62   
$
PURGE    OESF1I $   
*     CARD   1-20,22-24,28,31,59-62   
$
JUMP     LBLINTEX $ 
*     CARD   1-20,22-24,28,31,59-62   
$
LABEL    LBLINT3 $  
*     CARD   1-20,22-24,28,31,59-62   
$
SCAN     CASECC,OES2,OEF2/OESF2I/*OL2* $
*     CARD   1-20,22-24,28,31,59-62   
$
OFP      OESF2I,,,,,//S,N,CARDNO $  
*     CARD   1-20,22-24,28,31,59-62   
$
PURGE    OESF2I $   
*     CARD   1-20,22-24,28,31,59-62   
$
JUMP     LBLINTEX $ 
*     CARD   1-20,22-24,28,31,59-62   
$
LABEL    STEPSCAN $ 
*     CARD   1-20,22-24,28,31,59-62   
$
PARAM    //*NOT*/V,N,NOXYPT/V,N,XYPLOT $
*     CARD   1-20,22-24,28,31,59-62   
$
COND     LBLINTEX,NOXYPT $  
*     CARD   1-20,22-24,28,31,59-62   
$
PURGE    XYPLTI/NOXYPT $
*     CARD   1-20,22-24,28,31,59-62   
$
XYTRAN   XYCDB,OPG2,OQG2,OUGV2,OES2,OEF2/XYPLTI/*TRAN*/ 
         *PSET*/S,N,PFILE/S,N,CARDNO $  
*     CARD   1-20,22-24,28,31,59-62   
$
XYPLOT   XYPLTI// $ 
*     CARD   1-20,22-24,28,31,59-62   
$
SITEPLOT   $
*     CARD   1-20,22-24,28,31,59-62   
$
PURGE    XYPLTI $   
*     CARD   1-20,22-24,28,31,59-62   
$
JUMP     LBLINTEX $ 
*     CARD   1-20,22-24,28,31,59-62   
$
LABEL    LBLINTEX $ 
*     CARD   1-20,22-24,28,31,59-62   
$
REPT     AGAIN,400 $
*     CARD   1- 20, 22- 24, 28, 31, 59- 62
$
PRTPARM  //1/*STATICS* $
*     CARD   1- 20, 22- 24, 28, 31, 59- 62
$
LABEL    LBLINT1 $  
*     CARD   1- 20, 22- 24, 28, 31, 59- 62
$
LABEL    LBLINT01 $ 
*     CARD   1- 20, 22- 24, 28, 31, 59- 62
$
END      $  
*     CARD   1- 24, 28, 29, 31, 59- 62
*     RFMT 188-204,207-209
$
$*CARD BITS 
  1    AXIC     AXIF     CELAS1   CELAS2   CELAS3   CELAS4   CMASS1 
  1    CMASS2   
  1    CMASS3   CMASS4   CORD1C   CORD1R   CORD1S   CORD2C   CORD2R 
  1    CORD2S   
  1    GRDSET   GRID     GRIDB    POINTAX  RINGAX   RINGFL   SECTAX 
  1    SEQGP    SPOINT  
  2    ADUM1    ADUM2    ADUM3    ADUM4    ADUM5    ADUM6    ADUM7  
  2    ADUM8
  2    ADUM9    BAROR    CBAR     CCONEAX  CDUM1    CDUM2    CDUM3  
  2    CDUM4
  2    CDUM5    CDUM6    CDUM7    CDUM8    CDUM9    CHBDY    CHEXA1 
  2    CHEXA2   
  2    CIHEX1   CIHEX2   CIHEX3   CONROD   CQDMEM   CQDMEM1  CQDMEM2
  2    CQDPLT   CQUAD1   CQUAD2   CROD     CSHEAR   CTETRA   CTORDRG
  2    CTRAPAX  CQUAD4   CTRIA3
  2    CTRAPRG  CTRBSC   CTRIA1   CTRIA2   CTRIAAX  CTRIARG  CTRIM6 
  2    CTRMEM   
  2    CTRPLT   CTRPLT1  CTRSHL   CTUBE    CTWIST   CWEDGE  
  3    PBAR     PCONEAX  PDUM1    PDUM2    PDUM3    PDUM4    PDUM5  
  3    PDUM6
  3    PDUM7    PDUM8    PDUM9    PHBDY    PIHEX    PQDMEM   PQDMEM1
  3    PQDMEM2  PQDPLT   PQUAD1   PQUAD2   PROD     PSHEAR   PTORDRG
  3    PTRAPAX  PSHELL   PCOMP    PCOMP1   PCOMP2
  3    PTRBSC   PTRIA1   PTRIA2   PTRIAAX  PTRIM6   PTRMEM   PTRPLT 
  3    PTRPLT1  PTRSHL   PTUBE    PTWIST
  4    GENEL
  5    CONM1    CONM2    PMASS  
  6    PELAS
  7    AOUT$
  8    MAT1     MAT2     MAT3     MAT4     MAT5     MATT1    MATT2  
  8    MATT3    MAT8
  8    MATT4    MATT5    TABLEM1  TABLEM2  TABLEM3  TABLEM4  TEMPMT$
  8    TEMPMX$  
  9    AXISYM$  CRIGD1   CRIGD2   CRIGD3   CRIGDR   CRROD    CRBAR  
  9    CRTRPLT  CRBE1    CRBE2    CRBE3    CRSPLINE MPC      MPCADD 
  9    MPCAX    MPC$
 10    SPC      SPC1     SPCADD   SPCAX    SPC$ 
 11    ASET     ASET1    OMIT     OMIT1    OMITAX   SUPAX    SUPORT 
 13    TEMP     TEMPAX   TEMPD    TEMPP1   TEMPP2   TEMPP3   TEMPRB 
 14    WTMASS   
 15    GRDPNT   
 16    PLOTEL   
 17    IRES 
 18    PLOT$
 19    POUT$
 20    ASETOUT  
 21    OPT      GRDEQ   
 22    LOOP$
 23    LOOP1$   
 24    COUPMASS CPBAR    CPQDPLT  CPQUAD1  CPQUAD2  CPROD    CPTRBSC
 24    CPTRIA1  CPTRIA2  CPTRPLT  CPTUBE
 25    STRESS   
 26    STRAIN   
 27    NINTPTS  
 28    AUTOSPC  
 29    XYOUT$   
 31    NOLOOP$  
 59    DEFORM   DEFORM$  LOAD$    SPCD  
 60    FORCE    FORCE1   FORCE2   FORCEAX  LOAD     MOMAX    MOMENT 
 60    MOMENT1  PLOAD4
 60    MOMENT2  PLOAD    PLOAD1   PLOAD2   PLOAD3   PRESAX   SLOAD  
 61    GRAV     RFORCE  
 62    TEMPLD$  
$
$*FILE BITS 
 94    BGPDT    CSTM     EQEXIN   GPDT     GPL      SIL 
 95    ECT  
 96    GPTT     SLT 
 97    GPECT    EST      GEI      MPTX     PCOMPS   EPTX
 98    KGGX 
 99    MGG  
100    KGG  
101    RG       USET     YS       ASET     OGPST
102    GPST 
103    GM   
104    KNN  
105    KFF      KFS      KSS
106    GO       KAA      KOO      LOO   
107    KLL      KLR      KRR
108    LLL  
109    DM   
110    PG   
111    PL       PO       PS       QR
112    RULV     RUOV     ULV      UOOV  
113    PGG      QG       UGV
114    OEF1     OES1     OPG1     OQG1     OUGV1    PUGV1   
114    OEF1L    OES1L    OESF1    OESF1X
115    ELSETS   GPSETS   PLTPAR   PLTSETX   
116    KDICT    KELM     MDICT    MELM  
117    OPTP1
118    OPTP2    EST1
119    OGPFB1   ONRGY1  
120    PLOTX1   
121    OGPWG
122    OES1M    OES1G   
123    OES1A
124    OES1AM   OES1AG  
125    OUGV2    OPG2     OQG2     OEF2     OES2     OESF2
126    XYPLTT   
127    OQM1 
128    PLOTX2   
129    BGPDP    SIP 
$*  
=PAGE=
DISP18

DISP18 RIGID FORMAT IS NOT AVAILABLE

=PAGE=
DISP19

DISP19 RIGID FORMAT IS NOT AVAILABLE

=PAGE=
DISP20

DISP20 RIGID FORMAT IS NOT AVAILABLE

=PAGE=
DISP2

APR.93  
$$$$$$$$
BEGIN    DISP 02 - STATIC ANALYSIS WITH INERTIA RELIEF - APR. 1993 $
****CARD   1- 24, 59- 62
****RFMT 187,189-204,207-209
$$$$
PRECHK   ALL $  
****CARD   1- 24, 59- 62
****RFMT 187,189-204,207-209
$$$$
FILE     QG=APPEND/PGG=APPEND/UGV=APPEND/GM=SAVE/KNN=SAVE/MNN=SAVE  $   
****SBST   1,  3
****CARD   1- 24, 59- 62
****RFMT 187,189-204,207-209
$$$$
PARAM    //*MPY*/CARDNO/0/0 $   
****CARD   1-  3,  5- 10, 14, 15, 19, 21- 24, 61
****FILE 101,116,120,121
****PHS1   I1   
$$$$ GP1 generates coordinate system transformation matrices, tables of grid 
$$$$ point locations, and tables relating the internal and external grid point 
$$$$ numbers.
GP1      GEOM1,GEOM2,/GPL,EQEXIN,GPDT,CSTM,BGPDT,SIL/S,N,LUSET/ 
         NOGPDT/ALWAYS=-1 $ 
****CARD   1
****FILE  94
****PHS2   D5   
$$$$ PLTTRAN modifies special scalar grid points in the BGPDT and SIL tables.
PLTTRAN  BGPDT,SIL/BGPDP,SIP/LUSET/S,N,LUSEP $  
****CARD   1
****FILE 123
$$$$ GP2 generates Element Connection Table with internal indices.
GP2      GEOM2,EQEXIN/ECT $ 
****CARD   1,  2,  4,  5, 16
****FILE  95
$$$$
PARAML   PCDB//*PRES*////JUMPPLOT $ 
****SBST   7
****CARD  18
****FILE 117,119
****PHS2   DB5  
$$$$
PURGE    PLTSETX,PLTPAR,GPSETS,ELSETS/JUMPPLOT $
****SBST   7
****CARD  18
****FILE 117
$$$$ Go to label P2 if there are no structure plot requests.
COND     P1,JUMPPLOT $  
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 117,119
$$$$ PLTSET transforms user input into a form used to drive the structure 
$$$$ plotter.
PLTSET   PCDB,EQEXIN,ECT,EPT/PLTSETX,PLTPAR,GPSETS,ELSETS/S,N,NSIL/ 
         S,N,JUMPPLOT $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 117
$$$$ PRTMSG prints error messages associated with the structure plotter.
PRTMSG   PLTSETX// $
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 117
$$$$
PARAM    //*MPY*/PLTFLG/1/1 $   
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 119
$$$$
PARAM    //*MPY*/PFILE/0/0 $
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 119
$$$$ Go to label P1 if no undeformed structure plots are requested.
COND     P1,JUMPPLOT $  
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 119
$$$$ PLOT generates all requested undeformed structure plots.
PLOT     PLTPAR,GPSETS,ELSETS,CASECC,BGPDT,EQEXIN,SIL,,ECT,,,,/PLOTX1/  
         NSIL/LUSET/S,N,JUMPPLOT/S,N,PLTFLG/S,N,PFILE  $
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 119
$$$$ PRTMSG prints plotter data and engineering data for each undeformed plot 
$$$$ generated.
PRTMSG   PLOTX1// $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 119
$$$$
LABEL    P1 $   
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 117,119
****PHS2   DE5  
$$$$ GP3 generates Static Loads Table and Grid Point Temperature Table.
GP3      GEOM3,EQEXIN,GEOM2/SLT,GPTT/NOGRAV $   
****CARD   1,  2, 13, 60, 61
****FILE  96
$$$$ TA1 generates element tables for use in matrix assembly and stress 
$$$$ recovery.
TA1      ECT,EPT,BGPDT,SIL,GPTT,CSTM,MPT,EQEXIN/EST,GEI,GPECT,,,MPTX,
         PCOMPS,EPTX/LUSET/S,N,NOSIMP/1/S,N,NOGENL/GENEL/S,N,COMPS $   
****CARD   1-  6, 13, 16
****FILE  97
$$$$
EQUIV    MPTX,MPT/COMPS/EPTX,EPT/COMPS $
****CARD   1- 24, 59- 62
****FILE  97
$$$$ Go to label ERROR6 and print Error Message No. 6 if there are no 
$$$$ structural elements.
COND     ERROR6,NOSIMP $
****CARD   1-  6, 16
****FILE  97
****PHS2   D5   
$$$$
PARAM    //*ADD*/NOKGGX/1/0 $   
****CARD   1-  3,  6,  8
****FILE 118
$$$$
PARAM    //*ADD*/NOMGG/1/0 $
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE 118
****RFMT 187,190-192
$$$$ EMG generates structural element stiffness and mass matrix tables and 
$$$$ dictionaries for later assembly by the EMA module.
EMG      EST,CSTM,MPT,DIT,GEOM2,/KELM,KDICT,MELM,MDICT,,,/S,N,NOKGGX/   
         S,N,NOMGG////C,Y,COUPMASS/C,Y,CPBAR/   
         C,Y,CPROD/C,Y,CPQUAD1/C,Y,CPQUAD2/C,Y,CPTRIA1/C,Y,CPTRIA2/ 
         C,Y,CPTUBE/C,Y,CPQDPLT/C,Y,CPTRPLT/C,Y,CPTRBSC/
         C,Y,VOLUME/C,Y,SURFACE $   
****CARD   1-  3,  5,  6,  8, 13, 24, 61
****FILE 118
****RFMT 187,190-192
$$$$
PURGE    KGGX/NOKGGX $  
****CARD   1-  3,  6,  8
****FILE  98
$$$$ Go to label JMPKGG if no stiffness matrix is to be assembled.
COND     JMPKGG,NOKGGX $
****CARD   1-  3,  6,  8
****FILE  98
$$$$                                  x
$$$$ EMA assembles stiffness matrix [K  ] and Grid Point Singularity Table.
$$$$                                  gg
$$$$ 
EMA      GPECT,KDICT,KELM/KGGX $
****CARD   1-  3,  6,  8
****FILE  98
$$$$
PURGE    KDICT,KELM/ALWAYS $
****CARD   1-  3,  6,  8
****FILE 118
$$$$
LABEL    JMPKGG $   
****CARD   1-  3,  6,  8
****FILE  98
$$$$ Go to label ERROR1 and print Error Message No. 1 if no mass matrix is to 
$$$$ be assembled.
COND     ERROR1,NOMGG $ 
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE 118
****RFMT 187,189-204,207-209
****PHS2   D5   
$$$$ EMA assembles mass matrix [M  ].
$$$$                             gg
EMA      GPECT,MDICT,MELM/MGG/-1/C,Y,WTMASS=1.0 $   
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE  99
****RFMT 187,190-192
$$$$
PURGE    MDICT,MELM/ALWAYS $
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE 118
$$$$ Go to label LGPWG if no weight and balance information is requested.
COND     LGPWG,GRDPNT $ 
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE 120
$$$$ GPWG generates weight and balance information.
GPWG     BGPDP,CSTM,EQEXIN,MGG/OGPWG/V,Y,GRDPNT=-1/C,Y,WTMASS $ 
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE 120
$$$$ OFP formats the weight and balance information prepared by GPWG and 
$$$$ places it on the system output file for printing.
OFP      OGPWG,,,,,//S,N,CARDNO $   
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE 120
$$$$
LABEL    LGPWG $
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE 120
$$$$               x
$$$$ Equivalence [K  ] to [K  ] if no general elements exist.
$$$$               gg       gg
$$$$ 
EQUIV    KGGX,KGG/NOGENL $  
****CARD   1-  4,  6,  8
****FILE 100
****PHS2   DB5  
$$$$ Go to label LBL11A if no general elements exist.
COND     LBL11A,NOGENL $
****CARD   1-  4,  6,  8
****FILE 100
$$$$                                 x
$$$$ SMA3 adds general elements fo [K  ] to obtain stiffness matrix [K  ].
$$$$                                 gg                               gg
$$$$ 
SMA3     GEI,KGGX/KGG/LUSET/NOGENL/NOSIMP $ 
****CARD   1-  4,  6,  8
****FILE 100
$$$$
LABEL    LBL11A $   
****CARD   1-  4,  6,  8
****FILE 100
$$$$
GPSTGEN  KGG,SIL/GPST $ 
****CARD   1-  4,  6,  8
****FILE 102
****PHS2   DE5  
$$$$
PARAM    //*MPY*/NSKIP/0/0 $
****CARD   1,  9- 12, 22, 23, 59
****FILE 101
$$$$ Beginning of loop for multiple constraint sets.
LABEL    LBL11 $
****SBST   1,  3
****CARD  22, 23
****FILE 100
$$$$ GP4 generates flags defining members of various displacement sets (USET), 
$$$$ forms multipoint constraint equations [R ]{u } = 0, and forms enforced 
$$$$                                         g   g
$$$$ displacement vector {Y }.
$$$$                       s
$$$$ 
GP4      CASECC,GEOM4,EQEXIN,GPDT,BGPDT,CSTM,GPST/RG,YS,USET,   
         ASET,OGPST/LUSET/S,N,MPCF1/S,N,MPCF2/S,N,SINGLE/S,N,OMIT/  
         S,N,REACT/S,N,NSKIP/S,N,REPEAT/S,N,NOSET/S,N,NOL/S,N,NOA/  
         C,Y,ASETOUT/C,Y,AUTOSPC $  
****CARD   1-  4,  6,  8- 12, 20- 23, 59
****FILE 101
$$$$
OFP      OGPST,,,,,//S,N,CARDNO $   
****CARD   1-  4,  6,  8- 10, 21- 23
****FILE 101
$$$$ Go to label ERROR3 and print Error Message No. 3 if no independent 
$$$$ degrees of freedom are defined.
COND     ERROR3,NOL $   
****CARD   1,  9- 12, 22, 23, 59
****FILE 101
****RFMT 187,189-204,207-209
$$$$ Go to label ERROR4 and print Error Message No. 4 if no free-body supports 
$$$$ exist.
COND     ERROR4,REACT $ 
****CARD   1,  9- 12, 22, 23, 59
****FILE 101
****RFMT 187,189-204,207-209
****PHS1   D1   
$$$$
PURGE    GM/MPCF1/GO,KOO,LOO,MOO,MOA,PO,UOOV,RUOV/OMIT/KSS,KFS,PS/  
         SINGLE $   
****CARD   1,  9- 12, 22, 23, 59
****FILE 103,105,106,112,114
$$$$ Equivalence [K  ] to [K  ] and [M  ] to [M  ] if no multipoint 
$$$$               gg       nn        gg       nn
$$$$ constraints exist.
EQUIV    KGG,KNN/MPCF1/MGG,MNN/MPCF1 $  
****CARD   1-  6,  8,  9, 22, 23
****FILE 104
$$$$ Go to label LBL2 if the MPC set for the current pass is unchanged from 
$$$$ that of the previous pass.
COND     LBL2,MPCF2 $   
****CARD   1-  6,  8,  9, 22, 23
****FILE 103,104
$$$$ MCE1 partitions multipoint constraint equations [R ] = [R |R ] and solves
$$$$                                                   g      m  n
$$$$                                                             -1
$$$$ for multipoint constraint transformation matrix [G ] = -[R ]  [R ].
$$$$                                                   m       m     n
$$$$ 
MCE1     USET,RG/GM $   
****CARD   1,  9, 22, 23
****FILE 103
$$$$ MCE2 partitions stiffness and mass matrices
$$$$ 
$$$$           +_      +                        +_      +
$$$$           |K  |K  |                        |M  |M  |
$$$$           | nn| nm|                        | nn| nm|
$$$$   [K  ] = |---+---|                [M  ] = |---+---|
$$$$     gg    |K  |K  |                  gg    |M  |M  |
$$$$           | mn| mm|                        | mn| mm|
$$$$           +       +                        +       +
$$$$           
$$$$ and performs the matrix reductions
$$$$ 
$$$$            _        T           T           T
$$$$   [K  ] = [K  ] + [G ][K  ] + [K  ][G ] + [G ][K  ][G ]
$$$$     nn      nm      m   mn      mn   m      m   mm   m
$$$$ 
$$$$            _        T           T           T
$$$$   [M  ] = [M  ] + [G ][M  ] + [M  ][G ] + [G ][M  ][G ]
$$$$     nn      nm      m   mn      mn   m      m   mm   m
$$$$ 
MCE2     USET,GM,KGG,MGG,,/KNN,MNN,, $  
****CARD   1-  6,  8,  9, 22, 23
****FILE 104
$$$$
LABEL    LBL2 $ 
****CARD   1-  6,  8,  9, 22, 23
****FILE 103,104
$$$$ Equivalence [K  ] to [K  ] and [M  ] to [M  ] if no single-point
$$$$               nn       ff        nn       ff
$$$$ constraints exist.
EQUIV    KNN,KFF/SINGLE/MNN,MFF/SINGLE $
****CARD   1-  6,  8- 10, 22, 23
****FILE 105
$$$$ Go to label LBL3 if no single-point constraints exist.
COND     LBL3,SINGLE $  
****CARD   1-  6,  8- 10, 22, 23
****FILE 105
$$$$ SCE1 partitions out single-point constraints
$$$$ 
$$$$           +       +                        +       +
$$$$           |K  |K  |                        |M  |M  |
$$$$           | ff| fs|                        | ff| fs|
$$$$   [K  ] = |---+---|                [M  ] = |---+---|
$$$$     nn    |K  |K  |                  nn    |M  |M  |
$$$$           | sf| ss|                        | sf| ss|
$$$$           +       +                        +       +
$$$$ 
SCE1     USET,KNN,MNN,,/KFF,KFS,KSS,MFF,, $ 
****CARD   1-  6,  8- 10, 22, 23
****FILE 105
$$$$
LABEL    LBL3 $ 
****CARD   1-  6,  8- 10, 22, 23
****FILE 105
$$$$ Equivalence [K  ] to [K  ] and [M  ] to [M  ] if no omitted coordinates
$$$$               ff       aa        ff       aa
$$$$ exist.
EQUIV    KFF,KAA/OMIT/ MFF,MAA/OMIT $   
****CARD   1-  6,  8- 11, 22, 23
****FILE 106
$$$$ Go to label LBL5 if no omitted coordinates exist.
COND     LBL5,OMIT $
****CARD   1-  6,  8- 11, 22, 23
****FILE 106
$$$$ SMP1 partitions constrained stiffness and mass matrices
$$$$
$$$$           +_      +                        +_      +
$$$$           |K  |K  |                        |M  |M  |
$$$$           | aa| ao|                        | aa| ao|
$$$$   [K  ] = |---+---|                [M  ] = |---+---|
$$$$     ff    |K  |K  |                  ff    |M  |M  |
$$$$           | oa| oo|                        | oa| oo|
$$$$           +       +                        +       +
$$$$           
$$$$                                                -1
$$$$ solves for transformation matrix [G ] = -[[K  ]  [K  ]
$$$$                                    o        oo     oa
$$$$ 
$$$$                                         _        T
$$$$ and performs matrix reductions [K  ] = [K  ] + [K  ][G ]
$$$$                                  aa      aa      oa   o
$$$$              _        T           T           T
$$$$ and [M  ] = [M  ] + [M  ][G ] + [G ][M  ] + [G ][M  ][G ]
$$$$       aa      aa      oa   o      o   oa      o   oo   o
$$$$ 
SMP1     USET,KFF,MFF,,/GO,KAA,KOO,LOO,MAA,MOO,MOA,, $  
****CARD   1-  6,  8- 11, 22, 23
****FILE 106
$$$$
LABEL    LBL5 $ 
****CARD   1-  6,  8- 11, 22, 23
****FILE 106
$$$$ RBMG1 partitions out free-body supports
$$$$ 
$$$$           +       +                        +       +
$$$$           |K  |K  |                        |M  |M  |
$$$$           | ll| lr|                        | ll| lr|
$$$$   [K  ] = |---+---|                [M  ] = |---+---|
$$$$     aa    |K  |K  |                  aa    |M  |M  |
$$$$           | rl| rr|                        | rl| rr|
$$$$           +       +                        +       +
$$$$ 
RBMG1    USET,KAA,MAA/KLL,KLR,KRR,MLL,MLR,MRR $ 
****CARD   1-  6,  8- 12, 22, 23
****FILE 107
****PHS1   DB1  
****PHS3   DB1  
$$$$ RBMG2 decomposes constrained stiffness matrix [K  ] = [L  ][U  ].
$$$$                                                 ll      ll   ll
$$$$ 
RBMG2    KLL/LLL $  
****CARD   1-  4,  6,  8- 12, 22, 23
****FILE 108
$$$$ RBMG3 forms rigid body transformation matrix
$$$$ 
$$$$               -1
$$$$   [D] = -[K  ]  [K  ]
$$$$            ll     lr
$$$$ 
$$$$ calculates rigid body check matrix
$$$$
$$$$                   T
$$$$   [X] = [K  ] + [K  ][D]
$$$$           rr      lr
$$$$ 
$$$$ and calculates rigid body error ratio
$$$$ 
$$$$               ||X||
$$$$   epsilon = ---------
$$$$              ||K  ||
$$$$                 rr
$$$$ 
RBMG3    LLL,KLR,KRR/DM $   
****CARD   1-  4,  6,  8- 12, 22, 23
****FILE 109
$$$$ RBMG4 forms rigid body mass matrix
$$$$ 
$$$$                    T          T           T
$$$$   [m ] = [M  ] + [M  ][D] + [D ][M  ] + [D ][M  ][D]
$$$$     r      rr      lr             lr          ll
$$$$ 
RBMG4    DM,MLL,MLR,MRR/MR $
****CARD   1-  6,  8- 12, 22, 23
****FILE 110
****PHS1   DE1  
****PHS3   DE1  
$$$$ SSG1 generates static load vectors [P }.
$$$$                                      g
$$$$ 
SSG1     SLT,BGPDT,CSTM,SIL,EST,MPT,GPTT,EDT,MGG,CASECC,DIT,PCOMPS/ 
         PG,,,,/LUSET/NSKIP/COMPS $ 
****CARD   1-  3,  5,  6,  8, 13, 22, 23, 59- 62
****FILE 111
$$$$ SSG2 applies constraints to static load vectors
$$$$ 
$$$$           _
$$$$           P
$$$$            n            _       T
$$$$   {P } = {--} , {P } = {P } + [G ]{P }
$$$$     g     P       n      n      m   m
$$$$            m
$$$$ 
$$$$           _
$$$$           P
$$$$            f            _      
$$$$   {P } = {--} , {P } = {P } - [K  ]{Y }
$$$$     n     P       f      f      fs   s
$$$$            s
$$$$ 
$$$$           _
$$$$           P
$$$$            a            _       T
$$$$   {P } = {--} , {P } = {P } + [G ]{P }
$$$$     f     P       a      a      o   o
$$$$            o
$$$$ 
$$$$           P
$$$$            l
$$$$   {P } = {--}
$$$$     a     P
$$$$            r
$$$$                                                                T
$$$$ and calculates determinate forces of reaction {q } = -{P } - [D ]{P }.
$$$$                                                 r       r          l
$$$$ 
SSG2     USET,GM,YS,KFS,GO,DM,PG/QR,PO,PS,PL $  
****CARD   1-  3,  5,  6,  8- 13, 22, 23, 59- 62
****FILE 112
****PHS1   DB1  
****PHS3   DB7  
$$$$ SSG4 calculates inertia loads and combines them with static loads
$$$$ 
$$$$     i                                  -1
$$$$   {P } = {P } + ([M  ][D] + [M  ]) [m ]  {q }
$$$$     l      l       ll         lr     r     r
$$$$ 
$$$$     i                          T              -1
$$$$   {P } = {P } + ([M  ][G ] + [M  ]) [D/I] [m ]  {q }
$$$$     o      o       oo   o      ao           r     r
$$$$ 
SSG4     PL,QR,PO,MR,MLR,DM,MLL,MOO,MOA,GO,USET/PLI,POI/OMIT $  
****CARD   1-  3,  5,  6,  8- 13, 22, 23, 59- 62
****FILE 113
$$$$ SSG3 solves for displacements of independent coordinates
$$$$ 
$$$$               -1  i
$$$$   {u } = [K  ]  {P }
$$$$     l      ll     l
$$$$ 
$$$$ solves for displacements of omitted coordinates
$$$$ 
$$$$     o         -1  i
$$$$   {u } = [K  ]  {P }
$$$$     o      oo     o
$$$$ 
$$$$ calculates residual vector (RULV) and residual vector error ratio for 
$$$$ independent coordinates
$$$$ 
$$$$          i      i
$$$$   {deltaP } = {P } - [K  ]{u }
$$$$          l      l      ll   l
$$$$ 
$$$$                T        i
$$$$              {u }{deltaP }
$$$$                l        l
$$$$   epsilon  = -------------
$$$$          l       i T
$$$$                {P } {u }
$$$$                  l    l
$$$$ 
$$$$ and calculates residual vector (RUOV) and residual vector error ratio for
$$$$ omitted coordinates
$$$$ 
$$$$          i      i           o
$$$$   {deltaP } = {P } - [K  ]{u }
$$$$          o      o      oo   o
$$$$ 
$$$$                T        i
$$$$              {u }{deltaP }
$$$$                o        o
$$$$   epsilon  = -------------
$$$$          o       i T  o
$$$$                {P } {u }
$$$$                  o    o
$$$$
SSG3     LLL,KLL,PLI,LOO,KOO,POI/ULV,UOOV,RULV,RUOV/OMIT/V,Y,   
         IRES=-1/NSKIP/S,N,EPSI $   
****CARD   1-  6,  8- 13, 22, 23, 59- 62
****FILE 114
****RFMT 187
$$$$ Go to label LBL9 if residual vectors are not to be printed.
COND     LBL9,IRES $
****CARD   1-  6,  8- 13, 17, 22, 23, 59- 62
****RFMT 187,189-204,207-209
$$$$ MATGPR prints the residual vector for independent coordinates (RULV).
MATGPR   GPL,USET,SIL,RULV//*L* $   
****CARD   1-  6,  8- 13, 17, 22, 23, 59- 62
****RFMT 187,189-204,207-209
$$$$ MATGPR prints the residual vector for omitted coordinates (RUOV).
MATGPR   GPL,USET,SIL,RUOV//*O* $   
****CARD   1-  6,  8- 13, 17, 22, 23, 59- 62
****RFMT 187,189-204,207-209
$$$$
LABEL    LBL9 $ 
****CARD   1-  6,  8- 13, 17, 22, 23, 59- 62
****RFMT 187,189-204,207-209
****PHS3   DE7  
$$$$ SDR1 recovers dependent displacements
$$$$ 
$$$$    u
$$$$     l                                  o
$$$$   {--} = {u }  ,   {u } = [G ]{u ] + {u }  ,
$$$$    u       a         o      o   a      o
$$$$     r
$$$$ 
$$$$    u                u            
$$$$     a                f           
$$$$   {--} = {u }  ,   {--} = {u }  ,
$$$$    u       f        Y       n    
$$$$     o                s           
$$$$                                  
$$$$                        u         
$$$$                         n        
$$$$  {u } = [G ]{u ]  ,   {--} = {u }
$$$$    m      m   n        u       g 
$$$$                         m        
$$$$ 
$$$$ and recovers single-point forces of constraint
$$$$ 
$$$$                    T
$$$$   {q } = -{P } + [K  ]{u } + [K  ]{Y }
$$$$     s       s      fs   f      ss   s
$$$$
SDR1     USET,PG,ULV,UOOV,YS,GO,GM,PS,KFS,KSS,QR/UGV,PGG,QG/NSKIP/  
         *STATICS* $
****CARD   1-  6,  8- 13, 22, 23, 59- 62
****FILE 115
****RFMT 187,189-204,207-209
****PHS3   I7   
$$$$ Go to label LBL8 if all constraint sets have been processed.
COND     LBL8,REPEAT $  
****SBST   1,  3
****CARD  22, 23
****RFMT 187,189-204,207-209
$$$$ Go to label LBL11 if additional sets of constraints need to be processed.
REPT     LBL11,360 $
****SBST   1,  3
****CARD  22, 23
****RFMT 187,189-204,207-209
$$$$ Go to label ERROR2 and print Error Message No. 2 if the number of 
$$$$ constraint sets exceeds 360.
JUMP     ERROR2 $   
****SBST   1,  3
****CARD  22, 23
****RFMT 187,189-204,207-209
$$$$
PARAM    //*NOT*/TEST/REPEAT $  
****CARD  22, 23
****RFMT 187,189-204,207-209
$$$$ Go to label ERROR5 and print Error Message No. 5 if multiple boundary 
$$$$ conditions are attempted with an improper subset.
COND     ERROR5,TEST $  
****CARD  22, 23
****RFMT 187,189-204,207-209
$$$$
LABEL    LBL8 $ 
****SBST   1,  3
****CARD  22, 23
****RFMT 187,189-204,207-209
$$$$ Go to label NOMPCF if no multipoint constraint force balance is 
$$$$ requested.
COND     NOMPCF,GRDEQ $ 
****CARD   7
****FILE 121
****RFMT 187,189-204,207-209
****PHS2   DB5  
$$$$ EQMCK calculates the force and moment equilibrium check and prepares the 
$$$$ multipoint constraint force balance (OQMI) for output.
EQMCK    CASECC,EQEXIN,GPL,BGPDT,SIL,USET,KGG,GM,UGV,PGG,QG,CSTM/   
         OQM1/V,Y,OPT=0/V,Y,GRDEQ/NSKIP $   
****CARD   7
****FILE 121
****RFMT 187,189-204,207-209
$$$$ OFP formats the table prepared by EQMCK and places it on the system 
$$$$ output file for printing.
OFP      OQM1,,,,,//S,N,CARDNO $
****CARD   7
****FILE 121
****RFMT 187,189-204,207-209
$$$$
LABEL    NOMPCF $   
****CARD   7
****FILE 121
****RFMT 187,189-204,207-209
$$$$ SDR2 calculates element forces (OEF1) and stresses (OES1) and prepares 
$$$$ load vectors (OPG1), displacement vectors (OUGV1), and single-point 
$$$$ forces of constraint (OQG1) for output and translation components of the 
$$$$ displacement vector (PUGV1).
SDR2     CASECC,CSTM,MPT,DIT,EQEXIN,SIL,GPTT,EDT,BGPDP,,QG,UGV,EST,,PGG,
         PCOMPS/OPG1,OQG1,OUGV1,OES1,OEF1,PUGV1,OES1L,OEF1L/ 
         *STATICS*////COMPS $   
****CARD  18, 19
****FILE 116
$$$$ OFP formats the tables prepared by SDR2 and places them on the system 
$$$$ output file for printing.
OFP      OUGV1,OPG1,OQG1,OEF1,OES1,//S,N,CARDNO $   
****CARD  19
****FILE 116
$$$$
OFP      OEF1L,OES1L,,,,//S,N,CARDNO $   
****CARD  19
****FILE 116
$$$$ SCAN examines the element stresses and forces calculated by SDR2 and 
$$$$ generates scanned output that meets the specifications set by the user.
SCAN     CASECC,OES1,OEF1/OESF1/*RF* $  
****CARD 19 
****FILE 116
$$$$ OFP formats the scanned output table prepared by SCAN and places it on 
$$$$ the system output file for printing.
OFP      OESF1,,,,,//S,N,CARDNO $   
****CARD  19
****FILE 116
$$$$ Go to label P2 if no deformed structure plots are requested.
COND     P2,JUMPPLOT $  
****SBST   7
****CARD  18
****FILE 122
$$$$ PLOT generates all requested deformed structure and contour plots.
PLOT     PLTPAR,GPSETS,ELSETS,CASECC,BGPDT,EQEXIN,SIP,PUGV1,,GPECT,OES1,
         OES1L,/PLOTX2/NSIL/LUSEP/JUMPPLOT/PLTFLG/S,N,PFILE $   
****SBST   7
****CARD  18
****FILE 122
$$$$ PRTMSG prints plotter data, engineering data, and contour data for each 
$$$$ deformed plot generated.
PRTMSG   PLOTX2// $ 
****SBST   7
****CARD  18
****FILE 122
$$$$
LABEL    P2 $   
****SBST   7
****CARD  18
****FILE 122
$$$$ Go to label FINIS and make normal exit.
JUMP     FINIS $
****CARD   1- 24, 59- 62
****FILE 122
****RFMT 187,189-204,207-209
****PHS1   DE1  
$$$$ 
LABEL    ERROR1 $   
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE 118
****RFMT 187,189-204,207-209
$$$$ Print Error Message No. 1 and terminate execution.
PRTPARM  //-1/*INERTIA* $   
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE 118
****RFMT 187,189-204,207-209
****PHS2   DE5  
$$$$ 
LABEL    ERROR2 $   
****SBST   1,  3
****CARD  22, 23
****RFMT 187,189-204,207-209
$$$$ Print Error Message No. 2 and terminate execution.
PRTPARM  //-2/*INERTIA* $   
****SBST   1,  3
****CARD  22, 23
****RFMT 187,189-204,207-209
$$$$ 
LABEL    ERROR3 $   
****CARD   1,  9- 12, 22, 23, 59
****FILE 101
****RFMT 187,189-204,207-209
$$$$ Print Error Message No. 3 and terminate execution.
PRTPARM  //-3/*INERTIA* $   
****CARD   1,  9- 12, 22, 23, 59
****FILE 101
****RFMT 187,189-204,207-209
$$$$ 
LABEL    ERROR4 $   
****CARD   1,  9- 12, 22, 23, 59
****FILE 101
****RFMT 187,189-204,207-209
$$$$ Print Error Message No. 4 and terminate execution.
PRTPARM  //-4/*INERTIA* $   
****CARD   1,  9- 12, 22, 23, 59
****FILE 101
****RFMT 187,189-204,207-209
$$$$ 
LABEL    ERROR5 $   
****CARD  22, 23
****RFMT 187,189-204,207-209
$$$$ Print Error Message No. 5 and terminate execution.
PRTPARM  //-5/*INERTIA* $   
****CARD  22, 23
****RFMT 187,189-204,207-209
$$$$ 
LABEL    ERROR6 $   
****CARD   1-  6, 16
****FILE  97
****RFMT 187,189-204,207-209
$$$$ Print Error Message No. 6 and terminate execution.
PRTPARM  //-6/*INERTIA* $   
****CARD   1-  6, 16
****FILE  97
****RFMT 187,189-204,207-209
$$$$
LABEL    FINIS $
****CARD   1- 24, 59- 62
****FILE 122
****RFMT 187,189-204,207-209
$$$$
PURGE    DUMMY/ALWAYS $ 
****CARD   1- 24, 59- 62
****RFMT 187,189-204,207-209
$$$$
END      $  
****CARD   1- 24, 59- 62
****RFMT 187,189-204,207-209
$$$$
$*CARD BITS 
  1    AXIC     AXIF     CELAS1   CELAS2   CELAS3   CELAS4   CMASS1 
  1    CMASS2   
  1    CMASS3   CMASS4   CORD1C   CORD1R   CORD1S   CORD2C   CORD2R 
  1    CORD2S   
  1    GRDSET   GRID     GRIDB    POINTAX  RINGAX   RINGFL   SECTAX 
  1    SEQGP    SPOINT  
  2    ADUM1    ADUM2    ADUM3    ADUM4    ADUM5    ADUM6    ADUM7  
  2    ADUM8
  2    ADUM9    BAROR    CBAR     CCONEAX  CDUM1    CDUM2    CDUM3  
  2    CDUM4
  2    CDUM5    CDUM6    CDUM7    CDUM8    CDUM9    CELBOW   CHEXA1 
  2    CHEXA2   
  2    CIHEX1   CIHEX2   CIHEX3   CIS2D8   CONROD   CQDMEM   CQDMEM1
  2    CQDMEM2  CQDPLT   CQUAD1   CQUAD2   CROD     CSHEAR   CTETRA 
  2    CTORDRG  CQUAD4   CTRIA3
  2    CTRAPAX  CTRAPRG  CTRBSC   CTRIA1   CTRIA2   CTRIAAX  CTRIARG
  2    CTRIM6   
  2    CTRMEM   CTRPLT   CTRPLT1  CTRSHL   CTUBE    CTWIST   CWEDGE 
  3    PBAR     PCONEAX  PDUM1    PDUM2    PDUM3    PDUM4    PDUM5  
  3    PDUM6
  3    PDUM7    PDUM8    PDUM9    PELBOW   PIHEX    PIS2D8   PQDMEM 
  3    PQDMEM1  PQDMEM2  PQDPLT   PQUAD1   PQUAD2   PROD     PSHEAR 
  3    PTORDRG  PSHELL   PCOMP    PCOMP1   PCOMP2
  3    PTRAPAX  PTRBSC   PTRIA1   PTRIA2   PTRIAAX  PTRIM6   PTRMEM 
  3    PTRPLT   PTRPLT1  PTRSHL   PTUBE    PTWIST   
  4    GENEL
  5    CONM1    CONM2    PMASS  
  6    PELAS
  7    AOUT$
  8    MAT1     MAT2     MAT3     MATT1    MATT2    MATT3    MAT8
  8    TABLEM1  TABLEM2  TABLEM3  TABLEM4  TEMPMT$  TEMPMX$  
  9    AXISYM$  CRIGD1   CRIGD2   CRIGD3   CRIGDR   CRROD    CRBAR  
  9    CRTRPLT  CRBE1    CRBE2    CRBE3    CRSPLINE MPC      MPCADD 
  9    MPCAX    MPC$
 10    SPC      SPC1     SPCADD   SPCAX    SPC$ 
 11    ASET     ASET1    OMIT     OMIT1    OMITAX   SUPAX    SUPORT 
 13    TEMP     TEMPAX   TEMPD    TEMPP1   TEMPP2   TEMPP3   TEMPRB 
 14    WTMASS   
 15    GRDPNT   
 16    PLOTEL   
 17    IRES 
 18    PLOT$
 19    POUT$
 20    ASETOUT  
 21    AUTOSPC  
 22    LOOP$
 23    LOOP1$   
 24    COUPMASS CPBAR    CPQDPLT  CPQUAD1  CPQUAD2  CPROD    CPTRBSC
 24    CPTRIA1  CPTRIA2  CPTRPLT  CPTUBE
 25    OPT      GRDEQ   
 59    DEFORM   DEFORM$  LOAD$    SPCD  
 60    FORCE    FORCE1   FORCE2   FORCEAX  LOAD     MOMAX    MOMENT 
 60    MOMENT1  PLOAD4
 60    MOMENT2  PLOAD    PLOAD1   PLOAD2   PLOAD3   PRESAX   SLOAD  
 61    GRAV     RFORCE  
 62    TEMPLD$  
$$$$
$*FILE BITS 
 94    BGPDT    CSTM     EQEXIN   GPDT     GPL      SIL 
 95    ECT  
 96    GPTT     SLT 
 97    GPECT    EST      GEI      MPTX     PCOMPS   EPTX
 98    KGGX 
 99    MGG  
100    KGG  
101    ASET     RG       USET     YS       OGPST
102    GPST 
103    GM   
104    KNN      MNN 
105    KFF      KFS      KSS      MFF   
106    GO       KAA      KOO      LOO      MAA      MOA      MOO
107    KLL      KLR      KRR      MLL      MLR      MRR 
108    LLL  
109    DM   
110    MR   
111    PG   
112    PL       PO       PS       QR
113    PLI      POI 
114    RULV     RUOV     ULV      UOOV  
115    PGG      QG       UGV
116    OEF1     OES1     OPG1     OQG1     OUGV1    PUGV1   
116    OEF1L    OES1L    OESF1
117    ELSETS   GPSETS   PLTPAR   PLTSETX   
118    KDICT    MDICT    MELM  
119    PLOTX1   
120    OGPWG
121    OQM1 
122    PLOTX2   
123    BGPDP    SIP 
$*  
=PAGE=
DISP3

APR.93  
$$$$$$$$
BEGIN    DISP 03 - NORMAL MODES ANALYSIS - APR. 1993 $  
****CARD   1-  6,  8- 16, 18- 22, 24, 61, 62
****RFMT 187,188,190-204,207-209
$$$$
PRECHK   ALL $  
****CARD   1-  6,  8- 16, 18- 22, 24, 61, 62
****RFMT 187,188,190-204,207-209
$$$$
FILE     LAMA=APPEND/PHIA=APPEND $  
****CARD   1-  6,  8- 16, 18- 22, 24, 61, 62
****RFMT 187,188,190-204,207-209
$$$$
PARAM    //*MPY*/CARDNO/0/0 $   
****CARD   1-  6,  8- 12, 14, 15, 17, 19, 21, 22, 24, 61, 62
****FILE 112,122,124,130,131
****PHS1  I1
$$$$ GP1 generates coordinate system transformation matrices, tables of grid 
$$$$ point locations, and tables relating the internal and external grid point 
$$$$ numbers.
GP1      GEOM1,GEOM2,/GPL,EQEXIN,GPDT,CSTM,BGPDT,SIL/S,N,LUSET/ 
         NOGPDT/ALWAYS=-1 $ 
****CARD   1
****FILE  94
****PHS2  D5
$$$$ PLTTRAN modifies special scalar grid points in the BGPDT and SIL tables.
PLTTRAN  BGPDT,SIL/BGPDP,SIP/LUSET/S,N,LUSEP $  
****CARD   1
****FILE 115
$$$$ GP2 generates Element Connection Table with internal indices.
GP2      GEOM2,EQEXIN/ECT $ 
****CARD   1,  2,  4,  5, 16
****FILE  95
$$$$
PARAML   PCDB//*PRES*////JUMPPLOT $ 
****SBST   7
****CARD  18
****FILE 116,119
****PHS2  DB5   
$$$$
PURGE    PLTSETX,PLTPAR,GPSETS,ELSETS/JUMPPLOT $
****SBST   7
****CARD  18
****FILE 116
$$$$ Go to label P1 if there are no structure plot requests.
COND     P1,JUMPPLOT $  
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 116,119
$$$$ PLTSET transforms user input into a form used to drive the structure 
$$$$ plotter.
PLTSET   PCDB,EQEXIN,ECT,EPT/PLTSETX,PLTPAR,GPSETS,ELSETS/S,N,NSIL/ 
         S,N,JUMPPLOT $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 116
$$$$ PRTMSG prints error messages associated with the structure plotter.
PRTMSG   PLTSETX// $
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 116
$$$$
PARAM    //*MPY*/PLTFLG/1/1 $   
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 119
$$$$
PARAM    //*MPY*/PFILE/0/0 $
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 119
$$$$ Go to label P1 if no undeformed structure plots are requested.
COND     P1,JUMPPLOT $  
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 119
$$$$ PLOT generates all requested undeformed structure plots.
PLOT     PLTPAR,GPSETS,ELSETS,CASECC,BGPDT,EQEXIN,SIL,,ECT,,,,/PLOTX1/  
         NSIL/LUSET/S,N,JUMPPLOT/S,N,PLTFLG/S,N,PFILE  $
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 119
$$$$ PRTMSG prints plotter data and engineering data for each undeformed plot 
$$$$ generated.
PRTMSG   PLOTX1//$  
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 119
$$$$
LABEL    P1 $   
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 116,119
****PHS2  DE5   
$$$$ GP3 generates Grid Point Temperature Table.
GP3      GEOM3,EQEXIN,GEOM2/,GPTT/NOGRAV $  
****CARD   1,  2, 13
****FILE  96
$$$$ TA1 generates element tables for use in matrix assembly and stress 
$$$$ recovery.
TA1      ECT,EPT,BGPDT,SIL,GPTT,CSTM,MPT,EQEXIN/EST,GEI,GPECT,,,MPTX,
         PCOMPS,EPTX/LUSET/S,N,NOSIMP/1/S,N,NOGENL/GENEL/S,N,COMPS $   
****CARD   1-  6, 13, 16
****FILE  97
$$$$
EQUIV    MPTX,MPT/COMPS/EPTX,EPT/COMPS $
****CARD   1-  6,  8- 16, 18- 22, 24, 61, 62
****FILE  97
$$$$ Go to label ERROR4 and print Error Message No. 4 if there are no 
$$$$ structural elements.
COND     ERROR4,NOSIMP $
****CARD   1,  2,  4-  6,  8, 14, 16, 24
****FILE  97
****PHS2  D5
$$$$
PARAM    //*ADD*/NOKGGX/1/0 $   
****CARD   1-  3,  6,  8
****FILE 118
$$$$
PARAM    //*ADD*/NOMGG/1/0 $
****CARD   1-  3,  5,  8
****FILE 118
****RFMT 187,190-192
$$$$ EMG generates structural element stiffness and mass matrix tables and 
$$$$ dictionaries for later assembly by the EMA module.
EMG      EST,CSTM,MPT,DIT,GEOM2,/KELM,KDICT,MELM,MDICT,,,/S,N,NOKGGX/   
         S,N,NOMGG////C,Y,COUPMASS/C,Y,CPBAR/C,Y,CPROD/C,Y,CPQUAD1/
         C,Y,CPQUAD2/C,Y,CPTRIA1/C,Y,CPTRIA2/C,Y,CPTUBE/C,Y,CPQDPLT/
         C,Y,CPTRPLT/C,Y,CPTRBSC/C,Y,VOLUME/C,Y,SURFACE $   
****CARD   1-  3,  5,  6,  8, 13, 24
****FILE 118
****RFMT 187,190-192
$$$$
PURGE    KGGX/NOKGGX $  
****CARD   1-  3,  6,  8
****FILE  98
$$$$ Go to label JMPKGG if no stiffness matrix is to be assembled.
COND     JMPKGG,NOKGGX $
****CARD   1-  3,  6,  8
****FILE  98
$$$$                                  x
$$$$ EMA assembles stiffness matrix [K  ] and Grid Point Singularity Table.
$$$$                                  gg
$$$$ 
EMA      GPECT,KDICT,KELM/KGGX $
****CARD   1-  3,  6,  8
****FILE  98
$$$$
PURGE    KDICT,KELM/ALWAYS $
****CARD   1-  3,  6,  8
****FILE 118
$$$$
LABEL    JMPKGG $   
****CARD   1-  3,  6,  8
****FILE  98
$$$$ Go to label ERROR1 and print Error Message No. 1 if no mass matrix is to 
$$$$ be assembled.
COND     ERROR1,NOMGG $ 
****CARD   1-  3,  5,  8, 14, 24
****FILE  99
****RFMT 187,188,190-204,207-209
****PHS2 D5 
$$$$ EMA assembles mass matrix [M  ].
$$$$                             gg
$$$$ 
EMA      GPECT,MDICT,MELM/MGG/-1/C,Y,WTMASS=1.0 $   
****CARD   1-  3,  5,  8, 14, 24
****FILE  99
****RFMT 187,190-192
$$$$
PURGE    MDICT,MELM/ALWAYS $
****CARD   1-  3,  5,  8, 14, 24
****FILE 118
$$$$ Go to label LGPWG if no weight and balance information is requested.
COND     LGPWG,GRDPNT $ 
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 120
$$$$ GPWG generates weight and balance information.
GPWG     BGPDP,CSTM,EQEXIN,MGG/OGPWG/V,Y,GRDPNT=-1/C,Y,WTMASS $ 
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 120
$$$$ OFP formats the weight and balance information prepared by GPWG and 
$$$$ places it on the system output file for printing.
OFP      OGPWG,,,,,//S,N,CARDNO $   
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 120
$$$$
LABEL    LGPWG $
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 120
$$$$               x
$$$$ Equivalence [K  ] to [K  ] if no general elements exist.
$$$$               gg       gg
$$$$ 
EQUIV    KGGX,KGG/NOGENL $  
****CARD   1-  4,  6,  8
****FILE 100
****PHS2  DB5     
$$$$ Go to label LBL11 if no general elements exist.
COND     LBL11,NOGENL $ 
****CARD   1-  4,  6,  8
****FILE 100
$$$$                                 x
$$$$ SMA3 adds general elements to [K  ] to obtain stiffness matrix [K  ].
$$$$                                 gg                               gg
$$$$
SMA3     GEI,KGGX/KGG/LUSET/NOGENL/NOSIMP $ 
****CARD   1-  4,  6,  8
****FILE 100
$$$$
LABEL    LBL11 $
****CARD   1-  4,  6,  8
****FILE 100
$$$$
GPSTGEN  KGG,SIL/GPST $ 
****CARD   1-  4,  6,  8
****FILE 102
****PHS2  DE5   
$$$$
PARAM    //*MPY*/NSKIP/0/0 $
****CARD   1,  9- 12
****FILE 101
$$$$ GP4 generates flags defining members of various displacement sets (USET) 
$$$$ and forms multipoint constraint equations [R ]{u } = 0.
$$$$                                             g   g
$$$$ 
GP4      CASECC,GEOM4,EQEXIN,GPDT,BGPDT,CSTM,GPST/RG,YS,USET,   
         ASET,OGPST/LUSET/S,N,MPCF1/S,N,MPCF2/S,N,SINGLE/S,N,OMIT/  
         S,N,REACT/S,N,NSKIP/S,N,REPEAT/S,N,NOSET/S,N,NOL/S,N,NOA/  
         C,Y,ASETOUT/C,Y,AUTOSPC $  
****CARD   1-  4,  6,  8- 12, 20, 21
****FILE 101
$$$$
OFP      OGPST,,,,,//S,N,CARDNO $   
****CARD   1-  4,  6,  8- 10, 21
****FILE 101
$$$$ Go to label ERROR3 and print Error Message No. 3 if no independent 
$$$$ degrees of freedom are defined.
COND     ERROR3,NOL $   
****CARD   1,  9- 12
****FILE 101
****PHS1  I1
$$$$
PURGE    KRR,KLR,DM,MLR,MR/REACT/GM/MPCF1/GO/OMIT/KFS/SINGLE/QG/NOSET $ 
****CARD   1,  9- 12
****FILE 103,105-107,109,110,113
$$$$ Equivalence [K  ] to [K  ] and [M  ] to [M  ] if no multipoint 
$$$$               gg       nn        gg       nn
$$$$ constraints exist.
EQUIV    KGG,KNN/MPCF1/MGG,MNN/MPCF1 $  
****CARD   1-  6,  8,  9, 14, 24
****FILE 104
$$$$ Go to label LBL2 if no multipoint constraints exist.
COND     LBL2,MPCF1 $   
****CARD   1-  6,  8,  9, 14, 24
****FILE 103,104
$$$$ MCE1 partitions multipoint constraint equations [R ] = [R |R ] and solves
$$$$                                                   g      m  n
$$$$                                                             -1
$$$$ for multipoint constraint transformation matrix [G ] = -[R ]  [R ].
$$$$                                                   m       m     n
$$$$ 
MCE1     USET,RG/GM $   
****CARD   1,  9
****FILE 103
$$$$ MCE2 partitions stiffness and mass matrices
$$$$ 
$$$$           +_      +                        +_      +
$$$$           |K  |K  |                        |M  |M  |
$$$$           | nn| nm|                        | nn| nm|
$$$$   [K  ] = |---+---|                [M  ] = |---+---|
$$$$     gg    |K  |K  |                  gg    |M  |M  |
$$$$           | mn| mm|                        | mn| mm|
$$$$           +       +                        +       +
$$$$           
$$$$ and performs the matrix reductions
$$$$ 
$$$$            _        T           T           T
$$$$   [K  ] = [K  ] + [G ][K  ] + [K  ][G ] + [G ][K  ][G ]
$$$$     nn      nm      m   mn      mn   m      m   mm   m
$$$$ 
$$$$            _        T           T           T
$$$$   [M  ] = [M  ] + [G ][M  ] + [M  ][G ] + [G ][M  ][G ]
$$$$     nn      nm      m   mn      mn   m      m   mm   m
$$$$ 
MCE2     USET,GM,KGG,MGG,,/KNN,MNN,, $  
****CARD   1-  6,  8,  9, 14, 24
****FILE 104
$$$$
LABEL    LBL2 $ 
****CARD   1-  6,  8,  9, 14, 24
****FILE 103,104
$$$$ Equivalence [K  ] to [K  ] and [M  ] to [M  ] if no single-point 
$$$$               nn       ff        nn       ff
$$$$ constraints exist.
EQUIV    KNN,KFF/SINGLE/MNN,MFF/SINGLE $
****CARD   1-  6,  8- 10, 14, 24
****FILE 105
$$$$ Go to label LBL3 if no single-point constraints exist.
COND     LBL3,SINGLE $  
****CARD   1-  6,  8- 10, 14, 24
****FILE 105
$$$$ SCE1 partitions out single-point constraints
$$$$ 
$$$$           +       +                        +       +
$$$$           |K  |K  |                        |M  |M  |
$$$$           | ff| fs|                        | ff| fs|
$$$$   [K  ] = |---+---|                [M  ] = |---+---|
$$$$     nn    |K  |K  |                  nn    |M  |M  |
$$$$           | sf| ss|                        | sf| ss|
$$$$           +       +                        +       +
$$$$
SCE1     USET,KNN,MNN,,/KFF,KFS,,MFF,, $
****CARD   1-  6,  8- 10, 14, 24
****FILE 105
$$$$ 
LABEL    LBL3  $
****CARD   1-  6,  8- 10, 14, 24
****FILE 105
$$$$ Equivalence [K  ] to [K  ] if no omitted coordinates exist.
$$$$               ff       aa
$$$$ 
EQUIV    KFF,KAA/OMIT $ 
****CARD   1-  4,  6,  8- 11
****FILE 106
$$$$ Equivalence [M  ] to [M  ] if no omitted coordinates exist.
$$$$               ff       aa
$$$$
EQUIV    MFF,MAA/OMIT $ 
****CARD   1-  5,  8- 11, 14, 24
****FILE 117
$$$$ Go to label LBL5 if no omitted coordinates exist.
COND     LBL5,OMIT $
****CARD   1-  6,  8- 11, 14, 24
****FILE 106,117
$$$$ SMP1 partitions constrained stiffness matrix
$$$$
$$$$           +_      +
$$$$           |K  |K  |
$$$$           | aa| ao|
$$$$   [K  ] = |---+---|
$$$$     ff    |K  |K  |
$$$$           | oa| oo|
$$$$           +       +
$$$$           
$$$$                                                -1
$$$$ solves for transformation matrix [G ] = -[[K  ]  [K  ]
$$$$                                    o        oo     oa
$$$$ 
$$$$                                        _        T
$$$$ and performs matrix reduction [K  ] = [K  ] + [K  ]{G ].
$$$$                                 aa      aa      oa   o
$$$$ 
SMP1     USET,KFF,,,/GO,KAA,KOO,LOO,,,,, $  
****CARD   1-  4,  6,  8- 11
****FILE 106
$$$$ SMP2 partitions constrained mass matrix
$$$$
$$$$           +_      +
$$$$           |M  |M  |
$$$$           | aa| ao|
$$$$   [M  ] = |---+---|
$$$$     ff    |M  |M  |
$$$$           | oa| oo|
$$$$           +       +
$$$$           
$$$$ and performs matrix reduction
$$$$
$$$$            _        T           T           T
$$$$   [M  ] = [M  ] + [M  ][G ] + [G ][M  ] + [G ][M  ][G ]
$$$$     aa      aa      oa   o      o   oa      o   oo   o
$$$$ 
SMP2     USET,GO,MFF/MAA $  
****CARD   1-  6,  8- 11, 14, 24
****FILE 117
$$$$
LABEL    LBL5 $ 
****CARD   1-  6,  8- 11, 14, 24
****FILE 106,117
$$$$ Go to label LBL6 if no free-body supports exist.
COND     LBL6,REACT $   
****CARD   1-  6,  8- 12, 14, 24
****FILE 107-110
****PHS1  DB1   
****PHS3  DB1   
****FILE 109
$$$$ RBMG1 partitions out free-body supports
$$$$ 
$$$$           +       +                        +       +
$$$$           |K  |K  |                        |M  |M  |
$$$$           | ll| lr|                        | ll| lr|
$$$$   [K  ] = |---+---|                [M  ] = |---+---|
$$$$     aa    |K  |K  |                  aa    |M  |M  |
$$$$           | rl| rr|                        | rl| rr|
$$$$           +       +                        +       +
$$$$
RBMG1    USET,KAA,MAA/KLL,KLR,KRR,MLL,MLR,MRR $ 
****CARD   1-  6,  8- 12, 14, 24
****FILE 107
$$$$ RBMG2 decomposes constrained stiffness matrix [K  ] = [L  ][U  ].
$$$$                                                 ll      ll   ll
$$$$ 
RBMG2    KLL/LLL $  
****CARD   1-  4,  6,  8- 12
****FILE 108
$$$$ RBMG3 forms rigid body transformation matrix
$$$$ 
$$$$               -1
$$$$   [D] = -[K  ]  [K  ]
$$$$            ll     lr
$$$$ 
$$$$ calculates rigid body check matrix
$$$$
$$$$                   T
$$$$   [X] = [K  ] + [K  ][D]
$$$$           rr      lr
$$$$ 
$$$$ and calculates rigid body error ratio
$$$$ 
$$$$ 
$$$$               ||X||
$$$$   epsilon = ---------
$$$$              ||K  ||
$$$$                 rr
$$$$
RBMG3    LLL,KLR,KRR/DM $   
****CARD   1-  4,  6,  8- 12
****FILE 109
$$$$ RBMG4 forms rigid body mass matrix
$$$$ 
$$$$                    T          T           T
$$$$   [m ] = [M  ] + [M  ][D] + [D ][M  ] + [D ][M  ][D]
$$$$     r      rr      lr             lr          ll
$$$$
RBMG4    DM,MLL,MLR,MRR/MR $
****CARD   1-  6,  8- 12, 14, 24
****FILE 110
$$$$
LABEL    LBL6 $ 
****CARD   1-  6,  8- 12, 14, 24
****FILE 107-110
****PHS1  DE1   
****PHS3  DE1   
$$$$ DPD extracts Eigenvalue Extraction Data from Dynamics data block.
DPD      DYNAMICS,GPL,SIL,USET/GPLD,SILD,USETD,,,,,,,EED,EQDYN/ 
         LUSET/LUSETD/NOTFL/NODLT/NOPSDL/NOFRL/ 
         NONLFT/NOTRL/S,N,NOEED//NOUE $ 
****CARD   1,  9- 12, 61
****FILE 111
****PHS1  DB1   
****PHS3  DB7   
$$$$ Go to label ERROR2 and print Error Message No. 2 if there is no 
$$$$ Eigenvalue Extraction Data.
COND     ERROR2,NOEED $ 
****CARD   1,  9- 12, 61
****FILE 111
****RFMT 187,188,190-204,207-209
$$$$
PARAM    //*MPY*/NEIGV/1/-1 $   
****CARD   1-  6,  8- 14, 24
****FILE 112
$$$$ READ extracts real eigenvalues and eigenvectors from the equation
$$$$ 
$$$$   [K   - lambda M  ]{u } = 0
$$$$     aa           aa   a
$$$$ 
$$$$ calculates rigid body modes by finding a square matrix [phi  ] such that
$$$$                                                            ro
$$$$              T
$$$$   [m ] = [phi  ][m ][phi  ]
$$$$     o        ro   r     ro
$$$$ 
$$$$ is diagonal and normalized, computes rigid body eigenvectors
$$$$ 
$$$$             +       +
$$$$             |Dphi   |
$$$$             |    ro |
$$$$   [phi  ] = |-------|
$$$$       ao    |phi    |
$$$$             |   ro  |
$$$$             +       +
$$$$ 
$$$$ calculates modal mass matrix
$$$$ 
$$$$             T
$$$$   [m] = [phi ][M  ][phi ]
$$$$             a   aa     a
$$$$ 
$$$$ and normalizes eigenvectors according to one of the following user 
$$$$ requests:
$$$$   1. Unit value of a selected component.
$$$$   2. Unit value of the largest component.
$$$$   3. Unit value of the generalized mass.
READ     KAA,MAA,MR,DM,EED,USET,CASECC/LAMA,PHIA,MI,OEIGS/*MODES*/  
         S,N,NEIGV $
****CARD   1-  6,  8- 12, 14, 24, 61, 62
****FILE 112
$$$$ OFP formats the summary of eigenvalue extraction information (OEIGS) 
$$$$ prepared by READ and places it on the system output file for printing.
OFP      OEIGS,,,,,//S,N,CARDNO $   
****CARD   1-  6,  8- 12, 14, 24, 61, 62
****FILE 112
$$$$ Go to label FINIS and make normal exit if no eigenvalues were found.
COND     FINIS,NEIGV $  
****CARD   1-  6,  8- 12, 14, 24, 61, 62
****FILE 112-114,121,122
$$$$ OFP formats the eigenvalues (LAMA) prepared by READ and places them on 
$$$$ the system output file for printing.
OFP      LAMA,,,,,//S,N,CARDNO $
****CARD   1-  6,  8- 12, 14, 24, 61, 62
****FILE 112
****PHS3  DE7   
$$$$ SDR1 recovers dependent components of the eigenvectors
$$$$ 
$$$$                                     phi
$$$$                                        a
$$$$  {phi } = [G ]{phi }               {----} = {phi }
$$$$      o      o     a                 phi         f
$$$$                                        o
$$$$
$$$$    phi           
$$$$       f          
$$$$   {----} = {phi }                  {phi } = [G ]{phi }
$$$$    phi         n                       m      m     n
$$$$       s          
$$$$
$$$$    phi           
$$$$       n          
$$$$   {----} = {phi }
$$$$    phi         g 
$$$$       m          
$$$$ 
$$$$                                                            T
$$$$ and recovers single-point forces of constraint {q } = [K  ] {phi }.
$$$$                                                  s      fs      f
$$$$
SDR1     USET,,PHIA,,,GO,GM,,KFS,,/PHIG,,QG/1/*REIG* $  
****CARD   1-  6,  8- 12, 14, 24, 61, 62
****FILE 113
****RFMT 187,188,190-204,207-209
****PHS3  I7
$$$$ Go to label NOMPCF if no multipoint constraint force balance is 
$$$$ requested.
COND     NOMPCF,GRDEQ $ 
****CARD  17
****FILE 121
****RFMT 187,188,190-204,207-209
****PHS2  DB5   
$$$$ EQMCK calculates the force and moment equilibrium check and prepares the 
$$$$ multipoint constraint force balance (OQM1) for output.
EQMCK    CASECC,EQEXIN,GPL,BGPDT,SIL,USET,KGG,GM,PHIG,LAMA,QG,CSTM/ 
         OQM1/V,Y,OPT=0/V,Y,GRDEQ/-1 $  
****CARD  17
****FILE 121
****RFMT 187,188,190-204,207-209
$$$$ OFP formats the table prepared by EQMCK and places it on the system 
$$$$ output file for printing.
OFP      OQM1,,,,,//S,N,CARDNO $
****CARD  17
****FILE 121
****RFMT 187,188,190-204,207-209
$$$$
LABEL    NOMPCF $   
****CARD  17
****FILE 121
****RFMT 187,188,190-204,207-209
$$$$ SDR2 calculates element forces (OEF1) and stresses (OES1) and prepares 
$$$$ eigenvectors (OPHIG) and single-point forces of constraint (OQG1) for 
$$$$ output and translation components of the eigenvectors (PPHIG).
SDR2     CASECC,CSTM,MPT,DIT,EQEXIN,SIL,,,BGPDP,LAMA,QG,PHIG,EST,,, 
         PCOMPS/,OQG1,OPHIG,OES1,OEF1,PPHIG,OES1L,OEF1L/ 
         *REIG*////COMPS $  
****CARD  18, 19
****FILE 114
$$$$ OFP formats the tables prepared by SDR2 and places them on the system 
$$$$ output file for printing.
OFP      OPHIG,OQG1,OEF1,OES1,,//S,N,CARDNO $   
****CARD  19
****FILE 114
$$$$
OFP      OEF1L,OES1L,,,,//S,N,CARDNO $   
****CARD  19
****FILE 114
$$$$ SCAN examines the element stresses and forces calculated by SDR2 and 
$$$$ generates scanned output that meets the specifications set by the user.
SCAN     CASECC,OES1,OEF1/OESF1/*RF* $  
****CARD 19
****FILE 114
$$$$ OFP formats the scanned output table prepared by SCAN and places it on 
$$$$ the system output file for printing.
OFP      OESF1,,,,,//S,N,CARDNO $   
****CARD  19
****FILE 114
$$$$ computes element strains
GPFDR    CASECC,PHIG,KELM,KDICT,ECT,EQEXIN,GPECT,LAMA,/ONRGY1,/*REIG* $
****CARD  19
****FILE 114
$$$$ putout strains
OFP      ONRGY1,,,,,//S,N,CARDNO $
****CARD  19
****FILE 114
$$$$ since KDICT and KELM files are no longer needed, purge them
PURGE    KDICT,KELM/ALWAYS $
****CARD   1-  3,  6,  8
****FILE 118
$$$$ Go to label P2 if no deformed structure plots are requested.
COND     P2,JUMPPLOT $  
****SBST   7
****CARD  18
****FILE 122
$$$$ PLOT generates all requested deformed structure and contour plots.
PLOT     PLTPAR,GPSETS,ELSETS,CASECC,BGPDT,EQEXIN,SIP,,PPHIG,GPECT,OES1,
         OES1L,/PLOTX2/NSIL/LUSEP/JUMPPLOT/PLTFLG/S,N,PFILE $   
****SBST   7
****CARD  18
****FILE 122
$$$$ PRTMSG prints plotter data, engineering data, and contour data for each 
$$$$ deformed plot generated.
PRTMSG   PLOTX2// $ 
****SBST   7
****CARD  18
****FILE 122
$$$$
LABEL    P2 $   
****SBST   7
****CARD  18
****FILE 122
$$$$ Go to label FINIS and make normal exit.
JUMP     FINIS $
****CARD   1-  6,  8- 22, 24, 61, 62
****FILE 122
****RFMT 187,188,190-204,207-209
****PHS1  DE1   
$$$$
LABEL    ERROR1 $   
****CARD   1-  3,  5,  8, 14, 24
****FILE  99
****RFMT 187,188,190-204,207-209
$$$$ Print Error Message No. 1 and terminate execution.
PRTPARM  //-1/*MODES* $ 
****CARD   1-  3,  5,  8, 14, 24
****FILE  99
****RFMT 187,188,190-204,207-209
****PHS2  DE5   
$$$$
LABEL    ERROR2 $   
****CARD   1,  9- 12, 61
****FILE 111
****RFMT 187,188,190-204,207-209
$$$$ Print Error Message No. 2 and terminate execution.
PRTPARM  //-2/*MODES* $ 
****CARD   1,  9- 12, 61
****FILE 111
****RFMT 187,188,190-204,207-209
$$$$
LABEL    ERROR3 $   
****CARD   1,  9- 12
****FILE 101
****RFMT 187,188,190-204,207-209
$$$$ Print Error Message No. 3 and terminate execution.
PRTPARM  //-3/*MODES* $ 
****CARD   1,  9- 12
****FILE 101
****RFMT 187,188,190-204,207-209
$$$$
LABEL    ERROR4 $   
****CARD   1-  6, 13, 16
****FILE  97
****RFMT 187,188,190-204,207-209
$$$$ Print Error Message No. 4 and terminate execution.
PRTPARM  //-4/*MODES* $ 
****CARD   1-  6, 13, 16
****FILE  97
****RFMT 187,188,190-204,207-209
$$$$
LABEL    FINIS $
****CARD   1-  6,  8- 22, 24, 61, 62
****FILE 112-114,121,122
****RFMT 187,188,190-204,207-209
$$$$
PURGE    DUMMY/ALWAYS $ 
****CARD   1-  6,  8- 22, 24, 61, 62
****FILE 112-114,121,122
****RFMT 187,188,190-204,207-209
$$$$
END      $  
****CARD   1-  6,  8- 22, 24, 61, 62
****RFMT 187,188,190-204,207-209
$$$$
$*CARD BITS 
  1    AXIC     AXIF     AXSLOT   CELAS1   CELAS2   CELAS3   CELAS4 
  1    CMASS1   
  1    CMASS2   CMASS3   CMASS4   CORD1C   CORD1R   CORD1S   CORD2C 
  1    CORD2R   
  1    CORD2S   FREEPT   GRDSET   GRID     GRIDB    GRIDF    GRIDS  
  1    POINTAX  
  1    PRESPT   RINGAX   RINGFL   SECTAX   SEQGP    SLBDY    SPOINT 
  2    ADUM1    ADUM2    ADUM3    ADUM4    ADUM5    ADUM6    ADUM7  
  2    ADUM8
  2    ADUM9    BAROR    CAXIF2   CAXIF3   CAXIF4   CBAR     CCONEAX
  2    CDUM1
  2    CDUM2    CDUM3    CDUM4    CDUM5    CDUM6    CDUM7    CDUM8  
  2    CDUM9
  2    CELBOW   CFLUID2  CFLUID3  CFLUID4  CHEXA1   CHEXA2   CIHEX1 
  2    CIHEX2   
  2    CIHEX3   CIS2D8   CONROD   CQDMEM   CQDMEM1  CQDMEM2  CQDPLT 
  2    CQUAD1   CQUAD2   CROD     CSHEAR   CSLOT3   CSLOT4   CTETRA 
  2    CTORDRG  CQUAD4   CTRIA3
  2    CTRAPAX  CTRAPRG  CTRBSC   CTRIA1   CTRIA2   CTRIAAX  CTRIARG
  2    CTRIM6   
  2    CTRMEM   CTRPLT   CTRPLT1  CTRSHL   CTUBE    CTWIST   CWEDGE 
  3    PBAR     PCONEAX  PDUM1    PDUM2    PDUM3    PDUM4    PDUM5  
  3    PDUM6
  3    PDUM7    PDUM8    PDUM9    PELBOW   PIHEX    PIS2D8   PQDMEM 
  3    PQDMEM1  PQDMEM2  PQDPLT   PQUAD1   PQUAD2   PROD     PSHEAR 
  3    PTORDRG  PSHELL   PCOMP    PCOMP1   PCOMP2
  3    PTRAPAX  PTRBSC   PTRIA1   PTRIA2   PTRIAAX  PTRIM6   PTRMEM 
  3    PTRPLT   PTRPLT1  PTRSHL   PTUBE    PTWIST   
  4    GENEL
  5    CONM1    CONM2    PMASS    FSLIST
  6    PELAS
  8    MAT1     MAT2     MAT3     MATT1    MATT2    MATT3    MAT8
  8    TABLEM1  TABLEM2  TABLEM3  TABLEM4  TEMPMT$  TEMPMX$  
  9    AXISYM$  CRIGD1   CRIGD2   CRIGD3   CRIGDR   CRROD    CRBAR  
  9    CRTRPLT  CRBE1    CRBE2    CRBE3    CRSPLINE MPC      MPCADD 
  9    MPCAX    MPC$
 10    SPC      SPC1     SPCADD   SPCAX    SPC$ 
 11    ASET     ASET1    OMIT     OMIT1    OMITAX   SUPAX    SUPORT 
 13    TEMP     TEMPAX   TEMPD    TEMPP1   TEMPP2   TEMPP3   TEMPRB 
 14    WTMASS   
 15    GRDPNT   
 16    PLOTEL   
 17    AOUT$
 18    PLOT$
 19    POUT$
 20    ASETOUT  
 21    AUTOSPC  
 22    OPT      GRDEQ   
 24    COUPMASS CPBAR    CPQDPLT  CPQUAD1  CPQUAD2  CPROD    CPTRBSC
 24    CPTRIA1  CPTRIA2  CPTRPLT  CPTUBE
 61    EIGR 
 62    METHOD$  
$$$$
$*FILE BITS 
 94    BGPDT    CSTM     EQEXIN   GPDT     GPL      SIL 
 95    ECT  
 96    GPTT 
 97    EST      GEI      GPECT    MPTX     PCOMPS   EPTX
 98    KGGX 
 99    MGG  
100    KGG  
101    YS       RG       USET     ASET     OGPST
102    GPST 
103    GM   
104    KNN      MNN 
105    KFF      KFS      MFF
106    GO       KOO      LOO      KAA   
107    KLL      KLR      KRR      MLL      MLR      MRR 
108    LLL  
109    DM   
110    MR   
111    EED      EQDYN    GPLD     SILD     USETD
112    LAMA     MI       OEIGS    PHIA  
113    PHIG     QG  
114    OEF1     OES1     OPHIG    OQG1     PPHIG
114    OEF1L    OES1L    OESF1
115    BGPDP    SIP 
116    ELSETS   GPSETS   PLTPAR   PLTSETX   
117    MAA  
118    KDICT    KELM     MDICT    MELM  
119    PLOTX1   
120    OGPWG
121    OQM1 
122    PLOTX2   
$*  
=PAGE=
DISP4

APR.93
$$$$$$$$
BEGIN    DISP 04 - DIFFERENTIAL STIFFNESS ANALYSIS - APR. 1993 $
****CARD   1-  6,  8- 25, 59- 62
****RFMT 187-189,191-204,207-209
$$$$
PRECHK   ALL $  
****CARD   1-  6,  8- 25, 59- 62
****RFMT 187-189,191-204,207-209
$$$$
PARAM    //*MPY*/CARDNO/0/0 $   
****CARD   1-  3,  5,  6,  8- 10, 14, 15, 19, 21, 24, 61
****FILE 101,112,121,126
$$$$ GP1 generates coordinate system transformation matrices, tables of grid 
$$$$ point locations, and tables relating the internal and external grid point 
$$$$ numbers.
GP1      GEOM1,GEOM2,/GPL,EQEXIN,GPDT,CSTM,BGPDT,SIL/S,N,LUSET/ 
         S,N,NOGPDT/MINUS1=-1 $ 
****CARD   1
****FILE  94
$$$$ Go to label ERROR3 and print Error Message No. 3 if there is no Grid 
$$$$ Point Definition Table.
COND     ERROR3,NOGPDT $
****CARD   1
****FILE  94
$$$$ PLTTRAN modifies special scalar grid points in the BGPDT and SIL tables.
PLTTRAN  BGPDT,SIL/BGPDP,SIP/LUSET/S,N,LUSEP $  
****CARD   1
****FILE 140
$$$$ GP2 generates Element Connection Table with internal indices.
GP2      GEOM2,EQEXIN/ECT $ 
****CARD   1,  2,  4,  5, 16
****FILE  95
$$$$
PARAML   PCDB//*PRES*////JUMPPLOT $ 
****SBST   7
****CARD  18
****FILE 122,125
$$$$
PURGE    PLTSETX,PLTPAR,GPSETS,ELSETS/JUMPPLOT $
****SBST   7
****CARD  18
****FILE 122
$$$$ Go to label P1 if there are no structure plot requests.
COND     P1,JUMPPLOT $  
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 122,125
$$$$ PLTSET transforms user input into a form used to drive the structure 
$$$$ plotter.
PLTSET   PCDB,EQEXIN,ECT,EPT/PLTSETX,PLTPAR,GPSETS,ELSETS/S,N,NSIL/ 
         S,N,JUMPPLOT $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 122
$$$$ PRTMSG prints error messages associated with the structure plotter.
PRTMSG   PLTSETX// $
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 122
$$$$
PARAM    //*MPY*/PLTFLG/1/1 $   
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 125
$$$$
PARAM    //*MPY*/PFILE/0/0 $
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 125
$$$$ Go to label P1 if no undeformed structure plots are requested.
COND     P1,JUMPPLOT $  
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 125
$$$$ PLOT generates all requested undeformed structure plots.
PLOT     PLTPAR,GPSETS,ELSETS,CASECC,BGPDT,EQEXIN,SIL,,ECT,,,,/PLOTX1/  
         NSIL/LUSET/S,N,JUMPPLOT/S,N,PLTFLG/S,N,PFILE $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 125
$$$$ PRTMSG prints plotter data and engineering data for each undeformed plot 
$$$$ generated.
PRTMSG   PLOTX1// $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 125
$$$$
LABEL    P1 $   
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 122,125
$$$$ GP3 generates Static Loads Table and Grid Point Temperature Table.
GP3      GEOM3,EQEXIN,GEOM2/SLT,GPTT/S,N,NOGRAV $   
****CARD   1,  2, 13, 60, 61
****FILE  96
$$$$
PARAM    //*AND*/NOMGG/NOGRAV/V,Y,GRDPNT=-1 $   
****CARD   1,  2, 15, 61
****FILE 123
****RFMT 187-189,191-204,207-209
$$$$ TA1 generates element tables for use in matrix assembly and stress 
$$$$ recovery.
TA1      ECT,EPT,BGPDT,SIL,GPTT,CSTM,MPT,EQEXIN/EST,GEI,GPECT,,,MPTX,
         PCOMPS,EPTX/LUSET/S,N,NOSIMP/1/S,N,NOGENL/S,N,GENEL/S,N,COMPS $   
****CARD   1-  6, 13, 16
****FILE  97
$$$$
EQUIV    MPTX,MPT/COMPS/EPTX,EPT/COMPS $
****CARD   1-  6,  8- 25, 59- 62
****FILE  97
$$$$ Go to label ERROR1 and print Error Message No. 1 if there are no 
$$$$ structural elements.
COND     ERROR1,NOSIMP $
****CARD   1,  2,  4-  6,  8, 16
****FILE  97
****RFMT 187-189,191-204,207-209
$$$$
PARAM    //*ADD*/NOKGGX/1/0 $   
****CARD   1-  4,  6,  8
****FILE 123
$$$$ EMG generates structural element stiffness and mass matrix tables and 
$$$$ dictionaries for later assembly by the EMA module.
EMG      EST,CSTM,MPT,DIT,GEOM2,/KELM,KDICT,MELM,MDICT,,,/S,N,NOKGGX/   
         S,N,NOMGG////C,Y,COUPMASS/C,Y,CPBAR/   
         C,Y,CPROD/C,Y,CPQUAD1/C,Y,CPQUAD2/C,Y,CPTRIA1/C,Y,CPTRIA2/ 
         C,Y,CPTUBE/C,Y,CPQDPLT/C,Y,CPTRPLT/C,Y,CPTRBSC/
         C,Y,VOLUME/C,Y,SURFACE $   
****CARD   1-  6,  8, 13, 15, 24, 61
****FILE 123
$$$$
PURGE    KGGX/NOKGGX/MGG/NOMGG $
****CARD   1-  6,  8, 14, 24, 61
****FILE  98, 99
$$$$ Go to label JMPKGG if no stiffness matrix is to be assembled.
COND     JMPKGG,NOKGGX $
****CARD   1-  4,  6,  8
****FILE  98
$$$$                                  x
$$$$ EMA assembles stiffness matrix [K  ] and Grid Point Singularity Table.
$$$$                                  gg
$$$$ 
EMA      GPECT,KDICT,KELM/KGGX $
****CARD   1-  4,  6,  8
****FILE  98
$$$$
PURGE    KDICT,KELM/MINUS1 $
****CARD   1-  4,  6,  8
****CARD 123
$$$$
LABEL    JMPKGG $   
****CARD   1-  4,  6,  8
****FILE  98
$$$$ Go to label JMPMGG if no mass matrix is to be assembled.
COND     JMPMGG,NOMGG $ 
****CARD   1-  5,  8, 14, 24, 61
****FILE  99
$$$$ EMA assembles mass matrix [M  ].
$$$$                             gg
$$$$ 
EMA      GPECT,MDICT,MELM/MGG/-1/C,Y,WTMASS=1.0 $   
****CARD   1-  5,  8, 14, 24, 61
****FILE  99
$$$$
PURGE    MDICT,MELM/MINUS1 $
****CARD   1-  5,  8, 14, 24, 61
****CARD 123
$$$$
LABEL    JMPMGG $   
****CARD   1-  5,  8, 14, 24, 61
****FILE  99
$$$$ Go to label LBL1 if no weight and balance information is requested.
COND     LBL1,GRDPNT $  
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE 126
$$$$ Go to label ERROR4 and print Error Message No. 4 if no mass matrix 
$$$$ exists.
COND     ERROR4,NOMGG $ 
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE 126
$$$$ GPWG prints weight and balance information.
GPWG     BGPDP,CSTM,EQEXIN,MGG/OGPWG/V,Y,GRDPNT/C,Y,WTMASS $
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE 126
$$$$ OFP formats the weight and balance information prepared by GPWG and 
$$$$ places it on the system output file for printing.
OFP      OGPWG,,,,,//S,N,CARDNO $   
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE 126
$$$$
LABEL    LBL1 $ 
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE 126
$$$$               x
$$$$ Equivalence [K  ] to [K  ] if no general elements exist.
$$$$               gg       gg
$$$$ 
EQUIV    KGGX,KGG/NOGENL $  
****CARD   1-  4,  6,  8
****FILE 100
$$$$ Go to label LBL11 if no general elements exist.
COND     LBL11,NOGENL $ 
****CARD   1-  4,  6,  8
****FILE 100
$$$$                                 x
$$$$ SMA3 adds general elements to [K  ] to obtain stiffness matrix [K  ].
$$$$                                 gg                               gg
$$$$
SMA3     GEI,KGGX/KGG/LUSET/NOGENL/NOSIMP $ 
****CARD   1-  4,  6,  8
****FILE 100
$$$$
LABEL    LBL11 $
****CARD   1-  4,  6,  8
****FILE 100
$$$$
GPSTGEN  KGG,SIL/GPST $ 
****CARD   1-  4,  6,  8
****FILE 102
$$$$
PARAM    //*MPY*/NSKIP/0/0 $
****CARD   1,  9- 12, 59
****FILE 101
$$$$ CASE copies the first record of CASECC to CASEXX.
CASE     CASECC,/CASEXX/*TRANRESP*/0/NOLOOP $   
****CARD   1,  9- 11
****FILE 124
$$$$ GP4 generates flags defining memeber of various displacement sets (USET), 
$$$$ forms multipoint constraint equations [R ]{u } = 0, and forms 
$$$$                                         g   g
$$$$ displacement vector {Y }.
$$$$                       s
$$$$ 
GP4      CASEXX,GEOM4,EQEXIN,GPDT,BGPDT,CSTM,GPST/RG,YS,USET,   
         ASET,OGPST/LUSET/S,N,MPCF1/S,N,MPCF2/S,N,SINGLE/S,N,OMIT/  
         S,N,REACT/S,N,NSKIP/S,N,REPEAT/S,N,NOSET/S,N,NOL/S,N,NOA/  
         C,Y,ASETOUT/C,Y,AUTOSPC $  
****CARD   1-  4,  6,  8- 12, 20, 21, 59
****FILE 101
$$$$
OFP      OGPST,,,,,//S,N,CARDNO $   
****CARD   1-  4,  6,  8- 10, 21
****FILE 101
$$$$ Go to label ERROR5 and print Error Message No. 5 if no independent 
$$$$ degrees of freedom are defined.
COND     ERROR5,NOL $   
****CARD   1,  9- 12, 59
****FILE 101
****RFMT 187-189,193-204,207-209
$$$$ Go to label LBL4D if no free-body supports are supplied.
COND     LBL4D,REACT $  
****CARD   1, 12
****FILE 101
****RFMT 187-189,193-204,207-209
$$$$ Go to label ERROR2 and print Error Message No. 2.
JUMP     ERROR2 $   
****CARD   1, 12
****FILE 101
****RFMT 187-189,193-204,207-209
$$$$
LABEL    LBL4D $
****CARD   1, 12
****FILE 101
****RFMT 187-189,193-204,207-209
$$$$
PURGE    GM/MPCF1/GO,KOO,LOO,PO,UOOV,RUOV/OMIT/PS,KFS,KSS,QG,   
         YBS,PBS,KBFS,KBSS,KDFS,KDSS/SINGLE $   
****CARD   1,  9- 12, 59
****FILE 103,105,106,109-111,115,117
$$$$ Equivalence [K  ] to [K  ] if no multipoint constraints exist.
$$$$               gg       nn
$$$$ 
EQUIV    KGG,KNN/MPCF1 $
****CARD   1-  4,  6,  8,  9
****FILE 104
$$$$ Go to label LBL2 if no multipoint constraints exist.
COND     LBL2,MPCF1 $   
****CARD   1-  4,  6,  8,  9
****FILE 103,104
$$$$ MCE1 partitions multipoint constraint equations [R ] = [R |R ] and
$$$$                                                   g      m  n
$$$$ solves for multipoint constraint transformation matrix [G ] =
$$$$       -1                                                 m
$$$$ - [R ]   [R ].
$$$$     m      n
$$$$ 
MCE1     USET,RG/GM $   
****CARD   1,  9
****FILE 103
$$$$ MCE2 partitions stiffness matrix
$$$$ 
$$$$           +_      +
$$$$           |K  |K  |
$$$$           | nn| nm|
$$$$   [K  ] = |---+---|
$$$$     gg    |K  |K  |
$$$$           | mn| mm|
$$$$           +       +
$$$$           
$$$$ and performs the matrix reduction
$$$$ 
$$$$            _        T           T           T
$$$$   [K  ] = [K  ] + [G ][K  ] + [K  ][G ] + [G ][K  ][G ]
$$$$     nn      nm      m   mn      mn   m      m   mm   m
$$$$
MCE2     USET,GM,KGG,,,/KNN,,, $
****CARD   1-  4,  6,  8,  9
****FILE 104
$$$$
LABEL    LBL2 $ 
****CARD   1-  4,  6,  8,  9
****FILE 103,104
$$$$ Equivalence [K  ] to [K  ] if no single-point constraints exist.
$$$$               nn       ff
$$$$ 
EQUIV    KNN,KFF/SINGLE $   
****CARD   1-  4,  6,  8- 10
****FILE 105
$$$$ Go to label LBL3 if no single-point constraints exist.
COND     LBL3,SINGLE $  
****CARD   1-  4,  6,  8- 10
****FILE 105
$$$$ SCE1 partitions out single-point constraints.
$$$$ 
$$$$           +       +
$$$$           |K  |K  |
$$$$           | ff| fs|
$$$$   [K  ] = |---+---|
$$$$     nn    |K  |K  |
$$$$           | sf| ss|
$$$$           +       +
$$$$
SCE1     USET,KNN,,,/KFF,KFS,KSS,,, $   
****CARD   1-  4,  6,  8- 10
****FILE 105
$$$$
LABEL    LBL3 $ 
****CARD   1-  4,  6,  8- 10
****FILE 105
$$$$ Equivalence [K  ] to [K  ] if no omitted coordinates exist.
$$$$               ff       aa
$$$$ 
EQUIV    KFF,KAA/OMIT $ 
****CARD   1-  4,  6,  8- 11
****FILE 106
$$$$ Go to label LBL5 if no omitted coordinates exist.
COND     LBL5,OMIT $
****CARD   1-  4,  6,  8- 11
****FILE 106
$$$$ SMP1 partitions constrained stiffness matrix
$$$$
$$$$           +_      +
$$$$           |K  |K  |
$$$$           | aa| ao|
$$$$   [K  ] = |---+---|
$$$$     ff    |K  |K  |
$$$$           | oa| oo|
$$$$           +       +
$$$$           
$$$$                                                -1
$$$$ solves for transformation matrix [G ] = -[[K  ]  [K  ]
$$$$                                    o        oo     oa
$$$$ 
$$$$                                        _        T
$$$$ and performs matrix reduction [K  ] = [K  ] + [K  ]{G ].
$$$$                                 aa      aa      oa   o
$$$$
SMP1     USET,KFF,,,/GO,KAA,KOO,LOO,,,,, $  
****CARD   1-  4,  6,  8- 11
****FILE 106
$$$$
LABEL    LBL5 $ 
****CARD   1-  4,  6,  8- 11
****FILE 106
$$$$ RBMG2 decomposes constrained stiffness matrix [K  ] = [L  ][U  ].
$$$$                                                 aa      ll   ll
$$$$ 
RBMG2    KAA/LLL $  
****CARD   1-  4,  6,  8- 11
****FILE 107
$$$$ SSG1 generates static load vectors [P }.
$$$$                                      g
$$$$ 
SSG1     SLT,BGPDT,CSTM,SIL,EST,MPT,GPTT,EDT,MGG,CASEXX,DIT,PCOMPS/ 
         PG,,,,/LUSET/1/COMPS $ 
****CARD   1-  3,  5,  6,  8, 13, 59- 62
****FILE 108
$$$$ Equivalence {P } to {P } if no constraints are applied.
$$$$               g       l
$$$$ 
EQUIV    PG,PL/NOSET $  
****CARD   1-  3,  5,  6,  8- 11, 13, 59- 62
****FILE 109
$$$$ Go to label LBL10 if no constraints are applied.
COND     LBL10,NOSET $  
****CARD   1-  3,  5,  6,  8- 11, 13, 59- 62
****FILE 109
$$$$ SSG2 applies constraints to static load vectors
$$$$ 
$$$$           _
$$$$           P
$$$$            n            _       T
$$$$   {P } = {--} , {P } = {P } + [G ]{P }
$$$$     g     P       n      n      m   m
$$$$            m
$$$$ 
$$$$           _
$$$$           P
$$$$            f            _      
$$$$   {P } = {--} , {P } = {P } - [K  ]{Y }
$$$$     n     P       f      f      fs   s
$$$$            s
$$$$ 
$$$$           P
$$$$            a                    T
$$$$   {P } = {--} , {P } = {P } + [G ]{P }
$$$$     f     P       l      a      o   o
$$$$            o
$$$$
SSG2     USET,GM,YS,KFS,GO,,PG/,PO,PS,PL $  
****CARD   1-  3,  5,  6,  8- 11, 13, 59- 62
****FILE 109
$$$$
LABEL    LBL10 $
****CARD   1-  3,  5,  6,  8- 11, 13, 59- 62
****FILE 109
$$$$ SSG3 solves for displacements of independent coordinates
$$$$ 
$$$$               -1  
$$$$   {u } = [K  ]  {P }
$$$$     l      ll     l
$$$$ 
$$$$ solves for displacements of omitted coordinates
$$$$ 
$$$$     o         -1  
$$$$   {u } = [K  ]  {P }
$$$$     o      oo     o
$$$$ 
$$$$ calculates residual vector (RULV) and residual vector error ratio for 
$$$$ independent coordinates
$$$$                
$$$$   {deltaP } = {P } - [K  ]{u }
$$$$          l      l      ll   l
$$$$ 
$$$$                T        
$$$$              {u }{deltaP }
$$$$                l        l
$$$$   epsilon  = -------------
$$$$          l       T 
$$$$                {P }{u }
$$$$                  l   l
$$$$ 
$$$$ and calculates residual vector (RUOV) and residual vector error ratio for
$$$$ omitted coordinates
$$$$ 
$$$$                             o
$$$$   {deltaP } = {P } - [K  ]{u }
$$$$          o      o      oo   o
$$$$ 
$$$$                T        
$$$$              {u }{deltaP }
$$$$                o        o
$$$$   epsilon  = -------------
$$$$          o       T   o
$$$$                {P }{u }
$$$$                  o   o
$$$$
SSG3     LLL,KAA,PL,LOO,KOO,PO/ULV,UOOV,RULV,RUOV/OMIT/V,Y,IRES=-1/ 
         1/S,N,EPSI $   
****CARD   1-  6,  8- 11, 13, 59- 62
****FILE 110
****RFMT 188
$$$$ Go to label LBL9 if residual vectors are not to be printed.
COND     LBL9,IRES $
****CARD   1-  6,  8- 11, 13, 17, 59- 62
****RFMT 188,189,192-204,207-209
$$$$ MATGPR prints the residual vector for independent coordinates (RULV).
MATGPR   GPL,USET,SIL,RULV//*L* $   
****CARD   1-  6,  8- 11, 13, 17, 59- 62
****RFMT 188,189,192-204,207-209
$$$$ MATGPR prints the residual vector for omitted coordinates (RUOV).
MATGPR   GPL,USET,SIL,RUOV//*O* $   
****CARD   1-  6,  8- 11, 13, 17, 59- 62
****RFMT 188,189,192-204,207-209
$$$$
LABEL    LBL9 $ 
****CARD   1-  6,  8- 11, 13, 17, 59- 62
****RFMT 188,189,192-204,207-209
$$$$ SDR1 recovers dependent displacements
$$$$ 
$$$$    u
$$$$     l                                  o
$$$$   {--} = {u }  ,   {u } = [G ]{u ] + {u }  ,
$$$$    u       a         o      o   a      o
$$$$     r
$$$$ 
$$$$    u                u            
$$$$     a                f           
$$$$   {--} = {u }  ,   {--} = {u }  ,
$$$$    u       f        Y       n    
$$$$     o                s           
$$$$                                  
$$$$                        u         
$$$$                         n        
$$$$  {u } = [G ]{u ]  ,   {--} = {u }
$$$$    m      m   n        u       g 
$$$$                         m        
$$$$ 
$$$$ and recovers single-point forces of constraint
$$$$ 
$$$$                    T
$$$$   {q } = -{P } + [K  ]{u } + [K  ]{Y }
$$$$     s       s      fs   f      ss   s
$$$$
SDR1     USET,,ULV,UOOV,YS,GO,GM,PS,KFS,KSS,/UGV,PG1,QG/1/*DS0* $   
****CARD   1-  6,  8- 11, 13, 59- 62
****FILE 111
$$$$ SDR2 calculates the element forces (OEF1) and stresses (OES1) and 
$$$$ prepares load vectors (OPG1), displacement vectors (OUGV1), and single-
$$$$ point forces of constraint (OQG1) for output and translation components 
$$$$ of the displacement vectors (PUGV1) for the static solution.
SDR2     CASECC,CSTM,MPT,DIT,EQEXIN,SIL,GPTT,EDT,BGPDP,,QG,UGV,EST,,PG, 
         PCOMPS/OPG1,OQG1,OUGV1,OES1,OEF1,PUGV1,OES1L,OEF1L/ 
         *DS0*////COMPS $   
****CARD  19
****FILE 112
$$$$ OFP formats the tables prepared by SDR2 and places them on the system 
$$$$ output file for printing.
OFP      OUGV1,OPG1,OQG1,OEF1,OES1,//S,N,CARDNO $   
****CARD  19
****FILE 112
$$$$
OFP      OEF1L,OES1L,,,,//S,N,CARDNO $   
****CARD  19
****FILE 112
$$$$ SCAN examines the element stresses and forces calculated by SDR2 and 
$$$$ generates scanned output that meets the specifications set by the user.
SCAN     CASECC,OES1,OEF1/OESF1/*RF* $  
****CARD 19 
****FILE 112
$$$$ OFP formats the scanned output table prepared by SCAN and places it on 
$$$$ the system output file for printing.
OFP      OESF1,,,,,//S,N,CARDNO $   
****CARD  19
****FILE 112
$$$$ Go to label P2 if no deformed static solution structure plots are 
$$$$ requested.
COND     P2,JUMPPLOT $  
****SBST   7
****CARD  18
****FILE 127
$$$$ PLOT generates all requested static solution deformed structure and 
$$$$ contour plots.
PLOT     PLTPAR,GPSETS,ELSETS,CASECC,BGPDT,EQEXIN,SIP,PUGV1,,GPECT,OES1,
         OES1L,/PLOTX2/NSIL/LUSEP/JUMPPLOT/PLTFLG/S,N,PFILE $   
****SBST   7
****CARD  18
****FILE 127
$$$$ PRTMSG prints plotter data, engineering data, and contour data for each 
$$$$ deformed static solution plot generated.
PRTMSG   PLOTX2// $ 
****SBST   7
****CARD  18
****FILE 127
$$$$
LABEL    P2 $   
****SBST   7
****CARD  18
****FILE 127
$$$$ TA1 generates element tables for use in differential stiffness matrix 
$$$$ assembly.
TA1      ECT,EPT,BGPDT,SIL,GPTT,,CSTM,/X1,X2,X3,ECPT,GPCT,,,/LUSET/  
         NOSIMP/0/NOGENL/GENEL $
****CARD   1-  6,  8- 11, 59- 62
****FILE 128
$$$$                                                 d
$$$$ DSMG1 generates differential stiffness matrix [K  ].
$$$$                                                 gg
$$$$ 
DSMG1    CASECC,GPTT,SIL,EDT,UGV,CSTM,MPT,ECPT,GPCT,DIT/KDGG/   
         DSCOSET $  
****CARD   1-  6,  8- 11, 59- 62
****FILE 113
$$$$
PARAM    //*ADD*/SHIFT/-1/0 $   
****CARD   1-  6,  8- 11, 59- 62
$$$$
PARAM    //*ADD*/COUNT/ALWAYS=-1/NEVER= 1 $ 
****CARD   1-  6,  8- 11, 59- 62
$$$$
PARAMR   //*ADD*/DSEPSI/0.0/0.0 $   
****CARD   1-  6,  8- 11, 59- 62
$$$$
PARAML   YS//*NULL*////NOYS $   
****CARD   1-  6,  8- 11, 59- 62
$$$$ Beginning of outer (stiffness adjustment) loop for differential stiffness 
$$$$ iteration.
LABEL    OUTLPTOP $ 
****CARD   1-  6,  8- 11, 59- 62
$$$$ Equivalence {P } to {P  } if no enforced displacements are specified.
$$$$               g       g1
$$$$ 
EQUIV    PG,PG1/NOYS $  
****CARD   1-  6,  8- 11, 59- 62
****FILE 111
$$$$
PARAM    //*KLOCK*/TO $ 
****CARD   1-  6,  8- 11, 59- 62
****FILE 114
$$$$               d        d
$$$$ Equivalence [K  ] to [K  ] if no multipoint constraints exist.
$$$$               gg       nn
$$$$
EQUIV    KDGG,KDNN/MPCF1 $  
****CARD   1-  6,  8- 11, 59- 62
****FILE 114
$$$$ Go to label LBL2D if no multipoint constraints exist.
COND     LBL2D,MPCF1 $  
****CARD   1-  6,  8- 11, 59- 62
****FILE 114
$$$$ MCE2 partitions stiffness matrix
$$$$ 
$$$$           +       +
$$$$           |_d   d |
$$$$           |K  |K  |
$$$$     d     | nn| nm|
$$$$   [K  ] = |---+---|
$$$$     gg    | d   d |
$$$$           |K  |K  |
$$$$           | mn| mm|
$$$$           +       +
$$$$      
$$$$ and performs the matrix reduction
$$$$ 
$$$$     d      _d       T   d       d           T   d
$$$$   [K  ] = [K  ] + [G ][K  ] + [K  ][G ] + [G ][K  ][G ]
$$$$     nn      nm      m   mn      mn   m      m   mm   m
$$$$
MCE2     USET,GM,KDGG,,,/KDNN,,, $  
****CARD   1-  6,  8- 11, 59- 62
****FILE 114
$$$$
LABEL    LBL2D $
****CARD   1-  6,  8- 11, 59- 62
****FILE 114
$$$$               d        d
$$$$ Equivalence [K  ] to [K  ] if no single-point constraints exist.
$$$$               nn       ff
$$$$ 
EQUIV    KDNN,KDFF/SINGLE $ 
****CARD   1-  6,  8- 11, 59- 62
****FILE 115
$$$$ Go to label LBL3D if no single-point constraints exist.
COND     LBL3D,SINGLE $ 
****CARD   1-  6,  8- 11, 59- 62
****FILE 115
$$$$ SCE1 partitions out single-point constraints
$$$$ 
$$$$           +       +
$$$$           | d   d |
$$$$           |K  |K  |
$$$$     d     | ff| fs|
$$$$   [K  ] = |---+---|
$$$$     nn    | d   d |
$$$$           |K  |K  |
$$$$           | sf| ss|
$$$$           +       +
$$$$
SCE1     USET,KDNN,,,/KDFF,KDFS,KDSS,,, $   
****CARD   1-  6,  8- 11, 59- 62
****FILE 115
$$$$
LABEL    LBL3D $
****CARD   1-  6,  8- 11, 59- 62
****FILE 115
$$$$               d        d
$$$$ Equivalence [K  ] to [K  ] if no omitted coordinates exist.
$$$$               ff       aa
$$$$
EQUIV    KDFF,KDAA/OMIT $   
****CARD   1-  6,  8- 11, 59- 62
****FILE 116
$$$$ Go to label LBL5D if no omitted coordinates exist.
COND     LBL5D,OMIT $   
****CARD   1-  6,  8- 11, 59- 62
****FILE 116
$$$$ SMP2 partitions constrained differential stiffness matrix
$$$$ 
$$$$           +       +
$$$$           |_d   d |
$$$$           |K  |K  |
$$$$     d     | aa| ao|
$$$$   [K  ] = |---+---|
$$$$     ff    | d | d |
$$$$           |K  |K  |
$$$$           | oa  oo|
$$$$           +       +
$$$$ 
$$$$ and performs matrix reduction
$$$$
$$$$     d      _d       d  T           T  d         T  d
$$$$   [K  ] = [K  ] + [K  ] [G ] + [G ] [K  ] + [G ] [K  ][G ]
$$$$     aa      aa      oa    o      o    oa      o    oo   o
$$$$
SMP2     USET,GO,KDFF/KDAA $
****CARD   1-  6,  8- 11, 59- 62
****FILE 116
$$$$
LABEL    LBL5D $
****CARD   1-  6,  8- 11, 59- 62
****FILE 116
$$$$                 d             b
$$$$ ADD [K  ] and [K  ] to form [K  ].
$$$$       aa        aa            ll
$$$$ 
ADD      KAA,KDAA/KBLL/(1.0,0.0)/(1.0,0.0) $
****CARD   1-  6,  8- 11, 59- 62
****FILE 117
$$$$                 d             b
$$$$ ADD [K  ] and [K  ] to form [K  ].
$$$$       fs        fs            fs
$$$$
ADD      KFS,KDFS/KBFS/(1.0,0.0)/(1.0,0.0) $
****CARD   1-  6,  8- 11, 59- 62
****FILE 117
$$$$                 d             b
$$$$ ADD [K  ] and [K  ] to form [K  ].
$$$$       ss        ss            ss
$$$$
ADD      KSS,KDSS/KBSS/(1.0,0.0)/(1.0,0.0) $
****CARD   1-  6,  8- 11, 59- 62
****FILE 117
$$$$ Go to label PGOK if no enforced displacements are specified.
COND     PGOK,NOYS $
****CARD   1-  6,  8- 11, 59- 62
****FILE 129-133
$$$$                   b
$$$$ MPYAD multiples [K  ] and {Y } to form {P  }.
$$$$                   ss        s            ss
$$$$
MPYAD    KBSS,YS,/PSS/0/1/1/1 $ 
****CARD   1-  6,  8- 11, 59- 62
****FILE 129
$$$$                   b
$$$$ MPYAD multiples [K  ] and {Y } to form {P  }.
$$$$                   fs        s            fs
$$$$
MPYAD    KBFS,YS,/PFS/0/1/1/1 $ 
****CARD   1-  6,  8- 11, 59- 62
****FILE 130
$$$$                   b
$$$$ UMERGE combines [K  ] and {P  } to form {P }.
$$$$                   fs        ss            n
$$$$
UMERGE   USET,PFS,PSS/PN/*N*/*F*/*S* $  
****CARD   1-  6,  8- 11, 59- 62
****FILE 131
$$$$                       x
$$$$ Equivalence {P } to {P } if no multipoint constraints exist.
$$$$               n       g
$$$$ 
EQUIV    PN,PGX/MPCF1 $ 
****CARD   1-  6,  8- 11, 59- 62
****FILE 132
$$$$ Go to label LBL6D if no multipoint constraints exist.
COND     LBL6D,MPCF1 $  
****CARD   1-  6,  8- 11, 59- 62
****FILE 132
$$$$                               x
$$$$ UMERGE expands {P } to form {P }.
$$$$                  n            g
$$$$
UMERGE   USET,PN,/PGX/*G*/*N*/*M* $ 
****CARD   1-  6,  8- 11, 59- 62
****FILE 132
$$$$
LABEL    LBL6D $
****CARD   1-  6,  8- 11, 59- 62
****FILE 132
$$$$        x
$$$$ ADD -{P } and {P } to form {P  }.
$$$$        g        g            gg
$$$$ 
ADD      PGX,PG/PGG/(-1.0,0.0)/(1.0,0.0) $  
****CARD   1-  6,  8- 11, 59- 62
****FILE 133
$$$$
$$$$ Equivalence {P  } to {P  }.
$$$$               gg       g1
$$$$
EQUIV    PGG,PG1/ALWAYS $   
****CARD   1-  6,  8- 11, 59- 62
****FILE 111
$$$$
LABEL    PGOK $ 
****CARD   1-  6,  8- 11, 59- 62
****FILE 129-133
$$$$
$$$$ ADD {P  } and nothing to create {P  }.
$$$$       g1                          g0
$$$$
ADD      PG1,/PG0/(1.0,0.0) $   
****CARD   1-  6,  8- 11, 59- 62
****FILE 134
$$$$ RBMG2 decomposes the combined differential stiffness matrix and elastic 
$$$$ stiffness matrix
$$$$
$$$$     b       b    b
$$$$   {K  ] = [L  ][U  ]
$$$$     ll      ll   ll
$$$$
RBMG2    KBLL/LBLL/S,N,POWER/S,N,DET $  
****CARD   1-  6,  8- 11, 59- 62
****FILE 118
$$$$ PRTPARM prints the scaled value of the determinant of the combined 
$$$$ differential stiffness matrix and elastic stiffness matrix.
PRTPARM  //0/*DET* $
****CARD   1-  6,  8- 11, 59- 62
****FILE 118
$$$$ PRTPARM prints the scale factor (power of ten) of the determinant of the 
$$$$ combined differential stiffness matrix and elastic stiffness matrix.
PRTPARM  //0/*POWER* $  
****CARD   1-  6,  8- 11, 59- 62
****FILE 118
$$$$ Beginning of inner (load correction) loop for differential stiffness 
$$$$ iteration.
LABEL    INLPTOP $  
****CARD   1-  6,  8- 11, 22, 23, 59- 62
****FILE 118
$$$$
PARAM    //*KLOCK*/TI $ 
****CARD   1-  6,  8- 11, 22, 23, 59- 62
$$$$ SSG2 applies constraints to static load vectors
$$$$ 
$$$$            _b
$$$$            p
$$$$             n      b     _b      T   b
$$$$   {P  } = {--} , {p } = {p } + [G ]{p }
$$$$     g1      b      n      n      m   m
$$$$            p 
$$$$             m
$$$$ 
$$$$           _b
$$$$           p
$$$$     b      f            _b      d
$$$$   {p } = {--} , {P } = {P } - [K  ]{Y }
$$$$     n      b      f      f      fs   s
$$$$           p 
$$$$            s
$$$$ 
$$$$           _b
$$$$           p
$$$$     b      a      b      b      T   b
$$$$   {p } = {--} , {P } = {P } + [G ]{P }
$$$$     f      b      l      a      o   o
$$$$
SSG2     USET,GM,YS,KDFS,GO,,PG1/,PBO,PBS,PBL $ 
****CARD   1-  6,  8- 11, 22, 23, 59- 62
****FILE 117
$$$$ SSG3 solves for displacements of independent coordinates for current 
$$$$ differential stiffness load vector
$$$$ 
$$$$     b      b  -1  b
$$$$   {u } = [K  ]  {P }
$$$$     l      ll     l
$$$$ 
$$$$ and calculates residual vector (RULV) and residual vector error ratio for 
$$$$ current differential stiffness load vector
$$$$ 
$$$$          b      b      b    b
$$$$   {deltaP } = {P } - [K  ]{u }
$$$$          l      l      ll   l
$$$$ 
$$$$                b T       b
$$$$              {u } {deltaP }
$$$$          b     l         l
$$$$   epsilon  = -------------
$$$$          l       b T  b
$$$$                {P } {u }
$$$$                  l    l
$$$$ 
SSG3     LBLL,KBLL,PBL,,,/UBLV,,RUBLV,/-1/V,Y,IRES/NDSKIP/S,N,  
         EPSI $ 
****CARD   1-  6,  8- 11, 17, 22, 23, 59- 62
****FILE 119
$$$$ Go to lable LBL9D if the residual vector for current differential 
$$$$ stiffness solution is not to be printed.
COND     LBL9D,IRES $   
****CARD   1-  6,  8- 11, 17, 22, 23, 59- 62
$$$$ MATGPR prints the residual vector for current differential stiffness 
$$$$ solution.
MATGPR   GPL,USET,SIL,RUBLV//*L* $  
****CARD   1-  6,  8- 11, 17, 22, 23, 59- 62
$$$$
LABEL    LBL9D $
****CARD   1-  6,  8- 11, 17, 22, 23, 59- 62
$$$$ SDR1 recovers dependent displacements for the current differential 
$$$$ stiffness solution
$$$$ 
$$$$                                 b
$$$$                                u         
$$$$    b          b      ob         l        
$$$$  {u } = [G ]{u ] + {u  }  ,   {--} = {u }
$$$$    o      o   l      o          b      f 
$$$$                                u         
$$$$                                 m
$$$$ 
$$$$     b
$$$$    u
$$$$     f      b         b          b
$$$$   {--} = {u }  ,   {u } = [G ]{u }
$$$$     b      n         m      m   n
$$$$    Y 
$$$$     s
$$$$ 
$$$$     b        
$$$$    u         
$$$$     n      b 
$$$$   {--} = {u }
$$$$     b      g 
$$$$    u                             
$$$$     m        
$$$$ 
$$$$ and recovers single-point forces of constraint for the current 
$$$$ differential stiffness solution
$$$$ 
$$$$     b       b      b    b      b    b
$$$$   {q } = -{P } + [K  ]{u } + [K  ]{Y }
$$$$     s       s      sf   f      ff   s
$$$$
SDR1     USET,,UBLV,,YS,GO,GM,PBS,KBFS,KBSS,/UBGV,,QBG/1/*DS1* $
****CARD   1-  6,  8- 11, 22, 23, 59- 62
****FILE 120
****RFMT 187-189,191-204,207-209
$$$$         b                     d
$$$$ ADD - {U } and {U } to form {U }.
$$$$         g        g            g
$$$$
ADD      UBGV,UGV/DUGV/(-1.0,0.0)/(1.0,0.0) $   
****CARD   1-  6,  8- 11, 22, 23, 59- 62
****FILE 135
$$$$                                                       d
$$$$ DSMG1 generates differential stiffness matrix [delta K  ].
$$$$                                                       gg
$$$$
DSMG1    CASECC,GPTT,SIL,EDT,DUGV,CSTM,MPT,ECPT,GPCT,DIT/DKDGG/ 
         DSCOSET $  
****CARD   1-  6,  8- 11, 22, 23, 59- 62
****FILE 136
$$$$ MPYAD forms the load vector for inner loop iteration
$$$$
$$$$                    d    b
$$$$   {P   } = [delta K  ]{U } + {P  }
$$$$     g              gg   g      go
$$$$      I1
$$$$
MPYAD    DKDGG,UBGV,PG0/PGI1/0/1/1/0 $  
****CARD   1-  6,  8- 11, 22, 23, 59- 62
****FILE 137
$$$$ DSCHK performs differential stiffness convergence checks.
DSCHK    PG1,PGI1,UBGV//C,Y,EPSIO=1.E-5/S,N,DSEPSI/C,Y,NT=10/TO/
         TI/S,N,DONE/S,N,SHIFT/S,N,COUNT/C,Y,BETAD=4 $  
****CARD   1-  6,  8- 11, 22, 23, 25, 59- 62
$$$$ Go to label DONE if differential stiffness iteration is complete.
COND     DONE,DONE $
****CARD   1-  6,  8- 11, 22, 23, 59- 62
$$$$ Go to label SHIFT if additional differential stiffness matrix changes are 
$$$$ necessary for further iteration.
COND     SHIFT,SHIFT $  
****CARD   1-  6,  8- 11, 22, 23, 59- 62
$$$$ Break the previous equivalence of {P } to {P  } and {P  } to {P   }
$$$$                                     g       g1        g1       g
$$$$                                                                 I1
$$$$ and establish equivalence of {P   } to {P  }.
$$$$                                g         g1
$$$$                                 I1
$$$$
EQUIV    PG,PG1/NEVER/PGI1,PG1/ALWAYS/PG1,PGI1/NEVER $  
****CARD   1-  6,  8- 11, 22, 23, 59- 62
****FILE 111,137
$$$$ Go to label INLPTOP for an additional inner loop stiffness iteration.
REPT     INLPTOP,1000 $ 
****CARD   1-  6,  8- 11, 22, 23, 59- 62
$$$$ TABPT table prints vectors {P   }, {P  }, and {P }.
$$$$                              g       g1         g
$$$$                               I1
$$$$ 
TABPT    PGI1,PG1,PG,,// $  
****CARD   1-  6,  8- 11, 22, 23, 59- 62
$$$$
LABEL    SHIFT $
****CARD   1-  6,  8- 11, 22, 23, 59- 62
$$$$              d         d             d
$$$$ ADD -[delta K  ] and [K  ] to form [K   ].
$$$$              gg        gg            gg1
$$$$
ADD      DKDGG,KDGG/KDGG1/(-1.0,0.0)/(1.0,0.0) $
****CARD   1-  6,  8- 11, 22, 23, 59- 62
****FILE 138
$$$$               b                d         d
$$$$ Equivalence {U } to {U } and [K   ] to [K  ]>
$$$$               g       g        gg1       gg
$$$$
EQUIV    UBGV,UGV/ALWAYS/KDGG1,KDGG/ALWAYS $
****CARD   1-  6,  8- 11, 22, 23, 59- 62
****FILE 111,113
$$$$                                     d        d                  b
$$$$ Break the previous equivalence of [K  ] to [K   ] and {U } to {U }.
$$$$                                     gg       gg1        g       g
$$$$
EQUIV    KDGG,KDGG1/NEVER/UGV,UBGV/NEVER $  
****CARD   1-  6,  8- 11, 22, 23, 59- 62
****FILE 120,138
$$$$ Go to label OUTLPTOP for an additional outer loop differential stiffness 
$$$$ iteration.
REPT     OUTLPTOP,1000 $
****CARD   1-  6,  8- 11, 22, 23, 59- 62
$$$$                      d       d
$$$$ TABPT table prints [K   ], [K  ], and {U }.
$$$$                      gg1     gg         g
$$$$ 
TABPT    KDGG1,KDGG,UGV,,// $   
****CARD   1-  6,  8- 11, 22, 23, 59- 62
$$$$
LABEL    DONE $ 
****CARD   1-  6,  8- 11, 22, 23, 59- 62
$$$$ SDR2 calculates element forces (OEFB1) and stresses (OESB1) and prepares 
$$$$ displacement vectors (OUBGV1) and single-point forces of constraint 
$$$$ (OQBG1) for output and translation components of the vector (PUBGV1) for 
$$$$ the differential stiffness solution.
SDR2     CASECC,CSTM,MPT,DIT,EQEXIN,SIL,GPTT,EDT,BGPDP,,QBG,UBGV,EST,,, 
         PCOMPS/,OQBG1,OUBGV1,OESB1,OEFB1,PUBGV1,OESB1L,OEFB1L/ 
         *DS1*////COMPS $   
****CARD  18, 19
****FILE 121
$$$$ OFP formats the tables prepared by SDR2 and places them on the system 
$$$$ output file for printing.
OFP      OUBGV1,OQBG1,OEFB1,OESB1,,//S,N,CARDNO $   
****CARD  19
****FILE 121
$$$$
OFP      OEFB1L,OESB1L,,,,//S,N,CARDNO $
****CARD  19
****FILE 121
$$$$ Go to label P3 if no differential stiffness solution deformed plots are 
$$$$ requested.
COND     P3,JUMPPLOT $  
****SBST   7
****CARD  18
****FILE 139
$$$$ PLOT generates all requested differential stiffness solution deformed 
$$$$ structure and contour plots.
PLOT     PLTPAR,GPSETS,ELSETS,CASECC,BGPDT,EQEXIN,SIP,PUBGV1,,GPECT,
         OESB1,OESB1L,/PLOTX3/NSIL/LUSEP/JUMPPLOT/PLTFLG/S,N,   
         PFILE $
****SBST   7
****CARD  18
****FILE 139
$$$$ PRTMSG prints plotter data, engineering data, and contour data for each 
$$$$ differential stiffness solution deformed plot generated.
PRTMSG   PLOTX3// $ 
****SBST   7
****CARD  18
****FILE 139
$$$$
LABEL    P3 $   
****SBST   7
****CARD  18
****FILE 139
$$$$ Go to label FINIS and make normal exit.
JUMP     FINIS $
****CARD   1-  6,  8- 25, 59- 62
****FILE 139
****RFMT 187-189,191-204,207-209
$$$$
LABEL    ERROR1 $   
****CARD   1,  2,  4-  6,  8, 16
****FILE  97
****RFMT 187-189,191-204,207-209
$$$$ Print Error Message No. 1 and terminate execution.
PRTPARM  //-1/*DIFFSTIF* $  
****CARD   1,  2,  4-  6,  8, 16
****FILE  97
****RFMT 187-189,191-204,207-209
$$$$
LABEL    ERROR2 $   
****CARD   1, 12
****FILE 101
****RFMT 187-189,191-204,207-209
$$$$ Print Error Message No. 2 and terminate execution.
PRTPARM  //-2/*DIFFSTIF* $  
****CARD   1, 12
****FILE 101
****RFMT 187-189,191-204,207-209
$$$$
LABEL    ERROR3 $   
****CARD   1
****FILE  94
****RFMT 187-189,191-204,207-209
$$$$ Print Error Message No. 3 and terminate execution.
PRTPARM  //-3/*DIFFSTIF* $  
****CARD   1
****FILE  94
****RFMT 187-189,191-204,207-209
$$$$
LABEL    ERROR4 $   
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE 123
****RFMT 187-189,191-204,207-209
$$$$ Print Error Message No. 4 and terminate execution.
PRTPARM  //-4/*DIFFSTIF* $  
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE 123
****RFMT 187-189,191-204,207-209
$$$$
LABEL    ERROR5 $   
****CARD   1,  9- 12, 59
****FILE 101
****RFMT 187-189,191-204,207-209
$$$$ Print Error Message No. 5 and terminate execution.
PRTPARM  //-5/*DIFFSTIF* $  
****CARD   1,  9- 12, 59
****FILE 101
****RFMT 187-189,191-204,207-209
$$$$
LABEL    FINIS $
****CARD   1-  6,  8- 25, 59- 62
****FILE 139
****RFMT 187-189,191-204,207-209
$$$$
PURGE    DUMMY/MINUS1 $ 
****CARD   1-  6,  8- 25, 59- 62
****RFMT 187-189,191-204,207-209
$$$$
END      $  
****CARD   1-  6,  8- 25, 59- 62
****RFMT 187-189,191-204,207-209
$$$$
$*CARD BITS 
  1    AXIC     AXIF     CELAS1   CELAS2   CELAS3   CELAS4   CMASS1 
  1    CMASS2   
  1    CMASS3   CMASS4   CORD1C   CORD1R   CORD1S   CORD2C   CORD2R 
  1    CORD2S   
  1    GRDSET   GRID     GRIDB    POINTAX  RINGAX   RINGFL   SECTAX 
  1    SEQGP    SPOINT  
  2    ADUM1    ADUM2    ADUM3    ADUM4    ADUM5    ADUM6    ADUM7  
  2    ADUM8
  2    ADUM9    BAROR    CBAR     CCONEAX  CDUM1    CDUM2    CDUM3  
  2    CDUM4
  2    CDUM5    CDUM6    CDUM7    CDUM8    CDUM9    CELBOW   CHEXA1 
  2    CHEXA2   
  2    CIHEX1   CIHEX2   CIHEX3   CIS2D8   CONROD   CQDMEM   CQDMEM1
  2    CQDMEM2  CQDPLT   CQUAD1   CQUAD2   CROD     CSHEAR   CTETRA 
  2    CTORDRG  CQUAD4   CTRIA3
  2    CTRAPAX  CTRAPRG  CTRBSC   CTRIA1   CTRIA2   CTRIAAX  CTRIARG
  2    CTRIM6   
  2    CTRMEM   CTRPLT   CTRPLT1  CTRSHL   CTUBE    CTWIST   CWEDGE 
  3    PBAR     PCONEAX  PDUM1    PDUM2    PDUM3    PDUM4    PDUM5  
  3    PDUM6
  3    PDUM7    PDUM8    PDUM9    PELBOW   PIHEX    PIS2D8   PQDMEM 
  3    PQDMEM1  PQDMEM2  PQDPLT   PQUAD1   PQUAD2   PROD     PSHEAR 
  3    PTORDRG  PSHELL   PCOMP    PCOMP1   PCOMP2
  3    PTRAPAX  PTRBSC   PTRIA1   PTRIA2   PTRIAAX  PTRIM6   PTRMEM 
  3    PTRPLT   PTRPLT1  PTRSHL   PTUBE    PTWIST   
  4    GENEL
  5    CONM1    CONM2    PMASS  
  6    PELAS
  8    MAT1     MAT2     MAT3     MATT1    MATT2    MATT3    MAT8
  8    TABLEM1  TABLEM2  TABLEM3  TABLEM4  TEMPMT$  TEMPMX$  
  9    AXISYM$  CRIGD1   CRIGD2   CRIGD3   CRIGDR   CRROD    CRBAR  
  9    CRTRPLT  CRBE1    CRBE2    CRBE3    CRSPLINE MPC      MPCADD 
  9    MPCAX    MPC$
 10    SPC      SPC1     SPCADD   SPCAX    SPC$ 
 11    ASET     ASET1    OMIT     OMIT1    OMITAX   SUPAX    SUPORT 
 13    TEMP     TEMPAX   TEMPD    TEMPP1   TEMPP2   TEMPP3   TEMPRB 
 14    WTMASS   
 15    GRDPNT   
 16    PLOTEL   
 17    IRES 
 18    PLOT$
 19    POUT$
 20    ASETOUT  
 21    AUTOSPC  
 22    LOOP$
 23    LOOP1$   
 24    COUPMASS CPBAR    CPQDPLT  CPQUAD1  CPQUAD2  CPROD    CPTRBSC
 24    CPTRIA1  CPTRIA2  CPTRPLT  CPTUBE
 25    EPSIO    NT       BETAD  
 59    DEFORM   DEFORM$  LOAD$    RFORCE$  SPCD 
 60    FORCE    FORCE1   FORCE2   FORCEAX  LOAD     MOMAX    MOMENT 
 60    MOMENT1  PLOAD4
 60    MOMENT2  PLOAD    PLOAD1   PLOAD2   PLOAD3   PRESAX   SLOAD  
 61    GRAV     RFORCE  
 62    TEMPLD$  
$$$$
$*FILE BITS 
 94    BGPDT    CSTM     EQEXIN   GPDT     GPL      SIL 
 95    ECT  
 96    GPTT     SLT 
 97    EST      GEI      GPECT    MPTX     PCOMPS   EPTX
 98    KGGX 
 99    MGG  
100    KGG  
101    ASET     RG       USET     YS       OGPST
102    GPST 
103    GM   
104    KNN  
105    KFF      KFS      KSS
106    GO       KAA      KOO      LOO   
107    LLL  
108    PG   
109    PL       PO       PS 
110    RULV     RUOV     ULV      UOOV  
111    PG1      QG       UGV
112    OEF1     OES1     OPG1     OQG1     OUGV1    PUGV1   
112    OEF1L    OES1L    OESF1
113    KDGG 
114    KDNN 
115    KDFF     KDFS     KDSS   
116    KDAA 
117    KBLL     YBS      KBFS     KBSS     PBL      PBS      PBO
118    LBLL 
119    UBLV     RUBLV   
120    QBG      UBGV
121    OEFB1    OESB1    OQBG1    OUBGV1   PUBGV1   
121    OEFB1L   OESB1L  
122    ELSETS   GPSETS   PLTPAR   PLTSETX   
123    KDICT    KELM     MDICT    MELM  
124    CASEXX   
125    PLOTX1   
126    OGPWG
127    PLOTX2   
128    X1       X2       X3       ECPT     GPCT 
129    PSS  
130    PFS  
131    PN   
132    PGX  
133    PGG  
134    PGO  
135    DUGV 
136    DKDGG
137    PGI1 
138    KDGG1
139    PLOTX3   
140    BGPDP    SIP 
$*  
=PAGE=
DISP5

APR.93
$$$$$$$$
BEGIN    DISP 05 - BUCKLING ANALYSIS - APR. 1993 $  
****CARD   1-  6,  8- 21, 24, 57- 62
****RFMT 187-190,192-204,207-209
$$$$
PRECHK   ALL $  
****CARD   1-  6,  8- 21, 24, 57- 62
****RFMT 187-190,192-204,207-209
$$$$
FILE     LAMA=APPEND/PHIA=APPEND $  
****CARD   1-  6,  8- 21, 24, 57- 62
****RFMT 187-190,192-204,207-209
$$$$
PARAM    //*MPY*/CARDNO/0/0 $   
****CARD   1-  6,  8- 11, 14, 15, 19, 21, 24, 57- 62
****FILE 101,112,118,120,125
$$$$ GP1 generates coordinate system transformation matrices, tables of grid 
$$$$ point locations, and table relating the internal and external grid point 
$$$$ numbers.
GP1      GEOM1,GEOM2,/GPL,EQEXIN,GPDT,CSTM,BGPDT,SIL/S,N,LUSET/ 
         NOGPDT/MINUS1=-1 $ 
****CARD   1
****FILE  94
$$$$ PLTTRAN modifies special scalar grid points in the BGPDT and SIL tables.
PLTTRAN  BGPDT,SIL/BGPDP,SIP/LUSET/S,N,LUSEP $  
****CARD   1
****FILE 129
$$$$ GP2 generates Element Connection Table with internal indices.
GP2      GEOM2,EQEXIN/ECT $ 
****CARD   1,  2,  4,  5, 16
****FILE  95
$$$$
PARAML   PCDB//*PRES*////JUMPPLOT $ 
****SBST   7
****CARD  18
****FILE 122,124
$$$$
PURGE    PLTSETX,PLTPAR,GPSETS,ELSETS/JUMPPLOT $
****SBST   7
****CARD  18
****FILE 122
$$$$ Go to label P1 if there are no structure plot requests.
COND     P1,JUMPPLOT $  
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 122,124
$$$$ PLTSET transforms user input into a form used to drive the structure 
$$$$ plotter.
PLTSET   PCDB,EQEXIN,ECT,EPT/PLTSETX,PLTPAR,GPSETS,ELSETS/S,N,NSIL/ 
         S,N,JUMPPLOT $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 122
$$$$ PRTMSG prints error messages associated with the structure plotter.
PRTMSG   PLTSETX// $
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 122
$$$$
PARAM    //*MPY*/PLTFLG/1/1 $   
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 124
$$$$
PARAM    //*MPY*/PFILE/0/0 $
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 124
$$$$ Go to label P1 if no undeformed structure plots are requested.
COND     P1,JUMPPLOT $  
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 124
$$$$ PLOT generates all requested undeformed structure plots.
PLOT     PLTPAR,GPSETS,ELSETS,CASECC,BGPDT,EQEXIN,SIL,,ECT,,,,/PLOTX1/  
         NSIL/LUSET/S,N,JUMPPLOT/S,N,PLTFLG/S,N,PFILE $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 124
$$$$ PRTMSG prints plotter data and engineering data for each undeformed plot 
$$$$ generated.
PRTMSG   PLOTX1// $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 124
$$$$
LABEL    P1 $   
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 122,124
$$$$ GP3 generates Static Loads Table and Grid Point Temperature Table.
GP3      GEOM3,EQEXIN,GEOM2/SLT,GPTT/S,N,NOGRAV $   
****CARD   1,  2, 13, 57, 60
****FILE  96
$$$$
PARAM    //*AND*/NOMGG/NOGRAV/V,Y,GRDPNT=-1 $   
****CARD   1,  2, 15, 57
****FILE 123
****RFMT 187-190,192-204,207-209
$$$$ TA1 generates element tables for use in matrix assembly and stress 
$$$$ recovery.
TA1      ECT,EPT,BGPDT,SIL,GPTT,CSTM,MPT,EQEXIN/EST,GEI,GPECT,,,MPTX,
         PCOMPS,EPTX/LUSET/S,N,NOSIMP/1/S,N,NOGENL/S,N,GENEL/S,N,COMPS $   
****CARD   1-  6, 13, 16
****FILE  97
$$$$
EQUIV    MPTX,MPT/COMPS/EPTX,EPT/COMPS $
****CARD   1-  6,  8- 21, 24, 57- 62
****FILE  97
$$$$ Go to label ERROR1 and print Error Message No. 1 if no structural 
$$$$ elements have been defined.
COND     ERROR1,NOSIMP $
****CARD   1,  2,  4-  6,  8, 16
****FILE  97
****RFMT 187-190,192-204,207-209
$$$$
PARAM    //*ADD*/NOKGGX/1/0 $   
****CARD   1-  3,  6,  8
****FILE 123
$$$$ EMG generates structural element stiffness and mass matrix tables and 
$$$$ dictionaries for later assembly by the EMA module.
EMG      EST,CSTM,MPT,DIT,GEOM2,/KELM,KDICT,MELM,MDICT,,,/S,N,NOKGGX/   
         S,N,NOMGG////C,Y,COUPMASS/C,Y,CPBAR/   
         C,Y,CPROD/C,Y,CPQUAD1/C,Y,CPQUAD2/C,Y,CPTRIA1/C,Y,CPTRIA2/ 
         C,Y,CPTUBE/C,Y,CPQDPLT/C,Y,CPTRPLT/C,Y,CPTRBSC/
         C,Y,VOLUME/C,Y,SURFACE $   
****CARD   1-  3,  5,  6,  8, 13, 15, 24, 57
****FILE 123
$$$$
PURGE    KGGX/NOKGGX/MGG/NOMGG $
****CARD   1-  3,  5,  6,  8, 14, 15, 57
****FILE  98, 99
$$$$ Go to label JMPKGG if no stiffness matrix is to be assembled.
COND     JMPKGG,NOKGGX $
****CARD   1-  3,  6,  8
****FILE  98
$$$$                                  x
$$$$ EMA assembles stiffness matrix [K  ] and Grid Point Singularity Table.
$$$$                                  gg
$$$$ 
EMA      GPECT,KDICT,KELM/KGGX $
****CARD   1-  3,  6,  8
****FILE  98
$$$$
PURGE    KDICT,KELM/MINUS1 $
****CARD   1-  3,  6,  8
****FILE 123
$$$$
LABEL    JMPKGG $   
****CARD   1-  3,  6,  8
****FILE  98
$$$$ Go to label JMPMGG if no mass matrix is to be assembled.
COND     JMPMGG,NOMGG $ 
****CARD   1-  3,  5,  8, 14, 15, 57
****FILE  99
$$$$ EMA assembles mass matrix [M  ].
$$$$                             gg
$$$$
EMA      GPECT,MDICT,MELM/MGG/-1/C,Y,WTMASS=1.0 $   
****CARD   1-  3,  5,  8, 14, 15, 57
****FILE  99
$$$$
PURGE    MDICT,MELM/MINUS1 $
****CARD   1-  3,  5,  8, 14, 15, 57
****FILE 123
$$$$
LABEL    JMPMGG $   
****CARD   1-  3,  5,  8, 14, 15, 57
****FILE  99
$$$$ Go to label LBL1 if no weight and balance information is requested.
COND     LBL1,GRDPNT $  
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24, 57
****FILE 125
$$$$ Go to label ERROR5 and print Error Message No. 5 if no mass matrix 
$$$$ exists.
COND     ERROR5,NOMGG $ 
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24, 57
****FILE 125
$$$$ GPWG generates weight and balance information.
GPWG     BGPDP,CSTM,EQEXIN,MGG/OGPWG/V,Y,GRDPNT/C,Y,WTMASS $
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24, 57
****FILE 125
$$$$ OFP formats the weight and balance information prepared by GPWG and 
$$$$ places it on the system output file for printing.
OFP      OGPWG,,,,,//S,N,CARDNO $   
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24, 57
****FILE 125
$$$$
LABEL    LBL1 $ 
****CARD   1-  3,  5,  8, 14, 15, 57
****FILE 125
$$$$               x
$$$$ Equivalence [K  ] to [K  ] if there are no general elements.
$$$$               gg       gg
$$$$
EQUIV    KGGX,KGG/NOGENL $  
****CARD   1-  4,  6,  8
****FILE 100
$$$$ Go to label LBL11 if there are no general elements.
COND     LBL11,NOGENL $ 
****CARD   1-  4,  6,  8
****FILE 100
$$$$                                 x
$$$$ SMA3 adds general elements to [K  ] to obtain stiffness matrix [K  ].
$$$$                                 gg                               gg
$$$$
SMA3     GEI,KGGX/KGG/LUSET/NOGENL/NOSIMP $ 
****CARD   1-  4,  6,  8
****FILE 100
$$$$
LABEL    LBL11 $
****CARD   1-  4,  6,  8
****FILE 100
$$$$
GPSTGEN  KGG,SIL/GPST $ 
****CARD   1-  4,  6,  8
****FILE 102
$$$$
PARAM    //*MPY*/NSKIP/0/0 $
****CARD   1,  9- 12, 59
****FILE 101
$$$$ GP4 generates flags defining members of various displacement sets (USET), 
$$$$ forms multipoint constraint equations [R ]{u } = 0, and forms enforced
$$$$                                         g   g
$$$$ displacement vector {Y }.
$$$$                       s
$$$$ 
GP4      CASECC,GEOM4,EQEXIN,GPDT,BGPDT,CSTM,GPST/RG,YS,USET,   
         ASET,OGPST/LUSET/S,N,MPCF1/S,N,MPCF2/S,N,SINGLE/S,N,OMIT/  
         S,N,REACT/S,N,NSKIP/S,N,REPEAT/S,N,NOSET/S,N,NOL/S,N,NOA/  
         C,Y,ASETOUT/C,Y,AUTOSPC $  
****CARD   1-  4,  6,  8- 12, 20, 21, 59
****FILE 101
$$$$
OFP      OGPST,,,,,//S,N,CARDNO $   
****CARD   1-  4,  6,  8- 10, 21
****FILE 101
$$$$ Go to label ERROR6 and print Error Message No. 6 if no independent 
$$$$ degrees of freedom are defined.
COND     ERROR6,NOL $   
****CARD   1,  9- 12, 59
****FILE 101
****RFMT 187-190,192-204,207-209
$$$$ Go to label LBL4D if there are no free-body supports.
COND     LBL4D,REACT $  
****CARD   1, 12
****FILE 101
****RFMT 187-189,193-204,207-209
$$$$ Go to label ERROR2 and print Error Message No. 2.
JUMP     ERROR2 $   
****CARD   1, 12
****FILE 101
****RFMT 187-189,193-204,207-209
$$$$
LABEL    LBL4D $
****CARD   1, 12
****FILE 101
****RFMT 187-189,193-204,207-209
$$$$
PARAM    //*AND*/NOSR/SINGLE/REACT $
****CARD   1,  9- 12, 59
****FILE 101
$$$$
PURGE    GM/MPCF1/GO,KOO,LOO,PO,UOOV,RUOV/OMIT/PS,KFS,KSS,KDFS/SINGLE/  
         QG/NOSR $  
****CARD   1,  9- 12, 59
****FILE 103,105,106,109-111,115
$$$$ Equivalence [K  ] to [K  ] if there are not multipoint constraints.
$$$$               gg       nn
$$$$
EQUIV    KGG,KNN/MPCF1 $
****CARD   1-  4,  6,  8,  9
****FILE 104
$$$$ Go to label LBL2 if there are no multipoint constraints.
COND     LBL2,MPCF1 $   
****CARD   1-  4,  6,  8,  9
****FILE 103,104
$$$$ MCE1 partitions multipoint constraint equation [R ] = [R |R ] and solves
$$$$                                                  g      m  n
$$$$                                                             -1
$$$$ for multipoint constraint transformation matrix [G ] = -[R ]  [R ].
$$$$                                                   m       m     n
$$$$ 
MCE1     USET,RG/GM $   
****CARD   1,  9
****FILE 103
$$$$ MCE2 partitions stiffness matrix
$$$$ 
$$$$           +_      +
$$$$           |K  |K  |
$$$$           | nn| nm|
$$$$   [K  ] = |---+---|
$$$$     gg    |K  |K  |
$$$$           | mn| mm|
$$$$           +       +
$$$$           
$$$$ and performs the matrix reduction
$$$$ 
$$$$            _        T           T           T
$$$$   [K  ] = [K  ] + [G ][K  ] + [K  ][G ] + [G ][K  ][G ]
$$$$     nn      nm      m   mn      mn   m      m   mm   m
$$$$
MCE2     USET,GM,KGG,,,/KNN,,, $
****CARD   1-  4,  6,  8,  9
****FILE 104
$$$$
LABEL    LBL2 $ 
****CARD   1-  4,  6,  8,  9
****FILE 103,104
$$$$ Equivalence [K  ] to [K  ] if no single-point constraints exist.
$$$$               nn       ff
$$$$
EQUIV    KNN,KFF/SINGLE $   
****CARD   1-  4,  6,  8- 10
****FILE 105
$$$$ Go to label LBL3 if no single-point constraints exist.
COND     LBL3,SINGLE $  
****CARD   1-  4,  6,  8- 10
****FILE 105
$$$$ SCE1 partitions out single-point constraints.
$$$$ 
$$$$           +       +
$$$$           |K  |K  |
$$$$           | ff| fs|
$$$$   [K  ] = |---+---|
$$$$     nn    |K  |K  |
$$$$           | sf| ss|
$$$$           +       +
$$$$
SCE1     USET,KNN,,,/KFF,KFS,KSS,,, $   
****CARD   1-  4,  6,  8- 10
****FILE 105
$$$$
LABEL    LBL3 $ 
****CARD   1-  4,  6,  8- 10
****FILE 105
$$$$ Equivalence [K  ] to [K  ] if no omitted coordinates exist.
$$$$               ff       aa
$$$$
EQUIV    KFF,KAA/OMIT $ 
****CARD   1-  4,  6,  8- 11
****FILE 106
$$$$ Go to label LBL5 if no omitted coordinates exist.
COND     LBL5,OMIT $
****CARD   1-  4,  6,  8- 11
****FILE 106
$$$$ SMP1 partitions constrained stiffness matrix
$$$$
$$$$           +_      +
$$$$           |K  |K  |
$$$$           | aa| ao|
$$$$   [K  ] = |---+---|
$$$$     ff    |K  |K  |
$$$$           | oa| oo|
$$$$           +       +
$$$$           
$$$$                                                -1
$$$$ solves for transformation matrix [G ] = -[[K  ]  [K  ]
$$$$                                    o        oo     oa
$$$$ 
$$$$                                        _        T
$$$$ and performs matrix reduction [K  ] = [K  ] + [K  ]{G ].
$$$$                                 aa      aa      oa   o
$$$$
SMP1     USET,KFF,,,/GO,KAA,KOO,LOO,,,,, $  
****CARD   1-  4,  6,  8- 11
****FILE 106
$$$$
LABEL    LBL5 $ 
****CARD   1-  4,  6,  8- 11
****FILE 106
$$$$ RBMG2 decomposes constrained stiffness matrix [K  ] = [L  ][U  ].
$$$$                                                 aa      ll   ll
$$$$
RBMG2    KAA/LLL $  
****CARD   1-  4,  6,  8- 11
****FILE 107
$$$$ SSG1 generates static load vectors {P }.
$$$$                                      g
$$$$ 
SSG1     SLT,BGPDT,CSTM,SIL,EST,MPT,GPTT,EDT,MGG,CASECC,DIT,PCOMPS/ 
         PG,,,,/LUSET/1/COMPS $ 
****CARD   1-  3,  5,  6,  8, 13, 57- 60
****FILE 108
$$$$ Equivalence {P } to {P } if no constraints are applied.
$$$$               g       l
$$$$
EQUIV    PG,PL/NOSET $  
****CARD   1-  3,  5,  6,  8- 11, 13, 57- 60
****FILE 109
$$$$ Go to label LBL10 if no constraints are applied.
COND     LBL10,NOSET $  
****CARD   1-  3,  5,  6,  8- 11, 13, 57- 60
****FILE 109
$$$$ SSG2 applies constraints to static load vectors
$$$$ 
$$$$           _
$$$$           P
$$$$            n            _       T
$$$$   {P } = {--} , {P } = {P } + [G ]{P }
$$$$     g     P       n      n      m   m
$$$$            m
$$$$ 
$$$$           _
$$$$           P
$$$$            f            _      
$$$$   {P } = {--} , {P } = {P } - [K  ]{Y }
$$$$     n     P       f      f      fs   s
$$$$            s
$$$$ 
$$$$           P
$$$$            a                    T
$$$$   {P } = {--} , {P } = {P } + [G ]{P }
$$$$     f     P       l      a      o   o
$$$$            o
$$$$
SSG2     USET,GM,YS,KFS,GO,,PG/,PO,PS,PL $  
****CARD   1-  3,  5,  6,  8- 11, 13, 57- 60
****FILE 109
$$$$
LABEL    LBL10 $
****CARD   1-  3,  5,  6,  8- 11, 13, 57- 60
****FILE 109
$$$$ SSG3 solves for displacements of independent coordinates
$$$$ 
$$$$               -1  
$$$$   {u } = [K  ]  {P }
$$$$     l      ll     l
$$$$ 
$$$$ solves for displacements of omitted coordinates
$$$$ 
$$$$     o         -1  
$$$$   {u } = [K  ]  {P }
$$$$     o      oo     o
$$$$ 
$$$$ calculates residual vector (RULV) and residual vector error ratio for 
$$$$ independent coordinates
$$$$                
$$$$   {deltaP } = {P } - [K  ]{u }
$$$$          l      l      ll   l
$$$$ 
$$$$                T        
$$$$              {u }{deltaP }
$$$$                l        l
$$$$   epsilon  = -------------
$$$$          l       T 
$$$$                {P }{u }
$$$$                  l   l
$$$$ 
$$$$ and calculates residual vector (RUOV) and residual vector error ratio for
$$$$ omitted coordinates
$$$$ 
$$$$                             o
$$$$   {deltaP } = {P } - [K  ]{u }
$$$$          o      o      oo   o
$$$$ 
$$$$                T        
$$$$              {u }{deltaP }
$$$$                o        o
$$$$   epsilon  = -------------
$$$$          o       T   o
$$$$                {P }{u }
$$$$                  o   o
$$$$
SSG3     LLL,KAA,PL,LOO,KOO,PO/ULV,UOOV,RULV,RUOV/OMIT/V,Y,IRES=-1/ 
         1/S,N,EPSI $   
****CARD   1-  6,  8- 11, 13, 17, 57- 60
****FILE 110
****RFMT 188
$$$$ Go to label LBL9 if residual vectors are not to be printed.
COND     LBL9,IRES $
****CARD   1-  6,  8- 11, 13, 17, 57- 60
****RFMT 188,189,192-198
$$$$ MATGPR prints the residual vector for independent coordinates (RULV).
MATGPR   GPL,USET,SIL,RULV//*L* $   
****CARD   1-  6,  8- 11, 13, 17, 57- 60
****RFMT 188,189,192-198
$$$$ MATGPR prints the residual vector for omitted coordinates (RUOV).
MATGPR   GPL,USET,SIL,RUOV//*O* $   
****CARD   1-  6,  8- 11, 13, 17, 57- 60
****RFMT 188,189,192-198
$$$$
LABEL    LBL9 $ 
****CARD   1-  6,  8- 11, 13, 17, 57- 60
****RFMT 188,189,192-198
$$$$ SDR1 recovers dependent displacements
$$$$ 
$$$$   
$$$$                       o
$$$$   {u } = [G ]{u ] + {u }  ,
$$$$     o      o   l      o
$$$$ 
$$$$    u                u            
$$$$     a                f           
$$$$   {--} = {u }  ,   {--} = {u }  ,
$$$$    u       f        Y       n    
$$$$     o                s           
$$$$                                  
$$$$                        u         
$$$$                         n        
$$$$  {u } = [G ]{u ]  ,   {--} = {u }
$$$$    m      m   n        u       g 
$$$$                         m        
$$$$ 
$$$$ and recovers single-point forces of constraint
$$$$ 
$$$$                    T
$$$$   {q } = -{P } + [K  ]{u } + [K  ]{Y }
$$$$     s       s      fs   f      ss   s
$$$$
SDR1     USET,PG,ULV,UOOV,YS,GO,GM,PS,KFS,KSS,/UGV,PGG,QG/1/
         *BKL0* $   
****CARD   1-  6,  8- 11, 13, 57- 60
****FILE 111
$$$$ SDR2 calculates the element forces (OEF1) and stresses (OES1) and 
$$$$ prepares load vectors (OPG1), displacement vectors (OUGV1), and single-
$$$$ point forces of constraint (OQG1) for output and translation components 
$$$$ of the displacement vectors (PUGV1) for the static solution.
SDR2     CASECC,CSTM,MPT,DIT,EQEXIN,SIL,GPTT,EDT,BGPDP,,QG,UGV,EST,,PGG,
         PCOMPS/OPG1,OQG1,OUGV1,OES1,OEF1,PUGV1,OES1L,OEF1L/ 
         *BKL0*////COMPS $  
****CARD  19
****FILE 112
$$$$ OFP formats the tables prepared by SDR2 and places them on the system 
$$$$ output file for printing.
OFP      OUGV1,OPG1,OQG1,OEF1,OES1,//S,N,CARDNO $   
****CARD  19
****FILE 112
$$$$
OFP      OEF1L,OES1L,,,,//S,N,CARDNO $   
****CARD  19
****FILE 112
$$$$ SCAN examines the element stresses and forces calculated by SDR2 and 
$$$$ generates scanned output that meets the specifications set by the user.
SCAN     CASECC,OES1,OEF1/OESF1/*RF* $  
****CARD 19 
****FILE 112
$$$$ OFP formats the scanned output table prepared by SCAN and places it on 
$$$$ the system output file for printing.
OFP      OESF1,,,,,//S,N,CARDNO $   
****CARD  19
****FILE 112
$$$$ Go to label P2 if no static solution deformed structure plots are 
$$$$ requested.
COND     P2,JUMPPLOT $  
****SBST   7
****CARD  18
****FILE 126
$$$$ PLOT generates all requested static solution deformed structure and 
$$$$ contour plots.
PLOT     PLTPAR,GPSETS,ELSETS,CASECC,BGPDT,EQEXIN,SIP,PUGV1,,GPECT,OES1,
         OES1L,/PLOTX2/NSIL/LUSEP/JUMPPLOT/PLTFLG/S,N,PFILE $   
****SBST   7
****CARD  18
****FILE 126
$$$$ PRTMSG prints plotter data, engineering data, and contour data for each 
$$$$ static solution deformed plot generated.
PRTMSG   PLOTX2// $ 
****SBST   7
****CARD  18
****FILE 126
$$$$
LABEL    P2 $   
****SBST   7
****CARD  18
****FILE 126
$$$$ TA1 generates element tables for use in differential stiffness matrix 
$$$$ assembly.
TA1      ECT,EPT,BGPDT,SIL,GPTT,CSTM,,/X1,X2,X3,ECPT,GPCT,,,/LUSET/  
         NOSIMP/0/NOGENL/GENEL $
****CARD   1-  6,  8- 11, 57- 60
****FILE 127
$$$$                                                 d
$$$$ DSMG1 generates differential stiffness matrix [K  ].
$$$$                                                 gg
$$$$
DSMG1    CASECC,GPTT,SIL,EDT,UGV,CSTM,MPT,ECPT,GPCT,DIT/KDGG/   
         DSCOSET $  
****CARD   1-  6,  8- 11, 57- 60
****FILE 113
$$$$               d        d
$$$$ Equivalence [K  ] to [K  ] if no multipoint constraints exist.
$$$$               gg       nn
$$$$
EQUIV    KDGG,KDNN/MPCF1 $  
****CARD   1-  6,  8- 11, 57- 60
****FILE 114
$$$$ Go to label LBL2D if no multipoint constraints exist.
COND     LBL2D,MPCF1 $  
****CARD   1-  6,  8- 11, 57- 60
****FILE 114
$$$$ MCE2 partitions stiffness matrix
$$$$ 
$$$$           +       +
$$$$           |_d   d |
$$$$           |K  |K  |
$$$$     d     | nn| nm|
$$$$   [K  ] = |---+---|
$$$$     gg    | d   d |
$$$$           |K  |K  |
$$$$           | mn| mm|
$$$$           +       +
$$$$      
$$$$ and performs the matrix reduction
$$$$ 
$$$$     d      _d       T   d       d           T   d
$$$$   [K  ] = [K  ] + [G ][K  ] + [K  ][G ] + [G ][K  ][G ]
$$$$     nn      nm      m   mn      mn   m      m   mm   m
$$$$
MCE2     USET,GM,KDGG,,,/KDNN,,, $  
****CARD   1-  6,  8- 11, 57- 60
****FILE 114
$$$$
LABEL    LBL2D $
****CARD   1-  6,  8- 11, 57- 60
****FILE 114
$$$$               d        d
$$$$ Equivalence [K  ] to [K  ] if no single-point constraints exist.
$$$$               nn       ff
$$$$
EQUIV    KDNN,KDFF/SINGLE $ 
****CARD   1-  6,  8- 11, 57- 60
****FILE 115
$$$$ Go to label LBL3D if no single-point constraints exist.
COND     LBL3D,SINGLE $ 
****CARD   1-  6,  8- 11, 57- 60
****FILE 115
$$$$ SCE1 partitions out single-point constraints
$$$$ 
$$$$           +       +
$$$$           | d   d |
$$$$           |K  |K  |
$$$$     d     | ff| fs|
$$$$   [K  ] = |---+---|
$$$$     nn    | d   d |
$$$$           |K  |K  |
$$$$           | sf| ss|
$$$$           +       +
$$$$
SCE1     USET,KDNN,,,/KDFF,KDFS,,,, $   
****CARD   1-  6,  8- 11, 57- 60
****FILE 115
$$$$
LABEL    LBL3D $
****CARD   1-  6,  8- 11, 57- 60
****FILE 115
$$$$               d        d
$$$$ Equivalence [K  ] to [K  ] if no omitted coordinates exist.
$$$$               ff       aa
$$$$
EQUIV    KDFF,KDAA/OMIT $   
****CARD   1-  6,  8- 11, 57- 60
****FILE 116
$$$$ Go to label LBL5D if no omitted coordinates exist.
COND     LBL5D,OMIT $   
****CARD   1-  6,  8- 11, 57- 60
****FILE 116
$$$$ SMP2 partitions constrained differential stiffness matrix
$$$$ 
$$$$           +       +
$$$$           |_d   d |
$$$$           |K  |K  |
$$$$     d     | aa| ao|
$$$$   [K  ] = |---+---|
$$$$     ff    | d | d |
$$$$           |K  |K  |
$$$$           | oa  oo|
$$$$           +       +
$$$$ 
$$$$ and performs matrix reduction
$$$$
$$$$     d      _d       d  T           T  d         T  d
$$$$   [K  ] = [K  ] + [K  ] [G ] + [G ] [K  ] + [G ] [K  ][G ]
$$$$     aa      aa      oa    o      o    oa      o    oo   o
$$$$
SMP2     USET,GO,KDFF/KDAA $
****CARD   1-  6,  8- 11, 57- 60
****FILE 116
$$$$
LABEL    LBL5D $
****CARD   1-  6,  8- 11, 57- 60
****FILE 116
$$$$        d                           dm
$$$$ ADD -[K  ] and nothing to create [K  ].
$$$$        aa                          aa
$$$$
ADD      KDAA,/KDAAM/(-1.0,0.0)/(0.0,0.0) $ 
****CARD   1-  6,  8- 11, 57- 60
****FILE 121
$$$$ DPD extracts Eigenvalue Extraction Data from Dynamics data block.
DPD      DYNAMICS,GPL,SIL,USET/GPLD,SILD,USETD,,,,,,,EED,EQDYN/ 
         LUSET/LUSETD/NOTFL/NODLT/NOPSDL/NOFRL/ 
         NONLFT/NOTRL/S,N,NOEED//NOUE $ 
****CARD   1-  6,  8- 11, 57- 61
****FILE 117
$$$$ Go to label ERROR3 and print Error Message No. 3 if there is no 
$$$$ Eigenvalue Extraction Data.
COND     ERROR3,NOEED $ 
****CARD   1-  6,  8- 11, 57- 61
****FILE 117
****RFMT 187-189,191-204,207-209
$$$$
PARAM    //*MPY*/NEIGV/1/-1 $   
****CARD   1-  6,  8- 11, 57- 60
****FILE 118
$$$$ READ extracts real eigenvalues and eigenvectors from the equation
$$$$ 
$$$$                  dm
$$$$   [K   + lambda K  ]{u } = 0
$$$$     aa           aa   a
$$$$ 
$$$$ and normalizes eigenvectors according to one of the following user 
$$$$ requests:
$$$$   1. Unit value of a selected component.
$$$$   2. Unit value of the largest component.
$$$$
READ     KAA,KDAAM,,,EED,USET,CASECC/LAMA,PHIA,,OEIGS/*BUCKLING*/   
         S,N,NEIGV/2 $  
****CARD   1-  6,  8- 11, 57- 62
****FILE 118
$$$$ OFP formats the eigenvalues (LAMA) and summary of eigenvalue extraction 
$$$$ information (OEIGS) prepared by READ and places them on the system output 
$$$$ file for printing.
OFP      OEIGS,LAMA,,,,//S,N,CARDNO $   
****CARD   1-  6,  8- 11, 57- 62
****FILE 118
$$$$ Go to label ERROR4 and print Error Message No. 4 if no eigenvalues were 
$$$$ found.
COND     ERROR4,NEIGV $ 
****CARD   1-  6,  8- 11, 57- 62
****FILE 119
$$$$ SDR1 recovers dependent components of the eigenvectors
$$$$ 
$$$$                                     phi
$$$$                                        a
$$$$  {phi } = [G ]{phi }               {----} = {phi }
$$$$      o      o     a                 phi         f
$$$$                                        o
$$$$
$$$$    phi           
$$$$       f          
$$$$   {----} = {phi }                  {phi } = [G ]{phi }
$$$$    phi         n                       m      m     n
$$$$       s          
$$$$
$$$$    phi           
$$$$       n          
$$$$   {----} = {phi }
$$$$    phi         g 
$$$$       m          
$$$$ 
$$$$                                                         T
$$$$ and recovers single-point forces of constraint {q } = [K  ] {phi }.
$$$$                                                  s      fs      f
$$$$
SDR1     USET,,PHIA,,,GO,GM,,KFS,,/PHIG,,BQG/1/*BKL1* $ 
****CARD   1-  6,  8- 11, 57- 62
****FILE 119
****RFMT 187-189,191-204,207-209
$$$$ SDR2 calculates the element forces (OBEF1) and stresses (OBES1) and 
$$$$ prepares eigenvectors (OPHIG) and single-point forces of constraint
$$$$ (OBQG1) for output and translation components of the displacement
$$$$ vectors (PPHIG) for the static solution.
$$$$
SDR2     CASECC,CSTM,MPT,DIT,EQEXIN,SIL,,,BGPDP,LAMA,BQG,PHIG,EST,,,
         PCOMPS/,OBQG1,OPHIG,OBES1,OBEF1,PPHIG,OBES1L,OBEF1L/   
         *BKL1*////COMPS $  
****CARD  18, 19
****FILE 120
$$$$ OFP formats the tables prepared by SDR2 and places them on the system 
$$$$ output file for printing.
OFP      OPHIG,OBQG1,OBEF1,OBES1,,//S,N,CARDNO $
****CARD  19
****FILE 120
$$$$
OFP      OBEF1L,OBES1L,,,,//S,N,CARDNO $
****CARD  19
****FILE 120
$$$$ Go to label P3 if no buckling solution deformed structure plots are 
$$$$ requested.
COND     P3,JUMPPLOT $  
****SBST   7
****CARD  18
****FILE 128
$$$$ PLOT generates all requested buckling solution deformed structure and 
$$$$ contour plots.
PLOT     PLTPAR,GPSETS,ELSETS,CASECC,BGPDT,EQEXIN,SIP,,PPHIG,GPECT, 
         OBES1,OBES1L,/PLOTX3/NSIL/LUSEP/JUMPPLOT/PLTFLG/   
         S,N,PFILE $
****SBST   7
****CARD  18
****FILE 128
$$$$ PRTMSG prints plotter data, engineering data, and contour data for each 
$$$$ buckling solution deformed plot generated.
PRTMSG   PLOTX3// $ 
****SBST   7
****CARD  18
****FILE 128
$$$$
LABEL    P3 $   
****SBST   7
****CARD  18
****FILE 128
$$$$ Go to label FINIS and make normal exit.
JUMP     FINIS $
****CARD   1-  6,  8- 21, 24, 57- 62
****FILE 128
****RFMT 187-189,191-204,207-209
$$$$
LABEL    ERROR1 $   
****CARD   1,  2,  4-  6,  8, 16
****FILE  97
****RFMT 187-189,191-204,207-209
$$$$ Print Error Message No. 1 and terminate execution.
PRTPARM  //-1/*BUCKLING* $  
****CARD   1,  2,  4-  6,  8, 16
****FILE  97
****RFMT 187-189,191-204,207-209
$$$$
LABEL    ERROR2 $   
****CARD   1, 12
****FILE 101
****RFMT 187-189,191-204,207-209
$$$$ Print Error Message No. 2 and terminate execution.
PRTPARM  //-2/*BUCKLING* $  
****CARD   1, 12
****FILE 101
****RFMT 187-189,191-204,207-209
$$$$
LABEL    ERROR3 $   
****CARD   1-  6,  8- 11, 57- 61
****FILE 117
****RFMT 187-189,191-204,207-209
$$$$ Print Error Message No. 3 and terminate execution.
PRTPARM  //-3/*BUCKLING* $  
****CARD   1-  6,  8- 11, 57- 61
****FILE 117
****RFMT 187-189,191-204,207-209
$$$$
LABEL    ERROR4 $   
****CARD   1-  6,  8- 11, 57- 62
****FILE 118
****RFMT 187-189,191-204,207-209
$$$$ Print Error Message No. 4 and terminate execution.
PRTPARM  //-4/*BUCKLING* $  
****CARD   1-  6,  8- 11, 57- 62
****FILE 118
****RFMT 187-189,191-204,207-209
$$$$
LABEL    ERROR5 $   
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24, 57
****FILE 125
****RFMT 187-189,191-204,207-209
$$$$ Print Error Message No. 5 and terminate execution.
PRTPARM  //-5/*BUCKLING* $  
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24, 57
****FILE 125
****RFMT 187-189,191-204,207-209
$$$$
LABEL    ERROR6 $   
****CARD   1,  9- 12, 59
****FILE 101
****RFMT 187-189,191-204,207-209
$$$$ Print Error Message No. 6 and terminate execution.
PRTPARM  //-6/*BUCKLING* $  
****CARD   1,  9- 12, 59
****FILE 101
****RFMT 187-189,191-204,207-209
$$$$
LABEL    FINIS $
****CARD   1-  6,  8- 21, 24, 57- 62
****FILE 128
****RFMT 187-189,191-204,207-209
$$$$
PURGE    DUMMY/MINUS1 $ 
****CARD   1-  6,  8- 21, 24, 57- 62
****RFMT 187-189,191-204,207-209
$$$$
END      $  
****CARD   1-  6,  8- 21, 24, 57- 62
****RFMT 187-189,191-204,207-209
$$$$
$*CARD BITS 
  1    AXIC     AXIF     CELAS1   CELAS2   CELAS3   CELAS4   CMASS1 
  1    CMASS2   
  1    CMASS3   CMASS4   CORD1C   CORD1R   CORD1S   CORD2C   CORD2R 
  1    CORD2S   
  1    GRDSET   GRID     GRIDB    POINTAX  RINGAX   RINGFL   SECTAX 
  1    SEQGP    SPOINT  
  2    ADUM1    ADUM2    ADUM3    ADUM4    ADUM5    ADUM6    ADUM7  
  2    ADUM8
  2    ADUM9    BAROR    CBAR     CCONEAX  CDUM1    CDUM2    CDUM3  
  2    CDUM4
  2    CDUM5    CDUM6    CDUM7    CDUM8    CDUM9    CELBOW   CHEXA1 
  2    CHEXA2   
  2    CIHEX1   CIHEX2   CIHEX3   CIS2D8   CONROD   CQDMEM   CQDMEM1
  2    CQDMEM2  CQDPLT   CQUAD1   CQUAD2   CROD     CSHEAR   CTETRA 
  2    CTORDRG  CQUAD4   CTRIA3
  2    CTRAPAX  CTRAPRG  CTRBSC   CTRIA1   CTRIA2   CTRIAAX  CTRIARG
  2    CTRIM6   
  2    CTRMEM   CTRPLT   CTRPLT1  CTRSHL   CTUBE    CTWIST   CWEDGE 
  3    PBAR     PCONEAX  PDUM1    PDUM2    PDUM3    PDUM4    PDUM5  
  3    PDUM6
  3    PDUM7    PDUM8    PDUM9    PELBOW   PIHEX    PIS2D8   PQDMEM 
  3    PQDMEM1  PQDMEM2  PQDPLT   PQUAD1   PQUAD2   PROD     PSHEAR 
  3    PTORDRG  PSHELL   PCOMP    PCOMP1   PCOMP2
  3    PTRAPAX  PTRBSC   PTRIA1   PTRIA2   PTRIAAX  PTRIM6   PTRMEM 
  3    PTRPLT   PTRPLT1  PTRSHL   PTUBE    PTWIST   
  4    GENEL
  5    CONM1    CONM2    PMASS  
  6    PELAS
  8    MAT1     MAT2     MAT3     MATT1    MATT2    MATT3    MAT8
  8    TABLEM1  TABLEM2  TABLEM3  TABLEM4  TEMPMT$  TEMPMX$  
  9    AXISYM$  CRIGD1   CRIGD2   CRIGD3   CRIGDR   CRROD    CRBAR  
  9    CRTRPLT  CRBE1    CRBE2    CRBE3    CRSPLINE MPC      MPCADD 
  9    MPCAX    MPC$
 10    SPC      SPC1     SPCADD   SPCAX    SPC$ 
 11    ASET     ASET1    OMIT     OMIT1    OMITAX   SUPAX    SUPORT 
 13    TEMP     TEMPAX   TEMPD    TEMPP1   TEMPP2   TEMPP3   TEMPRB 
 14    WTMASS   
 15    GRDPNT   
 16    PLOTEL   
 17    IRES 
 18    PLOT$
 19    POUT$
 20    ASETOUT  
 21    AUTOSPC  
 24    COUPMASS CPBAR    CPQDPLT  CPQUAD1  CPQUAD2  CPROD    CPTRBSC
 24    CPTRIA1  CPTRIA2  CPTRPLT  CPTUBE
 57    GRAV     RFORCE  
 58    TEMPLD$  
 59    DEFORM   DEFORM$  LOAD$    SPCD  
 60    FORCE    FORCE1   FORCE2   FORCEAX  LOAD     MOMAX    MOMENT 
 60    MOMENT1  PLOAD4
 60    MOMENT2  PLOAD    PLOAD1   PLOAD2   PLOAD3   PRESAX   SLOAD  
 61    EIGB 
 62    METHOD$  
$$$$
$*FILE BITS 
 94    BGPDT    CSTM     EQEXIN   GPDT     GPL      SIL 
 95    ECT  
 96    GPTT     SLT 
 97    EST      GEI      GPECT    MPTX     PCOMPS   EPTX
 98    KGGX 
 99    MGG  
100    KGG  
101    ASET     RG       USET     YS       OGPST
102    GPST 
103    GM   
104    KNN  
105    KFF      KFS      KSS
106    GO       KAA      KOO      LOO   
107    LLL  
108    PG   
109    PL       PO       PS 
110    RULV     RUOV     ULV      UOOV  
111    PGG      QG       UGV
112    OEF1     OES1     OPG1     OQG1     OUGV1    PUGV1   
112    OEF1L    OES1L    OESF1
113    KDGG 
114    KDNN 
115    KDFF     KDFS     KDSS   
116    KDAA 
117    EED      EQDYN    GPLD     SILD     USETD
118    LAMA     OEIGS    PHIA   
119    BQG      PHIG
120    OBEF1    OBES1    OBQG1    OPHIG    PPHIG
120    OBEF1L   OBES1L  
121    KDAAM
122    ELSETS   GPSETS   PLTPAR   PLTSETX   
123    KDICT    KELM     MDICT    MELM  
124    PLOTX1   
125    OGPWG
126    PLOTX2   
127    X1       X2       X3       ECPT     GPCT 
128    PLOTX3   
129    BGPDP    SIP 
$*  
=PAGE=
DISP6

APR.93
$$$$$$$$
BEGIN    DISP 06 - PIECEWISE LINEAR STATIC ANALYSIS - APR. 1993 $   
****CARD   1-  6,  8- 24, 58- 62
****RFMT 187-191,193-198
$$$$
PRECHK   ALL $  
****CARD   1-  6,  8- 24, 58- 62
****RFMT 187-191,193-198
$$$$
FILE     QG1=APPEND/UGV1=APPEND/KGGSUM=SAVE/PGV1=APPEND $   
****CARD   1-  6,  8- 24, 58- 62
****RFMT 187-191,193-198
$$$$
PARAM    //*MPY*/CARDNO/0/0 $   
****CARD   1-  3,  5,  6,  8- 10, 14, 15, 19, 21- 24, 61
****FILE 102,117,120,124
$$$$ GPI generates coordinate system transformation matrices, tables of grid 
$$$$ locations, and tables relating the internal and external grid point 
$$$$ numbers.
GP1      GEOM1,GEOM2,/GPL,EQEXIN,GPDT,CSTM,BGPDT,SIL/S,N,LUSET/ 
         NOGPDT/MINUS1=-1 $ 
****CARD   1
****FILE  94
$$$$ PLTTRAN modifies special scalar grid points in the BGPDT and SIL tables.
PLTTRAN  BGPDT,SIL/BGPDP,SIP/LUSET/S,N,LUSEP $  
****CARD   1
****FILE 126
$$$$ GP2 generates the Element Connection Table with internal indices.
GP2      GEOM2,EQEXIN/ECT $ 
****CARD   1,  2,  4,  5, 16
****FILE  95
$$$$
PARAML   PCDB//*PRES*////JUMPPLOT $ 
****SBST   7
****CARD  18
****FILE 121,122
$$$$
PURGE    PLTSETX,PLTPAR,GPSETS,ELSETS/JUMPPLOT $
****SBST   7
****CARD  18
****FILE 121
$$$$ Go to label P1 if there are no structure plot requests.
COND     P1,JUMPPLOT $  
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 121,122
$$$$ PLTSET transforms user input into a form used to drive the structure 
$$$$ plotter.
PLTSET   PCDB,EQEXIN,ECT,EPT/PLTSETX,PLTPAR,GPSETS,ELSETS/S,N,NSIL/ 
         S,N,JUMPPLOT $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 121
$$$$ PRTMSG prints error messages associated with the structure plotter.
PRTMSG   PLTSETX// $
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 121
$$$$
PARAM    //*MPY*/PLTFLG/1/1 $   
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 122
$$$$
PARAM    //*MPY*/PFILE/0/0 $
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 122
$$$$ Go to label P1 if no undeformed structure plots are requested.
COND     P1,JUMPPLOT $  
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 122
$$$$ PLOT generates all requested undeformed structure plots.
PLOT     PLTPAR,GPSETS,ELSETS,CASECC,BGPDT,EQEXIN,SIL,,ECT,,,,/PLOTX1/  
         NSIL/LUSET/S,N,JUMPPLOT/S,N,PLTFLG/S,N,PFILE $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 122
$$$$ PRTMSG prints plotter data and engineering data for each undeformed plot 
$$$$ generated.
PRTMSG   PLOTX1// $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 122
$$$$
LABEL    P1 $   
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 121,122
$$$$ GP3 generates Static Loads Table and Grid Point Temperature Table.
GP3      GEOM3,EQEXIN,GEOM2/SLT,GPTT/S,N,NOGRAV $   
****CARD   1,  2, 13, 60, 61
****FILE  96
$$$$
PARAM    //*AND*/SKPMGG/NOGRAV/V,Y,GRDPNT $ 
****CARD   1,  2, 15, 61
****FILE 123
****RFMT 187-191,193-198
$$$$ TA1 generates element tables for use in matrix assembly and stress 
$$$$ recovery.
TA1      ECT,EPT,BGPDT,SIL,GPTT,CSTM,MPT,EQEXIN/EST,GEI,GPECT,ECPT,GPCT,
         MPTX,PCOMPS,EPTX/LUSET/S,N,NOSIMP/2/S,N,NOGENL/GENEL/S,N,COMPS
****CARD   1-  6, 13, 16
****FILE  97
$$$$
EQUIV    MPTX,MPT/COMPS/EPTX,EPT/COMPS $
****CARD   1-  6,  8- 24, 58- 62
****FILE  97
$$$$
PARAM    //*AND*/NOELMT/NOGENL/NOSIMP $ 
****CARD   1,  2,  4-  6, 16
****FILE  97
****RFMT 187-191,193-198
$$$$ Go to label ERROR4 and print Error Message No. 4 if no elements have been 
$$$$ defined.
COND     ERROR4,NOELMT $
****CARD   1,  2,  4-  6, 16
****FILE  97
****RFMT 187-191,193-198
$$$$
PURGE    KGGX/NOSIMP $  
****CARD   1,  2,  4-  6, 16
****FILE  98
$$$$ Go to label LBL1 if there are no structural elements.
COND     LBL1,NOSIMP $  
****CARD   1-  3,  5,  6,  8, 14, 15, 24, 61
****FILE  98, 99,123,124
$$$$
PARAM    //*ADD*/NOKGGX/1/0 $   
****CARD   1-  3,  6,  8
****FILE 123
$$$$
PARAM    //*ADD*/NOMGG/1/0 $
****CARD   1-  3,  6,  8
****FILE 123
$$$$ EMG generates the structural element stiffness and mass matrix tables and 
$$$$ dictionaries for later assembly by the EMA module.
EMG      EST,CSTM,MPT,DIT,GEOM2,/KELM,KDICT,MELM,MDICT,,,/S,N,NOKGGX/   
         S,N,NOMGG////C,Y,COUPMASS/C,Y,CPBAR/C,Y,CPROD/ 
         C,Y,CPQUAD1/C,Y,CPQUAD2/C,Y,CPTRIA1/C,Y,CPTRIA2/C,Y,CPTUBE/
         C,Y,CPQDPLT/C,Y,CTRPLT/C,Y,CPTRBSC/
         C,Y,VOLUME/C,Y,SURFACE $   
****CARD   1-  3,  5,  6,  8, 13, 24, 61
****FILE 123
$$$$
PURGE    KGGX/NOKGGX/MGG/NOMGG $
****CARD   1-  3,  5,  6,  8, 14, 15, 24, 61
****FILE  98, 99
$$$$ Go to label JMPKGG if no stiffness matrix is to be assembled.
COND     JMPKGG,NOKGGX $
****CARD   1-  3,  6,  8
****FILE  98
$$$$                                  x
$$$$ EMA assembles stiffness matrix [K  ] and Grid Point Singularity Table.
$$$$                                  gg
$$$$
EMA      GPECT,KDICT,KELM/KGGX $
****CARD   1-  3,  6,  8
****FILE  98
$$$$
PURGE    KDICT,KELM/MINUS1 $
****CARD   1-  3,  6,  8
****FILE 123
$$$$
LABEL    JMPKGG $   
****CARD   1-  3,  6,  8
****FILE  98
$$$$ Go to label JMPMGG if no mass matrix is to be assembled.
COND     JMPMGG,NOMGG $ 
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE  99
$$$$ EMA assembles mass matrix [M  ].
$$$$                             gg
$$$$
EMA      GPECT,MDICT,MELM/MGG/-1/C,Y,WTMASS=1.0 $   
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE  99
$$$$
PURGE    MDICT,MELM/MINUS1 $
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE 123
$$$$
LABEL    JMPMGG $   
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE  99
$$$$ Go to label LBL1 if no weight and balance information is requested.
COND     LBL1,GRDPNT $  
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE 124
$$$$ Go to label ERROR3 and print Error Message No. 3 if no mass matrix 
$$$$ exists.
COND     ERROR3,NOMGG $ 
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE 124
$$$$ GPWG generates weight and balance information.
GPWG     BGPDP,CSTM,EQEXIN,MGG/OGPWG/V,Y,GRDPNT=-1/V,Y,WTMASS $ 
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE 124
$$$$ OFP formats the weight and balance information prepared by GPWG and 
$$$$ places it on the system output file for printing.
OFP      OGPWG,,,,,//S,N,CARDNO $   
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE 124
$$$$
LABEL    LBL1 $ 
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE  98, 99,123,124
$$$$                                       x             xl
$$$$ PLA1 extracts the linear terms form [K  ] to give [K  ], extracts the
$$$$                                       gg            gg
$$$$ 
$$$$ nonlinear entries from the Element Connection and Properties Table to 
$$$$ give ECPTNL, and separates the linear and nonlinear entries in the 
$$$$ Element Summary Table to give ESTL and ESTNL.
PLA1     CSTM,MPT,ECPT,GPCT,DIT,CASECC,EST/KGGXL,ECPTNL,ESTL,ESTNL/S,N, 
         KGGLPG/S,N,NPLALIM/S,N,ECPTNLPG/S,N,PLSETNO/S,N,NONLSTR/S,N,   
         PLFACT $   
****CARD   1-  3,  6,  8
****FILE 100
$$$$ Go to label ERROR1 and print Error Message No. 1 if no elements have a 
$$$$ stress-dependent modulus of elasticity.
COND     ERROR1,ECPTNLPG $  
****CARD   1-  3,  6,  8
****FILE 100
$$$$
PURGE    ONLES,ESTNL1/NONLSTR $ 
****CARD   1-  3,  6,  8
****FILE 100
$$$$
PARAM    //*ADD*/ALWAYS/-1/0 $  
****CARD   1-  3,  6,  8
****FILE 100
$$$$
PARAM    //*ADD*/NEVER/1/0 $
****CARD   1-  3,  6,  8
****FILE 100
$$$$               x                  xl       l
$$$$ Equivalence [K  ] to [K  ] and [K  ] to [K  ] if there are no general
$$$$               gg       gg        gg       gg
$$$$ elements.
EQUIV    KGGX,KGG/NOGENL/KGGXL,KGGL/NOGENL $
****CARD   1-  4,  6,  8
****FILE 101
$$$$ Go to label LBL11 if there are no general elements.
COND     LBL11,NOGENL $ 
****CARD   1-  4,  6,  8
****FILE 101
$$$$                                 x
$$$$ SMA3 adds general elements to [K  ] to obtain stiffness matrix [K  ].
$$$$                                 gg                               gg
$$$$
SMA3     GEI,KGGX/KGG/LUSET/NOGENL/NOSIMP $ 
****CARD   1-  4,  6,  8
****FILE 101
$$$$                                 xl
$$$$ SMA3 adds general elements to [K  ] to obtain stiffness of linear
$$$$                                 gg
$$$$            l
$$$$ elements [K  ].
$$$$            gg
$$$$ 
SMA3     GEI,KGGXL/KGGL/LUSET/NOGENL/KGGLPG $   
****CARD   1-  4,  6,  8
****FILE 101
$$$$
LABEL    LBL11 $
****CARD   1-  4,  6,  8
****FILE 101
$$$$
GPSTGEN  KGG,SIL/GPST $ 
****CARD   1-  4,  6,  8
****FILE 104
$$$$
PARAM    //*MPY*/NSKIP/0/0 $
****CARD   1,  9- 12, 59
****FILE 102
$$$$ GP4 generates flags defining members of various displacement set (USET)
$$$$ and forms multipoint constraint equation [R ] {u } = 0.
$$$$                                            g    g
$$$$ 
GP4      CASECC,GEOM4,EQEXIN,GPDT,BGPDT,CSTM,GPST/RG,YS,USET,   
         ASET,OGPST/LUSET/S,N,MPCF1/S,N,MPCF2/S,N,SINGLE/S,N,OMIT/  
         S,N,REACT/S,N,NSKIP/S,N,REPEAT/S,N,NOSET/S,N,NOL/S,N,NOA/  
         C,Y,ASETOUT/C,Y,AUTOSPC $  
****CARD   1-  4,  6,  8- 12, 20, 21, 59
****FILE 102
$$$$
OFP      OGPST,,,,,//S,N,CARDNO $   
****CARD   1-  4,  6,  8- 10, 21
****FILE 102
$$$$
PARAM    //*AND*/NOSR/SINGLE/REACT $
****CARD   1,  9- 12, 59
****FILE 102
$$$$
PURGE    KRR,KLR,QR,DM/REACT/GM/MPCF1/GO,KOO,LOO,PO,UOOV,RUOV/OMIT/PS,  
         KFS,KSS/SINGLE/QG/NOSR $   
****CARD   1,  9- 12, 59
****FILE 105,107-109,111,113-115
$$$$                                           l
$$$$ SSG1 generates total static load vector {P }.
$$$$                                           g
$$$$
SSG1     SLT,BGPDT,CSTM,SIL,EST,MPT,,,MGG,CASECC,DIT,PCOMPS/PG1,,,,/
         LUSET/1/COMPS $
****CARD   1-  3,  5,  6,  8, 59- 62
****FILE 103
$$$$               l
$$$$ Equivalence {P } t {P } if no constraints are applied.
$$$$               g      l
$$$$
EQUIV    PG1,PL/NOSET $ 
****CARD   1-  3,  5,  6,  8, 59- 62
****FILE 103
$$$$
PARAM    //*ADD*/PLACOUNT/1/0 $ 
****CARD  22, 23
$$$$
$$$$ Equivalence [K  ] to [K  ] if no multipoint constraints exist.
$$$$               gg       nn
$$$$
EQUIV    KGG,KNN/MPCF1 $
****CARD   1-  4,  6,  8,  9, 22, 23
****FILE 106
$$$$ Go to label LBL2 if no multipoint constraints exist.
COND     LBL2,MPCF1 $   
****CARD   1-  4,  6,  8,  9, 22, 23
****FILE 105,106
$$$$ MCE1 partitions multipoint constraint equation [R ] = [R |R ] and solves
$$$$                                                  g      m  n
$$$$                                                             -1
$$$$ for multipoint constraint transformation matrix [G ] = -[R ]  [R ].
$$$$                                                   m       m     n
$$$$
MCE1     USET,RG/GM $   
****CARD   1,  9, 22, 23
****FILE 105
$$$$ Beginning of loop for additional load increments.
LABEL    LOOPBGN $  
****CARD   1-  4,  6,  8,  9, 22, 23
$$$$
$$$$ Equivalence [K  ] to [K  ] if no multipoint constraints exist.
$$$$               gg       nn
$$$$
EQUIV    KGG,KNN/MPCF1 $
****CARD   1-  4,  6,  8,  9, 22, 23
****FILE 106
$$$$ Go to label LBL2 if no multipoint constraints exist.
COND     LBL2,MPCF1 $   
****CARD   1-  4,  6,  8,  9, 22, 23
****FILE 106
$$$$ MCE2 partitions stiffness matrix
$$$$ 
$$$$           +_      +
$$$$           |K  |K  |
$$$$           | nn| nm|
$$$$   [K  ] = |---+---|
$$$$     gg    |K  |K  |
$$$$           | mn| mm|
$$$$           +       +
$$$$           
$$$$ and performs the matrix reduction
$$$$ 
$$$$            _        T           T           T
$$$$   [K  ] = [K  ] + [G ][K  ] + [K  ][G ] + [G ][K  ][G ]
$$$$     nn      nm      m   mn      mn   m      m   mm   m
$$$$
MCE2     USET,GM,KGG,,,/KNN,,, $
****CARD   1-  4,  6,  8,  9, 22, 23
****FILE 106
$$$$
LABEL    LBL2 $ 
****CARD   1-  4,  6,  8,  9, 22, 23
****FILE 105,106
$$$$ Equivalence [K  ] to [K  ] if no single-point constraints exist.
$$$$               nn       ff
$$$$
EQUIV    KNN,KFF/SINGLE $   
****CARD   1-  4,  6,  8- 10, 22, 23
****FILE 107
$$$$ Go to label LBL3 if no single-point constraints exist.
COND     LBL3,SINGLE $  
****CARD   1-  4,  6,  8- 10, 22, 23
****FILE 107
$$$$ SCE1 partitions out single-point constraints.
$$$$ 
$$$$           +       +
$$$$           |K  |K  |
$$$$           | ff| fs|
$$$$   [K  ] = |---+---|
$$$$     nn    |K  |K  |
$$$$           | sf| ss|
$$$$           +       +
$$$$
SCE1     USET,KNN,,,/KFF,KFS,KSS,,, $   
****CARD   1-  4,  6,  8- 10, 22, 23
****FILE 107
$$$$
LABEL    LBL3 $ 
****CARD   1-  4,  6,  8- 10, 22, 23
****FILE 107
$$$$ Equivalence [K  ] to [K  ] if no omitted coordinates exist.
$$$$               ff       aa
$$$$
EQUIV    KFF,KAA/OMIT $ 
****CARD   1-  4,  6,  8- 11, 22, 23
****FILE 108
$$$$ Go to label LBL5 if no omitted coordinates exist.
COND     LBL5,OMIT $
****CARD   1-  4,  6,  8- 11, 22, 23
****FILE 108
$$$$ SMP1 partitions constrained stiffness matrix
$$$$
$$$$           +_      +
$$$$           |K  |K  |
$$$$           | aa| ao|
$$$$   [K  ] = |---+---|
$$$$     ff    |K  |K  |
$$$$           | oa| oo|
$$$$           +       +
$$$$           
$$$$                                               -1
$$$$ solves for transformation matrix [G ] = -[K  ]  [K  ]
$$$$                                    o       oo     oa
$$$$ 
$$$$                                        _        T
$$$$ and performs matrix reduction [K  ] = [K  ] + [K  ][G ].
$$$$                                 aa      aa      oa   o
$$$$
SMP1     USET,KFF,,,/GO,KAA,KOO,LOO,,,,, $  
****CARD   1-  4,  6,  8- 11, 22, 23
****FILE 108
$$$$
LABEL    LBL5 $ 
****CARD   1-  4,  6,  8- 11, 22, 23
****FILE 108
$$$$ Equivalence [K  ] to [K  ] if no free-body supports exist.
$$$$               aa       ll
$$$$
EQUIV    KAA,KLL/REACT $
****CARD   1-  4,  6,  8- 12, 22, 23
****FILE 109
$$$$ Go to label LBL6 if no free-body supports exist.
COND     LBL6,REACT $   
****CARD   1-  4,  6,  8- 12, 22, 23
****FILE 109
$$$$ RBMG1 partitions out free-body supports
$$$$ 
$$$$           +       +
$$$$           |K  |K  |
$$$$           | ll| lr|
$$$$   [K  ] = |---+---|
$$$$     aa    |K  |K  |
$$$$           | rl| rr|
$$$$           +       +
$$$$
RBMG1    USET,KAA,/KLL,KLR,KRR,,, $ 
****CARD   1-  4,  6,  8- 12, 22, 23
****FILE 109
$$$$
LABEL    LBL6 $ 
****CARD   1-  4,  6,  8- 12, 22, 23
****FILE 109
$$$$ DECOMP decomposes constrained stiffness matrix [K  ] = [L  ][U  ].
$$$$                                                  ll      ll   ll
$$$$ 
DECOMP   KLL/LLL,/1/0/MINDIAGK/DETKLLXX/IDETKLLX/   
         S,N,SINGKLLX $ 
****CARD   1-  4,  6,  8- 12, 22, 23
****FILE 110
$$$$ Go to label PLALBL4 and print Error Message No. 5 if stiffness matrix
$$$$ [K  ] is singular (i.e., local plasticity).
$$$$   ll
$$$$ 
COND     PLALBL4,SINGKLLX $ 
****CARD   1-  6,  8- 13, 17, 22, 23, 58- 62
****FILE 110
$$$$ Go to label LBL7 if no free-body supports exist.
COND     LBL7,REACT $   
****CARD   1-  4,  6,  8- 12, 22, 23
****FILE 111
$$$$ RBMG3 forms rigid body transformation matrix
$$$$ 
$$$$               -1
$$$$   [D] = -[K  ]  [K  ]
$$$$            ll     lr
$$$$ 
$$$$ calculates rigid body check matrix
$$$$
$$$$              T
$$$$   [X] = [K  ]  + [K  ][D]
$$$$           rr       lr
$$$$ 
$$$$ and calculates rigid body error ratio
$$$$ 
$$$$               ||X||
$$$$   epsilon = ---------
$$$$              ||K  ||
$$$$                 rr
$$$$
RBMG3    LLL,KLR,KRR/DM $   
****CARD   1-  4,  6,  8- 12, 22, 23
****FILE 111
$$$$
LABEL    LBL7 $ 
****CARD   1-  4,  6,  8- 12, 22, 23
****FILE 111
$$$$                                    1
$$$$ ADD multiplies total load vector {P ]} by factor PLFACT and adds it to
$$$$                                    g
$$$$ nothing to obtain applied load vector {P } for current loop.
$$$$                                         g
$$$$
ADD      PG1,/PG/PLFACT $   
****CARD   1-  3,  5,  6,  8, 13, 22, 23, 58- 62
****FILE 112
****RFMT 187,188,190,191
$$$$ Go to label LBL10 if no constraints are applied.
COND     LBL10,NOSET  $ 
****CARD   1-  3,  5,  6,  8- 13, 22, 23, 58- 62
****FILE 113
****RFMT 187,188,190,191
$$$$ SSG2 applies constraints to static load vectors
$$$$ 
$$$$           _
$$$$           P
$$$$            n            _       T
$$$$   {P } = {--} , {P } = {P } + [G ]{P }
$$$$     g     P       n      n      m   m
$$$$            m
$$$$ 
$$$$           _
$$$$           P
$$$$            f            _      
$$$$   {P } = {--} , {P } = {P } - [K  ]{Y }
$$$$     n     P       f      f      fs   s
$$$$            s
$$$$ 
$$$$           P
$$$$            a                    T
$$$$   {P } = {--} , {P } = {P } + [G ]{P }
$$$$     f     P       l      a      o   o
$$$$            o
$$$$
$$$$           P  
$$$$            l 
$$$$   {P } = {--}
$$$$     a     P  
$$$$            r 
$$$$
$$$$ and calculates incremental determinate forces of reaction for current 
$$$$ loop
$$$$
$$$$                    T
$$$$   {q } = -{P } - [D ]{P }
$$$$     r       r          l
$$$$
SSG2     USET,GM,YS,KFS,GO,DM,PG/QR,PO,PS,PL $  
****CARD   1-  3,  5,  6,  8- 13, 22, 23, 58- 62
****FILE 113
****RFMT 187,188,190,191
$$$$
LABEL    LBL10  $   
****CARD   1-  3,  5,  6,  8- 13, 22, 23, 58- 62
****FILE 113
****RFMT 187,188,190,191
$$$$ SSG3 solves for displacements of independent coordinates
$$$$ 
$$$$               -1  
$$$$   {u } = [K  ]  {P }
$$$$     l      ll     l
$$$$ 
$$$$ solves for displacements of omitted coordinates
$$$$ 
$$$$     o         -1  
$$$$   {u } = [K  ]  {P }
$$$$     o      oo     o
$$$$ 
$$$$ calculates residual vector (RULV) and residual vector error ratio for 
$$$$ independent coordinates
$$$$                
$$$$   {deltaP } = {P } - [K  ]{u }
$$$$          l      l      ll   l
$$$$ 
$$$$                T        
$$$$              {u }{deltaP }
$$$$                l        l
$$$$   epsilon  = -------------
$$$$          l       T 
$$$$                {P }{u }
$$$$                  l   l
$$$$ 
$$$$ and calculates residual vector (RUOV) and residual vector error ratio for
$$$$ omitted coordinates
$$$$ 
$$$$                             o
$$$$   {deltaP } = {P } - [K  ]{u }
$$$$          o      o      oo   o
$$$$ 
$$$$                T        
$$$$              {u }{deltaP }
$$$$                o        o
$$$$   epsilon  = -------------
$$$$          o       T   o
$$$$                {P }{u }
$$$$                  o   o
$$$$
SSG3     LLL,KLL,PL,LOO,KOO,PO/ULV,UOOV,RULV,RUOV/OMIT/V,Y,IRES=-1/ 
         PLACOUNT/S,N,EPSI $
****CARD   1-  6,  8- 13, 17, 22, 23, 58- 62
****FILE 114
****RFMT 187,188,190,191
$$$$ Go to label LBL9 if residual vectors are not to be printed.
COND     LBL9,IRES   $  
****CARD   1-  6,  8- 13, 17, 22, 23, 58- 62
****RFMT 187-191,193-198
$$$$ MATGPR prints the residual vector for independent coordinates (RULV).
MATGPR   GPL,USET,SIL,RULV//*L* $   
****CARD   1-  6,  8- 13, 17, 22, 23, 58- 62
****RFMT 187-191,193-198
$$$$ MATGPR prints the residual vector for omitted coordinates (RUOV).
MATGPR   GPL,USET,SIL,RUOV//*O* $   
****CARD   1-  6,  8- 13, 17, 22, 23, 58- 62
****RFMT 187-191,193-198
$$$$
LABEL    LBL9  $
****CARD   1-  6,  8- 13, 17, 22, 23, 58- 62
****RFMT 187-191,193-198
$$$$ SDR1 recovers dependent displacements for current loop
$$$$ 
$$$$    u
$$$$     l                                  o
$$$$   {--} = {u }  ,   {u } = [G ]{u ] + {u }  ,
$$$$    u       a         o      o   a      o
$$$$     r
$$$$ 
$$$$    u                u            
$$$$     a                f           
$$$$   {--} = {u }  ,   {--} = {u }  ,
$$$$    u       f        Y       n    
$$$$     o                s           
$$$$                                  
$$$$                        u         
$$$$                         n        
$$$$  {u } = [G ]{u ]  ,   {--} = {u }
$$$$    m      m   n        u       g 
$$$$                         m        
$$$$ 
$$$$ and recovers single-point forces of constraint for current loop
$$$$ 
$$$$                          T
$$$$   {delta q } = -{P } + [K  ]{u }
$$$$           s       s      fs   f
$$$$
SDR1     USET,PG,ULV,UOOV,YS,GO,GM,PS,KFS,KSS,QR/DELTAUGV,DELTAPG,  
         DELTAQG/1/*STATICS* $  
****CARD   1-  6,  8- 13, 22, 23, 58- 62
****FILE 115
****RFMT 187-191,193-198
$$$$ PLA2 adds the incremental displacement vector (DELTAUGV) and the 
$$$$ incremental single-point forces of constraint vector (DELTAQG) for the 
$$$$ current loop to the accumulated sum of these vectors (DELTAPG).
$$$$ 
$$$$   {u    } = {delta u  } + {u  }
$$$$     g               g       g
$$$$      i+1             i       i
$$$$ 
$$$$   {q    } = {delta q  } + {q  }
$$$$     g               g       g
$$$$      i+1             i       i
$$$$ 
PLA2     DELTAUGV,DELTAPG,DELTAQG/UGV1,PGV1,QG1/S,N,PLACOUNT $  
****CARD   1-  6,  8- 12, 22, 23, 58- 62
****FILE 116
$$$$ Allocate separate files for ESTNL and ESTNL1 and for ECPTNL and ECPTNL1.
EQUIV    ESTNL,ESTNL1/NEVER/ECPTNL,ECPTNL1/NEVER $  
****CARD   1-  6,  8- 12, 22, 23, 58- 62
****FILE 117
$$$$ Go to label PLALBL2A if no stress output is requested for nonlinear 
$$$$ elements.
COND     PLALBL2A,NONLSTR $ 
****CARD  22, 23
****FILE 117
$$$$ PLA3 calculates incremental stresses in nonlinear elements (ONLES) for 
$$$$ which an output request has been made and updates the accumulated 
$$$$ stresses (ESTNL1) in these elements.
PLA3     CSTM,MPT,DIT,DELTAUGV,ESTNL,CASECC/ONLES,ESTNL1/PLACOUNT/  
         PLSETNO $  
****CARD  22, 23
****FILE 117
$$$$ OFP formats the accumulated stresses in nonlinear elements prepared by 
$$$$ PLA3 and places them on the system output file for printing.
OFP      ONLES,,,,,//S,N,CARDNO $   
****CARD  22, 23
****FILE 117
$$$$
LABEL    PLALBL2A $ 
****CARD  22, 23
****FILE 117
$$$$
PARAM    //*SUB*/DIFF/NPLALIM/PLACOUNT $
****CARD   1-  6,  8- 12, 22, 23, 58- 62
****FILE 118
$$$$ Go to label PLALBL5 if all loading increments have been completed.
COND     PLALBL5,DIFF $ 
****CARD   1-  6,  8- 12, 22, 23, 58- 62
****FILE 118,119
$$$$                                                          nl
$$$$ PLA4 generates stiffness matrix for nonlinear elements [K  ] and updates
$$$$                                                          gg
$$$$ stress information.
PLA4     CSTM,MPT,ECPTNL,GPCT,DIT,DELTAUGV/KGGNL,ECPTNL1/S,N,PLACOUNT/  
         S,N,PLSETNO/S,N,PLFACT $   
****CARD   1-  6,  8- 12, 22, 23, 58- 62
****FILE 118
$$$$               nl
$$$$ Equivalence [K  ] to [K  ] if all elements are nonlinear.
$$$$               gg       gg
$$$$
EQUIV    KGGNL,KGGSUM/KGGLPG $  
****CARD   1-  6,  8- 12, 22, 23, 58- 62
****FILE 119
$$$$ Go to label PLALBL3 if all elements are nonlinear.
COND     PLALBL3,KGGLPG $   
****CARD   1-  6,  8- 12, 22, 23, 58- 62
****FILE 119
$$$$ ADD stiffness matrix for nonlinear elements (KGGNL) to stiffness matrix
$$$$ for linear elements (KGGL)
$$$$
$$$$     nl      l       sum
$$$$   [K  ] + [K  ] = [K   ]
$$$$     gg      gg      gg
$$$$
ADD      KGGNL,KGGL/KGGSUM/(1.0,0.0)/(1.0,0.0) $
****CARD   1-  6,  8- 12, 22, 23, 58- 62
****FILE 119
$$$$
LABEL    PLALBL3 $  
****CARD   1-  6,  8- 12, 22, 23, 58- 62
****FILE 119
$$$$
$$$$ Equivalence existing element tables to updated tables and equivalence
$$$$   sum
$$$$ [K   ] to [K  ] for next pass through loop.
$$$$   gg        gg
$$$$
EQUIV    ESTNL1,ESTNL/ALWAYS/ECPTNL1,ECPTNL/ALWAYS/KGGSUM,KGG/ALWAYS $  
****CARD   1-  6,  8- 12, 22, 23, 58- 62
****FILE 100,101
$$$$ Go to label LOOPBGN if additional load increments need to be processed.
REPT     LOOPBGN,360 $  
****CARD   1-  6,  8- 12, 22, 23, 58- 62
$$$$ Go to label ERROR2 and print Error Message No. 2 if the number of load 
$$$$ increments exceeds 360.
JUMP     ERROR2 $   
****CARD   1-  6,  8- 12, 22, 23, 58- 62
$$$$
LABEL    PLALBL4 $  
****CARD   1-  6,  8- 13, 17, 22, 23, 58- 62
$$$$ Print Error Message No. 5 and terminate execution.
PRTPARM  //-5/*PLA* $   
****CARD   1-  4,  6,  8- 12, 22, 23
$$$$
LABEL     PLALBL5 $ 
****CARD   1-  6,  8- 12, 22, 23, 58- 62
****FILE 118,119
$$$$ SDR2 calculates element forces (OEF1) and stresses for linear elements 
$$$$ (OES1) and prepares load vectors (OPG1), displacement vectors (OUGV1), 
$$$$ and single-point forces of constraint (OQG1) for output and translation 
$$$$ components of the displacement vector (PUGV1).
SDR2     CASECC,CSTM,MPT,DIT,EQEXIN,SIL,GPTT,EDT,BGPDP,,QG1,UGV1,ESTL,, 
         PGV1,PCOMPS/OPG1,OQG1,OUGV1,OES1,OEF1,PUGV1,OES1L,OEF1L/
         *PLA*////COMPS $   
****CARD  18, 19
****FILE 120
$$$$ OFP formats the tables prepared by SDR2 and places them on the system 
$$$$ output file for printing.
OFP      OUGV1,OPG1,OQG1,OEF1,OES1,//S,N,CARDNO $   
****CARD  19
****FILE 120
$$$$
OFP      OEF1L,OES1L,,,,//S,N,CARDNO $   
****CARD  19
****FILE 120
$$$$ SCAN examines the element stresses and forces calculated by SDR2 and 
$$$$ generates scanned output that meets the specifications set by the user.
SCAN     CASECC,OES1,OEF1/OESF1/*RF* $  
****CARD 19 
****FILE 120
$$$$ OFP formats the scanned output table prepared by SCAN and places it on 
$$$$ the system output file for printing.
OFP      OESF1,,,,,//S,N,CARDNO $   
****CARD  19
****FILE 120
$$$$ Go to label P2 if no deformed structure plots are requested.
COND     P2,JUMPPLOT $  
****SBST   7
****CARD  18
****FILE 125
$$$$ PLOT generates all requested deformed structure and contour plots.
PLOT     PLTPAR,GPSETS,ELSETS,CASECC,BGPDT,EQEXIN,SIP,PUGV1,,ECPT,OES1, 
         OES1L,/PLOTX2/NSIL/LUSEP/JUMPPLOT/PLTFLG/S,N,PFILE $   
****SBST   7
****CARD  18
****FILE 125
$$$$ PRTMSG prints plotter data, engineering data, and contour data for each 
$$$$ deformed plot generated.
PRTMSG   PLOTX2// $ 
****SBST   7
****CARD  18
****FILE 125
$$$$
LABEL    P2 $   
****SBST   7
****CARD  18
****FILE 125
$$$$ Go to label FINIS and make normal exit.
JUMP     FINIS $
****CARD   1-  6,  8- 24, 58- 62
****FILE 125
****RFMT 187-191,193-198
$$$$
LABEL    ERROR1 $   
****CARD   1-  3,  6,  8
****FILE 100
****RFMT 187-191,193-198
$$$$ Print Error Message No. 1 and terminate execution.
PRTPARM  //-1/*PLA* $   
****CARD   1-  3,  6,  8
****FILE 100
****RFMT 187-191,193-198
$$$$
LABEL    ERROR2 $   
****CARD   1-  6,  8- 12, 22, 23, 58- 62
****RFMT 187-191,193-198
$$$$ Print Error Message No. 2 and terminate execution.
PRTPARM  //-2/*PLA* $   
****CARD   1-  6,  8- 12, 22, 23, 58- 62
****RFMT 187-191,193-198
$$$$
LABEL    ERROR3 $   
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE 124
****RFMT 187-191,193-198
$$$$ Print Error Message No. 3 and terminate execution.
PRTPARM  //-3/*PLA* $   
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24, 61
****FILE 124
****RFMT 187-191,193-198
$$$$
LABEL    ERROR4 $   
****CARD   1,  2,  4-  6, 16
****FILE  97
****RFMT 187-191,193-198
$$$$ Print Error Message No. 4 and terminate execution.
PRTPARM  //-4/*PLA* $   
****CARD   1,  2,  4-  6, 16
****FILE  97
****RFMT 187-191,193-198
$$$$
LABEL    FINIS $
****CARD   1-  6,  8- 24, 58- 62
****FILE 125
****RFMT 187-191,193-198
$$$$
PURGE    DUMMY/MINUS1 $ 
****CARD   1-  6,  8- 24, 58- 62
****RFMT 187-191,193-198
$$$$
END      $  
****CARD   1-  6,  8- 24, 58- 62
****RFMT 187-191,193-198
$$$$
$*CARD BITS 
  1    AXIC     AXIF     CELAS1   CELAS2   CELAS3   CELAS4   CMASS1 
  1    CMASS2   
  1    CMASS3   CMASS4   CORD1C   CORD1R   CORD1S   CORD2C   CORD2R 
  1    CORD2S   
  1    GRDSET   GRID     GRIDB    POINTAX  RINGAX   RINGFL   SECTAX 
  1    SEQGP    SPOINT  
  2    ADUM1    ADUM2    ADUM3    ADUM4    ADUM5    ADUM6    ADUM7  
  2    ADUM8
  2    ADUM9    BAROR    CBAR     CCONEAX  CDUM1    CDUM2    CDUM3  
  2    CDUM4
  2    CDUM5    CDUM6    CDUM7    CDUM8    CDUM9    CELBOW   CHEXA1 
  2    CHEXA2   
  2    CIHEX1   CIHEX2   CIHEX3   CIS2D8   CONROD   CQDMEM   CQDMEM1
  2    CQDMEM2  CQDPLT   CQUAD1   CQUAD2   CROD     CSHEAR   CTETRA 
  2    CTORDRG  CQUAD4   CTRIA3
  2    CTRAPAX  CTRAPRG  CTRBSC   CTRIA1   CTRIA2   CTRIAAX  CTRIARG
  2    CTRIM6   
  2    CTRMEM   CTRPLT   CTRPLT1  CTRSHL   CTUBE    CTWIST   CWEDGE 
  3    PBAR     PCONEAX  PDUM1    PDUM2    PDUM3    PDUM4    PDUM5  
  3    PDUM6
  3    PDUM7    PDUM8    PDUM9    PELBOW   PIHEX    PIS2D8   PQDMEM 
  3    PQDMEM1  PQDMEM2  PQDPLT   PQUAD1   PQUAD2   PROD     PSHEAR 
  3    PTORDRG  PSHELL   PCOMP    PCOMP1   PCOMP2
  3    PTRAPAX  PTRBSC   PTRIA1   PTRIA2   PTRIAAX  PTRIM6   PTRMEM 
  3    PTRPLT   PTRPLT1  PTRSHL   PTUBE    PTWIST   
  4    GENEL
  5    CONM1    CONM2    PMASS  
  6    PELAS
  8    MAT1     MAT2     MAT3     MATS1    MATS2    MATT1    MATT2  
  8    MATT3    MAT8
  8    TABLEM1  TABLEM2  TABLEM3  TABLEM4  TABLES1  TABLES2  TABLES3
  8    TABLES4  TEMPMT$  TEMPMX$
  9    AXISYM$  CRIGD1   CRIGD2   CRIGD3   CRIGDR   CRROD    CRBAR  
  9    CRTRPLT  CRBE1    CRBE2    CRBE3    CRSPLINE MPC      MPCADD 
  9    MPCAX    MPC$
 10    SPC      SPC1     SPCADD   SPCAX    SPC$ 
 11    ASET     ASET1    OMIT     OMIT1    OMITAX   SUPAX    SUPORT 
 13    TEMP     TEMPAX   TEMPD    TEMPP1   TEMPP2   TEMPP3   TEMPRB 
 14    WTMASS   
 15    GRDPNT   
 16    PLOTEL   
 17    IRES 
 18    PLOT$
 19    POUT$
 20    ASETOUT  
 21    AUTOSPC  
 22    LOOP$
 23    LOOP1$   
 24    COUPMASS CPBAR    CPQDPLT  CPQUAD1  CPQUAD2  CPROD    CPTRBSC
 24    CPTRIA1  CPTRIA2  CPTRPLT  CPTUBE
 58    PLCO$    PLFACT  
 59    DEFORM   DEFORM$  LOAD$    SPCD  
 60    FORCE    FORCE1   FORCE2   FORCEAX  LOAD     MOMAX    MOMENT 
 60    MOMENT1  PLOAD4
 60    MOMENT2  PLOAD    PLOAD1   PLOAD2   PLOAD3   PRESAX   SLOAD  
 61    GRAV     RFORCE  
 62    TEMPLD$  
$$$$
$*FILE BITS 
 94    BGPDT    CSTM     EQEXIN   GPDT     GPL      SIL 
 95    ECT  
 96    GPTT     SLT 
 97    ECPT     GPECT    EST      GEI      GPCT 
 97    MPTX     PCOMPS   EPTX   
 98    KGGX 
 99    MGG  
100    KGGXL    ECPTNL   ESTL     ESTNL 
101    KGG      KGGL
102    ASET     RG       USET     YS       OGPST
103    PG1  
104    GPST 
105    GM   
106    KNN  
107    KFF      KFS      KSS
108    GO       KAA      KOO      LOO   
109    KLL      KLR      KRR
110    LLL  
111    DM   
112    PG   
113    PL       PO       PS       QR
114    RULV     RUOV     ULV      UOOV  
115    DELTAPG  DELTAQG  DELTAUGV   
116    UGV1     PGV1     QG1
117    ONLES    ESTNL1  
118    KGGNL    ECPTNL1 
119    KGGSUM   
120    OEF1     OES1     OPG1     OQG1     OUGV1    PUGV1   
120    OEF1L    OES1L    OESF1
121    ELSETS   GPSETS   PLTPAR   PLTSETX   
122    PLOTX1   
123    KELM     KDICT    MELM     MDICT 
124    OGPWG
125    PLOTX2   
126    BGPDP    SIP 
$*  
=PAGE=
DISP7

APR.93
$$$$$$$$
BEGIN    DISP 07 - DIRECT COMPLEX EIGENVALUE ANALYSIS - APR. 1993 $ 
****CARD   1-  6,  8- 24, 52, 56- 62
****RFMT 187-192,194-204,207-209
$$$$
PRECHK   ALL $  
****CARD   1-  6,  8- 24, 52, 56- 62
****RFMT 187-192,194-204,207-209
$$$$
FILE     GOD=SAVE/GMD=SAVE $
****CARD   1-  6,  8- 24, 52, 56- 62
****RFMT 187-192,194-204,207-209
$$$$
PARAM    //*MPY*/CARDNO/0/0 $   
****CARD   1-  6,  8- 11, 14, 19- 24, 52, 56- 62
****FILE 101,111,112,114,128
$$$$ GP1 generates coordinate system transformation matrices, tables of grid 
$$$$ point locations, and tables relating the internal and external grid point 
$$$$ numbers.
GP1      GEOM1,GEOM2,/GPL,EQEXIN,GPDT,CSTM,BGPDT,SIL/S,N,LUSET/ 
         S,N,NOGPDT/MINUS1=-1 $ 
****CARD   1
****FILE  94
$$$$ PLTTRAN modifies special scalar grid points in the BGPDT and SIL tables.
PLTTRAN  BGPDT,SIL/BGPDP,SIP/LUSET/S,N,LUSEP $  
****CARD   1
****FILE 131
$$$$
PURGE    USET,GM,GO,KAA,BAA,MAA,K4AA,KFS,EST,ECT,PLTSETX,PLTPAR,GPSETS, 
         ELSETS/NOGPDT $
****CARD   1
****FILE  95, 97,101,103,105,106,120-123
$$$$ Go to label LBL5 if there is only Direct Matrix Input.
COND     LBL5,NOGPDT $  
****CARD   1-  6,  8- 11, 13- 18, 20, 24, 58, 59
****FILE  95-106,120-128
$$$$ GP2 generates Element Connection Table with internal indices.
GP2      GEOM2,EQEXIN/ECT $ 
****CARD   1,  2,  4,  5, 16, 58
****FILE  95
$$$$
PARAML   PCDB//*PRES*////JUMPPLOT $ 
****SBST   7
****CARD  18
****FILE 120,127
$$$$
PURGE    PLTSETX,PLTPAR,GPSETS,ELSETS/JUMPPLOT $
****SBST   7
****CARD  18
****FILE 120
$$$$ Go to label P1 if there are no structure plot requests.
COND     P1,JUMPPLOT $  
****SBST   7
****CARD   1,  2,  4,  5, 16, 18, 58
****FILE 120,127
$$$$ PLTSET transforms user input into a form used to drive the structure 
$$$$ plotter.
PLTSET   PCDB,EQEXIN,ECT,EPT/PLTSETX,PLTPAR,GPSETS,ELSETS/S,N,NSIL/ 
         S,N,JUMPPLOT $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18, 58
****FILE 120
$$$$ PRTMSG prints error messages associated with the structure plotter.
PRTMSG   PLTSETX// $
****SBST   7
****CARD   1,  2,  4,  5, 16, 18, 58
****FILE 120
$$$$
PARAM    //*MPY*/PLTFLG/1/1 $   
****SBST   7
****CARD   1,  2,  4,  5, 16, 18, 58
****FILE 127
$$$$
PARAM    //*MPY*/PFILE/0/0 $
****SBST   7
****CARD   1,  2,  4,  5, 16, 18, 58
****FILE 127
$$$$ Go to label P1 if no undeformed structure plots are requested.
COND     P1,JUMPPLOT $  
****SBST   7
****CARD   1,  2,  4,  5, 16, 18, 58
****FILE 127
$$$$ PLOT generates all requested undeformed structure plots.
PLOT     PLTPAR,GPSETS,ELSETS,CASECC,BGPDT,EQEXIN,SIL,,ECT,,,,/PLOTX1/  
         NSIL/LUSET/JUMPPLOT/PLTFLG/S,N,PFILE $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18, 58
****FILE 127
$$$$ PRTMSG prints plotter data and engineering data for each undeformed plot 
$$$$ generated.
PRTMSG   PLOTX1// $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18, 58
****FILE 127
$$$$
LABEL    P1 $   
****SBST   7
****CARD   1,  2,  4,  5, 16, 18, 58
****FILE 120,127
$$$$ GP3 generates Grid Point Temperature Table.
GP3      GEOM3,EQEXIN,GEOM2/,GPTT/NOGRAV $  
****CARD   1,  2, 13
****FILE  96
$$$$ TA1 generates element tables for use in matrix assembly and stress 
$$$$ recovery.
TA1      ECT,EPT,BGPDT,SIL,GPTT,CSTM,MPT,EQEXIN/EST,GEI,GPECT,,,MPTX,
         PCOMPS,EPTX/LUSET/S,N,NOSIMP=-1/1/S,N,NOGENL=-1/GENEL/
         S,N,COMPS $ 
****CARD   1-  6, 13, 16, 58, 59
****FILE  97
$$$$
EQUIV    MPTX,MPT/COMPS/EPTX,EPT/COMPS $
****CARD   1-  6,  8- 24, 52, 56- 62
****FILE  97
$$$$
PURGE    K4GG,MGG,BGG,K4NN,K4FF,K4AA,MNN,MFF,MAA,BNN,BFF,BAA,   
         KGGX/NOSIMP $  
****CARD   1,  2,  4-  6, 16, 58, 59
****FILE  98, 99,104-106,121-123,125
$$$$ Go to label LBL1 if there are no structural elements.
COND     LBL1,NOSIMP $  
****CARD   1-  3,  5,  6,  8, 13- 15, 24, 58, 59
****FILE  98, 99,104,105,121,122,124-126,128
$$$$
PARAM    //*ADD*/NOKGGX/1/0 $   
****CARD   1-  3,  6,  8
****FILE  98
$$$$
PARAM    //*ADD*/NOMGG/1/0 $
****CARD   1-  3,  5,  8, 14, 24
****FILE  99
****RFMT 187,190-192
$$$$
PARAM    //*ADD*/NOBGG=-1/1/0 $ 
****CARD   1-  3,  8
****FILE 125
****RFMT 187,190-192
$$$$
PARAM    //*ADD*/NOK4GG/1/0 $   
****CARD   1-  3,  6,  8
****FILE 126
$$$$ EMG generates structural element stiffness, mass and damping matrix 
$$$$ tables, and dictionaries for later assembly by the EMA module.
EMG      EST,CSTM,MPT,DIT,GEOM2,/KELM,KDICT,MELM,MDICT,BELM,BDICT,/S,N, 
         NOKGGX/S,N,NOMGG/S,N,NOBGG/S,N,NOK4GG//C,Y,COUPMASS/   
         C,Y,CPBAR/C,Y,CPROD/C,Y,CPQUAD1/C,Y,CPQUAD2/C,Y,CPTRIA1/   
         C,Y,CPTRIA2/C,Y,CPTUBE/C,Y,CPQDPLT/C,Y,CPTRPLT/C,Y,CPTRBSC/
         C,Y,VOLUME/C,Y,SURFACE $   
****CARD   1-  3,  5,  6,  8, 13, 24
****FILE 124
****RFMT 187,190-192
$$$$
PURGE    KGGX/NOKGGX/MGG/NOMGG $
****CARD   1-  3,  5,  6,  8, 14, 24
****FILE  98, 99
****RFMT 187,190-192
$$$$ Go to label LBLKGGX if no stiffness matrix is to be assembled.
COND     LBLKGGX,NOKGGX $   
****CARD   1-  3,  6,  8
****FILE  98
$$$$                                  x
$$$$ EMA assembles stiffness matrix [K  ] and Grid Point Singularity Table.
$$$$                                  gg
$$$$ 
EMA      GPECT,KDICT,KELM/KGGX $
****CARD   1-  3,  6,  8
****FILE  98
$$$$
LABEL    LBLKGGX $  
****CARD   1-  3,  6,  8
****FILE  98
$$$$ Go to label LBLMGG if no mass matrix is to be assembled.
COND     LBLMGG,NOMGG $ 
****CARD   1-  3,  5,  8, 14, 24
****FILE  99
****RFMT 187,190-192
$$$$ EMA assembles mass matrix [M  ].
$$$$                             gg
$$$$
EMA      GPECT,MDICT,MELM/MGG/-1/C,Y,WTMASS=1.0 $   
****CARD   1-  3,  5,  8, 14, 24
****FILE  99
****RFMT 187,190-192
$$$$
PURGE    MDICT,MELM/MINUS1 $
****CARD   1-  3,  5,  8, 14, 24
****FILE 124
$$$$
LABEL    LBLMGG $   
****CARD   1-  3,  5,  8, 14, 24
****FILE  99
****RFMT 187,190-192
$$$$ Go to label LBLBGG if no viscous damping matrix is to be assembled.
COND     LBLBGG,NOBGG $ 
****CARD   1-  3,  8, 58, 59
****FILE 125
****RFMT 187,190-192
$$$$ EMA assembles viscous damping matrix [B  ].
$$$$                                        gg
$$$$ 
EMA      GPECT,BDICT,BELM/BGG $ 
****CARD   1-  3,  8, 58, 59
****FILE 125
****RFMT 187,190-192
$$$$
PURGE    BDICT,BELM/MINUS1 $
****CARD   1-  3,  8, 58, 59
****FILE 124
$$$$
LABEL    LBLBGG $   
****CARD   1-  3,  8, 58, 59
****FILE 125
****RFMT 187,190-192
$$$$ Go to label LBLK4GG if no structural damping matrix is to be assembled.
COND     LBLK4GG,NOK4GG $   
****CARD   1-  3,  8
****FILE 126
$$$$                                           4
$$$$ EMA assembles structural damping matrix [K  ].
$$$$                                           gg
$$$$ 
EMA      GPECT,KDICT,KELM/K4GG/NOK4GG $ 
****CARD   1-  3,  6,  8
****FILE 126
$$$$
LABEL    LBLK4GG $  
****CARD   1-  3,  6,  8
****FILE 126
$$$$
PURGE    KDICT,KELM/MINUS1 $
****CARD   1-  3,  6,  8
****FILE 124
$$$$
PURGE    MNN,MFF,MAA/NOMGG $
****CARD   1-  3,  5,  8, 14, 24
****FILE 104,105,121
****RFMT 187,190-192
$$$$
PURGE    BNN,BFF,BAA/NOBGG $
****CARD   1-  3,  8, 58, 59
****FILE 104,105,122
****RFMT 187,190-192
$$$$ Go to label LBL1 if no weight and balance information is requested.
COND     LBL1,GRDPNT $  
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 128
$$$$ Go to label ERROR3 and print Error Message No. 3 if no mass matrix 
$$$$ exists.
COND     ERROR3,NOMGG $ 
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 128
$$$$ GPWG generates weight and balance information.
GPWG     BGPDP,CSTM,EQEXIN,MGG/OGPWG/V,Y,GRDPNT/C,Y,WTMASS $
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 128
$$$$ OFP formats the weight and balance information prepared by GPWG and 
$$$$ places it on the system output file for printing.
OFP      OGPWG,,,,,//S,N,CARDNO $   
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 128
$$$$
LABEL    LBL1 $ 
****CARD   1-  3,  5, 13- 15, 24, 58, 59
****FILE  98, 99,104,105,121,122,124-126,128
$$$$               x
$$$$ Equivalence [K  ] to [K  ] if there are no general elements.
$$$$               gg       gg
$$$$
EQUIV    KGGX,KGG/NOGENL $  
****CARD   1-  4,  6,  8
****FILE 100
$$$$ Go to label LBL11 if there are no general elements.
COND     LBL11,NOGENL $ 
****CARD   1-  4,  6,  8
****FILE 100
$$$$                                 x
$$$$ SMA3 adds general elements to [K  ] to obtain stiffness matrix [K  ].
$$$$                                 gg                               gg
$$$$ 
SMA3     GEI,KGGX/KGG/LUSET/NOGENL/NOSIMP $ 
****CARD   1-  4,  6,  8
****FILE 100
$$$$
LABEL    LBL11 $
****CARD   1-  4,  6,  8
****FILE 100
$$$$
GPSTGEN  KGG,SIL/GPST $ 
****CARD   1-  4,  6,  8
****FILE 102
$$$$
PARAM    //*MPY*/NSKIP/0/0 $
****CARD   1,  9- 12
****FILE 101
$$$$ GP4 generates flags defining members of various displacement sets (USET) 
$$$$ and forms multipoint constraint equations [R ] {u } = 0.
$$$$                                             g    g
$$$$ 
GP4      CASECC,GEOM4,EQEXIN,GPDT,BGPDT,CSTM,GPST/RG,,USET, 
         ASET,OGPST/LUSET/S,N,MPCF1/S,N,MPCF2/S,N,SINGLE/S,N,OMIT/  
         S,N,REACT/S,N,NSKIP/S,N,REPEAT/S,N,NOSET/S,N,NOL/S,N,NOA/  
         C,Y,ASETOUT/C,Y,AUTOSPC $  
****CARD   1-  4,  6,  8- 12, 17, 20
****FILE 101
$$$$ OFP formats the table of potential grid point singularities prepared by 
$$$$ GPSP and places it on the system output file for printing.
OFP      OGPST,,,,,//S,N,CARDNO $   
****CARD   1-  4,  6,  8- 10, 20
****FILE 101
$$$$
PURGE    GM,GMD/MPCF1/GO,GOD/OMIT/KFS,QPC/SINGLE $  
****CARD   1,  9- 12
****FILE 103,105,106,110,113
$$$$ Equivalence [K  ] to [K  ], [M  ] to [M  ], [B  ] to [B  ], and
$$$$               gg       nn     gg       nn     gg       nn
$$$$   4        4
$$$$ [K  ] to [K  ] if no multipoint constraints exist.
$$$$   gg       nn
$$$$ 
EQUIV    KGG,KNN/MPCF1/MGG,MNN/MPCF1/ BGG,BNN/MPCF1/K4GG,K4NN/MPCF1 $   
****CARD   1-  6,  8,  9, 14, 24, 58, 59
****FILE 104
$$$$ Go to label LBL2 if no multipoint constraints exist.
COND     LBL2,MPCF1 $   
****CARD   1-  6,  8,  9, 14, 24
****FILE 103,104
$$$$ MCE1 partitions multipoint constraint equation [R ] = [R |R ] and solves
$$$$                                                  g      m  n
$$$$                                                             -1
$$$$ for multipoint constraint transformation matrix [G ] = -[R ]  [R ].
$$$$                                                   m       m     n
$$$$
MCE1     USET,RG/GM $   
****CARD   1,  9
****FILE 103
$$$$ MCE2 partitions stiffness, mass, and damping matrices
$$$$ 
$$$$           +_      +                        +_      +
$$$$           |K  |K  |                        |M  |M  |
$$$$           | nn| nm|                        | nn| nm|
$$$$   [K  ] = |---+---|                [M  ] = |---+---|
$$$$     gg    |K  |K  |                  gg    |M  |M  |
$$$$           | mn| mm|                        | mn| mm|
$$$$           +       +                        +       +
$$$$ 
$$$$           +_      +                        +_4   4 +
$$$$           |B  |B  |                        |K  |K  |
$$$$           | nn| nm|                  4     | nn| nm|
$$$$   [B  ] = |---+---|                [K  ] = |---+---|
$$$$     gg    |B  |B  |                  gg    | 4 | 4 |
$$$$           | mn| mm|                        |K  |K  |
$$$$           +       +                        | mn| mm|
$$$$                                            +       +
$$$$ 
$$$$ and performs the matrix reductions
$$$$ 
$$$$            _        T           T           T
$$$$   [K  ] = [K  ] + [G ][K  ] + [K  ][G ] + [G ][K  ][G ]
$$$$     nn      nm      m   mn      mn   m      m   mm   m
$$$$ 
$$$$            _        T           T           T
$$$$   [M  ] = [M  ] + [G ][M  ] + [M  ][G ] + [G ][M  ][G ]
$$$$     nn      nm      m   mn      mn   m      m   mm   m
$$$$ 
$$$$            _        T           T           T
$$$$   [B  ] = [B  ] + [G ][B  ] + [B  ][G ] + [G ][B  ][G ]
$$$$     nn      nm      m   mn      mn   m      m   mm   m
$$$$ 
$$$$     4      _4       T   4       4  T         T   4
$$$$   [K  ] = [K  ] + [G ][K  ] + [K  ] [G ] + [G ][K  ][G ]
$$$$     nn      nm      m   mn      mn    m      m   mm   m
$$$$
MCE2     USET,GM,KGG,MGG,BGG,K4GG/KNN,MNN,BNN,K4NN $
****CARD   1-  6,  8,  9, 14, 24, 58, 59
****FILE 104
$$$$
LABEL    LBL2 $ 
****CARD   1-  6,  8,  9, 14, 24, 58, 59
****FILE 103,104
$$$$ Equivalence [K  ] to [K  ], [M  ] to [M  ], [B  ] to [B  ], and
$$$$               nn       ff     nn       ff     nn       ff
$$$$   4        4
$$$$ [K  ] to [K  ] if no single-point constraints exist.
$$$$   nn       ff
$$$$ 
EQUIV    KNN,KFF/SINGLE/MNN,MFF/SINGLE/BNN,BFF/SINGLE/K4NN,K4FF/SINGLE $
****CARD   1-  6,  8- 10, 14, 24, 58, 59
****FILE 105
$$$$ Go to label LBL3 if no single-point constraints exist.
COND     LBL3,SINGLE $  
****CARD   1-  6,  8- 10, 14, 24, 58, 59
****FILE 105
$$$$ SCE1 partitions out single-point constraints
$$$$ 
$$$$           +       +                        +       +
$$$$           |K  |K  |                        |M  |M  |
$$$$           | ff| fs|                        | ff| fs|
$$$$   [K  ] = |---+---|                [M  ] = |---+---|
$$$$     nn    |K  |K  |                  nn    |M  |M  |
$$$$           | sf| ss|                        | sf| ss|
$$$$           +       +                        +       +
$$$$
$$$$                                            +       +
$$$$           +       +                        | 4   4 |
$$$$           |B  |B  |                        |K  |K  |
$$$$           | ff| fs|                  4     | ff| fs|
$$$$   [B  ] = |---+---|                [K  ] = |---+---|
$$$$     nn    |B  |B  |                  nn    | 4 | 4 |
$$$$           | sf| ss|                        |K  |K  |
$$$$           +       +                        | sf| ss|
$$$$                                            +       +
$$$$
SCE1     USET,KNN,MNN,BNN,K4NN/KFF,KFS,,MFF,BFF,K4FF $  
****CARD   1-  6,  8- 10, 14, 24, 58, 59
****FILE 105
$$$$
LABEL    LBL3 $ 
****CARD   1-  6,  8- 10, 14, 24, 58, 59
****FILE 105
$$$$ Equivalence [K  ] to [K  ], [M  ] to [M  ], [B  ] to [B  ], and
$$$$               ff       aa     ff       aa     ff       aa
$$$$      4      4
$$$$ [K  ]  to [K  ] if no omitted coordinates exist.
$$$$   ff        aa
$$$$ 
EQUIV    KFF,KAA/OMIT/ MFF,MAA/OMIT/BFF,BAA/OMIT/K4FF,K4AA/OMIT $   
****CARD   1-  6,  8- 11, 14, 24, 58, 59
****FILE 106,121-123
$$$$ Go to label LBL5 if no omitted coordinates exist.
COND     LBL5,OMIT $
****CARD   1-  6,  8- 11, 14, 24, 58, 59
****FILE 106,121-123
$$$$ SMP1 partitions constrained stiffness matrix
$$$$
$$$$           +_      +
$$$$           |K  |K  |
$$$$           | aa| ao|
$$$$   [K  ] = |---+---|
$$$$     ff    |K  |K  |
$$$$           | oa| oo|
$$$$           +       +
$$$$           
$$$$                                               -1
$$$$ solves for transformation matrix [G ] = -[K  ]  [K  ]
$$$$                                    o       oo     oa
$$$$ 
$$$$                                        _        T
$$$$ and performs matrix reduction [K  ] = [K  ] + [K  ][G ].
$$$$                                 aa      aa      oa   o
$$$$
SMP1     USET,KFF,,,/GO,KAA,KOO,LOO,,,,, $  
****CARD   1-  4,  6,  8- 11
****FILE 106
$$$$ Go to label LBLM if no mass matrix exists.
COND     LBLM,NOMGG $   
****CARD   1-  6,  8- 11, 14, 24
****FILE 121
$$$$ SMP2 partitions constrained mass matrix
$$$$
$$$$           +_      +
$$$$           |M  |M  |
$$$$           | aa| ao|
$$$$   [M  ] = |---+---|
$$$$     ff    |M  |M  |
$$$$           | oa| oo|
$$$$           +       +
$$$$           
$$$$ and performs matrix reduction
$$$$
$$$$            _                         T      T
$$$$   [M  ] = [M  ] + [M  ][G ] + [M  G ]   + [G ][M  ][G ]
$$$$     aa      aa      oa   o      ao o        o   oo   o
$$$$
SMP2     USET,GO,MFF/MAA $  
****CARD   1-  6,  8- 11, 14, 24
****FILE 121
$$$$
LABEL    LBLM $ 
****CARD   1-  6,  8- 11, 14, 24
****FILE 121
$$$$ Go to label LBLB if no viscous damping matrix exists.
COND     LBLB,NOBGG $   
****CARD   1-  4,  6,  8- 11, 58, 59
****FILE 122
$$$$ SMP2 partitions constrained viscous damping matrix
$$$$
$$$$           +_      +
$$$$           |B  |B  |
$$$$           | aa| ao|
$$$$   [B  ] = |---+---|
$$$$     ff    |B  |B  |
$$$$           | oa| oo|
$$$$           +       +
$$$$           
$$$$ and performs matrix reduction
$$$$
$$$$            _                         T      T
$$$$   [B  ] = [B  ] + [B  ][G ] + [B  G ]   + [G ][B  ][G ]
$$$$     aa      aa      oa   o      ao o        o   oo   o
$$$$
SMP2     USET,GO,BFF/BAA $  
****CARD   1-  4,  6,  8- 11, 58, 59
****FILE 122
$$$$
LABEL    LBLB $ 
****CARD   1-  4,  6,  8- 11, 58, 59
****FILE 122
$$$$ Go to label LBL5 if no structural damping matrix exists.
COND     LBL5,NOK4GG $  
****CARD   1-  4,  6,  8- 11
****FILE 123
$$$$ SMP2 partitions constrained structural damping matrix
$$$$
$$$$           +       +
$$$$           |_4   4 |
$$$$           |K  |K  |
$$$$     4     | aa| ao|
$$$$   [K  ] = |---+---|
$$$$     ff    | 4 | 4 |
$$$$           |K  |K  |
$$$$           | oa| oo|
$$$$           +       +
$$$$ 
$$$$ and performs matrix reduction
$$$$
$$$$     4       4       4           4    T      T   4
$$$$   [K  ] = [K  ] + [K  ][G ] + [K  G ]   + [G ][K  ][G ]
$$$$     aa      aa      oa   o      ao o        o   oo   o
$$$$
SMP2     USET,GO,K4FF/K4AA $
****CARD   1-  4,  6,  8- 11
****FILE 123
$$$$
LABEL    LBL5 $ 
****CARD   1-  6,  8- 11, 13- 18, 20, 23- 28, 30, 58, 59
****FILE  95-106,120-128
$$$$ DPD generates flags defining members of various displacement sets used in 
$$$$ dynamic analysis (USETD), tables relating the internal and external grid 
$$$$ point numbers (GPLD), including extra points introduced for dynamic 
$$$$ analysis (SILD), and prepares Transfer Function Pool (TFPOOL), and 
$$$$ Eigenvalue Extraction Data (EED).
DPD      DYNAMICS,GPL,SIL,USET/GPLD,SILD,USETD,TFPOOL,,,,,,EED,EQDYN/   
         LUSET/S,N,LUSETD/NOTFL/NODLT/NOPSDL/NOFRL/ 
         NONLFT/NOTRL/S,N,NOEED/123/S,N,NOUE $  
****CARD   1,  9- 11, 57, 61
****FILE 107
$$$$ Go to label ERROR1 and print Error Message No. 1 if there is no 
$$$$ Eigenvalue Extraction Data.
COND     ERROR1,NOEED $ 
****CARD   1,  9- 11, 57, 61
****FILE 107
$$$$                       d                d
$$$$ Equivalence [G ] to [G ] and [G ] to [G ] if there are no extra points
$$$$               o       o        m       m
$$$$ introduced for dynamic analysis.
EQUIV    GO,GOD/NOUE/GM,GMD/NOUE $  
****CARD   1-  6,  8- 11, 14, 22- 24, 52, 56, 58- 60
****FILE 110
$$$$
PARAM    //*ADD*/NEVER/1/0 $
****CARD  19, 21- 23
$$$$
PARAM    //*MPY*/REPEATE/1/-1 $ 
****CARD   1-  6,  8- 14, 16, 19, 21- 23, 52, 56- 62
****FILE 108
****RFMT 187-192,194-204,207-209
$$$$ BMG generates DMIG card images describing the interconnection of the 
$$$$ fluid and the structure.
BMG      MATPOOL,BGPDT,EQEXIN,CSTM/BDPOOL/S,N,NOKBFL/S,N,NOABFL/
         S,N,MFACT $
****CARD   1, 52
****FILE 118
$$$$
PARAM    //*AND*/NOFL/NOABFL/NOKBFL $   
****CARD   1, 22, 23, 52, 57, 60
****FILE 119
$$$$
PURGE    KBFL/NOKBFL/ ABFL/NOABFL $ 
****CARD   1, 52
****FILE 118
$$$$ Go to label LBL13 if no fluid structure interface is defined.
COND     LBL13,NOFL $   
****CARD   1, 52
****FILE 119
$$$$ MTRXIN generates fluid boundary matrices [A    ] and [K    ]. The matrix
$$$$                                            b,fl        b,fl
$$$$ [K    ] is generated only for a nonzero gravity in the fluid.
$$$$   b,fl
$$$$
MTRXIN,  ,BDPOOL,EQDYN,,/ABFL,KBFL,/LUSETD/S,N,NOABFL/S,N,NOKBFL/   
         0 $
****CARD   1, 52
****FILE 119
$$$$ Beginning of loop for additional sets of direct input matrices.
LABEL    LBL13 $
****CARD   1-  6,  8- 16, 18, 19, 21- 23, 52, 56- 62
****RFMT 187-192,194-204,207-209
$$$$
PURGE    PHID,CLAMA,OPHID,OQPC1,OCPHIP,OESC1,OEFC1,CPHIP,QPC,   
         K2PP,M2PP,B2PP,K2DD,M2DD,B2DD/NEVER $  
****CARD  19, 21- 23
****FILE 109-114,129,130
$$$$ CASE extracts the appropriate record from CASECC corresponding to the 
$$$$ current loop and copies it into CASEXX.
CASE     CASECC,/CASEXX/*CEIGN*/S,N,REPEATE/S,N,NOLOOP $
****CARD   1-  6,  8- 16, 19, 21- 23, 25, 52, 56- 62
****FILE 108
****RFMT 187-192,194-204,207-209
$$$$                                            2d     2d         2
$$$$ MTRXIN selects the direct input matrices [K  ], [M  ], and [B  ] for the
$$$$                                            pp     pp         pp
$$$$ current loop.
MTRXIN   CASEXX,MATPOOL,EQDYN,,TFPOOL/K2DPP,M2DPP,B2PP/LUSETD/S,N,  
         NOK2DPP/S,N,NOM2DPP/S,N,NOB2PP $   
****CARD   1, 22, 23, 52, 57, 60
****FILE 109
$$$$
PARAM    //*AND*/NOM2PP/NOABFL/NOM2DPP $
****CARD   1, 22, 23, 52, 57, 60
****FILE 109
$$$$
PARAM    //*AND*/NOK2PP/NOFL  /NOK2DPP $
****CARD   1, 22, 23, 52, 57, 60
****FILE 109
$$$$               2        2d
$$$$ Equivalence [K  ] to [K  ] if no fluid-structure interface is defined
$$$$               pp       pp
$$$$                   2        2d
$$$$ and equivalence [M  ] to [M  ] if there is no [A    ].
$$$$                   pp       pp                   b,fl
$$$$
EQUIV    K2DPP,K2PP/NOFL/M2DPP,M2PP/NOABFL $
****CARD   1, 22, 23, 52, 57, 60
****FILE 129,130
$$$$ Go to label LBLFL2 if no fluid-structure interface is defined.
COND     LBLFL2,NOFL $  
****CARD   1, 22, 23, 52, 57, 60
****FILE 129,130
$$$$                         2d
$$$$ ADD5 adds [K    ] and [K  ] and subtracts [A    ] from them to form
$$$$             b,fl        pp                  b,fl
$$$$   2
$$$$ [K  ].
$$$$   pp
$$$$ 
ADD5     ABFL,KBFL,K2DPP,,/K2PP/(-1.0,0.0) $
****CARD   1, 22, 23, 52, 57, 60
****FILE 129
$$$$ Go to label LBLFL2 if there is no [A    ].
$$$$                                     b,fl 
$$$$ 
COND     LBLFL2,NOABFL $
****CARD   1, 22, 23, 52, 57, 60
****FILE 130
$$$$                                    T
$$$$ Transpose [A    ] to obtain [A    ] .
$$$$             b,fl              b,fl
$$$$
TRNSP    ABFL/ABFLT $   
****CARD   1, 22, 23, 52, 57, 60
****FILE 130
$$$$                              2                  T     2d
$$$$ ADD assembles input matrix [M  ] = MFACT [A    ]  + [M  ].
$$$$                              pp            b,fl       pp
$$$$
ADD      ABFLT,M2DPP/M2PP/MFACT/(1.0,0.0) $ 
****CARD   1, 22, 23, 52, 57, 60
****FILE 130
$$$$
LABEL    LBLFL2 $   
****CARD   1, 22, 23, 52, 57, 60
****FILE 129,130
$$$$
PARAM    //*AND*/BDEBA/NOUE/NOB2PP $
****CARD   1, 22, 23, 52, 57, 60
****FILE 110
$$$$
PARAM    //*AND*/MDEMA/NOUE/NOM2PP $
****CARD   1, 22, 23, 52, 57, 60
****FILE 110
$$$$
PARAM    //*AND*/KDEK2/NOGENL/NOSIMP $  
****CARD   1, 22, 23, 52, 57, 60
****FILE 110
$$$$
PURGE    K2DD/NOK2PP/M2DD/NOM2PP/B2DD/NOB2PP $  
****CARD   1-  6,  8- 11, 14, 22- 24, 52, 56- 60
****FILE 110
$$$$               2        2      2        2          2        2   
$$$$ Equivalence [M  ] to [M  ], [B  ] to [B  ], and [K  ] to [K  ] if no
$$$$               pp       dd     pp       dd         pp       dd  
$$$$ constraints are applied, [M  ] to [M  ] if there are no direct input
$$$$                            aa       dd
$$$$ mass matrices and no extra points, and [B  ] to [B  ] if there are no
$$$$                                          aa       dd
$$$$ direct input damping matrices and no extra points.
EQUIV    M2PP,M2DD/NOA/B2PP,B2DD/NOA/K2PP,K2DD/NOA/ 
          MAA,MDD/MDEMA/BAA,BDD/BDEBA $ 
****CARD   1-  6,  8- 11, 14, 22- 24, 52, 56- 60
****FILE 110
$$$$ Go to label LBL18 if only extra points are defined.
COND     LBL18,NOGPDT $ 
****CARD   1-  6,  8- 11, 14, 22- 24, 52, 56- 60
****FILE 110
****RFMT 195
$$$$ GKAD assembles stiffness, mass, and damping matrices for use in Direct 
$$$$ Complex Eigenvalue Analysis.
$$$$
$$$$                     1       2        4
$$$$   [K  ] = (1 + ig)[K  ] + [K  ] + i[K  ]
$$$$     dd              dd      dd       dd
$$$$
$$$$             1       2
$$$$   [M  ] = [M  ] + [M  ]
$$$$     dd      dd      dd
$$$$
$$$$             1       2
$$$$   [B  ] = [B  ] + [B  ]
$$$$     dd      dd      dd
$$$$
$$$$ Direct input matrices may be complex.
GKAD     USETD,GM,GO,KAA,BAA,MAA,K4AA,K2PP,M2PP,B2PP/KDD,BDD,MDD,GMD,   
         GOD,K2DD,M2DD,B2DD/*CMPLEV*/*DISP*/*DIRECT*/C,Y,G=0.0/ 
         0.0/0.0/NOK2PP/NOM2PP/NOB2PP/  
         MPCF1/SINGLE/OMIT/NOUE/NOK4GG/NOBGG/   
         KDEK2/-1 $ 
****CARD   1-  6,  8- 11, 14, 22- 24, 52, 56- 60
****FILE 110
****RFMT 195
$$$$
LABEL    LBL18 $
****CARD   1-  6,  8- 11, 14, 22- 24, 52, 56- 60
****FILE 110
****RFMT 195
$$$$               2
$$$$ Equivalence [K  ] to [K  ] if all stiffness is Direct Matrix Input,
$$$$               dd       dd
$$$$   2
$$$$ [M  ] to [M  ] if all mass is Direct Matrix Input, and
$$$$   dd       dd
$$$$   2
$$$$ [B  ] to [B  ] if all damping is Direct Matrix Input.
$$$$   dd       dd
$$$$
EQUIV    B2DD,BDD/NOBGG/ M2DD,MDD/NOSIMP/ K2DD,KDD/KDEK2 $  
****CARD   1-  6,  8- 11, 14, 22- 24, 52, 56- 60
****FILE 110
$$$$ CEAD extracts complex eigenvalues and eigenvectors from the equation
$$$$
$$$$        2
$$$$   [M  p  + B  p + K  ] {u } = 0
$$$$     dd      dd     dd    d
$$$$
$$$$ and normalizes eigenvectors according to one of the following user 
$$$$ requests:
$$$$   1. Unit magnitude of a selected component.
$$$$   2. Unit magnitude of the largest component.
CEAD     KDD,BDD,MDD,EED,CASEXX/PHID,CLAMA,OCEIGS,/S,N,EIGVS $  
****CARD   1-  6,  8- 11, 14, 22- 24, 52, 56- 62
****FILE 111
$$$$ OFP formats the summary of complex eigenvalues (CLAMA) and summary of 
$$$$ eigenvalue extraction information (OCEIGS) prepared by CEAD and places 
$$$$ them on the system output file for printing.
OFP      OCEIGS,CLAMA,,,,//S,N,CARDNO $ 
****CARD   1-  6,  8- 11, 14, 22- 24, 52, 56- 62
****FILE 111
$$$$ Go to label LBL16 if no eigenvalues were found.
COND     LBL16,EIGVS $  
****CARD   1-  6,  8- 11, 14, 19, 21- 24, 52, 56- 62
****FILE 112-114
$$$$ VDR prepares eigenvectors for output, using only the independent degrees 
$$$$ of freedom.
VDR      CASEXX,EQDYN,USETD,PHID,CLAMA,,/OPHID,/*CEIGN*/*DIRECT*/   
         0/S,N,NOD/S,N,NOP/0 $  
****CARD  19, 21
****FILE 112
$$$$ Go to label LBL15 if there is no output request for independent degrees 
$$$$ of freedom.
COND     LBL15,NOD $
****CARD  21
****FILE 112
$$$$ OFP formats the eigenvectors for independent degrees of freedom prepared 
$$$$ by VDR and places them on the system output file for printing.
OFP      OPHID,,,,,//S,N,CARDNO $   
****CARD  21
****FILE 112
$$$$
LABEL    LBL15 $
****CARD  21
****FILE 112
$$$$ Go to label LBL16 if there is no output request involving dependent 
$$$$ degrees of freedom or forces and stresses.
COND     LBL16,NOP $
****CARD   1-  6,  8- 11, 14, 22- 24, 52, 56- 62
****FILE 113
****RFMT 187-192,194-204,207-209
$$$$ Equivalence {phi } to {phi } if no constraints are applied.
$$$$                 d         p
$$$$
EQUIV    PHID,CPHIP/NOA $   
****CARD   1-  6,  8- 11, 14, 22- 24, 52, 56- 62
****FILE 113
****RFMT 187-192,194-204,207-209
$$$$ Go to label LBL17 if no constraints are applied.
COND     LBL17,NOA $
****CARD   1-  6,  8- 11, 14, 22- 24, 52, 56- 62
****FILE 113
****RFMT 187-192,194-204,207-209
$$$$ SDR1 recovers dependent components of eigenvectors
$$$$ 
$$$$                                     phi
$$$$             d                          d
$$$$  {phi } = [G ]{phi }               {----} = {phi  + phi }
$$$$      o      o     d                 phi         f      e
$$$$                                        o
$$$$
$$$$    phi +phi      
$$$$       f    e                                  d
$$$$   {---------} = {phi +phi }        {phi } = [G ]{phi  + phi }
$$$$      phi            n    e             m      m     n      e
$$$$         s        
$$$$
$$$$    phi +phi                
$$$$       n    e               
$$$$   {---------} = {phi }
$$$$      phi            p 
$$$$         m                  
$$$$ 
$$$$ and recovers single-point forces of constraint {q } = [K  ] {phi }.
$$$$                                                  s      fs      f
$$$$
SDR1     USETD,, PHID,,,GOD,GMD,,KFS,,/CPHIP,,QPC/1/*DYNAMICS* $
****CARD   1-  6,  8- 11, 14, 22- 24, 52, 56- 62
****FILE 113
****RFMT 187-192,194-204,207-209
$$$$
LABEL    LBL17 $
****CARD   1-  6,  8- 11, 14, 22- 24, 52, 56- 62
****FILE 113
$$$$ SDR2 calculates element forces (OEFC1) and stresses (OESC1) and prepares 
$$$$ eigenvectors (OCPHIP) and single-point forces of constraint (OQPC1) for 
$$$$ output.
SDR2     CASEXX,CSTM,MPT,DIT,EQDYN,SILD,,,,CLAMA,QPC,CPHIP,EST,,,/  
         ,OQPC1,OCPHIP,OESC1,OEFC1,,,/*CEIG* $  
****CARD  19
****FILE 114
$$$$ OFP formats the tables prepared by SDR2 and places them on the system 
$$$$ output file for printing.
OFP      OCPHIP,OQPC1,OEFC1,OESC1,,//S,N,CARDNO $   
****CARD  19
****FILE 114
$$$$
LABEL    LBL16 $
****CARD   1-  6,  8- 11, 14, 19, 21- 24, 52, 56- 62
****FILE 112-114
$$$$ Go to label FINIS if no additional sets of direct input matrices need to 
$$$$ be processed.
COND     FINIS,REPEATE $
****SBST   1,  3
****CARD  22, 23, 52, 56- 62
****RFMT 187-192,194-204,207-209
$$$$ Go to label LBL13 if additional sets of direct input matrices need to be 
$$$$ processed.
REPT     LBL13,100 $
****SBST   1,  3
****CARD  22, 23, 52, 56- 62
****RFMT 187-192,194-204,207-209
$$$$ Print Error Message No. 2 and terminate execution.
PRTPARM  //-2/*DIRCEAD* $   
****SBST   1,  3
****CARD  22, 23, 52, 56- 62
****RFMT 187-192,194-204,207-209
$$$$ Go to label FINIS and make normal exit.
JUMP     FINIS $
****CARD   1-  6,  8- 24, 56- 62
****RFMT 187-192,194-204,207-209
$$$$
LABEL    ERROR1 $   
****CARD   1,  9- 11, 57, 61
****RFMT 187-192,194-204,207-209
$$$$ Print Error Message No. 1 and terminate execution.
PRTPARM  //-1/*DIRCEAD* $   
****CARD   1,  9- 11, 57, 61
****RFMT 187-192,194-204,207-209
$$$$
LABEL    ERROR3 $   
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 128
****RFMT 187-192,194-204,207-209
$$$$ Print Error Message No. 3 and terminate execution.
PRTPARM  //-3/*DIRCEAD* $   
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 128
****RFMT 187-192,194-204,207-209
$$$$
LABEL    FINIS $
****CARD   1-  6,  8- 24, 52, 56- 62
****RFMT 187-192,194-204,207-209
$$$$
PURGE    DUMMY/MINUS1 $ 
****CARD   1-  6,  8- 24, 52, 56- 62
****RFMT 187-192,194-204,207-209
$$$$
END      $  
****CARD   1-  6,  8- 16, 18- 24, 52, 56- 62
****RFMT 187-192,194-204,207-209
$$$$
$*CARD BITS 
  1    AXIC     AXIF     CDAMP1   CDAMP2   CDAMP3   CDAMP4   CELAS1 
  1    CELAS2   
  1    CELAS3   CELAS4   CMASS1   CMASS2   CMASS3   CMASS4   CORD1C 
  1    CORD1R   
  1    CORD1S   CORD2C   CORD2R   CORD2S   FREEPT   GRDSET   GRID   
  1    GRIDB
  1    POINTAX  PREPT    RINGAX   RINGFL   SECTAX   SEQGP    SPOINT 
  2    ADUM1    ADUM2    ADUM3    ADUM4    ADUM5    ADUM6    ADUM7  
  2    ADUM8
  2    ADUM9    BAROR    CBAR     CCONEAX  CDUM1    CDUM2    CDUM3  
  2    CDUM4
  2    CDUM5    CDUM6    CDUM7    CDUM8    CDUM9    CELBOW   CFLUID2
  2    CFLUID3  
  2    CFLUID4  CHEXA1   CHEXA2   CIHEX1   CIHEX2   CIHEX3   CIS2D8 
  2    CONROD   
  2    CQDMEM   CQDMEM1  CQDMEM2  CQDPLT   CQUAD1   CQUAD2   CROD   
  2    CSHEAR   CTETRA   CTORDRG  CTRAPAX  CTRAPRG  CTRBSC   CTRIA1 
  2    CTRIA2   CQUAD4   CTRIA3
  2    CTRIAAX  CTRIARG  CTRIM6   CTRMEM   CTRPLT   CTRPLT1  CTRSHL 
  2    CTUBE    CTWIST   CWEDGE 
  3    PBAR     PCONEAX  PDUM1    PDUM2    PDUM3    PDUM4    PDUM5  
  3    PDUM6
  3    PDUM7    PDUM8    PDUM9    PELBOW   PIHEX    PIS2D8   PQDMEM 
  3    PQDMEM1  PQDMEM2  PQDPLT   PQUAD1   PQUAD2   PROD     PSHEAR 
  3    PTORDRG  PSHELL   PCOMP    PCOMP1   PCOMP2 
  3    PTRAPAX  PTRBSC   PTRIA1   PTRIA2   PTRIAAX  PTRIM6   PTRMEM 
  3    PTRPLT   PTRPLT1  PTRSHL   PTUBE    PTWIST   
  4    GENEL
  5    CONM1    CONM2    PMASS    FSLIST
  6    PELAS
  8    MAT1     MAT2     MAT3     MATT1    MATT2    MATT3    MAT8
  8    TABLEM1  TABLEM2  TABLEM3  TABLEM4  TEMPMT$  TEMPMX$  
  9    AXISYM$  CRIGD1   CRIGD2   CRIGD3   CRIGDR   CRROD    CRBAR  
  9    CRTRPLT  CRBE1    CRBE2    CRBE3    CRSPLINE MPC      MPCADD 
  9    MPCAX    MPC$
 10    SPC      SPC1     SPCADD   SPCAX    SPC$ 
 11    ASET     ASET1    OMIT     OMIT1    OMITAX   SUPAX    SUPORT 
 13    TEMP     TEMPAX   TEMPD    TEMPP1   TEMPP2   TEMPP3   TEMPRB 
 14    WTMASS   
 15    GRDPNT   
 16    PLOTEL   
 17    ASETOUT  
 18    PLOT$
 19    POUT$
 20    AUTOSPC  
 21    AOUT$
 22    LOOP$
 23    LOOP1$   
 24    COUPMASS CPBAR    CPQDPLT  CPQUAD1  CPQUAD2  CPROD    CPTRBSC
 24    CPTRIA1  CPTRIA2  CPTRPLT  CPTUBE
 25    NOLOOP$  
 52    BDYLIST  FLSYM   
 56    G
 57    EPOINT   SEQEP    TF 
 58    CVISC
 59    PDAMP    PVISC   
 60    DMIAX    DMIG     B2PP$    K2PP$    M2PP$    TF$ 
 61    EIGC     EIGP
 62    CMETHOD$ 
$$$$
$*FILE BITS 
 94    BGPDT    CSTM     EQEXIN   GPDT     GPL      SIL 
 95    ECT  
 96    GPTT 
 97    EST      GEI      GPECT    MPTX     PCOMPS   EPTX
 98    KGGX 
 99    MGG  
100    KGG  
101    ASET     RG       USET     OGPST 
102    GPST 
103    GM   
104    BNN      K4NN     KNN      MNN   
105    BFF      K4FF     KFF      KFS      MFF  
106    GO       KOO      LOO      KAA   
107    EED      EQDYN    GPLD     SILD     TFPOOL   USETD   
108    CASEXX   
109    B2PP     K2DPP    M2DPP  
110    B2DD     BDD      GMD      GOD      K2DD     KDD      M2DD   
110    MDD  
111    CLAMA    OCEIGS   PHID   
112    OPHID
113    CPHIP    QPC 
114    OCPHIP   OEFC1    OESC1    OQPC1 
118    BDPOOL   
119    ABFL     KBFL
120    ELSETS   GPSETS   PLTPAR   PLTSETX   
121    MAA  
122    BAA  
123    K4AA 
124    BDICT    BELM     KDICT    KELM     MDICT    MELM
125    BGG  
126    K4GG 
127    PLOTX1   
128    OGPWG
129    K2PP 
130    M2PP 
131    BGPDP    SIP 
$*  
=PAGE=
DISP8

APR.93
$$$$$$$$
BEGIN    DISP 08 - DIRECT FREQUENCY/RANDOM RESPONSE ANALYSIS-APR. 1993 $
****CARD   1-  6,  8- 16, 18- 29, 52, 54- 62
****RFMT 187-193,195-204,207-209
$$$$
PRECHK   ALL $  
****CARD   1-  6,  8- 16, 18- 29, 52, 54- 62
****RFMT 187-193,195-204,207-209
$$$$
FILE     KGGX=TAPE/KGG=TAPE/GOD=SAVE/GMD=SAVE/MDD=SAVE/BDD=SAVE $   
****CARD   1-  6,  8- 16, 18- 29, 52, 54- 62
****RFMT 187-193,195-204,207-209
$$$$
PARAM    //*MPY*/CARDNO/0/0 $   
****CARD   1-  6,  8- 10, 14, 15, 19, 21, 24, 29
****FILE 101,113,115,116,128
****PHS1   I1   
$$$$ GP1 generates coordinate system transformation matrices, tables of grid 
$$$$ point locations, and tables relating the internal and external grid point 
$$$$ numbers.
GP1      GEOM1,GEOM2,/GPL,EQEXIN,GPDT,CSTM,BGPDT,SIL/S,N,LUSET/ 
         S,N,NOGPDT/ALWAYS=-1 $ 
****CARD   1
****FILE  94
****PHS2   D8   
$$$$ PLTTRAN modifies special scalar grid points in the BGPDT and SIL tables.
PLTTRAN  BGPDT,SIL/BGPDP,SIP/LUSET/S,N,LUSEP $  
****CARD   1
****FILE 135
$$$$
PURGE    USET,GM,GO,KAA,BAA,MAA,K4AA,KFS,PSF,QPC,EST,ECT,PLTSETX,PLTPAR,
         GPSETS,ELSETS/NOGPDT $ 
****CARD   1
****FILE  95, 97,101,103,105,106,111,114,120,122,123
$$$$ Go to label LBL5 if there is only Direct Matrix Input.
COND     LBL5,NOGPDT $  
****CARD   1-  6,  8- 12, 14- 16, 18, 24, 28, 29, 58, 59, 61
****FILE  95-106,120-128
$$$$ GP2 generates Element Connection Table with internal indices.
GP2      GEOM2,EQEXIN/ECT $ 
****CARD   1,  2,  4,  5, 16, 58
****FILE  95
$$$$
PARAML   PCDB//*PRES*////JUMPPLOT $ 
****SBST   7
****CARD  18
****FILE 120,127
****PHS2   DB8  
$$$$
PURGE    PLTSETX,PLTPAR,GPSETS,ELSETS/JUMPPLOT $
****SBST   7
****CARD  18
****FILE 120
$$$$ Go to label P1 if there are no structure plot requests.
COND     P1,JUMPPLOT $  
****SBST   7
****CARD   1,  2,  4,  5, 16, 18, 58
****FILE 120,127
$$$$ PLTSET transforms user input into a form used to drive the structure 
$$$$ plotter.
PLTSET   PCDB,EQEXIN,ECT,EPT/PLTSETX,PLTPAR,GPSETS,ELSETS/S,N,NSIL/ 
         S,N,JUMPPLOT $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18, 58
****FILE 120
$$$$ PRTMSG prints error messages associated with the structure plotter.
PRTMSG   PLTSETX// $
****SBST   7
****CARD   1,  2,  4,  5, 16, 18, 58
****FILE 120
$$$$
PARAM    //*MPY*/PLTFLG/1/1 $   
****SBST   7
****CARD   1,  2,  4,  5, 16, 18, 58
****FILE 127
$$$$
PARAM    //*MPY*/PFILE/0/0 $
****SBST   7
****CARD   1,  2,  4,  5, 16, 18, 58
****FILE 127
$$$$ Go to label P1 if no undeformed structure plots are requested.
COND     P1,JUMPPLOT $  
****SBST   7
****CARD   1,  2,  4,  5, 16, 18, 58
****FILE 127
$$$$ PLOT generates all requested undeformed structure plots.
PLOT     PLTPAR,GPSETS,ELSETS,CASECC,BGPDT,EQEXIN,SIL,,ECT,,,,/PLOTX1/  
         NSIL/LUSET/S,N,JUMPPLOT/S,N,PLTFLG/S,N,PFILE $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18, 58
****FILE 127
$$$$ PRTMSG prints plotter data and engineering data for each undeformed plot 
$$$$ generated.
PRTMSG   PLOTX1//$  
****SBST   7
****CARD   1,  2,  4,  5, 16, 18, 58
****FILE 127
$$$$
LABEL    P1 $   
****SBST   7
****CARD   1,  2,  4,  5, 16, 18, 58
****FILE 120,127
****PHS2   DE8  
$$$$ GP3 generates Grid Point Temperature Table.
GP3      GEOM3,EQEXIN,GEOM2/,GPTT/NOGRAV $  
****CARD   1,  2, 13
****FILE  96
$$$$ TA1 generates element tables for use in matrix assembly and stress 
$$$$ recovery.
TA1      ECT,EPT,BGPDT,SIL,GPTT,CSTM,MPT,EQEXIN/EST,GEI,GPECT,,,MPTX,
         PCOMPS,EPTX/LUSET/S,N,NOSIMP/1/S,N,NOGENL/GENEL/S,N,COMPS $   
****CARD   1-  6, 13, 16, 58, 59
****FILE  97
$$$$
EQUIV    MPTX,MPT/COMPS/EPTX,EPT/COMPS $
****CARD   1-  6,  8- 16, 18- 29, 52, 54- 62
****FILE  97
$$$$
PURGE    K4GG,MGG,BGG,K4NN,K4FF,K4AA,MNN,MFF,MAA,BNN,BFF,BAA,   
         KGGX/NOSIMP $  
****CARD   1,  2,  4-  6, 16, 58, 59
****FILE  98, 99,104-106,121-123,125
****PHS2   DB8  
$$$$ Go to label LBL1 if there are no structural elements.
COND     LBL1,NOSIMP $  
****CARD   1-  3,  5,  6,  8, 13- 15, 24, 58, 59, 61
****FILE  98, 99,124-126,128
****PHS2   DE8  
$$$$
PARAM    //*ADD*/NOKGGX/1/0 $   
****CARD   1-  3,  6,  8
****FILE 124
$$$$
PARAM    //*ADD*/NOMGG/1/0 $
****CARD   1-  3,  5,  8, 14, 24
****FILE 124
****RFMT 187,190-192
$$$$
PARAM    //*ADD*/NOBGG=-1/1/0 $ 
****CARD   1-  3,  8
****FILE 124
****RFMT 187,190-192
$$$$
PARAM    //*ADD*/NOK4GG/1/0 $   
****CARD   1-  3,  6,  8
****FILE 124
$$$$ EMG generates structural element stiffness, mass and damping matrix 
$$$$ tables, and dictionaries for later assembly by the EMA module.
EMG      EST,CSTM,MPT,DIT,GEOM2,/KELM,KDICT,MELM,MDICT,BELM,BDICT,/ 
         S,N,NOKGGX/S,N,NOMGG/S,N,NOBGG/S,N,NOK4GG//C,Y,COUPMASS/   
         C,Y,CPBAR/C,Y,CPROD/C,Y,CPQUAD1/C,Y,CPQUAD2/C,Y,CPTRIA1/   
         C,Y,CPTRIA2/C,Y,CPTUBE/C,Y,CPQDPLT/C,Y,CPTRPLT/C,Y,CPTRBSC/
         C,Y,VOLUME/C,Y,SURFACE $   
****CARD   1-  3,  5,  6,  8, 13, 24, 61
****FILE 124
****RFMT 187,190-192
$$$$
PURGE    KGGX/NOKGGX/MGG/NOMGG $
****CARD   1-  3,  5,  6,  8, 14, 24
****FILE  98, 99
****RFMT 187,190-192
$$$$ Go to label LBLKGGX if no stiffness matrix is to be assembled.
COND     LBLKGGX,NOKGGX $   
****CARD   1-  3,  6,  8
****FILE  98
$$$$                                  x
$$$$ EMA assembles stiffness matrix [K  ] and Grid Point Singularity Table.
$$$$                                  gg
$$$$
EMA      GPECT,KDICT,KELM/KGGX $
****CARD   1-  3,  6,  8
****FILE  98
$$$$
LABEL    LBLKGGX $  
****CARD   1-  3,  6,  8
****FILE  98
$$$$ Go to label LBLMGG if no mass matrix is to be assembled.
COND     LBLMGG,NOMGG $ 
****CARD   1-  3,  5,  8, 14, 24
****FILE  99
****RFMT 187,190-192
$$$$ EMA assembles mass matrix [M  ].
$$$$                             gg
$$$$
EMA      GPECT,MDICT,MELM/MGG/-1/C,Y,WTMASS=1.0 $   
****CARD   1-  3,  5,  8, 14, 24
****FILE  99
****RFMT 187,190-192
$$$$
PURGE    MDICT,MELM/ALWAYS $
****CARD   1-  3,  5,  8, 14, 24
****FILE 124
$$$$
LABEL    LBLMGG $   
****CARD   1-  3,  5,  8, 14, 24
****FILE  99
****RFMT 187,190-192
$$$$ Go to label LBLBGG if no viscous damping matrix is to be assembled.
COND     LBLBGG,NOBGG $ 
****CARD   1-  3,  8, 58, 59
****FILE 125
****RFMT 187,190-192
$$$$ EMA assembles viscous damping matrix [B  ].
$$$$                                        gg
$$$$
EMA      GPECT,BDICT,BELM/BGG $ 
****CARD   1-  3,  8, 58, 59
****FILE 125
****RFMT 187,190-192
$$$$
PURGE    BDICT,BELM/ALWAYS $
****CARD   1-  3,  8, 58, 59
****FILE 124
$$$$
LABEL    LBLBGG $   
****CARD   1-  3,  8, 58, 59
****FILE 125
****RFMT 187,190-192
$$$$ Go to label LBLK4GG if no structural damping matrix is to be assembled.
COND     LBLK4GG,NOK4GG $   
****CARD   1-  3,  8
****FILE 126
$$$$                                           4
$$$$ EMA assembles structural damping matrix [K  ].
$$$$                                           gg
$$$$
EMA      GPECT,KDICT,KELM/K4GG/NOK4GG $ 
****CARD   1-  3,  6,  8
****FILE 126
$$$$
LABEL    LBLK4GG $  
****CARD   1-  3,  6,  8
****FILE 126
$$$$
PURGE    KDICT,KELM/ALWAYS $
****CARD   1-  3,  6,  8
****FILE 124
$$$$
PURGE    MNN,MFF,MAA/NOMGG $
****CARD   1-  3,  5,  8, 14, 24
****FILE 104,105,121
****RFMT 187,190-192
****PHS2   DB8  
$$$$
PURGE    BNN,BFF,BAA/NOBGG $
****CARD   1-  3,  8, 58, 59
****FILE 104,105,122
****RFMT 187,190-192
$$$$ Go to label LBL1 if no weight and balance information is requested.
COND     LBL1,GRDPNT $  
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 128
$$$$ Go to label ERROR4 and print Error Message No. 4 if no mass matrix 
$$$$ exists.
COND     ERROR4,NOMGG $ 
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 128
$$$$ GPWG generates weight and balance information.
GPWG     BGPDP,CSTM,EQEXIN,MGG/OGPWG/V,Y,GRDPNT=-1/C,Y,WTMASS $ 
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 128
$$$$ OFP formats the weight and balance information prepared by GPWG and 
$$$$ places it on the system output file for printing.
OFP      OGPWG,,,,,//S,N,CARDNO $   
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 128
$$$$
LABEL    LBL1 $ 
****CARD   1-  3,  5,  8, 13- 15, 24, 58, 59, 61
****FILE  98, 99,124-126,128
$$$$               x
$$$$ Equivalence [K  ] to [K  ] if there are no general elements.
$$$$               gg       gg
$$$$
EQUIV    KGGX,KGG/NOGENL $  
****CARD   1-  4,  6,  8
****FILE 100
$$$$ Go to label LBL11 if there are no general elements.
COND     LBL11,NOGENL $ 
****CARD   1-  4,  6,  8
****FILE 100
$$$$                                 x
$$$$ SMA3 adds general elements to [K  ] to obtain stiffness matrix [K  ].
$$$$                                 gg                               gg
$$$$
SMA3     GEI,KGGX/KGG/LUSET/NOGENL/NOSIMP $ 
****CARD   1-  4,  6,  8
****FILE 100
$$$$
LABEL    LBL11 $
****CARD   1-  4,  6,  8
****FILE 100
$$$$
GPSTGEN  KGG,SIL/GPST $ 
****CARD   1-  4,  6,  8
****FILE 102
****PHS2   DE8  
$$$$
PARAM    //*MPY*/NSKIP/0/0 $
****CARD   1,  9- 12
****FILE 101
$$$$ GP4 generates flags defining members of various displacement sets (USET) 
$$$$ and forms multipoint constraint equations [R ] {u } = 0.
$$$$                                             g    g
$$$$
GP4      CASECC,GEOM4,EQEXIN,GPDT,BGPDT,CSTM,GPST/RG,,USET, 
         ASET,OGPST/LUSET/S,N,MPCF1/S,N,MPCF2/S,N,SINGLE/S,N,OMIT/  
         S,N,REACT/S,N,NSKIP/S,N,REPEAT/S,N,NOSET/S,N,NOL/S,N,NOA/  
         C,Y,ASETOUT/C,Y,AUTOSPC $  
****CARD   1-  4,  6,  8- 12, 28, 29
****FILE 101
$$$$ OFP formats the table of potential grid point similarities prepared by 
$$$$ GPSP and places it on the system output file for printing.
OFP      OGPST,,,,,//S,N,CARDNO $   
****CARD   1-  4,  6,  8- 10, 29
****FILE 101
$$$$
PURGE    GM,GMD/MPCF1/GO,GOD/OMIT/KFS,PSF,QPC/SINGLE $  
****CARD   1,  9- 12
****FILE 103,105,106,110,111,114
****PHS1   I1   
$$$$ Equivalence [K  ] to [K  ], [M  ] to [M  ], [B  ] to [B  ], and
$$$$               gg       nn     gg       nn     gg       nn
$$$$   4        4
$$$$ [K  ] to [K  ] if no multipoint constraints exist.
$$$$   gg       nn
$$$$
EQUIV    KGG,KNN/MPCF1/MGG,MNN/MPCF1/ BGG,BNN/MPCF1/K4GG,K4NN/MPCF1 $   
****CARD   1-  6,  8,  9, 14, 24, 58, 59
****FILE 104
$$$$ Go to label LBL2 if no multipoint constraints exist.
COND     LBL2,MPCF1 $   
****CARD   1-  6,  8,  9, 14, 24, 58, 59
****FILE 103,104
$$$$ MCE1 partitions multipoint constraint equation [R ] = [R |R ] and solves
$$$$                                                  g      m  n
$$$$                                                             -1
$$$$ for multipoint constraint transformation matrix [G ] = -[R ]  [R ].
$$$$                                                   m       m     n
$$$$
MCE1     USET,RG/GM $   
****CARD   1,  9
****FILE 103
$$$$ MCE2 partitions stiffness, mass, and damping matrices
$$$$ 
$$$$           +_      +                        +_      +
$$$$           |K  |K  |                        |M  |M  |
$$$$           | nn| nm|                        | nn| nm|
$$$$   [K  ] = |---+---|                [M  ] = |---+---|
$$$$     gg    |K  |K  |                  gg    |M  |M  |
$$$$           | mn| mm|                        | mn| mm|
$$$$           +       +                        +       +
$$$$ 
$$$$           +_      +                        +_4   4 +
$$$$           |B  |B  |                        |K  |K  |
$$$$           | nn| nm|                  4     | nn| nm|
$$$$   [B  ] = |---+---|                [K  ] = |---+---|
$$$$     gg    |B  |B  |                  gg    | 4 | 4 |
$$$$           | mn| mm|                        |K  |K  |
$$$$           +       +                        | mn| mm|
$$$$                                            +       +
$$$$ 
$$$$ and performs the matrix reductions
$$$$ 
$$$$            _        T           T           T
$$$$   [K  ] = [K  ] + [G ][K  ] + [K  ][G ] + [G ][K  ][G ]
$$$$     nn      nm      m   mn      mn   m      m   mm   m
$$$$ 
$$$$            _        T           T           T
$$$$   [M  ] = [M  ] + [G ][M  ] + [M  ][G ] + [G ][M  ][G ]
$$$$     nn      nm      m   mn      mn   m      m   mm   m
$$$$ 
$$$$            _        T           T           T
$$$$   [B  ] = [B  ] + [G ][B  ] + [B  ][G ] + [G ][B  ][G ]
$$$$     nn      nm      m   mn      mn   m      m   mm   m
$$$$ 
$$$$     4      _4       T   4       4  T         T   4
$$$$   [K  ] = [K  ] + [G ][K  ] + [K  ] [G ] + [G ][K  ][G ]
$$$$     nn      nm      m   mn      mn    m      m   mm   m
$$$$
MCE2     USET,GM,KGG,MGG,BGG,K4GG/KNN,MNN,BNN,K4NN $
****CARD   1-  6,  8,  9, 14, 24, 58, 59
****FILE 104
$$$$
LABEL    LBL2 $ 
****CARD   1-  6,  8,  9, 14, 24, 58, 59
****FILE 103,104
$$$$ Equivalence [K  ] to [K  ], [M  ] to [M  ], [B  ] to [B  ], and
$$$$               nn       ff     nn       ff     nn       ff
$$$$   4        4
$$$$ [K  ] to [K  ] if no single-point constraints exist.
$$$$   nn       ff
$$$$
EQUIV    KNN,KFF/SINGLE/MNN,MFF/SINGLE/BNN,BFF/SINGLE/K4NN,K4FF/SINGLE $
****CARD   1-  6,  8- 10, 14, 24, 58, 59
****FILE 105
$$$$ Go to label LBL3 if no single-point constraints exist.
COND     LBL3,SINGLE $  
****CARD   1-  6,  8- 10, 14, 24, 58, 59
****FILE 105
$$$$ SCE1 partitions out single-point constraints
$$$$ 
$$$$           +       +                        +       +
$$$$           |K  |K  |                        |M  |M  |
$$$$           | ff| fs|                        | ff| fs|
$$$$   [K  ] = |---+---|                [M  ] = |---+---|
$$$$     nn    |K  |K  |                  nn    |M  |M  |
$$$$           | sf| ss|                        | sf| ss|
$$$$           +       +                        +       +
$$$$
$$$$                                            +       +
$$$$           +       +                        | 4   4 |
$$$$           |B  |B  |                        |K  |K  |
$$$$           | ff| fs|                  4     | ff| fs|
$$$$   [B  ] = |---+---|                [K  ] = |---+---|
$$$$     nn    |B  |B  |                  nn    | 4 | 4 |
$$$$           | sf| ss|                        |K  |K  |
$$$$           +       +                        | sf| ss|
$$$$                                            +       +
$$$$
SCE1     USET,KNN,MNN,BNN,K4NN/KFF,KFS,,MFF,BFF,K4FF $  
****CARD   1-  6,  8- 10, 14, 24, 58, 59
****FILE 105
$$$$
LABEL    LBL3 $ 
****CARD   1-  6,  8- 10, 14, 24, 58, 59
****FILE 105
$$$$ Equivalence [K  ] to [K  ], [M  ] to [M  ], [B  ] to [B  ], and
$$$$               ff       aa     ff       aa     ff       aa
$$$$      4      4
$$$$ [K  ]  to [K  ] if no omitted coordinates exist.
$$$$   ff        aa
$$$$
EQUIV    KFF,KAA/OMIT $ 
****CARD   1-  4,  6,  8- 11
****FILE 106
$$$$
EQUIV    MFF,MAA/OMIT $ 
****CARD   1-  5,  8- 11, 14, 24
****FILE 121
$$$$
EQUIV    BFF,BAA/OMIT $ 
****CARD   1-  4,  8- 11, 58, 59
****FILE 122
$$$$
EQUIV    K4FF,K4AA/OMIT $   
****CARD   1-  4,  6,  8- 11
****FILE 123
$$$$ Go to label LBL5 if no omitted coordinates exist.
COND     LBL5,OMIT $
****CARD   1-  6,  8- 11, 14, 24, 58, 59
****FILE 106,121-123
$$$$ SMP1 partitions constrained stiffness matrix
$$$$
$$$$           +_      +
$$$$           |K  |K  |
$$$$           | aa| ao|
$$$$   [K  ] = |---+---|
$$$$     ff    |K  |K  |
$$$$           | oa| oo|
$$$$           +       +
$$$$           
$$$$                                               -1
$$$$ solves for transformation matrix [G ] = -[K  ]  [K  ]
$$$$                                    o       oo     oa
$$$$ 
$$$$                                        _
$$$$ and performs matrix reduction [K  ] = [K  ] + [K  ][G ].
$$$$                                 aa      aa      oa   o
$$$$
SMP1     USET,KFF,,,/GO,KAA,KOO,LOO,,,,, $  
****CARD   1-  4,  6,  8- 11
****FILE 106
$$$$ Go to label LBLM if no mass matrix exists.
COND     LBLM,NOMGG $   
****CARD   1-  6,  8- 11, 14, 24
****FILE 121
$$$$ SMP2 partitions constrained mass matrix
$$$$
$$$$           +_      +
$$$$           |M  |M  |
$$$$           | aa| ao|
$$$$   [M  ] = |---+---|
$$$$     ff    |M  |M  |
$$$$           | oa| oo|
$$$$           +       +
$$$$           
$$$$ and performs matrix reduction
$$$$
$$$$            _                         T      T
$$$$   [M  ] = [M  ] + [M  ][G ] + [M  G ]   + [G ][M  ][G ]
$$$$     aa      aa      oa   o      ao o        o   oo   o
$$$$
SMP2     USET,GO,MFF/MAA $  
****CARD   1-  6,  8- 11, 14, 24
****FILE 121
$$$$
LABEL    LBLM $ 
****CARD   1-  6,  8- 11, 14, 24
****FILE 121
$$$$ Go to label LBLB if no viscous damping matrix exists.
COND     LBLB,NOBGG $   
****CARD   1-  4,  6,  8- 11, 58, 59
****FILE 122
$$$$ SMP2 partitions constrained viscous damping matrix
$$$$
$$$$           +_      +
$$$$           |B  |B  |
$$$$           | aa| ao|
$$$$   [B  ] = |---+---|
$$$$     ff    |B  |B  |
$$$$           | oa| oo|
$$$$           +       +
$$$$           
$$$$ and performs matrix reduction
$$$$
$$$$            _                         T      T
$$$$   [B  ] = [B  ] + [B  ][G ] + [B  G ]   + [G ][B  ][G ]
$$$$     aa      aa      oa   o      ao o        o   oo   o
$$$$
SMP2     USET,GO,BFF/BAA $  
****CARD   1-  4,  6,  8- 11, 58, 59
****FILE 122
$$$$
LABEL    LBLB $ 
****CARD   1-  4,  6,  8- 11, 58, 59
****FILE 122
$$$$ Go to label LBL5 if no structural damping matrix exists.
COND     LBL5,NOK4GG $  
****CARD   1-  4,  6,  8- 11
****FILE 123
$$$$ SMP2 partitions constrained structural damping matrix
$$$$
$$$$           +       +
$$$$           |_4   4 |
$$$$           |K  |K  |
$$$$     4     | aa| ao|
$$$$   [K  ] = |---+---|
$$$$     ff    | 4 | 4 |
$$$$           |K  |K  |
$$$$           | oa| oo|
$$$$           +       +
$$$$ 
$$$$ and performs matrix reduction
$$$$
$$$$     4       4       4           4    T      T   4
$$$$   [K  ] = [K  ] + [K  ][G ] + [K  G ]   + [G ][K  ][G ]
$$$$     aa      aa      oa   o      ao o        o   oo   o
$$$$
SMP2     USET,GO,K4FF/K4AA $
****CARD   1-  4,  6,  8- 11
****FILE 123
$$$$
LABEL    LBL5 $ 
****CARD   1-  6,  8- 12, 14- 16, 18, 24, 28, 29, 58, 59, 61
****FILE  95-106,120-128
****PHS3   I1   
$$$$ DPD generates flags defining members of various displacement sets used in 
$$$$ dynamic analysis (USETD), tables relating the internal and external grid 
$$$$ point numbers (GPLD), including extra points introduced for dynamic 
$$$$ analysis (SILD), and prepares Transfer Function Pool (TFPOOL), Dynamics 
$$$$ Load Table (DLT), Power Spectral Density List (PSDL), and Frequency 
$$$$ Response List (FRL).
$$$$
DPD      DYNAMICS,GPL,SIL,USET/GPLD,SILD,USETD,TFPOOL,DLT,PSDL,FRL,,,,  
         EQDYN/LUSET/S,N,LUSETD/NOTFL/S,N,NODLT/S,N,NOPSDL/S,N, 
         NOFRL/NONLFT/NOTRL/NOEED//S,N,NOUE $   
****CARD   1,  9- 11, 55, 57, 61
****FILE 107
****PHS1   DB1  
$$$$                       d                d
$$$$ Equivalence [G ] to [G ] and [G ] to [G ] if there are no extra points
$$$$               o       o        m       m
$$$$ introduced for dynamic analysis.
EQUIV    GO,GOD/NOUE/GM,GMD/NOUE $  
****CARD   1-  6,  8- 11, 14, 22- 24, 52, 56- 60
****FILE 110
$$$$
PARAM    //*ADD*/NEVER/1/0 $
****CARD  19, 21- 23
****PHS3   DB7  
$$$$
PARAM    //*MPY*/REPEATF/-1/1 $ 
****CARD   1-  6,  8- 14, 16, 19- 23, 27, 52, 54- 62
****FILE 108
****RFMT 187-193,195-204,207-209
$$$$ BMG generates DMIG card images describing the interconnection of the 
$$$$ fluid and the structure.
BMG      MATPOOL,BGPDT,EQEXIN,CSTM/BDPOOL/S,N,NOKBFL/S,N,NOABFL/
         S,N,MFACT $
****CARD   1, 52
****FILE 118
$$$$
PARAM    //*AND*/NOFL/NOABFL/NOKBFL $   
****CARD   1, 22, 23, 52, 57, 60
****FILE 109,119
$$$$
PURGE    KBFL/NOKBFL/ ABFL/NOABFL $ 
****CARD   1, 52
****FILE 119
$$$$ Go to label LBL13 if no fluid structure interface is defined.
COND     LBL13,NOFL $   
****CARD   1, 52
****FILE 119
$$$$ MTRXIN generates fluid boundary matrices [A    ] and [K    ]. The matrix
$$$$                                            b,fl        b,fl
$$$$ [K    ] is generated only for a nonzero gravity in the fluid.
$$$$   b,fl
$$$$
MTRXIN,  ,BDPOOL,EQDYN,,/ABFL,KBFL,/LUSETD/S,N,NOABFL/S,N,NOKBFL/   
         0 $
****CARD   1, 52
****FILE 119
$$$$ Beginning of loop for additional sets of direct input matrices.
LABEL    LBL13 $
****CARD   1-  6,  8- 16, 18- 23, 52, 56- 62
****RFMT 187-193,195-204,207-209
$$$$
PURGE    OUDVC1,OUDVC2,XYPLTFA,OPPC1,OQPC1,OUPVC1,OESC1,OEFC1,OPPC2,
         OQPC2,OUPVC2,OESC2,OEFC2,XYPLTF,PSDF,AUTO,XYPLTR,  
         K2PP,M2PP,B2PP,K2DD,M2DD,B2DD/NEVER $  
****CARD  19- 23, 27
****FILE 109,110,115-117,129-133
$$$$ CASE extracts the appropriate record from CASECC corresponding to the 
$$$$ current loop and copies it into CASEXX.
CASE     CASECC,PSDL/CASEXX/*FREQ*/S,N,REPEATF/S,N,NOLOOP $ 
****CARD   1-  6,  8- 14, 16, 19- 23, 25, 27, 52, 54- 62
****FILE 108
****RFMT 187-193,195-204,207-209
$$$$                                            2d     2d         2
$$$$ MTRXIN selects the direct input matrices [K  ], [M  ], and [B  ] for the
$$$$                                            pp     pp         pp
$$$$ current loop.
MTRXIN   CASEXX,MATPOOL,EQDYN,,TFPOOL/K2DPP,M2DPP,B2PP/LUSETD/S,N,  
         NOK2DPP/S,N,NOM2DPP/S,N,NOB2PP $   
****CARD   1, 22, 23, 52, 57, 60
****FILE 109
$$$$
PARAM    //*AND*/NOM2PP/NOABFL/NOM2DPP $
****CARD   1, 22, 23, 52, 57, 60
****FILE 109
$$$$
PARAM    //*AND*/NOK2PP/NOFL  /NOK2DPP $
****CARD   1, 22, 23, 52, 57, 60
****FILE 109
$$$$               2        2d
$$$$ Equivalence [K  ] to [K  ] if no fluid-structure interface is defined
$$$$               pp       pp
$$$$                   2        2d
$$$$ and equivalence [M  ] to [M  ] if there is no [A    ].
$$$$                   pp       pp                   b,fl
$$$$
EQUIV    K2DPP,K2PP/NOFL/M2DPP,M2PP/NOABFL $
****CARD   1, 22, 23, 52, 57, 60
****FILE 129,130
$$$$ Go to label LBLFL2 if no fluid-structure interface is defined.
COND     LBLFL2,NOFL $  
****CARD   1, 22, 23, 52, 57, 60
****FILE 129,130
$$$$                         2d
$$$$ ADD5 adds [K    ] and [K  ] and subtracts [A    ] from them to form
$$$$             b,fl        pp                  b,fl
$$$$   2
$$$$ [K  ].
$$$$   pp
$$$$
ADD5     ABFL,KBFL,K2DPP,,/K2PP/(-1.0,0.0) $
****CARD   1, 22, 23, 52, 57, 60
****FILE 129
$$$$ Go to label LBLFL2 if there is no [A    ].
$$$$                                     b,fl 
$$$$
COND     LBLFL2,NOABFL $
****CARD   1, 22, 23, 52, 57, 60
****FILE 130
$$$$                                    T
$$$$ Transpose [A    ] to obtain [A    ] .
$$$$             b,fl              b,fl
$$$$
TRNSP    ABFL/ABFLT $   
****CARD   1, 22, 23, 52, 57, 60
****FILE 130
$$$$                              2                  T     2d
$$$$ ADD assembles input matrix [M  ] = MFACT [A    ]  + [M  ].
$$$$                              pp            b,fl       pp
$$$$
ADD      ABFLT,M2DPP/M2PP/MFACT/(1.0,0.0) $ 
****CARD   1, 22, 23, 52, 57, 60
****FILE 130
$$$$
LABEL    LBLFL2 $   
****CARD   1, 22, 23, 52, 57, 60
****FILE 129,130
$$$$
PARAM    //*AND*/BDEBA/NOUE/NOB2PP $
****CARD   1, 22, 23, 52, 57, 60
****FILE 130
$$$$
PARAM    //*AND*/KDEK2/NOGENL/NOSIMP $  
****CARD   1, 22, 23, 52, 57, 60
****FILE 130
****PHS2   DB8  
$$$$
PARAM    //*AND*/MDEMA/NOUE/NOM2PP $
****CARD   1, 22, 23, 52, 57, 60
****FILE 130
****PHS2   DE8  
$$$$
PURGE    K2DD/NOK2PP/M2DD/NOM2PP/B2DD/NOB2PP $  
****CARD   1-  6,  8- 11, 14, 22- 24, 52, 56- 60
****FILE 110
$$$$               2        2      2        2          2        2   
$$$$ Equivalence [M  ] to [M  ], [B  ] to [B  ], and [K  ] to [K  ] if no
$$$$               pp       dd     pp       dd         pp       dd  
$$$$ constraints are applied, [M  ] to [M  ] if there are no direct input
$$$$                            aa       dd
$$$$ mass matrices and no extra points, and [B  ] to [B  ] if there are no
$$$$                                          aa       dd
$$$$ direct input damping matrices and no extra points.
EQUIV    M2PP,M2DD/NOA/B2PP,B2DD/NOA/K2PP,K2DD/NOA/ 
         MAA,MDD/MDEMA/BAA,BDD/BDEBA $  
****CARD   1-  6,  8- 11, 14, 22- 24, 52, 56- 60
****FILE 110
$$$$ Go to label LBL18 if only extra points are defined.
COND     LBL18,NOGPDT $ 
****CARD   1-  6,  8- 11, 14, 22- 24, 52, 56- 60
****FILE 110
****RFMT 195
$$$$ GKAD assembles stiffness, mass, and damping matrices for use in Direct 
$$$$ Frequency Response.
$$$$
$$$$                     1       2        4
$$$$   [K  ] = (1 + ig)[K  ] + [K  ] + i[K  ]
$$$$     dd              dd      dd       dd
$$$$
$$$$             1       2
$$$$   [M  ] = [M  ] + [M  ]
$$$$     dd      dd      dd
$$$$
$$$$             1       2
$$$$   [B  ] = [B  ] + [B  ]
$$$$     dd      dd      dd
$$$$
$$$$ Direct input matrices may be complex.
GKAD     USETD,GM,GO,KAA,BAA,MAA,K4AA,K2PP,M2PP,B2PP/KDD,BDD,MDD,GMD,   
         GOD,K2DD,M2DD,B2DD/*FREQRESP*/*DISP*/*DIRECT*/C,Y,G=0.0/   
         0.0/0.0/NOK2PP/NOM2PP/NOB2PP/  
         MPCF1/SINGLE/OMIT/NOUE/NOK4GG/NOBGG/   
         KDEK2/-1 $ 
****CARD   1-  6,  8- 11, 14, 22- 24, 52, 56- 60
****FILE 110
****RFMT 195
$$$$
LABEL    LBL18 $
****CARD   1-  6,  8- 11, 14, 22- 24, 52, 56- 60
****FILE 110
****RFMT 195
$$$$               2
$$$$ Equivalence [K  ] to [K  ] if all stiffness is Direct Matrix Input,
$$$$               dd       dd
$$$$   2
$$$$ [M  ] to [M  ] if all mass is Direct Matrix Input, and
$$$$   dd       dd
$$$$   2
$$$$ [B  ] to [B  ] if all damping is Direct Matrix Input.
$$$$   dd       dd
$$$$
EQUIV    B2DD,BDD/NOBGG/ M2DD,MDD/NOSIMP/ K2DD,KDD/KDEK2 $  
****CARD   1-  6,  8- 11, 14, 22- 24, 52, 56- 60
****FILE 110
****PHS2   D8   
$$$$ Go to label ERROR1 and print Error Message No. 1 if there is no Frequency 
$$$$ Response List.
COND     ERROR1,NOFRL $ 
****CARD   1-  6,  8- 11, 14, 22- 24, 52, 56- 62
****FILE 111
****RFMT 187-193,195-204,207-209
$$$$ Go to label ERROR2 and print Error Message No. 2 if there is no Dynamics 
$$$$ Load Table.
COND     ERROR2,NODLT $ 
****CARD   1-  6,  8- 11, 14, 22- 24, 52, 56- 62
****FILE 111
****RFMT 187-193,195-204,207-209
****PHS1   DE1  
$$$$ FRRD forms the dynamic load vectors {P } and solves for the displacements
$$$$                                       d
$$$$ using the following equation:
$$$$ 
$$$$             2
$$$$   [-M  omega  + iB  omega + K  ] {u } = {P }
$$$$      dd           dd         dd    d      d
$$$$ 
FRRD     CASEXX,USETD,DLT,FRL,GMD,GOD,KDD,BDD,MDD,,DIT/UDVF,PSF,PDF,PPF/
         *DISP*/*DIRECT*/LUSETD/MPCF1/SINGLE/OMIT/  
         NONCUP/FRQSET $
****CARD   1-  6,  8- 11, 14, 22- 24, 52, 56- 62
****FILE 111
****PHS1   DB1  
$$$$ Equivalence {P } to {P } if no constraints are applied.
$$$$               p       d
$$$$ 
EQUIV    PPF,PDF/NOSET $
****CARD   1-  6,  8- 11, 14, 22- 24, 52, 56- 62
****FILE 111
$$$$ VDR prepares solution set displacements, sorted by frequency, for output.
VDR      CASEXX,EQDYN,USETD,UDVF,PPF,XYCDB,/OUDVC1,/*FREQRESP*/ 
         *DIRECT*/S,N,NOSORT2/S,N,NOD/S,N,NOP/0 $   
****CARD  19- 21, 27
****FILE 112
$$$$ Go to label LBL15 if there is no output request for the solution set.
COND     LBL15,NOD $
****CARD  21, 27
****FILE 113,131
$$$$ Go to label LBL15A if there is no output request for solution set 
$$$$ displacements sorted by point number.
COND     LBL15A,NOSORT2 $   
****CARD  21, 27
****FILE 113
$$$$ SDR3 sorts the solution set displacements by point number.
SDR3     OUDVC1,,,,,/OUDVC2,,,,, $  
****CARD  21, 27
****FILE 113
$$$$ OFP formats the requested solution set displacements, sorted by point 
$$$$ number, prepared by SDR3 and places them on the system output file for 
$$$$ printing.
OFP      OUDVC2,,,,,//S,N,CARDNO $  
****CARD  21
****FILE 113
$$$$ XYTRAN prepares the input for requested X-Y plots of the solution set 
$$$$ displacements vs. frequency.
XYTRAN   XYCDB,OUDVC2,,,,/XYPLTFA/*FREQ*/*DSET*/S,N,PFILE/  
         S,N,CARDNO $   
****SBST   7
****CARD  27
****FILE 131
$$$$ XYPLOT prepares the requested X-Y plots of the solution set vs. 
$$$$ frequency.
XYPLOT   XYPLTFA// $
****SBST   7
****CARD  27
****FILE 131
$$$$ Go to label LBL15.
JUMP     LBL15 $
****CARD  21, 27
****FILE 131
$$$$
LABEL    LBL15A $   
****CARD  21, 27
****FILE 113
$$$$ OFP formats the requested solution set displacements, sorted by 
$$$$ frequency, prepared by VDR and places them on the system output file for 
$$$$ printing.
OFP      OUDVC1,,,,,//S,N,CARDNO $  
****CARD  21
****FILE 113
$$$$
LABEL    LBL15 $
****CARD  21, 27
****FILE 113,131
$$$$ Go to label LBL20 if there is no output request involving dependent 
$$$$ degrees of freedom or forces and stresses.
COND     LBL20,NOP $
****CARD   1-  6,  8- 11, 14, 18- 20, 22- 24, 26, 52, 54- 62
****FILE 114-117,132,133
****RFMT 187-193,195-204,207-209
$$$$ Equivalence {u } to {u } if no constraints are applied.
$$$$               d       p
$$$$
EQUIV    UDVF,UPVC/NOA $
****CARD   1-  6,  8- 11, 14, 22- 24, 52, 56- 62
****FILE 114
****RFMT 187-193,195-204,207-209
****PHS2   DB8  
$$$$ Go to label LBL19 if no constraints are applied.
COND     LBL19,NOA $
****CARD   1-  6,  8- 11, 14, 22- 24, 52, 56- 62
****FILE 114
****RFMT 187-193,195-204,207-209
****PHS3   DE7  
$$$$ SDR1 recovers dependent components of eigenvectors
$$$$ 
$$$$                                 u
$$$$           d                      d
$$$$  {u } = [G ]{u }               {----} = {u  + u }
$$$$    o      o   d                 u         f     e
$$$$                                   o
$$$$
$$$$    u + u      
$$$$     f   e                              d
$$$$   {-------} = {u + u }        {u } = [G ]{u  + u }
$$$$      u          n   e           m      m   f    e
$$$$       s        
$$$$
$$$$    u + u                
$$$$     n   e               
$$$$   {---------} = {u }
$$$$       u           p 
$$$$        m                  
$$$$                                                                 T
$$$$ and recovers single-point forces of constraint {q } = -{P } + [K  ]{u }.
$$$$                                                  s       s      fs   f
$$$$
SDR1     USETD,,UDVF,,,GOD,GMD,PSF,KFS,,/UPVC,,QPC/1/*DYNAMICS* $   
****CARD   1-  6,  8- 11, 14, 22- 24, 52, 56- 62
****FILE 114
****RFMT 187-193,195-204,207-209
$$$$
LABEL    LBL19 $
****CARD   1-  6,  8- 11, 14, 22- 24, 52, 56- 62
****FILE 114
****RFMT 187-193,195-204,207-209
****PHS3   I7   
$$$$ SDR2 calculates element forces (OEFC1) and stresses (OESC1) and 
$$$$ prepares load vectors (OPPC1), displacement vectors (OUPVC1), and single-
$$$$ point forces of constraint (OQPC1) for output and translation components 
$$$$ of the displacement vector (PUGVC1), sorted by frequency.
SDR2     CASEXX,CSTM,MPT,DIT,EQDYN,SILD,,,BGPDP,PPF,QPC,UPVC,EST,XYCDB, 
         PPF,/OPPC1,OQPC1,OUPVC1,OESC1,OEFC1,PUPVC1,,/*FREQRESP*/   
         S,N,NOSORT2 $  
****CARD  19, 20
****FILE 115
$$$$ Go to label LBL17 if there are no output requests sorted by point number 
$$$$ or element number.
COND     LBL17,NOSORT2 $
****CARD  19, 20, 26, 54, 55
****FILE 115-117,132,133
$$$$ SDR3 prepares requested output sorted by point number or element number.
SDR3     OPPC1,OQPC1,OUPVC1,OESC1,OEFC1,/OPPC2,OQPC2,OUPVC2,OESC2,  
         OEFC2, $   
****CARD  19, 20
****FILE 116
$$$$ OFP formats the tables prepared by SDR3 sorted by point number or element 
$$$$ number, and places them on the system output file for printing.
OFP      OPPC2,OQPC2,OUPVC2,OEFC2,OESC2,//S,N,CARDNO $  
****CARD  19
****FILE 116
$$$$ XYTRAN prepares the input for requested X-Y plots.
XYTRAN   XYCDB,OPPC2,OQPC2,OUPVC2,OESC2,OEFC2/XYPLTF/*FREQ*/*PSET*/ 
         S,N,PFILE/S,N,CARDNO $ 
****SBST   7
****CARD  20
****FILE 132
$$$$ XYPLOT prepares the requested X-Y plots of displacements, forces, 
$$$$ stresses, loads, and single-point forces of constraint vs. frequency.
XYPLOT   XYPLTF// $ 
****SBST   7
****CARD  20
****FILE 132
$$$$ Go to label LBL16 if there is no Power Spectral Density List.
COND     LBL16,NOPSDL $ 
****SBST   7
****CARD  20, 54, 55
****FILE 117
****RFMT 187-193,195-204,207-209
$$$$ RANDOM calculates power spectral density functions (PSDF) and 
$$$$ autocorrelation functions (AUTO) using the previously calculated 
$$$$ frequency response.
RANDOM   XYCDB,DIT,PSDL,OUPVC2,OPPC2,OQPC2,OESC2,OEFC2,CASEXX/PSDF,AUTO/
         S,N,NORD $ 
****SBST   7
****CARD  26, 54, 55
****FILE 117
****RFMT 187-193,195-204,207-209
$$$$ Go to label LBL16 if no RANDOM calculations are requested.
COND     LBL16,NORD $   
****SBST   7
****CARD  20, 26, 54, 55
****FILE 133
$$$$ XYTRAN prepares the input for requested X-Y plots of the RANDOM output.
XYTRAN   XYCDB,PSDF,AUTO,,,/XYPLTR/*RAND*/*PSET*/S,N,PFILE/ 
         S,N,CARDNO $   
****SBST   7
****CARD  20
****FILE 133
$$$$ XYPLOT prepares the requested X-Y plots of autocorrelation functions and 
$$$$ power spectral density functions.
XYPLOT   XYPLTR// $ 
****SBST   7
****CARD  20
****FILE 133
$$$$ Go to label LBL16.
JUMP     LBL16 $
****CARD  20
****FILE 133
$$$$
LABEL    LBL17 $
****CARD  19, 20, 26, 54, 55
****FILE 115-117,132,133
$$$$
PURGE    PSDF/NOSORT2 $ 
****CARD  19, 20, 26, 54, 55
****FILE 132
$$$$ OFP formats the frequency response output requests prepared by SDR2, 
$$$$ sorted by frequency, and places them on the system output file for 
$$$$ printing.
OFP      OUPVC1,OPPC1,OQPC1,OEFC1,OESC1,//S,N,CARDNO $  
****CARD  19
****FILE 115
$$$$
LABEL    LBL16 $
****CARD  20, 54, 55
****FILE 114-117,132,133
$$$$
PURGE    PSDF/NOPSDL $  
****CARD  20, 54, 55
****FILE 132
$$$$ Go to label LBL20 if no deformed structure plots are requested.
COND     LBL20,JUMPPLOT $   
****SBST   7
****CARD  18
****FILE 134
$$$$ PLOT prepares all requested deformed structure and contour plots.
PLOT     PLTPAR,GPSETS,ELSETS,CASEXX,BGPDT,EQEXIN,SIP,,PUPVC1,  
         GPECT,OESC1,,/PLOTX2/NSIL/LUSEP/JUMPPLOT/PLTFLG/   
         S,N,PFILE $
****SBST   7
****CARD  18
****FILE 134
$$$$ PRTMSG prints plotter data, engineering data, and contour data for each 
$$$$ deformed plot generated.
PRTMSG   PLOTX2// $ 
****SBST   7
****CARD  18
****FILE 134
****PHS2   DE8  
$$$$
LABEL    LBL20 $
****SBST   7
****CARD   1-  6,  8- 11, 14, 18- 20, 22- 24, 26, 52, 54- 62
****FILE 134
$$$$ Go to label FINIS if no additional sets of direct input matrices need to 
$$$$ be processed.
COND     FINIS,REPEATF $
****SBST   1,  3
****CARD  22, 23
****RFMT 187-193,195-204,207-209
****PHS3   DB7  
$$$$ Go to label LBL13 if additional sets of direct input matrices need to be 
$$$$ processed.
REPT     LBL13,100 $
****SBST   1,  3
****CARD  22, 23
****RFMT 187-193,195-204,207-209
$$$$ Print Error Message No. 3 and terminate execution.
PRTPARM  //-3/*DIRFRRD* $   
****SBST   1,  3
****CARD  22, 23
****FILE 101
****PHS1   DE1  
****PHS3   DE7  
****RFMT 187-193,195-204,207-209
$$$$ Go to label FINIS and make normal exit.
JUMP     FINIS $
****CARD   1-  6,  8- 16, 18- 29, 52, 56- 62
****RFMT 187-193,195-204,207-209
$$$$
LABEL    ERROR2 $   
****CARD   1-  6,  8- 11, 14, 22- 24, 52, 56- 62
****FILE 101
****RFMT 187-193,195-204,207-209
$$$$ Print Error Message No. 2 and terminate execution.
PRTPARM  //-2/*DIRFRRD* $   
****CARD   1-  6,  8- 11, 14, 22- 24, 52, 56- 62
****FILE 101
****RFMT 187-193,195-204,207-209
$$$$
LABEL    ERROR1 $   
****CARD   1-  6,  8- 11, 14, 22- 24, 52, 56- 62
****FILE 101
****RFMT 187-193,195-204,207-209
$$$$ Print Error Message No. 1 and terminate execution.
PRTPARM  //-1/*DIRFRRD* $   
****CARD   1-  6,  8- 11, 14, 22- 24, 52, 56- 62
****FILE 101
****RFMT 187-193,195-204,207-209
$$$$
LABEL    ERROR4 $   
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 128
****RFMT 187-193,195-204,207-209
$$$$ Print Error Message No. 4 and terminate execution.
PRTPARM  //-4/*DIRFRRD* $   
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 128
****RFMT 187-193,195-204,207-209
$$$$
LABEL    FINIS $
****CARD   1-  6,  8- 16, 18- 29, 52, 56- 62
****RFMT 187-193,195-204,207-209
$$$$
PURGE    DUMMY/ALWAYS $ 
****CARD   1-  6,  8- 16, 18- 29, 52, 56- 62
****RFMT 187-193,195-204,207-209
$$$$
END      $  
****CARD   1-  6,  8- 16, 18- 29, 52, 56- 62
****RFMT 187-193,195-204,207-209
$$$$
$*CARD BITS 
  1    AXIC     AXIF     CDAMP1   CDAMP2   CDAMP3   CDAMP4   CELAS1 
  1    CELAS2   
  1    CELAS3   CELAS4   CMASS1   CMASS2   CMASS3   CMASS4   CORD1C 
  1    CORD1R   
  1    CORD1S   CORD2C   CORD2R   CORD2S   FREEPT   GRDSET   GRID   
  1    GRIDB
  1    POINTAX  PRESPT   RINGAX   RINGFL   SECTAX   SEQGP    SPOINT 
  2    ADUM1    ADUM2    ADUM3    ADUM4    ADUM5    ADUM6    ADUM7  
  2    ADUM8
  2    ADUM9    BAROR    CBAR     CCONEAX  CDUM1    CDUM2    CDUM3  
  2    CDUM4
  2    CDUM5    CDUM6    CDUM7    CDUM8    CDUM9    CELBOW   CFLUID2
  2    CFLUID3  
  2    CFLUID4  CHEXA1   CHEXA2   CIHEX1   CIHEX2   CIHEX3   CIS2D8 
  2    CONROD   
  2    CQDMEM   CQDMEM1  CQDMEM2  CQDPLT   CQUAD1   CQUAD2   CROD   
  2    CSHEAR   CTETRA   CTORDRG  CTRAPAX  CTRAPRG  CTRBSC   CTRIA1 
  2    CTRIA2   CQUAD4   CTRIA3
  2    CTRIAAX  CTRIARG  CTRIM6   CTRMEM   CTRPLT   CTRPLT1  CTRSHL 
  2    CTUBE    CTWIST   CWEDGE 
  3    PBAR     PCONEAX  PDUM1    PDUM2    PDUM3    PDUM4    PDUM5  
  3    PDUM6
  3    PDUM7    PDUM8    PDUM9    PELBOW   PIHEX    PIS2D8   PQDMEM 
  3    PQDMEM1  PQDMEM2  PQDPLT   PQUAD1   PQUAD2   PROD     PSHEAR 
  3    PTORDRG  PSHELL   PCOMP    PCOMP1   PCOMP2
  3    PTRAPAX  PTRBSC   PTRIA1   PTRIA2   PTRIAAX  PTRIM6   PTRMEM 
  3    PTRPLT   PTRPLT1  PTRSHL   PTUBE    PTWIST   
  4    GENEL
  5    CONM1    CONM2    FSLIST   PMASS 
  6    PELAS
  8    MAT1     MAT2     MAT3     MATT1    MATT2    MATT3    MAT8
  8    TABLEM1  TABLEM2  TABLEM3  TABLEM4  TEMPMT$  TEMPMX$  
  9    AXISYM$  CRIGD1   CRIGD2   CRIGD3   CRIGDR   CRROD    CRBAR  
  9    CRTRPLT  CRBE1    CRBE2    CRBE3    CRSPLINE MPC      MPCADD 
  9    MPCAX    MPC$
 10    SPC      SPC1     SPCADD   SPCAX    SPC$ 
 11    ASET     ASET1    OMIT     OMIT1    OMITAX   SUPAX    SUPORT 
 13    TEMP     TEMPAX   TEMPD    TEMPP1   TEMPP2   TEMPP3   TEMPRB 
 14    WTMASS   
 15    GRDPNT   
 16    PLOTEL   
 18    PLOT$
 19    POUT$
 20    XYOUT$   
 21    AOUT$
 22    LOOP$
 23    LOOP1$   
 24    COUPMASS CPBAR    CPQDPLT  CPQUAD1  CPQUAD2  CPROD    CPTRBSC
 24    CPTRIA1  CPTRIA2  CPTRPLT  CPTUBE
 25    NOLOOP$  
 26    RANDOM$  
 27    AXYOUT$  
 28    ASETOUT  
 29    AUTOSPC  
 52    BDYLIST  FLSYM   
 55    RANDPS   RANDT1   RANDT2 
 54    TABRND1  TABRND2  TABRND3  TABRND4   
 56    G
 57    EPOINT   SEQEP    TF 
 58    CVISC
 59    PDAMP    PVISC   
 60    B2PP$    DMIAX    DMIG     K2PP$    M2PP$    TF$ 
 61    DAREA    DELAY    DLOAD    DPHASE   FREQ     FREQ1    FREQ2  
 61    RLOAD1   RLOAD2   TABLED1  TABLED2  TABLED3  TABLED4 
 62    DECOMOPT DLOAD$   FREQ$  
$$$$
$*FILE BITS 
 94    BGPDT    CSTM     EQEXIN   GPDT     GPL      SIL 
 95    ECT  
 96    GPTT 
 97    EST      GEI      GPECT    MPTX     PCOMPS   EPTX
 98    KGGX 
 99    MGG  
100    KGG  
101    ASET     RG       USET     OGPST 
102    GPST 
103    GM   
104    BNN      K4NN     KNN      MNN   
105    BFF      K4FF     KFF      KFS      MFF  
106    GO       KOO      LOO      KAA   
107    DLT      EQDYN    FRL      GPLD     PSDL     SILD     TFPOOL 
107    USETD
108    CASEXX   
109    B2PP     K2DPP    M2DPP  
110    B2DD     BDD      GMD      GOD      K2DD     KDD      M2DD   
110    MDD  
111    PDF      PPF      PSF      UDVF  
112    OUDVC1   
113    OUDVC2   
114    QPC      UPVC
115    OEFC1    PUPVC1   OESC1    OPPC1    OQPC1    OUPVC1  
116    OEFC2    OESC2    OPPC2    OQPC2    OUPVC2   
117    AUTO     PSDF
118    BDPOOL   
119    ABFL     KBFL
120    ELSETS   GPSETS   PLTPAR   PLTSETX   
121    MAA  
122    BAA  
123    K4AA 
124    BDICT    BELM     KDICT    KELM     MDICT    MELM
125    BGG  
126    K4GG 
127    PLOTX1   
128    OGPWG
129    K2PP 
130    M2PP 
131    XYPLTFA  
132    XYPLTF   
133    XYPLTR   
134    PLOTX2   
135    BGPDP    SIP 
$*  
=PAGE=
DISP9

APR.93
$$$$$$$$
BEGIN    DISP 09 - DIRECT TRANSIENT RESPONSE ANALYSIS - APR. 1993 $ 
****CARD   1-  6,  8- 28, 52, 56- 62
****RFMT 187-194,196-204,207-209
$$$$
PRECHK   ALL $  
****CARD   1-  6,  8- 28, 52, 56- 62
****RFMT 187-194,196-204,207-209
$$$$
FILE     UDVT=APPEND/TOL=APPEND $   
****CARD   1-  6,  8- 28, 52, 56- 62
****RFMT 187-194,196-204,207-209
$$$$
PARAM    //*MPY*/CARDNO/0/0 $   
****CARD   1-  3,  5,  6,  8- 10, 14, 15, 19, 21, 24, 28
****FILE 101,113,116,129
****PHS1  I1
$$$$ GP1 generates coordinate system transformation matrices, tables of grid 
$$$$ point locations, and tables relating the internal and external grid point 
$$$$ numbers.
GP1      GEOM1,GEOM2,/GPL,EQEXIN,GPDT,CSTM,BGPDT,SIL/S,N,LUSET/ 
         S,N,NOGPDT/ALWAYS=-1 $ 
****CARD   1
****FILE  94
****PHS2  D8
$$$$ PLTTRAN modifies special scalar grid points in the BGPDT and SIL tables.
PLTTRAN  BGPDT,SIL/BGPDP,SIP/LUSET/S,N,LUSEP $  
****CARD   1
****FILE 134
$$$$
PURGE    USET,GM,GO,KAA,BAA,MAA,K4AA,PST,KFS,QP,EST,ECT,PLTSETX,PLTPAR, 
         GPSETS,ELSETS/NOGPDT $ 
****CARD   1
****FILE  95, 97,101,103,105,106,111,114,120,122,123
$$$$ Go to label LBL5 if there is only Direct Matrix Input.
COND     LBL5,NOGPDT $  
****CARD   1-  6,  8- 12, 14- 16, 18, 24, 26, 28, 58, 59, 61
****FILE  95-106,120-126,128,129
$$$$ GP2 generates Element Connection Table with internal indices.
GP2      GEOM2,EQEXIN/ECT $ 
****CARD   1,  2,  4,  5, 16, 58
****FILE  95
$$$$
PARAML   PCDB//*PRES*////JUMPPLOT $ 
****SBST   7
****CARD  18
****FILE 120,128
****PHS2  DB8   
$$$$
PURGE    PLTSETX,PLTPAR,GPSETS,ELSETS/JUMPPLOT $
****SBST   7
****CARD  18
****FILE 120
$$$$ Go to label P1 if there are no structure plot requests.
COND     P1,JUMPPLOT $  
****SBST   7
****CARD   1,  2,  4,  5, 16, 18, 58
****FILE 120,128
$$$$ PLTSET transforms user input into a form used to drive the structure 
$$$$ plotter.
PLTSET   PCDB,EQEXIN,ECT,EPT/PLTSETX,PLTPAR,GPSETS,ELSETS/S,N,NSIL/ 
         S,N,JUMPPLOT $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18, 58
****FILE 120
$$$$ PRTMSG prints error messages associated with the structure plotter.
PRTMSG   PLTSETX// $
****SBST   7
****CARD   1,  2,  4,  5, 16, 18, 58
****FILE 120
$$$$
PARAM    //*MPY*/PLTFLG/1/1 $   
****SBST   7
****CARD   1,  2,  4,  5, 16, 18, 58
****FILE 128
$$$$
PARAM    //*MPY*/PFILE/0/0 $
****SBST   7
****CARD   1,  2,  4,  5, 16, 18, 58
****FILE 128
$$$$ Go to label P1 if no undeformed structure plots are requested.
COND     P1,JUMPPLOT $  
****SBST   7
****CARD   1,  2,  4,  5, 16, 18, 58
****FILE 128
$$$$ PLOT generates all requested undeformed structure plots.
PLOT     PLTPAR,GPSETS,ELSETS,CASECC,BGPDT,EQEXIN,SIL,,ECT,,,,/PLOTX1/  
         NSIL/LUSET/S,N,JUMPPLOT/S,N,PLTFLG/S,N,PFILE $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18, 58
****FILE 128
$$$$ PRTMSG prints plotter data and engineering data for each undeformed plot 
$$$$ generated.
PRTMSG   PLOTX1// $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18, 58
****FILE 128
$$$$
LABEL    P1 $   
****SBST   7
****CARD   1,  2,  4,  5, 16, 18, 58
****FILE 120,128
****PHS2  DE8   
$$$$ GP3 generates Grid Point Temperature Table.
GP3      GEOM3,EQEXIN,GEOM2/SLT,GPTT/NOGRAV $   
****CARD   1,  2, 13, 61
****FILE  96
$$$$ TA1 generates element tables for use in matrix assembly and stress 
$$$$ recovery.
TA1      ECT,EPT,BGPDT,SIL,GPTT,CSTM,MPT,EQEXIN/EST,GEI,GPECT,,,MPTX,
         PCOMPS,EPTX/LUSET/S,N,NOSIMP=-1/1/S,N,NOGENL=-1/GENEL/
         S,N,COMPS 
****CARD   1-  6, 13, 16, 58, 59
****FILE  97
$$$$
EQUIV    MPTX,MPT/COMPS/EPTX,EPT/COMPS $
****CARD   1-  6,  8- 28, 52, 56- 62
****FILE  97
$$$$
PURGE    K4GG,MGG,BGG,  
         K4NN,K4FF,K4AA,MNN,MFF,MAA,BNN,BFF,BAA,KGGX/NOSIMP $   
****CARD   1,  2,  4-  6, 58, 59
****FILE  98, 99,104,105,121,122,125,126
****PHS2  DB8   
$$$$ Go to label LBL1 if there are no structural elements.
COND     LBL1,NOSIMP $  
****CARD   1-  3,  5,  6,  8, 13- 15, 24, 58, 59, 61
****FILE  98, 99,124-126,129
****PHS2  DE8   
$$$$
PARAM    //*ADD*/NOKGGX/1/0 $   
****CARD   1-  3,  6,  8
****FILE  98
$$$$
PARAM    //*ADD*/NOMGG/1/0 $
****CARD   1-  3,  5,  8, 14, 24
****FILE  99
****RFMT 187,190-192
$$$$
PARAM    //*ADD*/NOBGG=-1/1/0 $ 
****CARD   1-  3,  8
****FILE 125
****RFMT 187,190-192
$$$$
PARAM    //*ADD*/NOK4GG/1/0 $   
****CARD   1-  3,  6,  8
****FILE 126
$$$$ EMG generates structural element stiffness, mass and damping matrix 
$$$$ tables, and dictionaries for later assembly by the EMA module.
EMG      EST,CSTM,MPT,DIT,GEOM2,/KELM,KDICT,MELM,MDICT,BELM,BDICT,/ 
         S,N,NOKGGX/S,N,NOMGG/S,N,NOBGG/S,N,NOK4GG//C,Y,COUPMASS/   
         C,Y,CPBAR/C,Y,CPROD/C,Y,CPQUAD1/C,Y,CPQUAD2/C,Y,CPTRIA1/   
         C,Y,CPTRIA2/C,Y,CPTUBE/C,Y,CPQDPLT/C,Y,CPTRPLT/C,Y,CPTRBSC/
         C,Y,VOLUME/C,Y,SURFACE $   
****CARD   1-  3,  5,  6,  8, 13, 24, 61
****FILE 124
****RFMT 187,190-192
$$$$
PURGE    KGGX/NOKGGX/MGG/NOMGG $
****CARD   1-  3,  5,  6,  8, 14, 24
****FILE  98, 99
****RFMT 187,190-192
$$$$ Go to label LBLKGGX if no stiffness matrix is to be assembled.
COND     LBLKGGX,NOKGGX $   
****CARD   1-  3,  6,  8
****FILE  98
$$$$                                  x
$$$$ EMA assembles stiffness matrix [K  ] and Grid Point Singularity Table.
$$$$                                  gg
$$$$
EMA      GPECT,KDICT,KELM/KGGX $
****CARD   1-  3,  6,  8
****FILE  98
$$$$
LABEL    LBLKGGX $  
****CARD   1-  3,  6,  8
****FILE  98
$$$$ Go to label LBLMGG if no mass matrix is to be assembled.
COND     LBLMGG,NOMGG $ 
****CARD   1-  3,  5,  8, 14, 24
****FILE  99
****RFMT 187,190-192
$$$$ EMA assembles mass matrix [M  ].
$$$$                             gg
$$$$
EMA      GPECT,MDICT,MELM/MGG/-1/C,Y,WTMASS=1.0 $   
****CARD   1-  3,  5,  8, 14, 24
****FILE  99
****RFMT 187,190-192
$$$$
PURGE    MDICT,MELM/ALWAYS $
****CARD   1-  3,  5,  8, 14, 24
****FILE 124
$$$$
LABEL    LBLMGG $   
****CARD   1-  3,  5,  8, 14, 24
****FILE  99
****RFMT 187,190-192
$$$$ Go to label LBLBGG if no viscous damping matrix is to be assembled.
COND     LBLBGG,NOBGG $ 
****CARD   1-  3,  8, 58, 59
****FILE 125
****RFMT 187,190-192
$$$$ EMA assembles viscous damping matrix [B  ].
$$$$                                        gg
$$$$
EMA      GPECT,BDICT,BELM/BGG $ 
****CARD   1-  3,  8, 58, 59
****FILE 125
****RFMT 187,190-192
$$$$
PURGE    BDICT,BELM/ALWAYS $
****CARD   1-  3,  8, 58, 59
****FILE 124
$$$$
LABEL    LBLBGG $   
****CARD   1-  3,  8, 58, 59
****FILE 125
****RFMT 187,190-192
$$$$ Go to label LBLK4GG if no structural damping matrix is to be assembled.
COND     LBLK4GG,NOK4GG $   
****CARD   1-  3,  8
****FILE 126
$$$$                                           4
$$$$ EMA assembles structural damping matrix [K  ].
$$$$                                           gg
$$$$
EMA      GPECT,KDICT,KELM/K4GG/NOK4GG $ 
****CARD   1-  3,  6,  8
****FILE 126
$$$$
LABEL    LBLK4GG $  
****CARD   1-  3,  6,  8
****FILE 126
$$$$
PURGE    KDICT,KELM/ALWAYS $
****CARD   1-  3,  6,  8
****FILE 124
$$$$
PURGE    MNN,MFF,MAA/NOMGG $
****CARD   1-  3,  5,  8, 14, 24
****FILE 104,105,121
****RFMT 187,190-192
****PHS2  DB8   
$$$$
PURGE    BNN,BFF,BAA/NOBGG $
****CARD   1-  3,  8, 58, 59
****FILE 104,105,122
****RFMT 187,190-192
$$$$ Go to label LBL1 if no weight and balance information is requested.
COND     LBL1,GRDPNT $  
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 129
$$$$ Go to label ERROR3 and print Error Message No. 3 if no mass matrix 
$$$$ exists.
COND     ERROR3,NOMGG $ 
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 129
$$$$ GPWG generates weight and balance information.
GPWG     BGPDP,CSTM,EQEXIN,MGG/OGPWG/V,Y,GRDPNT=-1/C,Y,WTMASS $ 
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 129
$$$$ OFP formats the weight and balance information prepared by GPWG and 
$$$$ places it on the system output file for printing.
OFP      OGPWG,,,,,//S,N,CARDNO $   
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 129
$$$$
LABEL    LBL1  $
****CARD   1-  3,  5, 14, 15, 24, 58, 59, 61
****FILE  98, 99,124-126,129
$$$$               x
$$$$ Equivalence [K  ] to [K  ] if there are no general elements.
$$$$               gg       gg
$$$$
EQUIV    KGGX,KGG/NOGENL $  
****CARD   1-  4,  6,  8
****FILE 100
$$$$ Go to label LBL11 if there are no general elements.
COND     LBL11,NOGENL $ 
****CARD   1-  4,  6,  8
****FILE 100
$$$$                                 x
$$$$ SMA3 adds general elements to [K  ] to obtain stiffness matrix [K  ].
$$$$                                 gg                               gg
$$$$
SMA3     GEI,KGGX/KGG/LUSET/NOGENL/NOSIMP $ 
****CARD   1-  4,  6,  8
****FILE 100
$$$$
LABEL    LBL11 $
****CARD   1-  4,  6,  8
****FILE 100
$$$$
GPSTGEN  KGG,SIL/GPST $ 
****CARD   1-  4,  6,  8
****FILE 102
****PHS2  DE8   
$$$$
PARAM    //*MPY*/NSKIP/0/0 $
****CARD   1,  9- 12
****FILE 101
$$$$ GP4 generates flags defining members of various displacement sets (USET) 
$$$$ and forms multipoint constraint equations [R ] {u } = 0.
$$$$                                             g    g
$$$$
GP4      CASECC,GEOM4,EQEXIN,GPDT,BGPDT,CSTM,GPST/RG,,USET, 
         ASET,OGPST/LUSET/S,N,MPCF1/S,N,MPCF2/S,N,SINGLE/S,N,OMIT/  
         S,N,REACT/S,N,NSKIP/S,N,REPEAT/S,N,NOSET/S,N,NOL/S,N,NOA/  
         C,Y,ASETOUT/C,Y,AUTOSPC $  
****CARD   1-  4,  6,  8- 12, 26, 28
****FILE 101
$$$$ OFP formats the table of potential grid point similarities prepared by 
$$$$ GPSP and places it on the system output file for printing.
OFP      OGPST,,,,,//S,N,CARDNO $   
****CARD   1-  4,  6,  8- 10, 28
****FILE 101
$$$$
PURGE    GM,GMD/MPCF1/GO,GOD/OMIT/KFS,PST,QP/SINGLE $   
****CARD   1,  9- 12
****FILE 103,105,106,110,111,114
****PHS1  I1
$$$$ Equivalence [K  ] to [K  ], [M  ] to [M  ], [B  ] to [B  ], and
$$$$               gg       nn     gg       nn     gg       nn
$$$$   4        4
$$$$ [K  ] to [K  ] if no multipoint constraints exist.
$$$$   gg       nn
$$$$
EQUIV    KGG,KNN/MPCF1/MGG,MNN/MPCF1/ BGG,BNN/MPCF1/K4GG,K4NN/MPCF1 $   
****CARD   1-  6,  8,  9, 14, 24
****FILE 104
$$$$ Go to label LBL2 if no multipoint constraints exist.
COND     LBL2,MPCF1 $   
****CARD   1-  6,  8,  9, 14, 24
****FILE 103,104
$$$$ MCE1 partitions multipoint constraint equation [R ] = [R |R ] and solves
$$$$                                                  g      m  n
$$$$                                                             -1
$$$$ for multipoint constraint transformation matrix [G ] = -[R ]  [R ].
$$$$                                                   m       m     n
$$$$
MCE1     USET,RG/GM $   
****CARD   1,  9
****FILE 103
$$$$ MCE2 partitions stiffness, mass, and damping matrices
$$$$ 
$$$$           +_      +                        +_      +
$$$$           |K  |K  |                        |M  |M  |
$$$$           | nn| nm|                        | nn| nm|
$$$$   [K  ] = |---+---|                [M  ] = |---+---|
$$$$     gg    |K  |K  |                  gg    |M  |M  |
$$$$           | mn| mm|                        | mn| mm|
$$$$           +       +                        +       +
$$$$ 
$$$$           +_      +                        +_4   4 +
$$$$           |B  |B  |                        |K  |K  |
$$$$           | nn| nm|                  4     | nn| nm|
$$$$   [B  ] = |---+---|                [K  ] = |---+---|
$$$$     gg    |B  |B  |                  gg    | 4 | 4 |
$$$$           | mn| mm|                        |K  |K  |
$$$$           +       +                        | mn| mm|
$$$$                                            +       +
$$$$ 
$$$$ and performs the matrix reductions
$$$$ 
$$$$            _        T           T           T
$$$$   [K  ] = [K  ] + [G ][K  ] + [K  ][G ] + [G ][K  ][G ]
$$$$     nn      nm      m   mn      mn   m      m   mm   m
$$$$ 
$$$$            _        T           T           T
$$$$   [M  ] = [M  ] + [G ][M  ] + [M  ][G ] + [G ][M  ][G ]
$$$$     nn      nm      m   mn      mn   m      m   mm   m
$$$$ 
$$$$            _        T           T           T
$$$$   [B  ] = [B  ] + [G ][B  ] + [B  ][G ] + [G ][B  ][G ]
$$$$     nn      nm      m   mn      mn   m      m   mm   m
$$$$ 
$$$$     4      _4       T   4       4  T         T   4
$$$$   [K  ] = [K  ] + [G ][K  ] + [K  ] [G ] + [G ][K  ][G ]
$$$$     nn      nm      m   mn      mn    m      m   mm   m
$$$$
MCE2     USET,GM,KGG,MGG,BGG,K4GG/KNN,MNN,BNN,K4NN  $   
****CARD   1-  6,  8,  9, 14, 24, 58, 59
****FILE 104
$$$$
LABEL    LBL2 $ 
****CARD   1-  6,  8,  9, 14, 24, 58, 59
****FILE 103,104
$$$$ Equivalence [K  ] to [K  ], [M  ] to [M  ], [B  ] to [B  ], and
$$$$               nn       ff     nn       ff     nn       ff
$$$$   4        4
$$$$ [K  ] to [K  ] if no single-point constraints exist.
$$$$   nn       ff
$$$$
EQUIV    KNN,KFF/SINGLE/MNN,MFF/SINGLE/BNN,BFF/SINGLE/K4NN,K4FF/SINGLE $
****CARD   1-  6,  8- 10, 14, 24, 58, 59
****FILE 105
$$$$ Go to label LBL3 if no single-point constraints exist.
COND     LBL3,SINGLE $  
****CARD   1-  6,  8- 10, 14, 24, 58, 59
****FILE 105
$$$$ SCE1 partitions out single-point constraints
$$$$ 
$$$$           +       +                        +       +
$$$$           |K  |K  |                        |M  |M  |
$$$$           | ff| fs|                        | ff| fs|
$$$$   [K  ] = |---+---|                [M  ] = |---+---|
$$$$     nn    |K  |K  |                  nn    |M  |M  |
$$$$           | sf| ss|                        | sf| ss|
$$$$           +       +                        +       +
$$$$
$$$$                                            +       +
$$$$           +       +                        | 4   4 |
$$$$           |B  |B  |                        |K  |K  |
$$$$           | ff| fs|                  4     | ff| fs|
$$$$   [B  ] = |---+---|                [K  ] = |---+---|
$$$$     nn    |B  |B  |                  nn    | 4 | 4 |
$$$$           | sf| ss|                        |K  |K  |
$$$$           +       +                        | sf| ss|
$$$$                                            +       +
$$$$
SCE1     USET,KNN,MNN,BNN,K4NN/KFF,KFS,   ,MFF,BFF,K4FF $   
****CARD   1-  6,  8- 10, 14, 24, 58, 59
****FILE 105
$$$$
LABEL    LBL3 $ 
****CARD   1-  6,  8- 10, 14, 24, 58, 59
****FILE 105
$$$$ Equivalence [K  ] to [K  ], [M  ] to [M  ], [B  ] to [B  ], and
$$$$               ff       aa     ff       aa     ff       aa
$$$$      4      4
$$$$ [K  ]  to [K  ] if no omitted coordinates exist.
$$$$   ff        aa
$$$$
EQUIV    KFF,KAA/OMIT $ 
****CARD   1-  4,  6,  8- 11
****FILE 106
$$$$
EQUIV    MFF,MAA/OMIT $ 
****CARD   1-  5,  8- 11, 14, 24
****FILE 121
$$$$
EQUIV    BFF,BAA/OMIT $ 
****CARD   1-  4,  8- 11, 58, 59
****FILE 122
$$$$
EQUIV    K4FF,K4AA/OMIT $   
****CARD   1-  4,  6,  8- 11
****FILE 123
$$$$ Go to label LBL5 if no omitted coordinates exist.
COND     LBL5,OMIT $
****CARD   1-  6,  8- 11, 14, 24, 58, 59
****FILE 106,121-123
$$$$ SMP1 partitions constrained stiffness matrix
$$$$
$$$$           +_      +
$$$$           |K  |K  |
$$$$           | aa| ao|
$$$$   [K  ] = |---+---|
$$$$     ff    |K  |K  |
$$$$           | oa| oo|
$$$$           +       +
$$$$           
$$$$                                               -1
$$$$ solves for transformation matrix [G ] = -[K  ]  [K  ]
$$$$                                    o       oo     oa
$$$$ 
$$$$                                 1
$$$$ and performs matrix reduction [K  ] = [K  ] + [K  ][G ].
$$$$                                 aa      aa      oa   o
$$$$
SMP1     USET,KFF,,,/GO,KAA,KOO,LOO,,,,, $  
****CARD   1-  4,  6,  8- 11
****FILE 106
$$$$ Go to label LBLM if no mass matrix exists.
COND     LBLM,NOMGG $   
****CARD   1-  6,  8- 11, 14, 24
****FILE 121
$$$$ SMP2 partitions constrained mass matrix
$$$$
$$$$           +       +
$$$$           |M  |M  |
$$$$           | aa| ao|
$$$$   [M  ] = |---+---|
$$$$     ff    |M  |M  |
$$$$           | oa| oo|
$$$$           +       +
$$$$           
$$$$ and performs matrix reduction
$$$$
$$$$     1                                T      T
$$$$   [M  ] = [M  ] + [M  ][G ] + [M  G ]   + [G ][M  ][G ]
$$$$     aa      aa      oa   o      ao o        o   oo   o
$$$$
SMP2     USET,GO,MFF/MAA $  
****CARD   1-  6,  8- 11, 14, 24
****FILE 121
$$$$
LABEL    LBLM $ 
****CARD   1-  6,  8- 11, 14, 24
****FILE 121
$$$$ Go to label LBLB if no viscous damping matrix exists.
COND     LBLB,NOBGG $   
****CARD   1-  4,  6,  8- 11, 58, 59
****FILE 122
$$$$ SMP2 partitions constrained viscous damping matrix
$$$$
$$$$           +       +
$$$$           |B  |B  |
$$$$           | aa| ao|
$$$$   [B  ] = |---+---|
$$$$     ff    |B  |B  |
$$$$           | oa| oo|
$$$$           +       +
$$$$           
$$$$ and performs matrix reduction
$$$$
$$$$     1                                T      T
$$$$   [B  ] = [B  ] + [B  ][G ] + [B  G ]   + [G ][B  ][G ]
$$$$     aa      aa      oa   o      ao o        o   oo   o
$$$$
SMP2     USET,GO,BFF/BAA $  
****CARD   1-  4,  6,  8- 11, 58, 59
****FILE 122
$$$$
LABEL    LBLB $ 
****CARD   1-  4,  6,  8- 11, 58, 59
****FILE 122
$$$$ Go to label LBL5 if no structural damping matrix exists.
COND     LBL5,NOK4GG $  
****CARD   1-  4,  6,  8- 11
****FILE 123
$$$$ SMP2 partitions constrained structural damping matrix
$$$$
$$$$           +       +
$$$$           | 4   4 |
$$$$           |K  |K  |
$$$$     4     | aa| ao|
$$$$   [K  ] = |---+---|
$$$$     ff    | 4 | 4 |
$$$$           |K  |K  |
$$$$           | oa| oo|
$$$$           +       +
$$$$ 
$$$$ and performs matrix reduction
$$$$
$$$$     4       4       4           4    T      T   4
$$$$   [K  ] = [K  ] + [K  ][G ] + [K  G ]   + [G ][K  ][G ]
$$$$     aa      aa      oa   o      ao o        o   oo   o
$$$$
SMP2     USET,GO,K4FF/K4AA $
****CARD   1-  4,  6,  8- 11
****FILE 123
$$$$
LABEL    LBL5 $ 
****CARD   1-  6,  8- 12, 14- 16, 18, 24, 26, 28, 58, 59, 61
****FILE  95-106,120-126,128,129
****PHS3  I1
$$$$ DPD generates flags defining members of various displacement sets used in 
$$$$ dynamic analysis (USETD), tables relating the internal and external grid 
$$$$ point numbers (GPLD), including extra points introduced for dynamic 
$$$$ analysis (SILD), and prepares Transfer Function Pool (TFPOOL), Dynamics 
$$$$ Load Table (DLT), Nonlinear Function Table (NLFT), and Transient Response
$$$$ List (TRL).
DPD      DYNAMICS,GPL,SIL,USET/GPLD,SILD,USETD,TFPOOL,DLT,,,NLFT,TRL,,  
         EQDYN/LUSET/S,N,LUSETD/NOTFL/S,N,NODLT/NOPSDL/ 
         NOFRL/S,N,NONLFT/S,N,NOTRL/NOEED//S,N,NOUE $   
****CARD   1,  9- 11, 57, 61
****FILE 107
****PHS1  DB1   
$$$$ Go to label ERROR1 if no potential grid point singularities exist.
COND     ERROR1,NOTRL $ 
****CARD   1,  9- 11, 57, 61
****FILE 107
$$$$
PURGE    PNLD/NONLFT$   
****CARD   1, 57, 61
****FILE 107
$$$$                       d                d
$$$$ Equivalence [G ] to [G ] and [G ] to [G ] if there are no extra points
$$$$               o       o        m       m
$$$$ introduced for dynamic analysis.
EQUIV    GO,GOD/NOUE/GM,GMD/NOUE  $ 
****CARD   1-  6,  8- 11, 14, 24, 52, 56- 60
****FILE 110
$$$$ BMG generates DMIG card images describing the interconnection of the 
$$$$ fluid and the structure.
BMG      MATPOOL,BGPDT,EQEXIN,CSTM/BDPOOL/S,N,NOKBFL/S,N,NOABFL/
         S,N,MFACT $
****CARD   1, 52
****FILE 118
****PHS3  DB7   
$$$$
PARAM    //*AND*/NOFL/NOABFL/NOKBFL $   
****CARD   1, 52, 57, 60
****FILE 109,119
$$$$
PURGE    KBFL/NOKBFL/ ABFL/NOABFL $ 
****CARD   1, 52
****FILE 119
$$$$ Go to label LBL13 if no fluid structure interface is defined.
COND     LBLFL3,NOFL $  
****CARD   1, 52
****FILE 119
$$$$ MTRXIN generates fluid boundary matrices [A    ] and [K    ]. The matrix
$$$$                                            b,fl        b,fl
$$$$ [K    ] is generated only for a nonzero gravity in the fluid.
$$$$   b,fl
$$$$
MTRXIN,  ,BDPOOL,EQDYN,,/ABFL,KBFL,/LUSETD/S,N,NOABFL/S,N,NOKBFL/   
         0 $
****CARD   1, 52
****FILE 119
$$$$
LABEL    LBLFL3 $   
****CARD   1, 52
****FILE 119
$$$$                                            2d     2d         2
$$$$ MTRXIN selects the direct input matrices [K  ], [M  ], and [B  ].
$$$$                                            pp     pp         pp
$$$$ 
MTRXIN   CASECC,MATPOOL,EQDYN,,TFPOOL/K2DPP,M2DPP,B2PP/LUSETD/S,N,  
         NOK2DPP/S,N,NOM2DPP/S,N,NOB2PP $   
****CARD   1, 52, 57, 60
****FILE 109
$$$$
PARAM    //*AND*/NOM2PP/NOABFL/NOM2DPP $
****CARD   1, 52, 57, 60
****FILE 109
$$$$
PARAM    //*AND*/NOK2PP/NOFL  /NOK2DPP $
****CARD   1, 52, 57, 60
****FILE 109
$$$$               2        2d
$$$$ Equivalence [K  ] to [K  ] if no fluid structure interface is defined
$$$$               pp       pp
$$$$                   2        2d
$$$$ and equivalence [M  ] to [M  ] if there is no [A    ].
$$$$                   pp       pp                   b,fl
$$$$
EQUIV    K2DPP,K2PP/NOFL/M2DPP,M2PP/NOABFL $
****CARD   1, 52, 57, 60
****FILE 130,131
$$$$ Go to label LBLFL2 if no fluid structure interface is defined.
COND     LBLFL2,NOFL $  
****CARD   1, 52, 57, 60
****FILE 130,131
$$$$                         2d
$$$$ ADD5 adds [K    ] and [K  ] and subtracts [A    ] from them to form
$$$$             b,fl        pp                  b,fl
$$$$   2
$$$$ [K  ].
$$$$   pp
$$$$
ADD5     ABFL,KBFL,K2DPP,,/K2PP/(-1.0,0.0) $
****CARD   1, 52, 57, 60
****FILE 130
$$$$ Go to label LBLFL2 if there is no [A    ].
$$$$                                     b,fl 
$$$$
COND     LBLFL2,NOABFL $
****CARD   1, 52, 57, 60
****FILE 131
$$$$                                    T
$$$$ Transpose [A    ] to obtain [A    ] .
$$$$             b,fl              b,fl
$$$$
TRNSP    ABFL/ABFLT $   
****CARD   1, 52, 57, 60
****FILE 131
$$$$                              2                  T     2d
$$$$ ADD assembles input matrix [M  ] = MFACT [A    ]  + [M  ].
$$$$                              pp            b,fl       pp
$$$$
ADD      ABFLT,M2DPP/M2PP/MFACT/(1.0,0.0) $ 
****CARD   1, 52, 57, 60
****FILE 131
$$$$
LABEL    LBLFL2 $   
****CARD   1, 52, 57, 60
****FILE 130,131
$$$$
PARAM    //*AND*/KDEKA/NOUE/NOK2PP $
****CARD   1, 52, 57, 60
****FILE 110
$$$$
PARAM    //*AND*/MDEMA/NOUE/NOM2PP $
****CARD   1, 52, 57, 60
****FILE 110
****PHS2   DB8  
$$$$
PARAM    //*AND*/KDEK2/NOGENL/NOSIMP $  
****CARD   1, 52, 57, 60
****FILE 110
****PHS2  DE8   
$$$$
PURGE    K2DD/NOK2PP/M2DD/NOM2PP/B2DD/NOB2PP $  
****CARD   1-  6,  8- 11, 14, 24, 52, 57- 60
****FILE 110
$$$$               2        2      2        2          2        2   
$$$$ Equivalence [M  ] to [M  ], [B  ] to [B  ], and [K  ] to [K  ] if no
$$$$               pp       dd     pp       dd         pp       dd  
$$$$ constraints are applied, [M  ] to [M  ] if there are no direct input
$$$$                            aa       dd
$$$$ mass matrices and no extra points, and [K  ] to [K  ] if there are no
$$$$                                          aa       dd
$$$$ direct input stiffness matrices and no extra points.
EQUIV    M2PP,M2DD/NOA/B2PP,B2DD/NOA/K2PP,K2DD/NOA/MAA,MDD/MDEMA/   
         KAA,KDD/KDEKA $
****CARD   1-  6,  8- 11, 14, 24, 52, 57- 60
****FILE 110
$$$$ Go to label LBL16 if only extra points are defined.
COND     LBL16,NOGPDT $ 
****CARD   1-  6,  8- 11, 14, 24, 52, 57- 60
****FILE 110
****RFMT 193,194
$$$$ GKAD assembles stiffness, mass, and damping matrices for use in Direct 
$$$$ Frequency Response.
$$$$
$$$$             1       2
$$$$   [K  ] = [K  ] + [K  ]
$$$$     dd      dd      dd
$$$$
$$$$             1       2
$$$$   [M  ] = [M  ] + [M  ]
$$$$     dd      dd      dd
$$$$
$$$$             1       2       g      1       1      4
$$$$   [B  ] = [B  ] + [B  ] + ------ [K  ] + ------ [K  ]
$$$$     dd      dd      dd    omega    dd    omega    dd
$$$$                                3              4
$$$$ 
$$$$ where
$$$$ 
$$$$   +      +
$$$$   |K  |  |
$$$$   | aa| 0|       1
$$$$   |---+--| ==> [K  ]
$$$$   |0  | 0|       dd
$$$$   +      +
$$$$ 
$$$$   +      +
$$$$   |M  |  |
$$$$   | aa| 0|       1
$$$$   |---+--| ==> [M  ]
$$$$   |0  | 0|       dd
$$$$   +      +
$$$$ 
$$$$   +      +
$$$$   |B  |  |
$$$$   | aa| 0|       1
$$$$   |---+--| ==> [B  ]
$$$$   |0  | 0|       dd
$$$$   +      +
$$$$ 
$$$$   + 4    +
$$$$   |K  |  |
$$$$   | aa| 0|       4
$$$$   |---+--| ==> [K  ]
$$$$   |0  | 0|       dd
$$$$   +      +
$$$$ 
$$$$ All matrices are real.
GKAD     USETD,GM,GO,KAA,BAA,MAA,K4AA,K2PP,M2PP,B2PP/KDD,BDD,MDD,GMD,   
         GOD,K2DD,M2DD,B2DD/*TRANRESP*/*DISP*/*DIRECT*/C,Y,G=0.0/   
         C,Y,W3=0.0/C,Y,W4=0.0/NOK2PP/NOM2PP/NOB2PP/
         MPCF1/SINGLE/OMIT/NOUE/NOK4GG/NOBGG/   
         KDEK2/-1 $ 
****CARD   1-  6,  8- 11, 14, 24, 52, 56- 60
****FILE 110
****RFMT 193,194
$$$$
LABEL    LBL16 $
****CARD   1-  6,  8- 11, 14, 24, 52, 56- 60
****FILE 110
****RFMT 193,194
$$$$               2
$$$$ Equivalence [K  ] to [K  ] if all stiffness is Direct Matrix Input,
$$$$               dd       dd
$$$$   2
$$$$ [M  ] to [M  ] if all mass is Direct Matrix Input, and
$$$$   dd       dd
$$$$   2
$$$$ [B  ] to [B  ] if all damping is Direct Matrix Input.
$$$$   dd       dd
$$$$
EQUIV    M2DD,MDD/NOSIMP/B2DD,BDD/NOGPDT/K2DD,KDD/KDEK2 $   
****CARD   1-  6,  8- 11, 14, 24, 52, 56- 60
****FILE 110
****PHS2  D8
$$$$
PARAM    //*ADD*/NEVER/1/0 $
****CARD  14, 22- 24
$$$$
PARAM    //*MPY*/REPEATT/1/-1 $ 
****CARD   1-  6,  8- 14, 16, 19- 24, 27, 52, 56- 62
****FILE 108
****RFMT 187-194,196-204,207-209
$$$$ Beginning of loop for additional dynamic load sets.
LABEL    LBL13 $
****SBST   1,  3
****CARD   1-  6,  8- 16, 18- 25, 52, 56- 62
****FILE 108
****RFMT 187-194,196-204,207-209
$$$$
PURGE    PNLD,OUDV1,OPNL1,OUDV2,OPNL2,XYPLTTA,OPP1,OQP1,OUPV1,OES1, 
         OEF1,OPP2,OQP2,OUPV2,OES2,OEF2,PLOTX2,XYPLTT/NEVER $   
****CARD  14, 19- 24, 27
****FILE 112,113,115,116,127,132,133,135
$$$$ CASE extracts the appropriate record from CASECC corresponding to the 
$$$$ current loop and copies it into CASEXX.
CASE     CASECC,/CASEXX/*TRAN*/S,N,REPEATT/S,N,NOLOOP $ 
****CARD   1-  6,  8- 14, 16, 19- 25, 27, 52, 56- 62
****FILE 108
****RFMT 187-194,196-204,207-209
$$$$
PARAM    //*MPY*/NCOL/0/1 $ 
****SBST   4
****CARD   1-  6,  8- 11, 14, 24, 52, 56- 60
****FILE 111
$$$$                                                 t     t         t
$$$$ TRLG generates matrices of loads versus time. {P }, {P }, and {P } are
$$$$                                                 p     s         d
$$$$ generated with one column per output time step. {P } is generated with
$$$$                                                   d
$$$$ one column per solution time step, and the Transient Output List (TOL) is
$$$$ a list of output time steps.
TRLG     CASEXX,USETD,DLT,SLT,BGPDT,SIL,CSTM,TRL,DIT,GMD,GOD,,EST,MGG,  
         MPT/PPT,PST,PDT,PD,,TOL/S,N,NOSET/NCOL $   
****CARD   1-  6,  8- 11, 14, 22- 24, 52, 56- 62
****FILE 111
$$$$               t       t
$$$$ Equivalence {P } to {P } if the d and p sets are the same.
$$$$               p       d
$$$$
EQUIV    PPT,PDT/NOSET $
****CARD   1-  6,  8- 11, 14, 22- 24, 52, 56- 62
****FILE 111
****PHS1  DE1   
$$$$                                              nl
$$$$ TRD forms the linear, {P }, and nonlinear, {P  }, dynamic load vectors
$$$$                         d                    d
$$$$ and integrates the equations of motion (using the standard or alternate 
$$$$ starting procedure) over specified time periods to solve for the 
$$$$ displacements, velocities, and accelerations, using the following 
$$$$ equation:
$$$$
$$$$        2                               nl
$$$$   {M  p  + B  p + K  ] {u } = {P } + {P  }
$$$$     dd      dd     dd    d      d      d
$$$$
$$$$
TRD      CASEXX,TRL,NLFT,DIT,KDD,BDD,MDD,PD/UDVT,PNLD/*DIRECT*/ 
         NOUE/NONCUP/S,N,NCOL/C,Y,ISTART $  
****CARD   1-  6,  8- 11, 14, 17, 22- 24, 52, 56- 62
****FILE 127
****PHS1   DB1  
$$$$ VDR prepares displacements, velocities, and accelerations, sorted by time 
$$$$ step, for output using only the solution set degrees of freedom.
VDR      CASEXX,EQDYN,USETD,UDVT,TOL,XYCDB,PNLD/OUDV1,OPNL1/
         *TRANRESP*/*DIRECT*/0/S,N,NOD/S,N,NOP/0 $  
****CARD  19- 21, 27
****FILE 112
$$$$ Go to label LBL15 if there is no output request for the solution set.
COND     LBL15,NOD $
****CARD  21, 27
****FILE 113,135
$$$$ SDR3 prepares the requested output of the solution set displacements, 
$$$$ velocities, accelerations, and nonlinear load vectors sorted by point 
$$$$ number or element number.
SDR3     OUDV1,OPNL1,,,,/OUDV2,OPNL2,,,, $  
****CARD  21, 27
****FILE 113
$$$$ OFP formats the tables prepared by SDR3 sorted by point number or element 
$$$$ number, and places them on the system output file for printing.
OFP      OUDV2,OPNL2,,,,//S,N,CARDNO $  
****CARD  21
****FILE 113
$$$$ XYTRAN prepares the input for requested X-Y plots of the solution set 
$$$$ quantities.
XYTRAN   XYCDB,OUDV2,OPNL2,,,/XYPLTTA/*TRAN*/*DSET*/S,N,PFILE/  
         S,N,CARDNO $   
****SBST   7
****CARD  27
****FILE 135
$$$$ XYPLOT prepares the requested X-Y plots of the solution set 
$$$$ displacements, velocities, accelerations, and nonlinear load vectors vs. 
$$$$ time.
XYPLOT   XYPLTTA// $
****SBST   7
****CARD  27
****FILE 135
$$$$
LABEL    LBL15 $
****CARD  21, 27
****FILE 113,135
$$$$
PARAM    //*AND*/PJUMP/NOP/JUMPPLOT $   
****CARD   1-  6,  8- 11, 14, 22- 24, 52, 56- 62
****FILE 114
****RFMT 187-194,196-204,207-209
$$$$ Go to label LBL18 if no further output is requested.
COND     LBL18,PJUMP $  
****CARD   1-  6,  8- 11, 14, 18- 20, 22- 24, 52, 56- 62
****FILE 114-116,132,133
****RFMT 187-194,196-204,207-209
$$$$ Equivalence {u } to {u } if no constraints are applied.
$$$$               d       p
$$$$
EQUIV    UDVT,UPV/NOA $ 
****CARD   1-  6,  8- 11, 14, 22- 24, 52, 56- 62
****FILE 114
****RFMT 187-194,196-204,207-209
****PHS2   DB8  
$$$$ Go to label LBL17 if no constraints are applied.
COND     LBL17,NOA $
****CARD   1-  6,  8- 11, 14, 22- 24, 52, 56- 62
****FILE 114
****RFMT 187-194,196-204,207-209
****PHS3  DE7   
$$$$ SDR1 recovers dependent components of displacements
$$$$ 
$$$$                                 u
$$$$           d                      d
$$$$  {u } = [G ]{u }               {----} = {u  + u  }
$$$$    o      o   d                 u         f     e
$$$$                                   o
$$$$
$$$$    u + u      
$$$$     f   e                              d
$$$$   {-------} = {u + u }        {u } = [G ]{u  + u }
$$$$      u          n   e           m      m   f    e
$$$$       s        
$$$$
$$$$    u + u                
$$$$     n   e               
$$$$   {---------} = {u }
$$$$       u           p 
$$$$        m                  
$$$$                                                                 T
$$$$ and recovers single-point forces of constraint {q } = -{P } + [K  ]{u }.
$$$$                                                  s       s      fs   f
$$$$
SDR1     USETD,,UDVT,,,GOD,GMD,PST,KFS,,/UPV,,QP/1/*DYNAMICS* $ 
****CARD   1-  6,  8- 11, 14, 22- 24, 52, 56- 62
****FILE 114
****RFMT 187-194,196-204,207-209
$$$$
LABEL    LBL17 $
****CARD   1-  6,  8- 11, 14, 22- 24, 52, 56- 62
****FILE 114
****RFMT 187-194,196-204,207-209
****PHS3  I7
$$$$ SDR2 calculates element forces (OEF1) and stresses (OES1) and 
$$$$ prepares load vectors (OPP1), displacement, velocity, and acceleration
$$$$ vectors (OUPV1), and single-point forces of constraint (OQP1) for output 
$$$$ and translation components of the displacement vector (PUGV1), sorted by 
$$$$ time step. 
SDR2     CASEXX,CSTM,MPT,DIT,EQDYN,SILD,,,BGPDP,TOL,QP,UPV,EST,XYCDB,   
         PPT,/OPP1,OQP1,OUPV1,OES1,OEF1,PUGV,,/*TRANRESP* $ 
****CARD  18- 20
****FILE 115
$$$$ SDR3 prepares requested output sorted by point number or element number.
SDR3     OPP1,OQP1,OUPV1,OES1,OEF1,/
         OPP2,OQP2,OUPV2,OES2,OEF2, $   
****CARD  18- 20
****FILE 116
$$$$ OFP formats the tables prepared by SDR3 for output sorted by point number 
$$$$ or element number and places them on the system output file for printing.
OFP      OPP2,OQP2,OUPV2,OEF2,OES2,//S,N,CARDNO $   
****CARD  19
****FILE 116
$$$$ SCAN examines the element stresses and forces calculated by SDR3 and 
$$$$ generates scanned output that meets the specifications set by the user.
SCAN     CASECC,OES2,OEF2/OESF2/*RF* $  
****CARD 19 
****FILE 116
$$$$ OFP formats the scanned output table prepared by SCAN and places it on 
$$$$ the system output file for printing.
OFP      OESF2,,,,,//S,N,CARDNO $   
****CARD  19
****FILE 116
$$$$ Go to label P2 if no deformed structure plots are requested.
COND     P2,JUMPPLOT $  
****SBST   7
****CARD  18
****FILE 132
$$$$ PLOT prepares all requested deformed structure and contour plots.
PLOT     PLTPAR,GPSETS,ELSETS,CASEXX,BGPDT,EQEXIN,SIP,,PUGV,GPECT,OES1, 
         ,/PLOTX2/NSIL/LUSEP/JUMPPLOT/PLTFLG/S,N,PFILE $
****SBST   7
****CARD  18
****FILE 132
$$$$ PRTMSG prints plotter data, engineering data, and contour data for each 
$$$$ deformed plot generated.
PRTMSG   PLOTX2// $ 
****SBST   7
****CARD  18
****FILE 132
$$$$
LABEL    P2 $   
****SBST   7
****CARD  18
****FILE 132
$$$$ XYTRAN prepares the input for requested X-Y plots.
XYTRAN   XYCDB,OPP2,OQP2,OUPV2,OES2,OEF2/XYPLTT/*TRAN*/*PSET*/  
         S,N,PFILE/S,N,CARDNO $ 
****SBST   7
****CARD  20
****FILE 133
$$$$ XYPLOT prepares the requested X-Y plots of displacements, velocities, 
$$$$ accelerations, forces, stresses, loads, and single-point forces of 
$$$$ constraint versus time.
XYPLOT   XYPLTT// $ 
****SBST   7
****CARD  20
****FILE 133
****PHS2   DE8  
$$$$
LABEL    LBL18 $
****CARD   1-  6,  8- 11, 14, 18- 20, 22- 24, 52, 56- 62
****FILE 114-116,132,133
$$$$ Go to label FINIS if no additional dynamic load sets need to be 
$$$$ processed.
COND     FINIS,REPEATT $
****SBST   1,  3
****CARD  22, 23
****RFMT 187-194,196-204,207-209
****PHS3  DB7   
$$$$ Go to label LBL13 if additional dynamic load sets need to be processed.
REPT     LBL13,100 $
****SBST   1,  3
****CARD  22, 23
****RFMT 187-194,196-204,207-209
$$$$ Print Error Message No. 2 and terminate execution.
PRTPARM  //-2/*DIRTRD* $
****SBST   1,  3
****CARD  22, 23
****RFMT 187-194,196-204,207-209
****PHS1  DE1   
****PHS3  DE7   
$$$$ Go to label FINIS and make normal exit.
JUMP     FINIS $
****CARD   1-  6,  8- 28, 52, 56- 62
****RFMT 187-194,196-204,207-209
$$$$
LABEL    ERROR1 $   
****CARD   1,  9- 11, 57, 61
****RFMT 187-194,196-204,207-209
$$$$ Print Error Message No. 1 and terminate execution.
PRTPARM  //-1/*DIRTRD* $
****CARD   1,  9- 11, 57, 61
****RFMT 187-194,196-204,207-209
$$$$
LABEL    ERROR3 $   
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 129
****RFMT 187-194,196-204,207-209
$$$$ Print Error Message No. 3 and terminate execution.
PRTPARM  //-3/*DIRTRD* $
****SBST   8
****CARD   1-  3,  5,  8, 14, 15, 24
****FILE 129
****RFMT 187-194,196-204,207-209
$$$$
LABEL    FINIS  $   
****CARD   1-  6,  8- 28, 52, 56- 62
****RFMT 187-194,196-204,207-209
$$$$
PURGE    DUMMY/ALWAYS $ 
****CARD   1-  6,  8- 28, 52, 56- 62
****RFMT 187-194,196-204,207-209
$$$$
END      $  
****CARD   1-  6,  8- 28, 52, 56- 62
****RFMT 187-194,196-204,207-209
$$$$
$*CARD BITS 
  1    AXIC     AXIF     CDAMP1   CDAMP2   CDAMP3   CDAMP4   CELAS1 
  1    CELAS2   
  1    CELAS3   CELAS4   CMASS1   CMASS2   CMASS3   CMASS4   CORD1C 
  1    CORD1R   
  1    CORD1S   CORD2C   CORD2R   CORD2S   FREEPT   GRDSET   GRID   
  1    GRIDB
  1    POINTAX  PRESPT   RINGAX   RINGFL   SECTAX   SEQGP    SPOINT 
  2    ADUM1    ADUM2    ADUM3    ADUM4    ADUM5    ADUM6    ADUM7  
  2    ADUM8
  2    ADUM9    BAROR    CBAR     CCONEAX  CDUM1    CDUM2    CDUM3  
  2    CDUM4
  2    CDUM5    CDUM6    CDUM7    CDUM8    CDUM9    CELBOW   CFLUID2
  2    CFLUID3  
  2    CFLUID4  CHEXA1   CHEXA2   CIHEX1   CIHEX2   CIHEX3   CIS2D8 
  2    CONROD   
  2    CQDMEM   CQDMEM1  CQDMEM2  CQDPLT   CQUAD1   CQUAD2   CROD   
  2    CSHEAR   CTETRA   CTORDRG  CTRAPAX  CTRAPRG  CTRBSC   CTRIA1 
  2    CTRIA2   CQUAD4   CTRIA3
  2    CTRIAAX  CTRIARG  CTRIM6   CTRMEM   CTRPLT   CTRPLT1  CTRSHL 
  2    CTUBE    CTWIST   CWEDGE 
  3    PBAR     PCONEAX  PDUM1    PDUM2    PDUM3    PDUM4    PDUM5  
  3    PDUM6
  3    PDUM7    PDUM8    PDUM9    PELBOW   PIHEX    PIS2D8   PQDMEM 
  3    PQDMEM1  PQDMEM2  PQDPLT   PQUAD1   PQUAD2   PROD     PSHEAR 
  3    PTORDRG  PSHELL   PCOMP    PCOMP1   PCOMP2
  3    PTRAPAX  PTRBSC   PTRIA1   PTRIA2   PTRIAAX  PTRIM6   PTRMEM 
  3    PTRPLT   PTRPLT1  PTRSHL   PTUBE    PTWIST   
  4    GENEL
  5    CONM1    CONM2    FSLIST   PMASS 
  6    PELAS
  8    MAT1     MAT2     MAT3     MATT1    MATT2    MATT3    MAT8
  8    TABLEM1  TABLEM2  TABLEM3  TABLEM4  TEMPMT$  TEMPMX$  
  9    AXISYM$  CRIGD1   CRIGD2   CRIGD3   CRIGDR   CRROD    CRBAR  
  9    CRTRPLT  CRBE1    CRBE2    CRBE3    CRSPLINE MPC      MPCADD 
  9    MPCAX    MPC$
 10    SPC      SPC1     SPCADD   SPCAX    SPC$ 
 11    ASET     ASET1    OMIT     OMIT1    OMITAX   SUPAX    SUPORT 
 13    TEMP     TEMPAX   TEMPD    TEMPP1   TEMPP2   TEMPP3   TEMPRB 
 14    WTMASS   
 15    GRDPNT   
 16    PLOTEL   
 17    ISTART   
 18    PLOT$
 19    POUT$
 20    XYOUT$   
 21    AOUT$
 22    LOOP$
 23    LOOP1$   
 24    COUPMASS CPBAR    CPQDPLT  CPQUAD1  CPQUAD2  CPROD    CPTRBSC
 24    CPTRIA1  CPTRIA2  CPTRPLT  CPTUBE
 25    NOLOOP$  
 26    ASETOUT  
 27    AXYOUT$  
 28    AUTOSPC  
 52    BDYLIST  FLSYM   
 56    G        W3       W4 
 57    EPOINT   SEQEP    TF 
 58    CVISC
 59    PDAMP    PVISC   
 60    DMIAX    DMIG     B2PP$    K2PP$    M2PP$    TF$ 
 61    DAREA    DELAY    DLOAD    FORCE    FORCE1   FORCE2   GRAV   
 61    MOMENT   
 61    MOMENT1  MOMENT2  NOLIN1   NOLIN2   NOLIN3   NOLIN4   NOLIN6 
 61    PLOAD    PLOAD4
 61    PLOAD1   PLOAD2   SLOAD    TABLED1  TABLED2  TABLED3  TABLED4
 61    TIC      TLOAD1   TLOAD2   TSTEP 
 62    DLOAD$   IC$      NLFORCE  TSTEP$
$$$$
$*FILE BITS 
 94    BGPDT    CSTM     EQEXIN   GPDT     GPL      SIL 
 95    ECT  
 96    GPTT     SLT 
 97    EST      GEI      GPECT    MPTX     PCOMPS   EPTX
 98    KGGX 
 99    MGG  
100    KGG  
101    ASET     RG       USET     OGPST 
102    GPST 
103    GM   
104    BNN      K4NN     KNN      MNN   
105    BFF      K4FF     KFF      KFS      MFF  
106    GO       KOO      LOO      KAA   
107    DLT      EQDYN    GPLD     NLFT     SILD     TFPOOL   TRL
107    USETD
108    CASEXX   
109    B2PP     K2DPP    M2DPP  
110    B2DD     BDD      GMD      GOD      K2DD     KDD      M2DD   
110    MDD  
111    PD       PDT      PPT      PST      TOL  
112    OUDV1    OPNL1   
113    OUDV2    OPNL2   
114    QP       UPV 
115    OEF1     OES1     OPP1     OQP1     OUPV1    PUGV
116    OEF2     OES2     OPP2     OQP2     OUPV2    OESF2
118    BDPOOL   
119    ABFL     KBFL
120    ELSETS   GPSETS   PLTPAR   PLTSETX   
121    MAA  
122    BAA  
123    K4AA 
124    BDICT    BELM     KDICT    KELM     MDICT    MELM
125    BGG  
126    K4GG 
127    PNLD     UDVT
128    PLOTX1   
129    OGPWG
130    K2PP 
131    M2PP 
132    PLOTX2   
133    XYPLTT   
134    BGPDP    SIP 
135    XYPLTTA  
$*  
=PAGE=
HEAT1

APR.93
$$$$$$$$
BEGIN    HEAT 01 - STATIC HEAT TRANSFER ANALYSIS - APR. 1993 $  
****CARD   1- 19, 22, 23, 59- 62
****RFMT 187-204,208,209
$$$$
PRECHK   ALL $  
****CARD   1- 19, 22, 23, 59- 62
****RFMT 187-204,208,209
$$$$
FILE     HQG=APPEND/HPGG=APPEND/HUGV=APPEND/HGM=SAVE/HKNN=SAVE $
****SBST   1,  3
****CARD   1- 19, 22, 23, 59- 62
****RFMT 187-204,208,209
$$$$
PARAM    //*MPY*/CARDNO/0/0 $   
****CARD   1-  3,  6,  8- 10, 15, 19, 22, 23
****FILE 101,114
$$$$ GP1 generates coordinate system transformation matrices, tables of grid 
$$$$ point locations, and tables relating the internal and external grid point 
$$$$ numbers.
GP1      GEOM1,GEOM2,/GPL,EQEXIN,GPDT,CSTM,BGPDT,HSIL/S,N,HLUSET/  
         NOGPDT/MINUS1=-1 $ 
****CARD   1
****FILE  94
$$$$ PLTTRAN modifies special scalar grid points in the BGPDT and SIL tables.
PLTTRAN  BGPDT,HSIL/BGPDP,HSIP/HLUSET/S,N,HLUSEP $  
****CARD   1
****FILE 119
$$$$ GP2 generates Element Connection Table with internal indices.
GP2      GEOM2,EQEXIN/ECT $
****CARD   1,  2,  4, 16
****FILE  95
$$$$
PARAML   PCDB//*PRES*////JUMPPLOT $ 
****SBST   7
****CARD  18
****FILE 115,117
$$$$
PURGE    PLTSETX,PLTPAR,GPSETS,ELSETS/JUMPPLOT $
****SBST   7
****CARD  18
****FILE 115
$$$$ Go to label HP1 if there are no structure plot requests.
COND     HP1,JUMPPLOT $ 
****SBST   7
****CARD   1,  2,  4, 16, 18
****FILE 115,117
$$$$ PLTSET transforms user input into a form used to drive the structure 
$$$$ plotter.
PLTSET   PCDB,EQEXIN,ECT,EPT/PLTSETX,PLTPAR,GPSETS,ELSETS/S,N,HNSIL/   
         S,N,JUMPPLOT $ 
****SBST   7
****CARD   1,  2,  4, 16, 18
****FILE 115
$$$$ PRTMSG prints error messages associated with the structure plotter.
PRTMSG   PLTSETX// $
****SBST   7
****CARD   1,  2,  4, 16, 18
****FILE 115
$$$$
PARAM    //*MPY*/PLTFLG/1/1 $   
****SBST   7
****CARD   1,  2,  4, 16, 18
****FILE 117
$$$$
PARAM    //*MPY*/PFILE/0/0 $
****SBST   7
****CARD   1,  2,  4, 16, 18
****FILE 117
$$$$ Go to label HP1 if no boundary and structure (heat conduction) element 
$$$$ plots are requested.
COND     HP1,JUMPPLOT $ 
****SBST   7
****CARD   1,  2,  4, 16, 18
****FILE 117
$$$$ PLOT generates all requested boundary and heat conduction element plots.
PLOT     PLTPAR,GPSETS,ELSETS,CASECC,BGPDT,EQEXIN,HSIL,,ECT,,,,/PLOTX1/
         HNSIL/HLUSET/S,N,JUMPPLOT/S,N,PLTFLG/S,N,PFILE $   
****SBST   7
****CARD   1,  2,  4, 16, 18
****FILE 117
$$$$ PRTMSG prints plotter data and engineering data for each plot generated. 
PRTMSG   PLOTX1// $ 
****SBST   7
****CARD   1,  2,  4, 16, 18
****FILE 117
$$$$
LABEL    HP1 $  
****SBST   7
****CARD   1,  2,  4, 16, 18
****FILE 115,117
$$$$ GP3 generates applied Static (Thermal) Loads Table (HSLT) and Grid Point 
$$$$ Temperature Table.
GP3      GEOM3,EQEXIN,GEOM2/HSLT,GPTT/NOGRAV $ 
****CARD   1,  2, 13, 60
****FILE  96
$$$$ TA1 generates element tables for use in matrix assembly, load generation, 
$$$$ and data recovery.
TA1      ECT,EPT,BGPDT,HSIL,GPTT,CSTM,,EQEXIN/HEST,HGEI,HGPECT,,,,,/   
         HLUSET/S,N,NOSIMP/1/S,N,NOGENL/GENEL $ 
****CARD   1-  6, 13, 16
****FILE  97
$$$$
PARAM    //*AND*/NOELMT/NOGENL/NOSIMP $ 
****CARD   1,  2,  4-  6, 16
****FILE  97
$$$$ Go to label ERROR4 and print Error Message No. 4 if no elements have been 
$$$$ defined.
COND     ERROR4,NOELMT $
****CARD   1,  2,  4-  6, 16
****FILE  97
****RFMT 187-204,208,209
$$$$
PURGE    HKGGX/NOSIMP $ 
****CARD   1,  2,  4-  6, 16
****FILE  98
$$$$ Go to label HLBL1 if there are no heat conduction elements.
COND     HLBL1,NOSIMP $ 
****CARD   1-  3,  6,  8
****FILE  98,116
$$$$
PARAM    //*ADD*/HNOKGG/1/0 $   
****CARD   1-  3,  6,  8
****FILE 116
$$$$ EMG generates heat conduction matrix tables and dictionaries for later 
$$$$ assembly by the EMA module. 
EMG      HEST,CSTM,MPT,DIT,GEOM2,/HKELM,HKDICT,,,,,/S,N,HNOKGG $
****CARD   1-  3,  6,  8
****FILE 116
$$$$
PURGE    HKGGX/HNOKGG $ 
****CARD   1-  3,  6,  8
****FILE  98
$$$$ Go to label HLBL1 if no heat conduction matrix is to be assembled.
COND     HLBL1,HNOKGG $ 
****CARD   1-  3,  6,  8
****FILE  98
$$$$                                        x
$$$$ EMA assembles heat conduction matrix [K  ] and Grid Point Singularity
$$$$                                        gg
$$$$ Table.
EMA      HGPECT,HKDICT,HKELM/HKGGX $
****CARD   1-  3,  6,  8
****FILE  98
$$$$
PURGE    HKDICT,HKELM/MINUS1 $  
****CARD   1-  3,  6,  8
****FILE 116
$$$$
LABEL    HLBL1 $
****CARD   1-  3,  6,  8
****FILE  98,116
$$$$
EQUIV    HKGGX,HKGG/NOGENL $
****CARD   1-  4,  6,  8
****FILE 100
$$$$
COND     HLBL11A,NOGENL $   
****CARD   1-  4,  6,  8
****FILE 100
$$$$
SMA3     HGEI,HKGGX/HKGG/HLUSET/NOGENL/NOSIMP $ 
****CARD   1-  4,  6,  8
****FILE 100
$$$$
LABEL    HLBL11A $  
****CARD   1-  4,  6,  8
****FILE 100
$$$$
GPSTGEN  HKGG,HSIL/GPST $   
****CARD   1-  4,  6,  8
****FILE 102
$$$$
PARAM    //*MPY*/NSKIP/0/0 $
****CARD   1,  9- 12, 22, 23
****FILE 101
$$$$ Beginning of loop for additional constraint sets.
LABEL    HLBL11 $   
****SBST   1,  3
****CARD  22, 23
****FILE 101
$$$$ GP4 generates flags defining members of various displacement sets (USET), 
$$$$ forms multipoint constraint equations [R ] {u } = 0, and forms enforced
$$$$                                         g    g
$$$$ displacement vector {Y }.
$$$$                       s
$$$$ 
GP4      CASECC,GEOM4,EQEXIN,GPDT,BGPDT,CSTM,GPST/RG,YS,HUSET, 
         HASET,OGPST/HLUSET/S,N,MPCF1/S,N,MPCF2/S,N,SINGLE/S,N,OMIT/
         S,N,REACT/S,N,NSKIP/S,N,HREPEAT/S,N,NOSET/S,N,NOL/S,N,NOA/ 
         C,Y,ASETOUT/C,Y,AUTOSPC $  
****CARD   1-  4,  6,  8- 12, 14, 15, 22, 23, 59
****FILE 101
$$$$
OFP      OGPST,,,,,//S,N,CARDNO $   
****CARD   1-  4,  6,  8- 12, 14, 15, 22, 23, 59
****FILE 101
$$$$ Go to label ERROR3 and print Error Message No. 3 if no independent 
$$$$ degrees of freedom are defined.
COND     ERROR3,NOL $   
****CARD   1,  9- 12, 14, 15, 22, 23, 59
****FILE 101
****RFMT 187-204,208,209
$$$$
PARAM    //*AND*/NOSR/SINGLE/REACT $
****CARD   1,  9- 12, 22, 23, 59
****FILE 101
$$$$
PURGE    HKRR,HKLR,HQR,HDM/REACT/GM/MPCF1/HGO,HKOO,HLOO,HPO,HUOOV,  
         HRUOV/OMIT/HPS,HKFS,HKSS/SINGLE/HQG/NOSR $ 
****CARD   1,  9- 12, 22, 23, 59
****FILE 103,105-107,109,111-113
$$$$ Equivalence [K  ] to [K  ] if no multipoint constraints exist.
$$$$               gg       nn
$$$$
EQUIV    HKGG,HKNN/MPCF1 $  
****CARD   1-  4,  6,  8,  9, 22, 23
****FILE 104
$$$$ Go to label HLBL2 if no multipoint constraints exist.
COND     HLBL2,MPCF1 $  
****CARD   1-  4,  6,  8,  9, 22, 23
****FILE 103,104
$$$$ MCE1 partitions multipoint constraint equation [R ] = [R |R ] and solves
$$$$                                                  g      m  n
$$$$                                                             -1
$$$$ for multipoint constraint transformation matrix [G ] = -[R ]  [R ].
$$$$                                                   m       m     n
$$$$
MCE1     HUSET,RG/GM $  
****CARD   1,  9, 22, 23
****FILE 103
$$$$ MCE2 partitions heat conduction matrix
$$$$ 
$$$$           +_      +
$$$$           |K  |K  |
$$$$           | nn| nm|
$$$$   [K  ] = |---+---|
$$$$     gg    |K  |K  |
$$$$           | mn| mm|
$$$$           +       +
$$$$ 
$$$$ and performs matrix reduction
$$$$ 
$$$$            _        T           T           T
$$$$   [K  ] = [K  ] + [G ][K  ] + [K  ][G ] + [G ][K  ][G ]
$$$$     nn      nn      m   mn      mn   m      m   mm   m
$$$$
MCE2     HUSET,GM,HKGG,,,/HKNN,,, $ 
****CARD   1-  4,  6,  8,  9, 22, 23
****FILE 104
$$$$
LABEL    HLBL2 $
****CARD   1-  4,  6,  8,  9, 22, 23
****FILE 103,104
$$$$ Equivalence [K  ] to [K  ] if no single-point constraints exist.
$$$$               nn       ff
$$$$
EQUIV    HKNN,HKFF/SINGLE $ 
****CARD   1-  4,  6,  8- 10, 22, 23
****FILE 105
$$$$ Go to label HLBL3 if no single-point constraints exist.
COND     HLBL3,SINGLE $ 
****CARD   1-  4,  6,  8- 10, 22, 23
****FILE 105
$$$$ SCE1 partitions out single-point constraints
$$$$ 
$$$$           +       +
$$$$           |K  |K  |
$$$$           | ff| fs|
$$$$   [K  ] = |---+---|
$$$$     nn    |K  |K  |
$$$$           | sf| ss|
$$$$           +       +
$$$$
SCE1     HUSET,HKNN,,,/HKFF,HKFS,HKSS,,, $  
****CARD   1-  4,  6,  8- 10, 22, 23
****FILE 105
$$$$
LABEL    HLBL3 $
****CARD   1-  4,  6,  8- 10, 22, 23
****FILE 105
$$$$ Equivalence [K  ] to [K  ] if no omitted coordinates exist.
$$$$               ff       aa
$$$$
EQUIV    HKFF,HKAA/OMIT $   
****CARD   1-  4,  6,  8- 11, 22, 23
****FILE 106
$$$$ Go to label HLBL5 if no omitted coordinates exist.
COND     HLBL5,OMIT $   
****CARD   1-  4,  6,  8- 11, 22, 23
****FILE 106
$$$$ SMP1 partitions constrained heat conduction matrix
$$$$
$$$$           +_      +
$$$$           |K  |K  |
$$$$           | aa| ao|
$$$$   [K  ] = |---+---|
$$$$     ff    |K  |K  |
$$$$           | oa| oo|
$$$$           +       +
$$$$           
$$$$                                               -1
$$$$ solves for transformation matrix [G ] = -[K  ]  [K  ]
$$$$                                    o       oo     oa
$$$$ 
$$$$                                        _        T
$$$$ and performs matrix reduction [K  ] = [K  ] + [K  ][G ].
$$$$                                 aa      aa      oa   o
$$$$
SMP1     HUSET,HKFF,,,/HGO,HKAA,HKOO,HLOO,,,,, $
****CARD   1-  4,  6,  8- 11, 22, 23
****FILE 106
$$$$
LABEL    HLBL5 $
****CARD   1-  4,  6,  8- 11, 22, 23
****FILE 106
$$$$ Equivalence [K  ] to [K  ] if no free-body supports exist.
$$$$               aa       ll
$$$$
EQUIV    HKAA,HKLL/REACT $  
****CARD   1-  4,  6,  8- 12, 22, 23
****FILE 107
$$$$ Go to label HLBL6 if no free-body supports exist.
COND     HLBL6,REACT $  
****CARD   1-  4,  6,  8- 12, 22, 23
****FILE 107
$$$$ RBMG1 partitions out free-body supports
$$$$ 
$$$$           +       +
$$$$           |K  |K  |
$$$$           | ll| lr|
$$$$   [K  ] = |---+---|
$$$$     aa    |K  |K  |
$$$$           | rl| rr|
$$$$           +       +
$$$$
RBMG1    HUSET,HKAA,/HKLL,HKLR,HKRR,,, $
****CARD   1-  4,  6,  8- 12, 22, 23
****FILE 107
$$$$
LABEL    HLBL6 $
****CARD   1-  4,  6,  8- 12, 22, 23
****FILE 107
$$$$ RBMG2 decomposes constrained heat conduction matrix [K  ] = [L  ][U  ].
$$$$                                                       ll      ll   ll
$$$$
RBMG2    HKLL/HLLL $
****CARD   1-  4,  6,  8- 12, 22, 23
****FILE 108
$$$$ Go to label HLBL7 if no free-body supports exist.
COND     HLBL7,REACT $  
****CARD   1-  4,  6,  8- 12, 22, 23
****FILE 109
$$$$ RBMG3 forms rigid body transformation matrix
$$$$ 
$$$$               -1
$$$$   [D] = -[K  ]  [K  ]
$$$$            ll     lr
$$$$ 
$$$$ calculates rigid body check matrix
$$$$
$$$$                   T
$$$$   [X] = [K  ] + [K  ][D]
$$$$           rr      lr
$$$$ 
$$$$ and calculates rigid body error ratio
$$$$ 
$$$$ 
$$$$               ||X||
$$$$   epsilon = ---------
$$$$              ||K  ||
$$$$                 rr
$$$$
RBMG3    HLLL,HKLR,HKRR/HDM $   
****CARD   1-  4,  6,  8- 12, 22, 23
****FILE 109
$$$$
LABEL    HLBL7 $
****CARD   1-  4,  6,  8- 12, 22, 23
****FILE 109
$$$$ SSG1 generates static thermal load vectors {P }.
$$$$                                              g
$$$$ 
SSG1     HSLT,BGPDT,CSTM,HSIL,HEST,MPT,GPTT,EDT,,CASECC,DIT,/   
         HPG,,,,SCR/HLUSET/NSKIP $  
****CARD   1-  3,  6,  8, 22, 23, 59- 62
****FILE 110
$$$$ Equivalence {P } to {P } if no constraints are applied.
$$$$               g       l
$$$$
EQUIV    HPG,HPL/NOSET $
****CARD   1-  3,  6,  8- 12, 22, 23, 59- 62
****FILE 111
$$$$ Go to label HLBL10 if no constraints are applied.
COND     HLBL10,NOSET $ 
****CARD   1-  3,  6,  8- 12, 22, 23, 59- 62
****FILE 111
$$$$ SSG2 applies constraints to static thermal load vectors
$$$$ 
$$$$           _
$$$$           P
$$$$            n            _       T
$$$$   {P } = {--} , {P } = {P } + [G ]{P }
$$$$     g     P       n      n      m   m
$$$$            m
$$$$ 
$$$$           _
$$$$           P
$$$$            f            _      
$$$$   {P } = {--} , {P } = {P } - [K  ]{Y }
$$$$     n     P       f      f      fs   s
$$$$            s
$$$$ 
$$$$           _
$$$$           P
$$$$            a            _       T
$$$$   {P } = {--} , {P } = {P } + [G ]{P }
$$$$     f     P       a      a      o   o
$$$$            o
$$$$
$$$$           P  
$$$$            l 
$$$$   {P } = {--}
$$$$     a     P  
$$$$            r 
$$$$ 
$$$$                                                            T
$$$$ and calculates determinate thermal powers {q } = -{P } - [D ]{P }.
$$$$                                             r       r          l
$$$$ 
SSG2     HUSET,GM,YS,HKFS,HGO,HDM,HPG/HQR,HPO,HPS,HPL $ 
****CARD   1-  3,  6,  8- 12, 22, 23, 59- 62
****FILE 111
$$$$
LABEL    HLBL10 $   
****CARD   1-  3,  6,  8- 12, 22, 23, 59- 62
****FILE 111
$$$$ SSG3 solves for displacements of independent coordinates
$$$$ 
$$$$               -1  
$$$$   {u } = [K  ]  {P }
$$$$     l      ll     l
$$$$ 
$$$$ solves for displacements of omitted coordinates
$$$$ 
$$$$     o         -1  
$$$$   {u } = [K  ]  {P }
$$$$     o      oo     o
$$$$ 
$$$$ calculates residual vector (HRULV) and residual vector error ratio for 
$$$$ independent coordinates
$$$$                
$$$$   {deltaP } = {P } - [K  ]{u }
$$$$          l      l      ll   l
$$$$ 
$$$$                T        
$$$$              {u }{deltaP }
$$$$                l        l
$$$$   epsilon  = -------------
$$$$          l       T 
$$$$                {P }{u }
$$$$                  l   l
$$$$ 
$$$$ and calculates residual vector (HRUOV) and residual vector error ratio
$$$$ for omitted coordinates
$$$$ 
$$$$                             o
$$$$   {deltaP } = {P } - [K  ]{u }
$$$$          o      o      oo   o
$$$$ 
$$$$                T        
$$$$              {u }{deltaP }
$$$$                o        o
$$$$   epsilon  = -------------
$$$$          o       T   o
$$$$                {P }{u }
$$$$                  o   o
$$$$
SSG3     HLLL,HKLL,HPL,HLOO,HKOO,HPO/HULV,HUOOV,HRULV,HRUOV/OMIT/   
         V,Y,IRES=-1/NSKIP/S,N,EPSI $   
****CARD   1-  6,  8- 12, 17, 22, 23, 59- 62
****FILE 112
****RFMT 188
$$$$ Go to lable HLBL9 if residual vectors are not to be printed.
COND     HLBL9,IRES $   
****CARD   1-  6,  8- 12, 17, 22, 23, 59- 62
****RFMT 187-204,208,209
$$$$ MATGPR prints the residual vector for independent coordinates (HRULV).
MATGPR   GPL,HUSET,HSIL,HRULV//*L* $
****CARD   1-  6,  8- 12, 17, 22, 23, 59- 62
****RFMT 187-204,208,209
$$$$ MATGPR prints the residual vector for omitted coordinates (HRUOV).
MATGPR   GPL,HUSET,HSIL,HRUOV//*O* $
****CARD   1-  6,  8- 12, 17, 22, 23, 59- 62
****RFMT 187-204,208,209
$$$$
LABEL    HLBL9 $
****CARD   1-  6,  8- 12, 17, 22, 23, 59- 62
****RFMT 187-204,208,209
$$$$ SDR1 recovers dependent temperatures
$$$$ 
$$$$    u            
$$$$     l                                                  o
$$$$   {--} = {u }  ,                   {u } = [G ]{u ] + {u }  ,
$$$$    u       a                         o      o   a      o
$$$$     r           
$$$$ 
$$$$    u                                u            
$$$$     a                                f           
$$$$   {--} = {u }  ,                   {--} = {u }  ,
$$$$    u       f                        Y       n    
$$$$     o                                s           
$$$$                                  
$$$$                                     u         
$$$$                                      n        
$$$$  {u } = [G ]{u ]  ,                {--} = {u }
$$$$    m      m   n                     u       g 
$$$$                                      m        
$$$$ 
$$$$ and recovers single-point powers of sustained thermal constraint
$$$$ 
$$$$                    T
$$$$   {q } = -{P } + [K  ]{u } +[K  ]{Y }
$$$$     s       s      fs   f     ss   s
$$$$
SDR1     HUSET,HPG,HULV,HUOOV,YS,HGO,GM,HPS,HKFS,HKSS,HQR/HUGV,HPGG,
         HQG/NSKIP/*HSTATICS* $ 
****CARD   1-  6,  8- 12, 22, 23, 59- 62
****FILE 113
****RFMT 187-204,208,209
$$$$ Go to label HLBL8 if all constraint sets have been processed.
COND     HLBL8,HREPEAT $
****SBST   1,  3
****CARD  22, 23
****RFMT 187-204,208,209
$$$$ Go to label HLBL11 if additional sets of constraints need to be 
$$$$ processed.
REPT     HLBL11,100 $   
****SBST   1,  3
****CARD  22, 23
****RFMT 187-204,208,209
$$$$ Go to label ERROR1 and print Error Message No. 1 if the number of 
$$$$ constraint sets exceeds 100.
JUMP     ERROR1 $   
****SBST   1,  3
****CARD  22, 23
****RFMT 187-204,208,209
$$$$
PARAM    //*NOT*/HTEST/HREPEAT $
****CARD  22, 23
****RFMT 187-204,208,209
$$$$ Go to label ERROR2 and print Error Message No. 2 if multiple boundary 
$$$$ conditions are attempted with an improper subset.
COND     ERROR2,HTEST $ 
****CARD  22, 23
****RFMT 187-204,208,209
$$$$
LABEL    HLBL8 $
****SBST   1,  3
****CARD  22, 23
****RFMT 187-204,208,209
$$$$ SDR2 calculates conduction and boundary element heat flows and gradients 
$$$$ (HOEF1) and prepares thermal load vectors (HOPG1), temperature vectors 
$$$$ (HOUGV1), and single-point powers of constraint (HOQG1) for output and 
$$$$ components of the temperature vector (HPUGV1).
SDR2     CASECC,CSTM,MPT,DIT,EQEXIN,HSIL,GPTT,EDT,BGPDP,,HQG,HUGV, 
         HEST,,HPGG,/HOPG1,HOQG1,HOUGV1,HOES1,HOEF1,HPUGV1,,/   
         *STATICS* $
****CARD  18, 19
****FILE 114
$$$$ OFP formats the tables prepared by SDR2 and places them on the system 
$$$$ output file for printing.
OFP      HOUGV1,HOPG1,HOQG1,HOEF1,,//S,N,CARDNO $   
****CARD  19
****FILE 114
$$$$ Go to label HP2 if no temperature profile plots are requested.
COND     HP2,JUMPPLOT $ 
****SBST   7
****CARD  18
****FILE 118
$$$$ PLOT generates all requested temperature profile and thermal contour 
$$$$ plots.
PLOT     PLTPAR,GPSETS,ELSETS,CASECC,BGPDT,EQEXIN,HSIP,HPUGV1,HOES1,   
         HGPECT,,,/PLOTX2/HNSIL/HLUSEP/JUMPPLOT/PLTFLG/ 
         S,N,PFILE $
****SBST   7
****CARD  18
****FILE 118
$$$$ PRTMSG prints plotter data, engineering data, and contour data for each 
$$$$ temperature profile and thermal contour plot generated.
PRTMSG   PLOTX2// $ 
****SBST   7
****CARD  18
****FILE 118
$$$$
LABEL    HP2 $  
****SBST   7
****CARD  18
****FILE 118
$$$$ Go to label FINIS and make normal exit.
JUMP     FINIS $
****CARD   1-  6,  8- 16, 18, 19, 22, 23, 59- 62
****RFMT 187-204,208,209
$$$$
LABEL    ERROR1 $   
****SBST   1,  3
****CARD  22, 23
****RFMT 187-204,208,209
$$$$ Print Error Message No. 1 and terminate execution.
PRTPARM  //-1/*HSTA* $  
****SBST   1,  3
****CARD  22, 23
****RFMT 187-204,208,209
$$$$
LABEL    ERROR2 $   
****CARD  22, 23
****RFMT 187-204,208,209
$$$$ Print Error Message No. 2 and terminate execution.
PRTPARM  //-2/*HSTA* $  
****CARD  22, 23
****RFMT 187-204,208,209
$$$$
LABEL    ERROR3 $   
****CARD   1,  9- 12, 14, 15, 22, 23, 59
****FILE 101
****RFMT 187-204,208,209
$$$$ Print Error Message No. 3 and terminate execution.
PRTPARM  //-3/*HSTA* $  
****CARD   1,  9- 12, 14, 15, 22, 23, 59
****FILE 101
****RFMT 187-204,208,209
$$$$
LABEL    ERROR4 $   
****CARD   1,  2,  4-  6, 16
****FILE  97
****RFMT 187-204,208,209
$$$$ Print Error Message No. 4 and terminate execution.
PRTPARM  //-4/*HSTA* $  
****CARD   1,  2,  4-  6, 16
****FILE  97
****RFMT 187-204,208,209
$$$$
LABEL    FINIS $
****CARD   1-  6,  8- 16, 18, 19, 22, 23, 59- 62
****RFMT 187-204,208,209
$$$$
PURGE    DUMMY/MINUS1 $ 
****CARD   1-  6,  8- 16, 18, 19, 22, 23, 59- 62
****RFMT 187-204,208,209
$$$$
END      $  
****CARD   1-  6,  8- 16, 18, 19, 22, 23, 59- 62
****RFMT 187-204,208,209
$$$$
$*CARD BITS 
  1    CELAS1   CELAS2   CELAS3   CELAS4
  1    CORD1C   CORD1R   CORD1S   CORD2C   CORD2R   CORD2S  
  1    GRDSET   GRID     GRIDB    POINTAX  RINGAX   RINGFL   SECTAX 
  1    SEQGP    SPOINT  
  2    ADUM1    ADUM2    ADUM3    ADUM4    ADUM5    ADUM6    ADUM7  
  2    ADUM8
  2    ADUM9    BAROR    CBAR     CDUM1    CDUM2    CDUM3    CDUM4  
  2    CDUM5    CDUM6    CDUM7    CDUM8    CDUM9    CELBOW   CFTUBE 
  2    CHBDY
  2    CHEXA1   CHEXA2   CIHEX1   CIHEX2   CIHEX3   CIS2D8   CONROD 
  2    CQDMEM   CQDMEM1  CQDMEM2  CQUAD1   CQUAD2   CQUAD4   CROD
  2    CTETRA   CTRAPRG  CTRIA1   CTRIA2   CTRIA3
  2    CTRIARG  CTRMEM   CTUBE  
  2    CWEDGE   
  3    PBAR     PDUM1    PDUM2    PDUM3    PDUM4    PDUM5    PDUM6  
  3    PDUM7    PDUM8    PDUM9    PELBOW   PFTUBE   PHBDY    PIHEX  
  3    PIS2D8   PQDMEM   PQDMEM1  PQDMEM2  PQUAD1   PQUAD2   PROD   
  3    PTRIA1   PTRIA2   PSHELL   PCOMP    PCOMP1   PCOMP2
  3    PTRMEM   PTUBE   
  4    GENEL
  6    PELAS
  8    MAT1     MAT2     MAT3     MAT4     MAT5     MATT1    MATT2  
  8    MATT3    MATT4    MATT5    MAT8     TABLEM1  TABLEM2  TABLEM3
  8    TABLEM4  TEMPMT$  TEMPMX$  
  9    AXISYM$  MPC      MPCADD   MPCAX    MPC$ 
 10    SPC      SPC1     SPCADD   SPCAX    SPC$ 
 11    ASET     ASET1    OMIT     OMIT1    OMITAX   SUPAX    SUPORT 
 13    TEMP     TEMPAX   TEMPD    TEMPP1   TEMPP2   TEMPP3   TEMPRB 
 14    ASETOUT  
 15    AUTOSPC  
 16    PLOTEL   
 17    IRES 
 18    PLOT$
 19    POUT$
 22    LOOP$
 23    LOOP1$   
 59    DEFORM   DEFORM$  LOAD$    SPCD  
 60    FORCE    FORCE1   FORCE2   FORCEAX  LOAD     MOMAX    MOMENT 
 60    MOMENT1  PLOAD4
 60    MOMENT2  PLOAD    PLOAD1   PLOAD2   PLOAD3   PRESAX   QBDY1  
 60    QBDY2    QHBDY    QVECT    QVOL     SLOAD
 62    TEMPLD$  
$$$$
$*FILE BITS 
 94    BGPDT    CSTM     EQEXIN   GPDT     GPL      HSIL
 95    ECT  
 96    GPTT     HSLT
 97    HGPECT   HEST     HGEI   
 98    HKGGX
100    HKGG 
101    HASET    RG       HUSET    YS       OGPST
102    GPST 
103    GM   
104    HKNN 
105    HKFF     HKSS     HKFS   
106    HGO      HKAA     HKOO     HLOO  
107    HKLL     HKLR     HKRR   
108    HLLL 
109    HDM  
110    HPG      SCR 
111    HPL      HPO      HPS      HQR   
112    HRULV    HRUOV    HULV     HUOOV 
113    HPGG     HQG      HUGV   
114    HOEF1    HOPG1    HOQG1    HOUGV1   HPUGV1   HOES1   
115    ELSETS   GPSETS   PLTPAR   PLTSETX   
116    HKDICT   HKELM   
117    PLOTX1   
118    PLOTX2   
119    BGPDP    HSIP
$*  
=PAGE=
HEAT3

APR.93
$$$$$$$$
BEGIN    HEAT 03 - NONLINEAR STATIC HEAT TRANSFER ANALYSIS - APR. 1993 $
****CARD   1-  3,  6,  8- 11, 13, 15- 19, 54, 55, 59, 60, 62
****RFMT 187-204,207,209
$$$$
PRECHK   ALL $  
****CARD   1-  3,  6,  8- 11, 13, 15- 19, 54, 55, 59, 60, 62
****RFMT 187-204,207,209
$$$$
PARAM    //*MPY*/CARDNO/0/0 $   
****CARD   1-  3,  6,  8- 10, 15, 19
****FILE 101,114,117
$$$$ GP1 generates coordinate system transformation matrices, tables of grid 
$$$$ point locations, and tables relating the internal and external grid point 
$$$$ numbers.
GP1      GEOM1,GEOM2,/GPL,EQEXIN,GPDT,CSTM,BGPDT,HSIL/S,N,HLUSET/  
         NOGPDT/MINUS1=-1 $ 
****CARD   1
****FILE  94
$$$$ PLTTRAN modifies special scalar grid points in the BGPDT and SIL tables.
PLTTRAN  BGPDT,HSIL/BGPDP,HSIP/HLUSET/S,N,HLUSEP $  
****CARD   1
****FILE 113
$$$$ GP2 generates Element Connection Table with internal indices.
GP2      GEOM2,EQEXIN/ECT $
****CARD   1,  2, 16
****FILE  95
$$$$
PARAML   PCDB//*PRES*////JUMPPLOT $ 
****SBST   7
****CARD  18
****FILE 115,118
$$$$
PURGE    HPLTSETX,HPLTPAR,HGPSETS,HELSETS/JUMPPLOT $
****SBST   7
****CARD  18
****FILE 115
$$$$ Go to label HP1 if there are no structure plot requests.
COND     HP1,JUMPPLOT $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 115,118
$$$$ PLTHBDY modifies the data in the ECT, HSIL, HEQEXIN, and BGPDT tables to 
$$$$ permit the plotting of HBDY (thermal boundary) elements.
PLTHBDY  GEOM2,ECT,EPT,HSIL,EQEXIN,BGPDT/PECT,PSIL,PEQEXIN,PBGPDT/ 
         S,N,NHBDY/V,Y,MESH=NO $
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 115
$$$$ Equivalence PECT to ECT, PSIL to HSIL, PEQEXIN to HEQEXIN, and PBGPDT to 
$$$$ BGPDT if there are no HBDY elements.
EQUIV    ECT,PECT/NHBDY/HSIL,PSIL/NHBDY/EQEXIN,PEQEXIN/NHBDY/  
         BGPDT,PBGPDT/NHBDY $   
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 115
$$$$ PLTSET transforms user input into a form used to drive the structure 
$$$$ plotter.
PLTSET   PCDB,PEQEXIN,PECT,/HPLTSETX,HPLTPAR,HGPSETS,HELSETS/S,N,HNSIL/  
         S,N,JUMPPLOT $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 115
$$$$ PRTMSG prints error messages associated with the structure plotter.
PRTMSG   HPLTSETX// $   
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 115
$$$$
PARAM    //*MPY*/PLTFLG/1/1 $   
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 118
$$$$
PARAM    //*MPY*/PFILE/0/0 $
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 118
$$$$ Go to label HP1 if no boundary and structure (heat conduction) element 
$$$$ plots are requested.
COND     HP1,JUMPPLOT $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 118
$$$$ PLOT generates all requested boundary and heat conduction element plots.
PLOT     HPLTPAR,HGPSETS,HELSETS,CASECC,PBGPDT,PEQEXIN,PSIL,,,,,,/  
         PLOTX1/HNSIL/HLUSET/S,N,JUMPPLOT/S,N,PLTFLG/S,N,PFILE $
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 118
$$$$ PRTMSG prints plotter data and engineering data for each plot generated. 
PRTMSG   PLOTX1// $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 118
$$$$
LABEL    HP1 $  
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 115,118
$$$$ GP3 generates applied Static (Heat Flux) Loads Table (HSLT) and Grid
$$$$ Point Temperature Table.
GP3      GEOM3,EQEXIN,GEOM2/HSLT,GPTT/NOGRAV $ 
****CARD   1,  2, 13, 60
****FILE  96
$$$$
SETVAL   //S,N,REPEATH/-1 $ 
****SBST   1,  3
****CARD  22, 23
****FILE 101
$$$$
LABEL    LOOPTOP $  
****SBST   1,  3
****CARD  22, 23
****FILE 101
$$$$ CASE extracts the appropriate record from CASECC corresponding to the 
$$$$ current loop and copies it to CASEXX.
CASE     CASECC,/CASEXX/*TRANRESP*/S,N,REPEATH/S,N,NOLOOP $ 
****CARD  22, 23
****FILE 101
$$$$ PARAML extracts the 8th word in the data record of CASEXX (representing 
$$$$ the thermal material set ID) and stores its value in the parameter 
$$$$ TEMPMATE.
PARAML   CASEXX//*TABLE1*/1/8//TEMPMATE $   
****SBST   1,  3
****CARD  22, 23
****FILE 101
$$$$ PARAM stores the value of the parameter TEMPMATE in the 10th word of 
$$$$ COMMON /SYSTEM/.
PARAM    //*STSR*/TEMPMATE/-10 $
****SBST   1,  3
****CARD  22, 23
****FILE 101
$$$$ TA1 generates element tables for use in matrix assembly, load generation, 
$$$$ and heat flux data recovery.
TA1      ECT,EPT,BGPDT,HSIL,GPTT,CSTM,,EQEXIN/HEST,,HGPECT,,,,,/   
         HLUSET/S,N,NOSIMP/1/NOGENL/GENEL $ 
****CARD   1-  3,  6, 13, 16
****FILE  97
$$$$ Go to label ERROR2 and print Error Message No. 2 if no elements have been 
$$$$ defined.
COND     ERROR2,NOSIMP $
****CARD   1,  2,  6,  8, 16
****FILE  97
****RFMT 187-204,207,209
$$$$
PARAM    //*ADD*/HNOKGG/1/0 $   
****CARD   1-  3,  6,  8
****FILE  98
$$$$ EMG generates heat conduction matrix tables and dictionaries for later 
$$$$ assembly by the EMA module. 
EMG      HEST,CSTM,MPT,DIT,GEOM2,/HKELM,HKDICT,,,,,/S,N,HNOKGG $
****CARD   1-  3,  6,  8
****FILE  98
$$$$
PURGE    HKGG/HNOKGG $  
****CARD   1-  3,  6,  8
****FILE  99
$$$$ Go to label JMPKGGX if no heat conduction matrix is to be assembled.
COND     JMPKGGX,HNOKGG $   
****CARD   1-  3,  6,  8
****FILE  99
$$$$                                        x
$$$$ EMA assembles heat conduction matrix [K  ] and Grid Point Singularity
$$$$                                        gg
$$$$ Table.
EMA      HGPECT,HKDICT,HKELM/HKGGX $
****CARD   1-  3,  6,  8
****FILE  99
$$$$
PURGE    HKDICT,HKELM/MINUS1 $  
****CARD   1-  3,  6,  8
****FILE  98
$$$$
LABEL    JMPKGGX $  
****CARD   1-  3,  6,  8
****FILE  99
$$$$ RMG generates the radiation matrix, [R  ], and adds the estimated linear
$$$$                                       gg
$$$$ component of radiation to the heat conduction matrix. The element 
$$$$ radiation flux matrix, [Q  ], is also generated for use in recovery data
$$$$                          ge
$$$$ for the HBDY elements.
RMG      HEST,MATPOOL,GPTT,HKGGX/HRGG,HQGE,HKGG/C,Y,TABS/C,Y,SIGMA=0.0/ 
         S,N,HNLR/HLUSET $  
****CARD   1-  3,  6,  8, 55
****FILE 100
$$$$               x
$$$$ Equivalence [K  ] to [K  ] if there is no linear component of radiation.
$$$$               gg       gg
$$$$
EQUIV    HKGGX,HKGG/HNLR $  
****CARD   1-  3,  6,  8, 55
****FILE 100
$$$$
GPSTGEN  HKGG,HSIL/GPST $   
****CARD   1-  3,  6,  8, 55
****FILE 102
$$$$
PURGE    HQGE,HRGG/HNLR $   
****CARD   1-  3,  6,  8, 55
****FILE 100
$$$$
PARAM    //*MPY*/NSKIP/0/0 $
****CARD   1-  3,  6,  8- 10, 14, 15, 59
****FILE 101
$$$$ GP4 generates flags defining members of various displacement sets
$$$$ (HUSET), and forms multipoint constraint equations [R ] {u } = 0.
$$$$                                                      g    g
$$$$
GP4      CASEXX,GEOM4,EQEXIN,GPDT,BGPDT,CSTM,GPST/RG,,HUSET,   
         HASET,OGPST/HLUSET/S,N,MPCF1/S,N,MPCF2/S,N,SINGLE/S,N,OMIT/
         S,N,REACT/S,N,NSKIP/REPEATG/S,N,NOSET/S,N,NOL/S,N,NOA/ 
         C,Y,ASETOUT/C,Y,AUTOSPC $  
****CARD   1-  3,  6,  8- 10, 14, 15, 55, 59
****FILE 101
$$$$
OFP      OGPST,,,,,//S,N,CARDNO $   
****CARD   1-  3,  6,  8- 10, 14, 15, 55, 59
****FILE 101
$$$$ Go to label ERROR1 and print Error Message No. 1 if no independent 
$$$$ degrees of freedom are defined.
COND     ERROR1,NOL $   
****CARD   1,  9, 10, 14, 15, 59
****FILE 101
****RFMT 187-204,207,209
$$$$
PURGE    GM/MPCF1/HPS,HKFS,HKSS,HKSF,HRSN,HQG/SINGLE $  
****CARD   1,  9, 10, 59
****FILE 103,105,107,111,112
$$$$ Equivalence [K  ] to [K  ] and [R  ] to [R  ] if no multipoint
$$$$               gg       nn        gg       nn
$$$$ constraints exist.
EQUIV    HKGG,HKNN/MPCF1/HRGG,HRNN/MPCF1 $  
****CARD   1-  3,  6,  8,  9, 13
****FILE 100,104
$$$$ Go to label HLBL1 if no multipoint constraints exist.
COND     HLBL1,MPCF1 $  
****CARD   1-  3,  6,  8,  9, 13
****FILE 103,104
$$$$ MCE1 partitions multipoint constraint equation [R ] = [R |R ] and solves
$$$$                                                  g      m  n
$$$$                                                             -1
$$$$ for multipoint constraint transformation matrix [G ] = -[R ]  [R ].
$$$$                                                   m       m     n
$$$$
MCE1     HUSET,RG/GM $  
****CARD   1,  9
****FILE 103
$$$$ MCE2 partitions heat conduction and radiation matrices
$$$$ 
$$$$           +_      +                        +_      +
$$$$           |K  |K  |                        |R  |R  |
$$$$           | nn| nm|                        | nn| nm|
$$$$   [K  ] = |---+---|                [R  ] = |---+---|
$$$$     gg    |K  |K  |                  gg    |R  |R  |
$$$$           | mn| mm|                        | mn| mm|
$$$$           +       +                        +       +
$$$$ 
$$$$ and performs matrix reductions
$$$$ 
$$$$            _        T           T           T
$$$$   [K  ] = [K  ] + [G ][K  ] + [K  ][G ] + [G ][K  ][G ]
$$$$     nn      nn      m   mn      mn   m      m   mm   m
$$$$
$$$$            _        T           T           T
$$$$   [R  ] = [R  ] + [G ][R  ] + [R  ][G ] + [G ][R  ][G ]
$$$$     nn      nn      m   mn      mn   m      m   mm   m
$$$$
MCE2     HUSET,GM,HKGG,HRGG,,/HKNN,HRNN,, $ 
****CARD   1-  3,  6,  8,  9, 55
****FILE 104
$$$$
LABEL    HLBL1 $
****CARD   1-  3,  6,  8,  9, 55
****FILE 103,104
$$$$ Equivalence [K  ] to [K  ] and [R  ] to [R  ] if no single-point
$$$$               nn       ff        nn       fn
$$$$ constraints exist.
EQUIV    HKNN,HKFF/SINGLE/HRNN,HRFN/SINGLE $
****CARD   1-  3,  6,  8- 10, 55
****FILE 105,107
$$$$ Go to label HLBL2 if no single-point constraints exist.
COND     HLBL2,SINGLE $ 
****CARD   1-  3,  6,  8- 10, 55
****FILE 105,107,116
$$$$ VEC generates a partitioning vector {u } -> {u } + {u }.
$$$$                                       n       g      s
$$$$
VEC      HUSET/VFS/*N*/*F*/*S* $
****CARD   1-  3,  6,  8- 10, 55
****FILE 116
$$$$ PARTN partitions the heat conduction matrix
$$$$ 
$$$$           +       +
$$$$           |K  |K  |
$$$$           | ff| fs|
$$$$   [K  ] = |---+---|
$$$$     nn    |K  |K  |
$$$$           | fs| ss|
$$$$           +       +
$$$$ 
PARTN    HKNN,VFS,/HKFF,HKSF,HKFS,HKSS $
****CARD   1-  3,  6,  8- 10, 55
****FILE 105
$$$$ PARTN partitions the radiation matrix
$$$$
$$$$           +     +
$$$$           | R   |
$$$$           |  fn |
$$$$   [R  ] = | --- |
$$$$     nn    | R   |
$$$$           |  sn |
$$$$           +     +
$$$$
PARTN    HRNN,,VFS/HRFN,HRSN,,/1 $  
****CARD   1-  3,  6,  8- 10, 55
****FILE 107
$$$$
LABEL    HLBL2 $
****CARD   1-  3,  6,  8- 10, 55
****FILE 105,107,116
$$$$ DECOMP decomposes the potentially unsymmetric matrix [K  ] into upper
$$$$                                                        ff
$$$$ and lower triangular factors [U  ] and [L  ].
$$$$                                ll        ll
$$$$
DECOMP   HKFF/HLLL,HULL/0/0/MDIAG/DET/PWR/S,N,KSING $   
****CARD   1-  3,  6,  8- 10, 55
****FILE 108
$$$$ Go to label ERROR3 and print Error Message No. 3 if the matrix is 
$$$$ singular.
COND     ERROR3,KSING $ 
****CARD   1-  3,  6,  8- 10, 55
****FILE 108
$$$$ SSG1 generates the input heat flux vector {P }.
$$$$                                             g
$$$$
SSG1     HSLT,BGPDT,CSTM,HSIL,HEST,MPT,GPTT,EDT,,CASEXX,DIT,/   
         HPG,,,,SCR/HLUSET/NSKIP $  
****CARD   1-  3,  6,  8, 13, 55, 59, 60, 62
****FILE 110
$$$$ Equivalence {P } to {P } if no constraints are applied.
$$$$               g       f
$$$$
EQUIV    HPG,HPF/NOSET $
****CARD   1-  3,  6,  8- 10, 13, 55, 59, 60, 62
****FILE 111
$$$$ Go to label HLBL3 if no constraints of any kind exist.
COND     HLBL3,NOSET $  
****CARD   1-  3,  6,  8- 10, 13, 55, 59, 60, 62
****FILE 111
$$$$ SSG2 reduces the heat flux vector
$$$$ 
$$$$           _
$$$$           P
$$$$            n                               _       T
$$$$   {P } = {--} ,                    {P } = {P } + [G ]{P } ,
$$$$     g     P                          n      n      m   m
$$$$            m
$$$$ 
$$$$           _
$$$$           P
$$$$            f
$$$$   {P } = {--}
$$$$     n     P
$$$$            s
$$$$ 
SSG2     HUSET,GM,,HKFS,,,HPG/,,HPS,HPF  $  
****CARD   1-  3,  6,  8- 10, 13, 55, 59, 60, 62
****FILE 111
$$$$
LABEL    HLBL3 $
****CARD   1-  3,  6,  8- 10, 13, 55, 59, 60, 62
****FILE 111
$$$$ SSGHT solves the nonlinear heat transfer problem by an iteration 
$$$$ technique which is limited by parameters EPSHT and MAXIT. The output data
$$$$ are {u ], the solution temperature vector; {q ], the heat flux due to
$$$$       g                                      g
$$$$ single-point constraints; and {deltaP }, the matrix of residual heat
$$$$                                      l
$$$$ fluxes at each iteration step.
SSGHT    HUSET,HSIL,GPTT,GM,HEST,MPT,DIT,HPF,HPS,HKFF,HKFS,HKSF,
         HKSS,HRFN,HRSN,HLLL,HULL/HUGV,HQG,HRULV/HNNLK=1/HNLR/  
         C,Y,EPSHT=.001/C,Y,TABS=0.0/C,Y,MAXIT=4/V,Y,IRES/  
         MPCF1/SINGLE $ 
****CARD   1-  3,  6,  8- 11, 13, 17, 54, 55, 59, 60, 62
****FILE 112
$$$$ Go to label HLBL4 if residual vectors are not to be printed.
COND     HLBL4,IRES $   
****CARD   1-  3,  6,  8- 11, 13, 17, 54, 55, 59, 60, 62
$$$$ MATGPR prints the residual vectors for independent coordinates (HRULV).
MATGPR   GPL,HUSET,HSIL,HRULV//*F* $
****CARD   1-  3,  6,  8- 11, 13, 17, 54, 55, 59, 60, 62
$$$$
LABEL    HLBL4 $
****CARD   1-  3,  6,  8- 11, 13, 17, 54, 55, 59, 60, 62
****FILE 114
$$$$ SDR2 calculates the heat flux due to conduction and convection in the 
$$$$ elements (HOEF1) and prepares the temperature vector (HOUGV1), the load 
$$$$ vector (HOPG1), and the power of constraint (HOQG1) for output and 
$$$$ components of the temperature vector (HPUGV1).
SDR2     CASEXX,CSTM,MPT,DIT,EQEXIN,HSIL,GPTT,EDT,BGPDP,,HQG,HUGV, 
         HEST,,HPG,/HOPG1,HOQG1,HOUGV1,HOES1,HOEF1,HPUGV1,,/
         *STATICS* $
****CARD  18, 19
****FILE 114
$$$$ OFP formats the tables prepared by SDR2 and places them on the system 
$$$$ output file for printing.
OFP      HOUGV1,HOPG1,HOQG1,,,//S,N,CARDNO $
****CARD  19
****FILE 114
$$$$ SDRHT processes the HBDY elements to produce heat flux into the elements 
$$$$ (HOEF1X) due to convection, radiation, and applied flux.
SDRHT    HSIL,HUSET,HUGV,HOEF1,HSLT,HEST,DIT,HQGE,,/HOEF1X/C,Y,TABS/
         HNLR $ 
****CARD  18, 19
****FILE 117
$$$$ OFP formats the element flux table prepared by SDRHT and places it on the 
$$$$ system output file for printing.
OFP      HOEF1X,,,,,//S,N,CARDNO $  
****CARD  19
****FILE 117
$$$$ Go to label HP2 if no temperature profile plots are requested.
COND     HP2,JUMPPLOT $ 
****SBST   7
****CARD  18
****FILE 119
$$$$ PLTSET transforms user input into a form used to drive the structure 
$$$$ plotter.
PLTSET   PCDB,EQEXIN,ECT/PSMES,DPLTPAR,DGPSETS,DELSETS/S,N,DSIL/DJ $   
****SBST   7
****CARD  18
****FILE 119
$$$$ PLOT generates all requested temperature profile and thermal contour 
$$$$ plots.
PLOT     DPLTPAR,DGPSETS,DELSETS,CASEXX,BGPDT,EQEXIN,HSIP,HPUGV1,, 
         HGPECT,HOES1,,/PLOTX2/DSIL/HLUSEP/JUMPPLOT/PLTFLG/S,N,PFILE $  
****SBST   7
****CARD  18
****FILE 119
$$$$ PRTMSG prints plotter data, engineering data, and contour data for each 
$$$$ temperature profile and thermal plot generated.
PRTMSG   PLOTX2// $ 
****SBST   7
****CARD  18
****FILE 119
$$$$
LABEL    HP2 $  
****SBST   7
****CARD  18
****FILE 119
$$$$ Go to label FINIS and make normal exit if all constraint sets have been 
$$$$ processed.
COND     FINIS,REPEATH $
****SBST   1,  3
****CARD   1-  3,  6,  8- 10, 14, 15, 59
****FILE 101
****RFMT 187-204,207,209
$$$$ Go to label LOOPTOP if additional constraint sets need to be processed.
REPT     LOOPTOP,100 $  
****SBST   1,  3
****CARD   1-  3,  6,  8- 10, 14, 15, 59
****FILE 101
****RFMT 187-204,207,209
$$$$ Go to label FINIS and make normal exit.
JUMP     FINIS $
****CARD   1-  3,  6,  8- 11, 13, 15- 19, 54, 55, 59, 60, 62
****RFMT 187-204,207,209
$$$$
LABEL    ERROR1 $   
****CARD   1,  9, 10, 14, 15, 59
****FILE 101
****RFMT 187-204,207,209
$$$$ Print Error Message No. 1 and terminate execution.
PRTPARM  //-1/*HNLI* $  
****CARD   1,  9, 10, 14, 15, 59
****FILE 101
****RFMT 187-204,207,209
$$$$
LABEL    ERROR2 $   
****CARD   1,  2,  6,  8, 16
****FILE  97
****RFMT 187-204,207,209
$$$$ Print Error Message No. 2 and terminate execution.
PRTPARM  //-2/*HNLI* $  
****CARD   1,  2,  6,  8, 16
****FILE  97
****RFMT 187-204,207,209
$$$$
LABEL    ERROR3 $   
****CARD   1-  3,  6,  8- 10, 55
****FILE 108
****RFMT 187-204,207,209
$$$$ Print Error Message No. 3 and terminate execution.
PRTPARM  //-3/*HNLI* $  
****CARD   1-  3,  6,  8- 10, 55
****FILE 108
****RFMT 187-204,207,209
$$$$
LABEL    FINIS$ 
****CARD   1-  3,  6,  8- 11, 13, 15- 19, 54, 55, 59, 60, 62
****RFMT 187-204,207,209
$$$$
PURGE    DUMMY/MINUS1 $ 
****CARD   1-  3,  6,  8- 11, 13, 15- 19, 54, 55, 59, 60, 62
****RFMT 187-204,207,209
$$$$
END      $  
****CARD   1-  3,  6,  8- 11, 13, 15- 19, 54, 55, 59, 60, 62
$$$$
$*CARD BITS 
  1    CELAS1   CELAS2   CELAS3   CELAS4   CORD1C   CORD1R   CORD1S 
  1    CORD2C   CORD2R   CORD2S   GRDSET   GRID     SEQGP    SPOINT 
  2    ADUM1    ADUM2    ADUM3    ADUM4    ADUM5    ADUM6    ADUM7  
  2    ADUM8
  2    ADUM9    BAROR    CBAR     CDUM1    CDUM2    CDUM3    CDUM4  
  2    CDUM5
  2    CDUM6    CDUM7    CDUM8    CDUM9    CELBOW   CFTUBE   CHBDY  
  2    CHEXA1   
  2    CHEXA2   CIHEX1   CIHEX2   CIHEX3   CIS2D8   CONROD   CQDMEM 
  2    CQDMEM1  CQDMEM2  CQUAD1   CQUAD2   CROD     CTETRA   CTRAPRG
  2    CTRIA1   CTRIA2   CTRIARG  CTRMRM   CQUAD4   CTRIA3
  2    CTUBE    CWEDGE  
  3    PBAR     PDUM1    PDUM2    PDUM3    PDUM4    PDUM5    PDUM6  
  3    PDUM7
  3    PDUM8    PDUM9    PELBOW   PFTUBE   PHBDY    PIHEX    PIS2D8 
  3    PQDMEM   PQDMEM1  PQDMEM2  PQUAD1   PQUAD2   PROD     PTUBE
  3    PTRIA1   PTRIA2   PTRMEM   PSHELL   PCOMP    PCOMP1   PCOMP2
  6    PELAS
  8    MAT1     MAT2     MAT3     MAT4     MAT5     MATT1    MATT2  
  8    MATT3    MATT4    MATT5    MAT8     TABLEM1  TABLEM2  TABLEM3
  8    TABLEM4  TEMPMT$  TEMPMX$  
  9    MPC      MPCADD   MPC$   
 10    SPC      SPC1     SPCADD   SPC$  
 11    IRES 
 13    TEMP     TEMPD    TEMPP1   TEMPP2   TEMPP3   TEMPRB  
 14    ASETOUT  
 15    AUTOSPC  
 16    PLOTEL   
 17    IRES 
 18    PLOT$
 19    POUT$
 22    LOOP$
 23    LOOP1$   
 54    EPSHT    MAXIT   
 55    RADMTX   RADLST   SIGMA    TABS  
 59    LOAD$    SPCD
 60    LOAD     QBDY1    QBDY2    QHBDY    QVECT    QVOL     SLOAD  
 62    TEMPLD$  
$$$$
$*FILE BITS 
 94    BGPDT    CSTM     EQEXIN   GPDT     GPL      HSIL
 95    ECT  
 96    GPTT     HSLT
 97    HEST     HGEI     HGPECT 
 98    HKDICT   HKELM   
 99    HKGGX
100    HRGG     HKGG     HQGE   
101    HASET    RG       HUSET    OGPST 
102    GPST 
103    GM   
104    HKNN     HRNN
105    HKFF     HKFS     HKSF     HKSS  
107    HRFN     HRSN
108    HLLL     HULL
110    HPG  
111    HPF      HPS 
112    HRULV    HQG      HUGV   
113    BGPDP    HSIP
114    HOES1    HOEF1    HOPG1    HOQG1    HOUGV1   HPUGV1  
115    HELSETS  HGPSETS  HPLTPAR  HPLTSETX  
116    VFS  
117    HOEF1X   
118    PLOTX1   
119    PLOTX2   
$*  
=PAGE=
HEAT9

APR.93
$$$$$$$$
BEGIN    HEAT 09 - TRANSIENT HEAT TRANSFER ANALYSIS - APR. 1993 $   
****CARD   1-  3,  6,  8- 23, 25, 55- 57, 59- 62
****RFMT 187-204,207,208
$$$$
PRECHK   ALL $  
****CARD   1-  3,  6,  8- 23, 25, 55- 57, 59- 62
****RFMT 187-204,207,208
$$$$
PARAM    //*MPY*/CARDNO/0/0 $   
****CARD   1-  3,  6,  8- 10, 13, 15, 19, 21, 55- 57, 59- 62
****FILE 101,113,116
$$$$ GP1 generates coordinate system transformation matrices, tables of grid 
$$$$ point locations, and tables relating the internal and external grid point 
$$$$ numbers.
GP1      GEOM1,GEOM2,/GPL,EQEXIN,GPDT,CSTM,BGPDT,HSIL/S,N,HLUSET/  
         S,N,NOGPDT/MINUS1=-1 $ 
****CARD   1
****FILE  94
$$$$ PLTTRAN modifies special scalar grid points in the BGPDT and SIL tables.
PLTTRAN  BGPDT,HSIL/BGPDP,HSIP/HLUSET/S,N,HLUSEP $  
****CARD   1
****FILE 118
$$$$
PURGE    HUSET,GM,HGO,HKAA,HBAA,HPSO,HKFS,HQP,HEST/NOGPDT $ 
****CARD   1
****FILE  97,101,103,105,106,114,117,122
$$$$ Go to label HLBL5 if there is no Grid Point Definition Table.
COND     HLBL5,NOGPDT $ 
****CARD   1-  3,  6,  8- 11, 55, 59
****FILE  95-106,120-124
$$$$ GP2 generates Element Connection Table with internal indices.
GP2      GEOM2,EQEXIN/ECT $
****CARD   1,  2, 16
****FILE  95
$$$$
PARAML   PCDB//*PRES*////JUMPPLOT $ 
****SBST   7
****CARD  18
****FILE 120,124
$$$$
PURGE    PLTSETX,PLTPAR,GPSETS,ELSETS/JUMPPLOT $
****SBST   7
****CARD  18
****FILE 120
$$$$ Go to label HP1 if there are no structure plot requests.
COND     HP1,JUMPPLOT $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 120,124
$$$$ PLTSET transforms user input into a form used to drive the structure 
$$$$ plotter.
PLTSET   PCDB,EQEXIN,ECT,EPT/PLTSETX,PLTPAR,GPSETS,ELSETS/S,N,HNSIL/   
         S,N,JUMPPLOT $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 120
$$$$ PRTMSG prints error messages associated with the structure plotter.
PRTMSG   PLTSETX// $
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 120
$$$$
PARAM    //*MPY*/PLTFLG/1/1 $   
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 124
$$$$
PARAM    //*MPY*/PFILE/0/0 $
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 124
$$$$ Go to label HP1 if no boundary and structure (heat conduction) element 
$$$$ plots are requested.
COND     HP1,JUMPPLOT$  
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 124
$$$$ PLOT generates all requested boundary and heat conduction element plots.
PLOT     PLTPAR,GPSETS,ELSETS,CASECC,BGPDT,EQEXIN,HSIL,,ECT,,,,/PLOTX1/
         HNSIL/HLUSET/S,N,JUMPPLOT/S,N,PLTFLG/S,N,PFILE $   
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 124
$$$$ PRTMSG prints plotter data and engineering data for each plot generated. 
PRTMSG   PLOTX1// $ 
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 124
$$$$
LABEL    HP1 $  
****SBST   7
****CARD   1,  2,  4,  5, 16, 18
****FILE 120,124
$$$$ GP3 generates applied Static (Heat Flux) Loads Table (HSLT) and Grid
$$$$ Point Temperature Table.
GP3      GEOM3,EQEXIN,GEOM2/HSLT,GPTT/1 $  
****CARD   1,  2, 13
****FILE  96
$$$$ TA1 generates element tables for use in matrix assembly, load generation, 
$$$$ and heat flux data recovery.
TA1      ECT,EPT,BGPDT,HSIL,GPTT,CSTM,,EQEXIN/HEST,,HGPECT,,,,,/   
         HLUSET/S,N,NOSIMP=-1/1/123/123 $   
****CARD   1-  3,  6, 13, 16, 59
****FILE  97
$$$$
PURGE    HKGG,HBGG/NOSIMP $ 
****CARD   1-  3,  6,  8, 59
****FILE  98, 99,123
$$$$ Go to label HLBL1 if no heat conduction or boundary elements exist.
COND     HLBL1,NOSIMP $ 
****CARD   1-  3,  6,  8, 59
****FILE  98, 99,123
$$$$
PARAM    //*ADD*/NOKGGX/1/0 $   
****CARD   1-  3,  6,  8
****FILE 123
$$$$
PARAM    //*ADD*/NOBGG/1/0 $
****CARD   1-  3,  8, 59
****FILE 123
$$$$ EMG generates element heat conduction and capacitance matrix tables and 
$$$$ dictionaries for later assembly by the EMA module. 
EMG      HEST,CSTM,MPT,DIT,GEOM2,/HKELM,HKDICT,,,HBELM,HBDICT,/S,N, 
         NOKGGX//S,N,NOBGG $
****CARD   1-  3,  6,  8, 59
****FILE 123
$$$$
PURGE    HKGGX/NOKGGX $ 
****CARD   1-  3,  6,  8
****FILE  98
$$$$ Go to label JMPKGGX if no heat conduction matrix is to be assembled.
COND     JMPKGGX,NOKGGX $   
****CARD   1-  3,  6,  8
****FILE  98
$$$$                                        x
$$$$ EMA assembles heat conduction matrix [K  ] and Grid Point Singularity
$$$$                                        gg
$$$$ Table.
EMA      HGPECT,HKDICT,HKELM/HKGGX $
****CARD   1-  3,  6,  8
****FILE  98
$$$$
PURGE    HKDICT,HKELM/MINUS1 $  
****CARD   1-  3,  6,  8
****FILE 123
$$$$
LABEL    JMPKGGX $  
****CARD   1-  3,  6,  8
****FILE  98
$$$$ Go to label JMPHBGG if no heat capacitance matrix is to be assembled.
COND     JMPHBGG,NOBGG $
****CARD   1-  3,  8, 59
****FILE  99
$$$$ EMA assembles heat capacitance matrix [B  ].
$$$$                                         gg
$$$$
EMA      HGPECT,HBDICT,HBELM/HBGG $ 
****CARD   1-  3,  8, 59
****FILE  99
$$$$
PURGE    HBDICT,HBELM/MINUS1 $  
****CARD   1-  3,  8, 59
****FILE 123
$$$$
LABEL    JMPHBGG $  
****CARD   1-  3,  8, 59
****FILE  99
$$$$
PURGE    HBNN,HBFF,HBAA,HBGG/NOBGG $
****CARD   1-  3,  8, 59
****FILE  99,104,105,122
$$$$
LABEL    HLBL1 $
****CARD   1-  3,  6,  8, 59
****FILE  98, 99,123
$$$$ RMG generates the radiation matrix, [R  ], and adds the estimated linear
$$$$                                       gg
$$$$ component of radiation to the heat conduction matrix. The element 
$$$$ radiation flux matrix, [Q  ], is also generated for use in data recovery.
$$$$                          ge
$$$$
RMG      HEST,MATPOOL,GPTT,HKGGX/HRGG,HQGE,HKGG/C,Y,TABS/C,Y,SIGMA=0.0/ 
         S,N,HNLR/HLUSET $  
****CARD   1-  3,  6,  8, 55
****FILE 100
$$$$               x
$$$$ Equivalence [K  ] to [K  ] if there is no linear component of radiation.
$$$$               gg       gg
$$$$
EQUIV    HKGGX,HKGG/HNLR $  
****CARD   1-  3,  6,  8, 55
****FILE 100
$$$$
GPSTGEN  HKGG,HSIL/GPST $   
****CARD   1-  3,  6,  8, 55
****FILE 102
$$$$
PURGE    HRGG,HRNN,HRFF,HRAA,HRDD/HNLR $
****CARD   1-  3,  6,  8, 55
****FILE 100,104,105,110,121
$$$$ GP4 generates flags defining members of various displacement sets
$$$$ (HUSET), and forms multipoint constraint equations [R ] {u } = 0.
$$$$                                                      g    g
$$$$
GP4      CASECC,GEOM4,EQEXIN,GPDT,BGPDT,CSTM,GPST/RG,,HUSET,   
         ASET,OGPST/HLUSET/S,N,MPCF1/S,N,MPCF2/S,N,SINGLE/S,N,OMIT/ 
         S,N,REACT/0/123/S,N,NOSET/S,N,NOL/S,N,NOA/ 
         C,Y,ASETOUT/C,Y,AUTOSPC $  
****CARD   1-  3,  6,  8- 11, 14, 15, 55
****FILE 101
$$$$ OFP formats a table of potential grid point singularities prepared by 
$$$$ GPSP and places it on the system output file for printing.
OFP      OGPST,,,,,//S,N,CARDNO $   
****CARD   1-  3,  6,  8- 11, 14, 15, 55
****FILE 101
$$$$
PURGE    GM,GMD/MPCF1/HGO,HGOD/OMIT/HKFS,HPSO,HQP/SINGLE $  
****CARD   1,  9- 11
****FILE 103,105,106,110,114,117
$$$$ Equivalence [K  ] to [K  ], [R  ] to [R  ], and [B  ] to [B  ]
$$$$               gg       nn     gg       nn         gg       nn
$$$$ if no multipoint constraints exist.
EQUIV    HKGG,HKNN/MPCF1/HRGG,HRNN/MPCF1/HBGG,HBNN/MPCF1 $  
****CARD   1-  3,  6,  8,  9
****FILE 104
$$$$ Go to label HLBL3 if no multipoint constraints exist.
COND     HLBL3,MPCF1 $  
****CARD   1-  3,  6,  8,  9, 55, 59
****FILE 103,104
$$$$ MCE1 partitions multipoint constraint equation [R ] = [R |R ] and solves
$$$$                                                  g      m  n
$$$$                                                             -1
$$$$ for multipoint constraint transformation matrix [G ] = -[R ]  [R ].
$$$$                                                   m       m     n
$$$$
MCE1     HUSET,RG/GM $  
****CARD   1,  9
****FILE 103
$$$$ MCE2 partitions heat conduction and radiation matrices
$$$$ 
$$$$           +_      +                        +_      +
$$$$           |K  |K  |                        |R  |R  |
$$$$           | nn| nm|                        | nn| nm|
$$$$   [K  ] = |---+---|                [R  ] = |---+---|
$$$$     gg    |K  |K  |                  gg    |R  |R  |
$$$$           | mn| mm|                        | mn| mm|
$$$$           +       +                        +       +
$$$$ 
$$$$           +_      +
$$$$           |B  |B  |
$$$$           | nn| nm|
$$$$   [B  ] = |---+---|
$$$$     gg    |B  |B  |
$$$$           | mn| mm|
$$$$           +       +
$$$$ 
$$$$ and performs matrix reductions
$$$$ 
$$$$            _        T           T           T
$$$$   [K  ] = [K  ] + [G ][K  ] + [K  ][G ] + [G ][K  ][G ]
$$$$     nn      nn      m   mn      mn   m      m   mm   m
$$$$
$$$$            _        T           T           T
$$$$   [R  ] = [R  ] + [G ][R  ] + [R  ][G ] + [G ][R  ][G ]
$$$$     nn      nn      m   mn      mn   m      m   mm   m
$$$$
$$$$            _        T           T           T
$$$$   [B  ] = [B  ] + [G ][B  ] + [B  ][G ] + [G ][B  ][G ]
$$$$     nn      nn      m   mn      mn   m      m   mm   m
$$$$
MCE2     HUSET,GM,HKGG,HRGG,HBGG,/HKNN,HRNN,HBNN, $ 
****CARD   1-  3,  6,  8,  9, 55, 59
****FILE 104
$$$$
LABEL    HLBL3 $
****CARD   1-  3,  6,  8,  9, 55, 59
****FILE 103,104
$$$$ Equivalence [K  ] to [K  ], [B  ] to [B  ], and [R  ] to [R  ]
$$$$               nn       ff     nn       ff         nn       fn
$$$$ if no single-point constraints exist.
EQUIV    HKNN,HKFF/SINGLE/HRNN,HRFF/SINGLE/HBNN,HBFF/SINGLE $   
****CARD   1-  3,  6,  8- 10, 55, 59
****FILE 105
$$$$ Go to label HLBL4 if no single-point constraints exist.
COND     HLBL4,SINGLE $ 
****CARD   1-  3,  6,  8- 10, 55, 59
****FILE 105
$$$$ SCE1 partitions the matrices as follows:
$$$$ 
$$$$           +_      +
$$$$           |K  |K  |
$$$$           | ff| fs|
$$$$   [K  ] = |---+---|
$$$$     nn    |K  |K  |
$$$$           | sf| ss|
$$$$           +       +
$$$$ 
$$$$ [R  ] and [B  ] are partitioned in the same manner, except that only the
$$$$   nn        nn
$$$$ ff partitions are saved.
SCE1     HUSET,HKNN,HRNN,HBNN,/HKFF,HKFS,,HRFF,HBFF, $  
****CARD   1-  3,  6,  8- 10, 55, 59
****FILE 105
$$$$
LABEL    HLBL4 $
****CARD   1-  3,  6,  8- 10, 55, 59
****FILE 105
$$$$ Equivalence [K  ] to [K  ] if no omitted coordinates exist.
$$$$               ff       aa
$$$$
EQUIV    HKFF,HKAA/OMIT $   
****CARD   1-  3,  6,  8- 11
****FILE 106
$$$$ Equivalence [R  ] to [R  ] if no omitted coordinates exist.
$$$$               ff       aa
$$$$
EQUIV    HRFF,HRAA/OMIT $   
****CARD   1-  3,  8,  9, 55
****FILE 121
$$$$ Equivalence [B  ] to [B  ] if no omitted coordinates exist.
$$$$               ff       aa
$$$$
EQUIV    HBFF,HBAA/OMIT $   
****CARD   1-  3,  8,  9, 59
****FILE 122
$$$$ Go to label HLBL5 if no omitted coordinates exist.
COND     HLBL5,OMIT $   
****CARD   1-  3,  6,  8- 11, 55, 59
****FILE 106,121,122
$$$$ SMP1 partitions heat conduction matrix
$$$$
$$$$           +_      +
$$$$           |K  |K  |
$$$$           | aa| ao|
$$$$   [K  ] = |---+---|
$$$$     ff    |K  |K  |
$$$$           | oa| oo|
$$$$           +       +
$$$$           
$$$$                                               -1
$$$$ solves for transformation matrix [G ] = -[K  ]  [K  ]
$$$$                                    o       oo     oa
$$$$ 
$$$$                                        _
$$$$ and performs matrix reduction [K  ] = [K  ] + [K  ][G ].
$$$$                                 aa      aa      oa   o
$$$$
SMP1     HUSET,HKFF,,,/HGO,HKAA,HKOO,HLOO,,,,, $
****CARD   1-  3,  6,  8- 11, 55
****FILE 106
$$$$ Go to label HLBLR if no radiation matrix exists.
COND     HLBLR,HNLR $   
****CARD   1-  3,  6,  8- 11, 55
****FILE 121
$$$$ SMP2 partitions constrained radiation matrix
$$$$
$$$$           +       +
$$$$           |_      |
$$$$           |R  |R  |
$$$$           | aa| ao|
$$$$   [R  ] = |---+---|
$$$$     ff    |   |   |
$$$$           |R  |R  |
$$$$           | oa| oo|
$$$$           +       +
$$$$ 
$$$$ and performs matrix reduction
$$$$
$$$$            _        T           T           T
$$$$   [R  ] = [R  ] + [R  ][G ] + [G ][R  ] + [G ][R  ][G ]
$$$$     aa      aa      oa   o      o   oa      o   oo   o
$$$$
SMP2     HUSET,HGO,HRFF/HRAA $  
****CARD   1-  3,  6,  8- 11, 55
****FILE 121
$$$$
LABEL    HLBLR $
****CARD   1-  3,  6,  8- 11, 55
****FILE 121
$$$$ Go to label HLBL5 if no heat capacitance matrix exists.
COND     HLBL5,NOBGG $  
****CARD   1-  3,  6,  8- 11, 59
****FILE 122
$$$$ SMP2 partitions reduced heat capacitance matrix
$$$$
$$$$           +       +
$$$$           |_      |
$$$$           |B  |B  |
$$$$           | aa| ao|
$$$$   [B  ] = |---+---|
$$$$     ff    |   |   |
$$$$           |B  |B  |
$$$$           | oa| oo|
$$$$           +       +
$$$$ 
$$$$ and performs matrix reduction
$$$$
$$$$            _        T           T           T
$$$$   [B  ] = [B  ] + [B  ][G ] + [G ][B  ] + [G ][B  ][G ]
$$$$     aa      aa      oa   o      o   oa      o   oo   o
$$$$
SMP2     HUSET,HGO,HBFF/HBAA $  
****CARD   1-  3,  6,  8- 11, 59
****FILE 122
$$$$
LABEL    HLBL5 $
****CARD   1-  3,  6,  8- 11, 55, 59
****FILE  95-106,120-124
$$$$ DPD generates the table defining the displacement sets each degree of 
$$$$ freedom belongs to (HUSETD), including extra points. It prepares the 
$$$$ Transfer Function Pool, the Dynamics Load Table, the Nonlinear Function 
$$$$ Table, and the Transient Response List.
DPD      DYNAMICS,GPL,HSIL,HUSET/GPLD,HSILD,HUSETD,TFPOOL,HDLT,,,   
         HNLFT,HTRL,,HEQDYN/HLUSET/S,N,HLUSETD/123 /S,N,NODLT/  
         123/123/S,N,NONLFT/S,N,NOTRL/123//S,N,NOUE $   
****CARD   1,  9- 11, 57, 60- 62
****FILE 107
$$$$ Go to label ERROR1 and print Error Message No. 1 if there is no Transient 
$$$$ Response List.
COND     ERROR1,NOTRL $ 
****CARD   1, 57, 61
****FILE 107
$$$$                       d                d
$$$$ Equivalence [G ] to [G ] and [G ] to [G ] if no extra points were 
$$$$               o       o        m       m
$$$$ defined.
EQUIV    HGO,HGOD/NOUE/GM,GMD/NOUE $
****CARD   1, 57, 61
****FILE 110
$$$$
PURGE    HPPO,HPSO,HPDO,HPDT/NODLT $
****CARD   1, 57, 61
****FILE 107
$$$$                                            2         2
$$$$ MTRXIN selects the direct input matrices [K  ] and [B  ].
$$$$                                            pp        pp
$$$$
MTRXIN   CASECC,MATPOOL,HEQDYN,,TFPOOL/HK2PP,,HB2PP/HLUSETD/
         S,N,NOK2PP/123/S,N,NOB2PP $
****CARD   1, 57, 60
****FILE 109
$$$$
PARAM    //*AND*/KDEKA/NOUE/NOK2PP $
****CARD   1, 57, 60
****FILE 109
$$$$
PURGE    HK2DD/NOK2PP/HB2DD/NOB2PP $
****CARD   1-  3,  6,  8- 11, 57, 59, 60
****FILE 110
$$$$                        1
$$$$ Equivalence [K  ] to [K  ] if there are no direct input stiffness
$$$$               aa       dd
$$$$                                          2                  2
$$$$ matrices and no extra points; [b  ] to [B  ] and [K  ] to [K  ] if only
$$$$                                 pp       dd        pp       dd
$$$$ extra points are used; and [R  ] to [R  ] if no extra points are used.
$$$$                              aa       dd
$$$$
EQUIV    HKAA,HKDD/KDEKA/HB2PP,HB2DD/NOA/HK2PP,HK2DD/NOA/HRAA,HRDD/ 
         NOUE $ 
****CARD   1-  3,  6,  8- 11, 57, 59, 60
****FILE 110
$$$$ Go to label HLBL6 if there is no Grid Point Definition Table.
COND     HLBL6,NOGPDT $ 
****CARD   1-  3,  6,  8- 11, 17, 57, 59, 60
****FILE 110
$$$$ GKAD expands the matrices to include extra points and assembles heat 
$$$$ conduction, capacitance, and radiation matrices for use in the transient 
$$$$ analysis:
$$$$ 
$$$$            +       +
$$$$            |K  |   |
$$$$     1      | aa| 0 |
$$$$   [K  ] =  |---+---|
$$$$     dd     | 0 | 0 |
$$$$            |   |   |
$$$$            +       +
$$$$ 
$$$$            +       +
$$$$            |B  |   |
$$$$     1      | aa| 0 |
$$$$   [B  ] =  |---+---|
$$$$     dd     | 0 | 0 |
$$$$            |   |   |
$$$$            +       +
$$$$ 
$$$$            +       +
$$$$            |R  |   |
$$$$            | aa| 0 |
$$$$   [R  ] =  |---+---|
$$$$     dd     | 0 | 0 |
$$$$            |   |   |
$$$$            +       +
$$$$ 
$$$$ and
$$$$ 
$$$$             1       2
$$$$   [K  ] = [K  ] + [K  ]
$$$$     dd      dd      dd
$$$$ 
$$$$             1       2
$$$$   [B  ] = [B  ] + [B  ]
$$$$     dd      dd      dd
$$$$ 
$$$$ Nonzero values of the parameters W4, G, and W3 (see the PARAM bulk data 
$$$$ card) are not recommended for use in heat transfer analysis and therefore
$$$$ do not appear in the above equations.
GKAD     HUSETD,GM,HGO,HKAA,HBAA,HRAA,,HK2PP,,HB2PP/HKDD,HBDD,  
         HRDD,GMD,HGOD,HK2DD,,HB2DD/*TRANRESP*/*DISP*/  
         *DIRECT*/C,Y,G=0.0/C,Y,W3=0.0/C,Y,W4=0.0/NOK2PP/-1/
         NOB2PP/MPCF1/SINGLE/OMIT/NOUE/ 
         -1/NOBGG/NOSIMP/-1 $   
****CARD   1-  3,  6,  8- 11, 17, 57, 59, 60
****FILE 110
$$$$
LABEL    HLBL6 $
****CARD   1-  3,  6,  8- 11, 17, 57, 59, 60
****FILE 110
$$$$               2                  2
$$$$ Equivalence [K  ] to [K  ] and [B  ] to [B  ] if no matrices were
$$$$               dd       dd        dd       dd
$$$$ generated from the element heat conduction and capacitance assemblers.
EQUIV    HK2DD,HKDD/NOSIMP/HB2DD,HBDD/NOGPDT $  
****CARD   1-  3,  6,  8- 11, 57, 59, 60
****FILE 110
$$$$
PARAM    //*MPY*/REPEATT/1/-1 $ 
****CARD   1-  3,  6,  8- 11, 57, 59, 60
****FILE 110
$$$$ Beginning of loop for additional dynamic load sets.
LABEL    HLBL10 $   
****SBST   1,  3
****RFMT 187-204,207,208
$$$$ CASE extracts the appropriate record from CASECC corresponding to the 
$$$$ current loop and copies it into CASEXX.
CASE     CASECC,/CASEXX/*TRAN*/S,N,REPEATT/S,N,NOLOOP $ 
****CARD   1-  3,  6,  8- 11, 13, 16, 19, 21- 23, 25, 55- 57, 59- 62
****FILE 108
$$$$                                                           o     o
$$$$ TRLG generates matrices of heat flux loads versus time. {P }, {P }, and
$$$$                                                           p     s
$$$$   o                                                        t
$$$$ {P } are generated with one column per output time step. {P } is 
$$$$   d                                                        d
$$$$ generated with one column per solution time step, and the Transient
$$$$ Output List is a list of output time steps.
TRLG     CASEXX,HUSETD,HDLT,HSLT,BGPDT,HSIL,CSTM,HTRL,DIT,GMD,HGOD,,
         HEST,,/HPPO,HPSO,HPDO,HPDT,,HTOL/S,N,NOSET $   
****CARD   1-  3,  6,  8- 11, 55, 57, 61
****FILE 117
$$$$               o       o
$$$$ Equivalence {P } to {P } if the d and p sets are the same.
$$$$               p       d
$$$$
EQUIV    HPPO,HPDO/NOSET $  
****CARD   1-  3,  6,  8- 11, 55, 57, 61
****FILE 117
$$$$ TRHT integrates the equation of motion:
$$$$
$$$$         .
$$$$   [B  ]{u} + [K  ]{u} = {P } + {N }
$$$$     dd         dd         d      d
$$$$
$$$$ where
$$$$
$$$$   {u} is a vector of the temperatures at any time
$$$$    .
$$$$   {u} is the time derivative of {u} ("velocity")
$$$$
$$$$   {P } is the applied heat flux at any time step
$$$$     d
$$$$
$$$$   {N } is the total nonlinear heat flux from radiation and/or NOLINi
$$$$     d  data, extrapolated from the previous solution vector
$$$$
$$$$                              t
$$$$ The output consists of the [u ] matrix containing temperature vectors
$$$$                              d
$$$$ and temperature "velocity" vectors for the output time steps.
TRHT     CASEXX,HUSETD,HNLFT,DIT,GPTT,HKDD,HBDD,HRDD,HPDT,HTRL/ 
         HUDVT,HPNLD/C,Y,BETA=.55/C,Y,TABS=0.0/HNLR/C,Y,RADLIN=-1/  
         C,Y,SIGMA=0.0 $
****CARD   1-  3,  6,  8- 11, 13, 55- 57, 59- 62
****FILE 111
$$$$ VDR prepares the solution set temperatures, temperature "velocities", and 
$$$$ nonlinear loads, sorted by time step, for output.
VDR      CASEXX,HEQDYN,HUSETD,HUDVT,HTOL,XYCDB,HPNLD/HOUDV1,HOPNL1/ 
         *TRANRESP*/*DIRECT*/0/S,N,NOD/S,N,NOP/0 $  
****CARD  13, 19- 21, 27, 55- 57, 59- 62
****FILE 112
$$$$ Go to label HLBL7 if there is no output request for the solution set.
COND     HLBL7,NOD $
****CARD  13, 21, 27, 55- 57, 59- 62
****FILE 113,128
$$$$ SDR3 prepares the requested output of the solution set temperatures, 
$$$$ temperature "velocities", and nonlinear loads sorted by point number or 
$$$$ element number.
SDR3     HOUDV1,HOPNL1,,,,/HOUDV2,HOPNL2,,,, $  
****CARD  13, 21, 27, 55- 57, 59- 62
****FILE 113
$$$$ OFP formats the tables prepared by SDR3 for output sorted by point number 
$$$$ or element number and places them on the system output file for printing.
OFP      HOUDV2,HOPNL2,,,,//S,N,CARDNO $
****CARD  13, 21, 55- 57, 59- 62
****FILE 113
$$$$ XYTRAN prepares the input for requested X-Y plots of the solution set 
$$$$ quantities.
XYTRAN   XYCDB,HOUDV2,HOPNL2,,,/HXYPLTTA/*TRAN*/*DSET*/S,N,HPFILE/  
         S,N,HCARDNO $  
****SBST   7
****CARD  27
****FILE 128
$$$$ XYPLOT prepares the requested X-Y plots of the solution set temperatures, 
$$$$ "velocities", and nonlinear loads versus time.
XYPLOT   HXYPLTTA// $   
****SBST   7
****CARD  27
****FILE 128
$$$$
LABEL    HLBL7 $
****CARD  21, 27
****FILE 113,128
$$$$
PARAM    //*AND*/PJUMP/NOP/JUMPPLOT $   
****CARD   1-  3,  6,  8- 11, 22, 23, 57, 59- 62
****FILE 114
****RFMT 187-204,207,208
$$$$ Go to label HLBL9 if no further output is requested.
COND     HLBL9,PJUMP $  
****CARD   1-  3,  6,  8- 11, 18- 20, 22, 23, 55- 57, 59- 62
****FILE 114-116,125-127
****RFMT 187-204,207,208
$$$$ Equivalence {u } to {u } if no structure points were input.
$$$$               d       p
$$$$
EQUIV    HUDVT,HUPV/NOA $   
****CARD   1-  3,  6,  8- 11, 22, 23, 57, 59- 62
****FILE 114
****RFMT 187-204,207,208
$$$$ Go to label HLBL8 if no structure points were input.
COND     HLBL8,NOA $
****CARD   1-  3,  6,  8- 11, 22, 23, 57, 59- 62
****FILE 114
****RFMT 187-204,207,208
$$$$ SDR1 recovers dependent temperatures
$$$$ 
$$$$    u            
$$$$     d                                       d
$$$$   {--} = {u }  ,                   {u } = [G ]{u } ,
$$$$    u       f                         o      o   d
$$$$     o           
$$$$ 
$$$$    u +u
$$$$     f  e
$$$$   {-----} = {u }  ,
$$$$      u        n
$$$$       s
$$$$                                  
$$$$                                     u +u         
$$$$           d                          n  e          
$$$$  {u } = [G ]{u +u } ,              {-----} = {u }  
$$$$    m      m   f  e                    u        p   
$$$$                                        m           
$$$$                                                    
$$$$ The module also recovers the heat flux into the points having single-
$$$$ point constraints:
$$$$ 
$$$$                    T
$$$$   {q } = -{P } + [K  ]{u }
$$$$     s       s      fs   f
$$$$
SDR1     HUSETD,,HUDVT,,,HGOD,GMD,HPSO,HKFS,,/HUPV,,HQP/1/  
         *DYNAMICS* $   
****CARD   1-  3,  6,  8- 11, 22, 23, 57, 59- 62
****FILE 114
****RFMT 187-204,207,208
$$$$
LABEL    HLBL8 $
****CARD   1-  3,  6,  8- 11, 22, 23, 57, 59- 62
****FILE 114
****RFMT 187-204,207,208
$$$$ SDR2 calculates requested heat flux transfer in the elements and prepares 
$$$$ temperatures, "velocities", and heat flux loads for output sorted by time 
$$$$ step.
SDR2     CASEXX,CSTM,MPT,DIT,HEQDYN,HSILD,,,BGPDP,HTOL,HQP,HUPV,HEST,   
         XYCDB,HPPO,/HOPP1,HOQP1,HOUPV1,HOES1,HOEF1,HPUGV,,/
         *TRANRESP* $   
****CARD  18- 20
****FILE 115
$$$$ SDRHT modifies the HOEF1 data block by combining the heat flow data from 
$$$$ different sources for the HBDY elements and writes the results on the 
$$$$ HOEF1X output data block.
SDRHT    HSILD,HUSETD,HUPV,HOEF1,HSLT,HEST,DIT,HQGE,HDLT,/HOEF1X/C,Y,   
         TABS/HNLR $
****CARD  18- 20
****FILE 125
$$$$ Equivalence HOEF1 data block to HOEF1X data block.
EQUIV    HOEF1X,HOEF1/MINUS1 $  
****CARD  18- 20
****FILE 125
$$$$ SDR3 prepares requested output sorted by point number or element number.
SDR3     HOPP1,HOQP1,HOUPV1,HOES1,HOEF1,/HOPP2,HOQP2,HOUPV2,HOES2,  
         HOEF2, $   
****CARD  18- 20
****FILE 116
$$$$ OFP formats the tables prepared by SDR3 for output and places them on the 
$$$$ system output file for printing.
OFP      HOPP2,HOQP2,HOUPV2,HOEF2,HOES2,//S,N,CARDNO $  
****CARD  19
****FILE 116
$$$$ Go to label HP2 if no temperature profile plots are requested.
COND     HP2,JUMPPLOT $ 
****SBST   7
****CARD  18
****FILE 126
$$$$ PLOT generates all requested temperature profile plots and thermal 
$$$$ contours for specified times.
PLOT     PLTPAR,GPSETS,ELSETS,CASEXX,BGPDT,EQEXIN,HSIP,,HPUGV, 
         HGPECT,,,/PLOTX2/HNSIL/HLUSEP/JUMPPLOT/PLTFLG/ 
         S,N,PFILE $
****SBST   7
****CARD  18
****FILE 126
$$$$ PRTMSG prints plotter data, engineering data, and contour data for each 
$$$$ temperature profile and thermal contour plot generated.
PRTMSG   PLOTX2// $ 
****SBST   7
****CARD  18
****FILE 126
$$$$
LABEL    HP2 $  
****SBST   7
****CARD  18
****FILE 126
$$$$ XYTRAN prepares the input for requested X-Y plots.
XYTRAN   XYCDB,HOPP2,HOQP2,HOUPV2,HOES2,HOEF2/HXYPLTT/*TRAN*/*PSET*/S,N,
         PFILE/S,N,CARDNO $ 
****SBST   7
****CARD  20
****FILE 127
$$$$ XYPLOT prepares the requested X-Y plots of temperatures, "velocities", 
$$$$ element flux, and applied heat loads versus time.
XYPLOT   HXYPLTT// $
****SBST   7
****CARD  20
****FILE 127
$$$$
LABEL    HLBL9 $
****CARD  20
****FILE 114-116,125-127
$$$$ Go to label FINIS and make normal exit if no additional dynamic load sets 
$$$$ need to be processed.
COND     FINIS,REPEATT $
****SBST   1,  3
****CARD   1-  3,  6,  8- 23, 25, 55- 57, 59- 62
****RFMT 187-204,207,208
$$$$ Go to label HLBL10 if additional dynamic load sets need to be processed.
REPT     HLBL10,100 $   
****SBST   1,  3
****CARD  22, 23
****RFMT 187-204,207,208
$$$$ Print Error Message No. 2 and terminate execution.
PRTPARM  //-2/*HTRD* $  
****SBST   1,  3
****CARD  22, 23
****RFMT 187-204,207,208
$$$$ Go to label FINIS and make normal exit.
JUMP     FINIS $
****CARD   1-  3,  6,  8- 23, 25, 55- 57, 59- 62
****RFMT 187-204,207,208
$$$$
LABEL    ERROR1 $   
****CARD   1, 57, 61
****FILE  97
****RFMT 187-204,207,208
$$$$ Print Error Message No. 1 and terminate execution.
PRTPARM  //-1/*HTRD* $  
****CARD   1, 57, 61
****FILE  97
****RFMT 187-204,207,208
$$$$
LABEL    FINIS$ 
****CARD   1-  3,  6,  8- 23, 25, 55- 57, 59- 62
****RFMT 187-204,207,208
$$$$
PURGE    DUMMY/MINUS1 $ 
****CARD   1-  3,  6,  8- 23, 25, 55- 57, 59- 62
****RFMT 187-204,207,208
$$$$
END      $  
****CARD   1-  3,  6,  8- 23, 25, 55- 57, 59- 62
****RFMT 187-204,207,208
$$$$
$*CARD BITS 
  1    CDAMP1   CDAMP2   CDAMP3   CDAMP4   CELAS1   CELAS2   CELAS3 
  1    CELAS4   
  1    CORD1C   CORD1R   CORD1S   CORD2C   CORD2R   CORD2S   GRDSET 
  1    GRID     SEQGP    SPOINT 
  2    ADUM1    ADUM2    ADUM3    ADUM4    ADUM5    ADUM6    ADUM7  
  2    ADUM8
  2    ADUM9    BAROR    CBAR     CDUM1    CDUM2    CDUM3    CDUM4  
  2    CDUM5
  2    CDUM6    CDUM7    CDUM8    CDUM9    CELBOW   CFTUBE   CHBDY  
  2    CHEXA1   
  2    CHEXA2   CIHEX1   CIHEX2   CIHEX3   CIS2D8   CONROD   CQDMEM 
  2    CQDMEM1  CQDMEM2  CQUAD1   CQUAD2   CROD     CTETRA   CTRAPRG
  2    CTRIA1   CTRIA2   CTRIARG  CTRMEM   CQUAD4   CTRIA3
  2    CTUBE    CWEDGE  
  3    PBAR     PDUM1    PDUM2    PDUM3    PDUM4    PDUM5    PDUM6  
  3    PDUM7
  3    PDUM8    PDUM9    PELBOW   PFTUBE   PHBDY    PIHEX    PIS2D8 
  3    PQDMEM   PQDMEM1  PQDMEM2  PQUAD1   PQUAD2   PROD     PTUBE
  3    PTRIA1   PTRIA2   PTRMEM   PSHELL   PCOMP    PCOMP1   PCOMP2
  6    PELAS
  8    MAT1     MAT2     MAT3     MAT4     MAT5     MATT1    MATT2  
  8    MATT3    MATT4    MATT5    MAT8     TABLEM1  TABLEM2  TABLEM3
  8    TABLEM4  TEMPMT$  TEMPMX$  
  9    MPC      MPCADD   MPC$   
 10    SPC      SPC1     SPCADD   SPC$  
 11    ASET     ASET1    OMIT     OMIT1    SUPAX    SUPORT  
 13    TEMP     TEMPD    TEMPP1   TEMPP2   TEMPP3   TEMPRB  
 14    ASETOUT  
 15    AUTOSPC  
 16    PLOTEL   
 17    G        W3       W4 
 18    PLOT$
 19    POUT$
 20    XYOUT$   
 21    AOUT$
 22    LOOP$
 23    LOOP1$   
 25    NOLOOP$  
 27    AXYOUT$  
 55    RADMTX   RADLST   SIGMA    TABS     TREF 
 56    QBDY1    QBDY2    QVECT    QHBDY    QVOL     LOAD     SLOAD  
 57    EPOINT   SEQEP    TF 
 59    PDAMP
 60    DMIG     B2PP$    K2PP$    TF$   
 61    DAREA    DELAY    DLOAD    DLOAD$   TABLED1  TABLED2  TABLED3
 61    TABLED4  TSTEP$   TLOAD1   TLOAD2   TSTEP
 62    BETA     IC$      NLFORCE  NOLIN1   NOLIN2   NOLIN3   NOLIN4 
 62    NOLIN5   NOLIN6   RADLIN   NFTUBE   TIC  
$$$$
$*FILE BITS 
 94    BGPDT    CSTM     EQEXIN   GPDT     GPL      HSIL
 95    ECT  
 96    GPTT     HSLT
 97    HEST     HGPECT  
 98    HKGGX
 99    HBGG 
100    HKGG     HRGG     HQGE   
101    RG       ASET     HUSET    OGPST 
102    GPST 
103    GM   
104    HBNN     HKNN     HRNN   
105    HBFF     HKFF     HKFS     HRFF  
106    HGO      HKOO     HLOO     HKAA  
107    HDLT     HEQDYN   GPLD     HNLFT    HSILD    TFPOOL   HTRL   
107    HUSETD   
108    CASEXX   
109    HB2PP    HK2PP   
110    HB2DD    HM2DD    HBDD     GMD      HGOD     HK2DD    HKDD   
110    HRDD 
111    HPNLD    HUDVT   
112    HOUDV1   HOPNL1  
113    HOUDV2   HOPNL2  
114    HQP      HUPV
115    HOES1    HOEF1    HOPP1    HOQP1    HOUPV1   HPUGV   
116    HOES2    HOEF2    HOPP2    HOQP2    HOUPV2   
117    HPDO     HPDT     HPPO     HPSO     HTOL 
118    BGPDP    HSIP
120    ELSETS   GPSETS   PLTPAR   PLTSETX   
121    HRAA 
122    HBAA 
123    HKDICT   HKELM    HBDICT   HBELM 
124    PLOTX1   
125    HOEF1X   
126    PLOTX2   
127    HXYPLTT  
128    HXYPLTTA 
$*  
